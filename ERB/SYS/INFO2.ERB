;=============================================================================
;능력 표시(2형 표시)
;=============================================================================
;表示順は@PRINT_ABL_COUNTのコメント参照
@NEW_PRINT_ABL, ARG
PRINTL □ 능력
LOCAL:1 = 0
REPEAT 25
	LOCAL = PRINT_ABL_COUNT(ARG)
	IF LOCAL < 0
		CONTINUE
	ELSEIF ABL:ARG:LOCAL > 0
		PRINTFORM %ABLNAME:LOCAL, 8%:LV{ABL:ARG:LOCAL}
		LOCAL:1 += 1
		;改行するのか？ 続けて出力するのか？
		IF LOCAL:1 >= 5
			PRINTL 
			LOCAL:1 = 0
		ELSE
			PRINT 　
		ENDIF
	ENDIF
REND
SIF LOCAL:1 != 0
	PRINTL 
CUSTOMDRAWLINE ･

;=============================================================================
;소질 표시(2형 표시)
;=============================================================================
;PRINT_TALENT （素質）との入れ替え
@NEW_PRINT_TALENT, ARG
PRINTL □ 소질
LOCAL = 0
;リピート回数-1 の番号までの素質が表示される
;リピート回数をあらかじめ大きめに設定（最大1000）しておくと、
;素質の最大番号が増えたときに書き換える手間が減るが、
;増やしすぎると処理速度に影響するので要注意
REPEAT 270
	IF TALENT:ARG:COUNT
		CALL SHOW_INFO_EACH_TALENT, ARG
		SIF (++LOCAL % 6) == 0
			PRINTL 
	ENDIF
REND
REPEAT 10
	IF TALENT:ARG:(830+COUNT)
		PRINTFORM [%TALENTNAME:(830+COUNT)%] 
		SIF (++LOCAL % 6) == 0
			PRINTL 
	ENDIF
REND
IF TALENT:ARG:998
	PRINTFORM [%TALENTNAME:998%] 
	SIF (++LOCAL % 6) == 0
		PRINTL 
ENDIF
IF CFLAG:ARG:0
	PRINTFORM [\@(EQUIP:ARG:0 == 1) ? ｅｘ # ＥＸ\@] 
	SIF (++LOCAL % 6) == 0
		PRINTL 
ENDIF

SIF LOCAL % 6 != 0
	PRINTL 
CUSTOMDRAWLINE ･

;=============================================================================
;각인 표시(2형 표시)
;=============================================================================
;PRINT_MARK (각인)와의 바꿔치기
@NEW_PRINT_MARK, ARG
PRINTL □ 각인
LOCAL = 0
REPEAT 11
	SIF COUNT >= 5 && COUNT < 8
		CONTINUE
	IF FLAG:3998 & 1 && ((COUNT == 8 && MARK:ARG:96) || (COUNT == 9 && MARK:ARG:97))
		FONTBOLD
		PRINTFORM %MARKNAME:COUNT%:LV{MARK:ARG:COUNT} [
		;오리지날
		;BAR MARK:ARG:COUNT, 3, 3
		;개수판
		CALL PRINT_COLORBAR, MARK:(ARG:0):(LOCAL:0), 3, 3,  0XFFC0CB, 0X240725, UNICODE(0x258A), UNICODE(0x258A)
		PRINT ]
		FONTREGULAR
	ELSE
		PRINTFORM %MARKNAME:COUNT%:LV{MARK:ARG:COUNT} [
		;오리지날
		;BAR MARK:ARG:COUNT, 3, 3
		;개수판
		CALL PRINT_COLORBAR, MARK:(ARG:0):(LOCAL:0), 3, 3,  0XFF1493, 0X240725, UNICODE(0x258A), UNICODE(0x258A)
		PRINT ]
	ENDIF

	LOCAL += 1
	;마지막의 루프 말고는 간격을 벌려둔다
	IF LOCAL%4 == 0
		PRINTL 
	ELSE
		PRINT 　
	ENDIF
REND

;Debug 모드(무조건) 또는 주인이 [초상계산]을 가지고 있으며 난이도 EASY～HARD일 때는 각인 취득 이력도 표시
IF TALENT:MASTER:998 || (TALENT:MASTER:198 && (FLAG:3 >= 1 && FLAG:3 <= 3)) 
	FOR COUNT, 96, 98
		PRINTFORM \@(COUNT == 96) ? 공포이력 # 반발이력\@:LV{MARK:ARG:COUNT} [
		;오리지날
		;BAR MARK:ARG:COUNT, 3, 3
		;개수판
		CALL PRINT_COLORBAR, MARK:(ARG:0):(LOCAL:0), 3, 3,  0XFF1493, 0X240725, UNICODE(0x258A), UNICODE(0x258A)
		PRINT ]

		LOCAL += 1
		;마지막의 루프 말고는 간격을 벌려둔다
		IF LOCAL%3 == 0
			PRINTL 
		ELSE
			PRINT 　
		ENDIF
	NEXT
ENDIF

;마지막 루프에서 개행하지 않았으면, 여기서 개행해둔다
SIF LOCAL%3 != 0
	PRINTL 
CUSTOMDRAWLINE ･

;=============================================================================
;경혐 표시(2형 표시)
;=============================================================================
;表示順は@PRINT_EXP_COMMONのコメント参照
@NEW_PRINT_EXP, ARG
PRINTL □ 경험
CALL PRINT_EXP_COMMON, ARG
CUSTOMDRAWLINE ･

;=============================================================================
;구슬 표시(2형 표시)
;=============================================================================
;表示順
; 2,快Ａ　 3,快Ｂ　 0,快Ｃ　 1,快Ｖ
;12,恭順　13,欲情　14,屈服　11,習得
;15,恥情　16,苦痛　17,恐怖　30,薬毒
;31,侵蝕　32,先導 100,否定 198,触手(主人のみ)
;SHOW_JUEL の珠表示部分を独立した関数に
@NEW_PRINT_JUEL, ARG
PRINTL □ 구슬
VARSET LOCALS,""
SPLIT "2,3,0,1,12,13,14,11,15,16,17,30,31,32,100,198", ",", LOCALS
REPEAT 15 + (ARG == MASTER)
	LOCAL = TOINT(LOCALS:COUNT)
	PRINTFORM 　%PALAMNAME:LOCAL%의 구슬:{JUEL:ARG:LOCAL, 7}

	;改行or表示位置あけ
	IF COUNT%4 == 3
		PRINTL 
	ELSE
		PRINT  
	ENDIF
REND

;마지막 루프에서 개행하지 않았으면, 여기서 개행해둔다
SIF COUNT%4 != 0
	PRINTL 
CUSTOMDRAWLINE ･
