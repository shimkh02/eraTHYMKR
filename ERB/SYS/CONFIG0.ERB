;=============================================================================
;게임 개시용
;=============================================================================
@START_CONFIGURATION
$LOOP_SELECT
LOADGLOBAL
LOCAL:0 = RESULT
DRAWLINE
PRINTL ★★ 추가 기능은 어떻게 하겠습니까? ★★
PRINTL 
DRAWLINE
PRINTL [0] 필요 없음　　　　：추가 기능을 설정하지 않고 스타트
PRINTL [1] 기본 세트　　　　：처음으로 eratohoYM에 접하는 분 지향
PRINTL [2] wiki 세트　　　　：eratoho 일본어 wiki내 페이지에 소개되어 있는 설정
PRINTL [3] 애정 세트　　　　：노예와 애정을 가지고 접하고 싶은 분 지향
PRINTL [4] 자학 세트　　　　：비참한 결말을 맞이하고 싶은 분 지향
PRINTL [5] 비주차 세트　　　：주차 플레이를 고려하지 않는 분 지향
PRINTL [6] YM 권장 세트 　　：어쨌든 우선 eratohoYM을 즐겨보고 싶으신 분 지향
SIF LOCAL:0
	PRINTL [7] 데이터 읽어오기　：이미 있는 컨피그 데이터를 읽어오기
PRINTL [8] 개별 설정　　　　：컨피그를 일일이 설정합니다.
;PRINTL [9] 전부해방　　：제법 게임에 익숙해져 뭐든 되는 분 지향

;토리코모드 전용
SETCOLOR 250,150,250
PRINTL [10] 토리코 기본 세트：YM 권장 세트+토리코 모드 편의기능. eratohoYM 초심자 혹은 즐겜유저 지향.
PRINTL [11] 토리코 EX 세트  ：웬만큼 eratohoYM을 꿰고 있는 분들을 위한 하드코어 기능 세트. 숙련자 지향.
PRINTL [12] 경비원 세트 　　：토리코 모드 패치의 신 게임 모드인 경비 시스템 추가. 숙련자 지향.
RESETCOLOR

PRINTL ※설정은 나중에도 변경 가능합니다
$INPUT_LOOP
INPUT
IF RESULT == 0
	PRINTFORMW 추가 기능을 유효로 하지 않고 게임을 시작합니다.
ELSEIF RESULT == 1
	PRINTFORMW 기본 세트의 추가 기능을 사용할 수 있도록 설정했습니다.
	CALL CONFIGURATION_MODE_NOVICE
ELSEIF RESULT == 2
	PRINTW wiki 세트의 추가 기능을 사용할 수 있도록 설정했습니다.
	CALL CONFIGURATION_MODE_WIKI
ELSEIF RESULT == 3
	PRINTW 애정 세트의 추가 기능을 사용할 수 있도록 설정했습니다.
	CALL CONFIGURATION_MODE_LOVER
ELSEIF RESULT == 4
	PRINTW 자학 세트의 추가 기능을 사용할 수 있도록 설정했습니다.
	CALL CONFIGURATION_MODE_SUICIDE
ELSEIF RESULT == 5
	PRINTW 비주차 세트의 추가 기능을 사용할 수 있도록 설정했습니다.
	CALL CONFIGURATION_MODE_NONCYCLE
ELSEIF RESULT == 6
	PRINTW YM 권장 세트의 추가 기능을 사용할 수 있도록 설정했습니다.
	CALL CONFIGURATION_MODE_YMPATCHES
ELSEIF RESULT == 7 && LOCAL:0
	PRINTW 컨피그 데이터를 읽어오도록 설정했습니다.
	CALL CONFIGURATION_LOAD
ELSEIF RESULT == 8
	PRINTW 컨피그를 각 항목별로 설정합니다.
	TFLAG:201 = 555
	CALL SWEET_POTATO_CONFIGURATION
ELSEIF RESULT == 10
	PRINTW YM 통합+토리코 모드의 일부 추가 기능을 사용할 수 있도록 설정했습니다.
		CALL CONFIGURATION_TORIKOMODE_BASIC
ELSEIF RESULT == 11
	PRINTL
	PRINTW ※주의! 본 모드는 게임의 여러 부분을 추가/수정했기에 난이도가 오를 수 있습니다.
	PRINTW ※eratohoYM 자체의 기능도 상당히 복잡하고 숨겨진 부분이 많기에,
	PRINTW ※가능하면 eratohoYM의 일반 모드에 익숙해진 후 플레이하는 것을 추천합니다.
	PRINTL
	PRINTL 토리코 모드로 시작하시겠습니까?
	PRINTL [0] 아직은 아냐.
	PRINTL [1] 시작한다!
	INPUT
	IF RESULT == 1
		PRINTW 토리코 모드의 추가 기능을 사용하도록 설정했습니다.
		CALL CONFIGURATION_TORIKOMODE_ON
	ELSE
		GOTO LOOP_SELECT
	ENDIF
ELSEIF RESULT == 12
	PRINTL
	PRINTW ※주의! 본 모드는 새로운 게임 모드를 도입했기에 기존 에라토호YM과 게임 플레이가 다를 수 있습니다.
	PRINTW ※eratohoYM 자체의 기능도 상당히 복잡하고 숨겨진 부분이 많기에,
	PRINTW ※가능하면 eratohoYM의 일반 모드에 익숙해진 후 플레이하는 것을 추천합니다.
	PRINTL
	PRINTL 토리코 경비원 모드로 시작하시겠습니까?
	PRINTL [0] 아직은 아냐.
	PRINTL [1] 시작한다!
	INPUT
	IF RESULT == 1
		PRINTW 토리코 모드와 경비 시스템의 추가 기능을 사용하도록 설정했습니다.
		CALL CONFIGURATION_TORIKOMODE_GUARD
	ELSE
		GOTO LOOP_SELECT
	ENDIF
ELSEIF RESULT == 9
	PRINTW 추가 기능을 모두 사용할 수 있도록 설정했습니다
	CALL CONFIGURATION_MODE_ALL
ELSE
	GOTO INPUT_LOOP
ENDIF

;=============================================================================
;강하게 뉴 게임 개시시의 재설정용
;=============================================================================
@NEWGAME_CONFIGURATION_RESET
VARSET LOCALS,""
SPLIT "SHOP · 이벤트 확장,아이템 확장,조교 모드 확장,커맨드 확장,eratohoYM 추가 패치",",",LOCALS
$INPUT_LOOP_0
LOADGLOBAL
LOCAL:0 = RESULT
PRINTL 변경하고 싶은 내용을 선택해 주세요
DRAWLINE
REPEAT 5
	PRINTFORML  [ {COUNT}] %LOCALS:COUNT%
REND
PRINTL  [ 5] 공작 기능 패치
PRINTL  [94] 커스텀 커맨드 필터
PRINTL  [95] 텍스트 표시의 설정
PRINTL  [96] 기본 세트
PRINTL  [97] 전부 해방
PRINTL  [99] CONFIG 설명
SIF LOCAL:0
	PRINTL  [98] 컨피그 데이터 불러오기
PRINTL [100] ＯＫ
PRINTL ※설정은 나중에도 변경 가능합니다
$INPUT_LOOP_1
INPUT
IF RESULT == 100
	RETURN 0
ELSEIF RESULT >= 0 && RESULT <= 4
	PRINTFORML %LOCALS:RESULT%의 설정을 변경합니다
	CALLFORM CONFIGURATION_{RESULT}
ELSEIF RESULT == 5
	PRINTFORML 공작 기능 패치의 설정을 변경합니다.
	CALL CONFIG_CUSTOMTOOL
ELSEIF RESULT == 94
	PRINTL 커맨드 필터의 개별 설정을 변경합니다
	CALL CUSTOM_COMMAND_FILTER
ELSEIF RESULT == 95
	PRINTL 텍스트 표시 설정을 변경합니다
	CALL CONFIGURATION_TEXT
ELSEIF RESULT == 96
	PRINTFORMW 기본 세트 추가 기능을 사용할 수 있도록 설정했습니다
	CALL CONFIGURATION_MODE_NOVICE
	RETURN 0
ELSEIF RESULT == 97
	PRINTFORMW 추가 기능을 모두 사용할 수 있도록 설정했습니다
	CALL CONFIGURATION_MODE_ALL
	RETURN 0
ELSEIF RESULT == 98 && LOCAL:0
	PRINTFORMW 컨피그 데이터를 읽어오도록 설정했습니다
	CALL CONFIGURATION_LOAD
	RETURN 0
ELSEIF RESULT == 99
	CALL DICTIONARY_CONFIG_MAIN
	GOTO INPUT_LOOP_0
ELSEIF RESULT != 100
	GOTO INPUT_LOOP_1
ENDIF
PRINT 그밖에 
GOTO INPUT_LOOP_0

;=============================================================================
;SHOP 메뉴에서 호출용
;=============================================================================
;컨피그 설정(사용 플래그를 FLAG:2, FLAG:3→23～25로 변경)
;각 수치와 &로써 취득. eratohoJ 것을 참고했습니다
;로드 시에 아이템이 소멸하는 등의 에러를 일으킬 수밖에 없어서 외부로부터의 로드 기능은 순삭했습니다
@SWEET_POTATO_CONFIGURATION
VARSET LOCALS,""
SPLIT "SHOP · 이벤트 확장,아이템 확장,조교 확장,커맨드 설정,eratohoYM 확장 패치,토리코 모드 패치",",",LOCALS
PRINTL 추가 기능의 설정을 변경합니다.
$INPUT_LOOP_0
PRINTL 변경하고 싶은 내용을 선택해 주세요
DRAWLINE
REPEAT 6
	PRINTFORML  [ {COUNT}] %LOCALS:COUNT%
REND

PRINTL  [ 6] 공작 기능 패치

;주인공 이름변경. 토리코 모드 사양이나, 플래그 조건은 없다. 주인공이 아나타일 때만 가능함.
SIF NO:MASTER == 0
	PRINTL  [95] 주인 이름 변경

PRINTL  [96] 커스텀 커맨드 필터
PRINTL  [97] 텍스트 표시의 설정
SIF TFLAG:201 == 0
	PRINTL  [98] 현재 컨피그를 보존한다
PRINTL  [99] CONFIG 설명
PRINTFORML [100] \@(TFLAG:201 == 0) ? 그만둔다 # 설정을 종료한다\@
$INPUT_LOOP_1
INPUT
IF RESULT == 100
	RETURN 0
ELSEIF RESULT >= 0 && RESULT <= 5
	PRINTFORML %LOCALS:RESULT%의 설정을 변경합니다.
	CALLFORM CONFIGURATION_{RESULT}
ELSEIF RESULT == 6
	PRINTFORML 공작 기능 패치의 설정을 변경합니다.
	CALL CONFIG_CUSTOMTOOL
;주인공 이름변경. 토리코 모드 사양이나, 플래그 조건은 없다. 주인공이 아나타일 때만 가능함.
ELSEIF RESULT == 95 && NO:MASTER == 0
	CALL CHANGE_NAME_ANATA_TORIKOMODE
ELSEIF RESULT == 96
	PRINTL 커맨드 필터의 개별 설정을 변경합니다.
	CALL CUSTOM_COMMAND_FILTER
ELSEIF RESULT == 97
	PRINTL 텍스트 표시의 설정을 변경합니다.
	CALL CONFIGURATION_TEXT
ELSEIF RESULT == 98 && TFLAG:201 == 0
	PRINTL 현재의 컨피그를 저장합니다.
	CALL CONFIGURATION_SAVE
ELSEIF RESULT == 99
	CALL DICTIONARY_CONFIG_MAIN
	GOTO INPUT_LOOP_0
ELSE
	CLEARLINE 1
	REUSELASTLINE 올바른 값을 입력해 주세요.
	GOTO INPUT_LOOP_1
ENDIF
DRAWLINE
PRINT 그밖에 
GOTO INPUT_LOOP_0

;=============================================================================
;각종 일괄 해방 · 봉인 처리
;=============================================================================
;-------------------------------------------------
;전부 해방
;-------------------------------------------------
@CONFIGURATION_MODE_ALL
FLAG:10 = 1
FLAG:11 = 2
FLAG:12 = 32767
FLAG:13 = 127
FLAG:14 = 255
FLAG:15 = 131071
FLAG:18 = 7
FLAG:1100 = 1
FLAG:1200 = 6
FLAG:1300 = 1
FLAG:1500 = 1
FLAG:1600 = 1
FLAG:1700 = 1
FLAG:3998 = 1
FLAG:3999 = 1

;-------------------------------------------------
;기본 세트 일괄 처리
;-------------------------------------------------
@CONFIGURATION_MODE_NOVICE
FLAG:10 = 1
FLAG:12 = 16403
FLAG:14 = 64

;-------------------------------------------------
;wiki 세트 일괄 처리
;-------------------------------------------------
@CONFIGURATION_MODE_WIKI
FLAG:10 = 1
FLAG:12 = 16437
FLAG:13 = 0
FLAG:14 = 64
FLAG:15 = 15

;-------------------------------------------------
;애정 세트 일괄 처리
;-------------------------------------------------
@CONFIGURATION_MODE_LOVER
FLAG:10 = 1
FLAG:12 = 247
FLAG:13 = 2
FLAG:14 = 83
FLAG:15 = 2568
FLAG:3998 = 1

;-------------------------------------------------
;자학 세트 일괄 처리
;-------------------------------------------------
@CONFIGURATION_MODE_SUICIDE
FLAG:12 = 513
FLAG:13 = 0
FLAG:14 = 4
FLAG:15 = 16
FLAG:18 = 6

;-------------------------------------------------
;비주차 세트 일괄 처리
;-------------------------------------------------
@CONFIGURATION_MODE_NONCYCLE
FLAG:10 = 1
FLAG:12 = 500
FLAG:13 = 3
FLAG:14 = 112
FLAG:15 = 84623

;-------------------------------------------------
;YM 권장 세트 일괄 처리
;-------------------------------------------------
@CONFIGURATION_MODE_YMPATCHES
FLAG:10 = 1
FLAG:11 = 2
FLAG:12 = 131583
FLAG:13 = 1027
FLAG:14 = 115
FLAG:15 = 131071
FLAG:21 = 3
FLAG:1200 = 2
FLAG:1700 = 1
FLAG:3998 = 1

;-------------------------------------------------
;조교 모드 일괄 "끊기" 처리
;-------------------------------------------------
@CONFIGURATION_MODE_NONE
;Ｖ調教フィルタ
FLAG:2 = 0

;-------------------------------------------------
;커맨드 확장 일괄 "끊기" 처리
;-------------------------------------------------
@CONFIGURATION_TRAIN_NONE
;캐릭터 전용 커맨드
FLAG:15 &= ~98304

;=============================================================================
;컨피그 데이터 관련 처리
;=============================================================================
;-------------------------------------------------
;컨피그 데이터를 보존한다
;-------------------------------------------------
@CONFIGURATION_SAVE
LOADGLOBAL
LOCAL:99 = RESULT
PRINTL CONFIG 데이터를 전역 데이터로 저장하겠습니까?
SIF LOCAL:99
	PRINTL 단, 지금의 전역 데이터 파일은 사라집니다.
PRINTL [0] 예, 저장합니다. (※주: 환상향연기가 초기화됩니다)
PRINTL [1] 아니오, 저장하지 않습니다.
$INPUT_LOOP_GLOBAL_SAVE
INPUT
IF RESULT == 1
	RETURN 0
ELSEIF RESULT != 0
	GOTO INPUT_LOOP_GLOBAL_SAVE
ENDIF

LOADGLOBAL

FOR LOCAL,10,22
	GLOBAL:(LOCAL - 9) = FLAG:LOCAL
NEXT
GLOBAL:13 = FLAG:1100
GLOBAL:14 = FLAG:1200
GLOBAL:15 = FLAG:1300
GLOBAL:16 = FLAG:1500
GLOBAL:17 = FLAG:3998
GLOBAL:18 = FLAG:3999
GLOBAL:19 = FLAG:1600
GLOBAL:20 = FLAG:1700
FOR LOCAL,0,600
	GLOBAL:(LOCAL + 21) = DITEMTYPE:999:LOCAL
NEXT
SAVEGLOBAL
PRINTL CONFIG 데이터를 전역 데이터로 저장했습니다.

;-------------------------------------------------
;컨피그 데이터를 읽어들이기
;-------------------------------------------------
@CONFIGURATION_LOAD
LOADGLOBAL

FOR LOCAL,10,22
	FLAG:LOCAL = GLOBAL:(LOCAL - 9) 
NEXT
FLAG:1100 = GLOBAL:13
FLAG:1200 = GLOBAL:14
FLAG:1300 = GLOBAL:15
FLAG:1500 = GLOBAL:16
FLAG:3998 = GLOBAL:17
FLAG:3999 = GLOBAL:18
FLAG:1600 = GLOBAL:19
FLAG:1700 = GLOBAL:20
FOR LOCAL,0,600
	DITEMTYPE:999:LOCAL = GLOBAL:(LOCAL + 21)
NEXT
