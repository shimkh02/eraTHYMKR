;==================================================================================================
;체질 변화약의 사용 처리
;==================================================================================================
;-------------------------------------------------
;진정차(스트레스 해소)
;-------------------------------------------------
@USE_DRUG_9, ARG, ARG:1
PRINTFORMW %조사처리(CALLNAME:MASTER,"가")% \@(ARG == MASTER) ? 〔진정차〕를 먹었다. # %CALLNAME:ARG%에게 〔진정차〕를 먹였다.\@
SIF ARG != MASTER
	CALL SELF_KOJO, ARG, 309, 0
PRINTW  
PRINTFORMW ＜%CALLNAME:ARG%의 스트레스가 저하됐다＞
PRINTW  
SIF ARG != MASTER
	CALL SELF_KOJO, ARG, 309, 1
;회복 처리로
CALL DRUG_HEAL_MENTAL, ARG
RETURN 1

;-------------------------------------------------
;영양제(체력 회복)
;-------------------------------------------------
@USE_DRUG_10, ARG, ARG:1
PRINTFORMW %조사처리(CALLNAME:MASTER,"가")% \@(ARG == MASTER) ? 〔영양제〕를 먹었다. # %CALLNAME:ARG%에게 〔영양제〕를 먹였다.\@
SIF ARG != MASTER
	CALL SELF_KOJO, ARG, 310, 0
PRINTW  
IF ARG != MASTER
	PRINTFORML 약을 다 마시고 \@(BASE:ARG:0 < 500) ? 꾸벅 꾸벅 졸던 상태에서 # 피곤한 표정을 짓던\@ %조사처리(CALLNAME:ARG,"가")% 
	PRINTL 어느 정도 체력을 되찾았는지, 확실히 건강해보이는 표정을 띠기 시작했다…….
	PRINTW  
ENDIF
PRINTFORMW ＜%CALLNAME:ARG%의 체력이 회복됐다＞
PRINTW  
SIF ARG != MASTER
	CALL SELF_KOJO, ARG, 310, 1
;회복 처리로
CALL DRUG_HEAL_LIFE, ARG
RETURN 1

;-------------------------------------------------
;후타나리약
;-------------------------------------------------
@USE_DRUG_11, ARG, ARG:1
PRINTFORMW %조사처리(CALLNAME:MASTER,"가")% \@(ARG == MASTER) ? 〔후타나리약〕을 복용했다. # %CALLNAME:ARG%에게 〔후타나리약〕을 건넸다.\@
IF TALENT:ARG:121
	IF ARG == MASTER
		PRINTFORMW ＜%조사처리(CALLNAME:ARG,"는")% 〔후타나리〕를 잃었다＞
	ELSE
		CALL SELF_KOJO, ARG, 311, 0
		PRINTFORML 약을 마시고 잠시 뒤에, %CALLNAME:ARG%의 다리 사이에서 존재감을 과시하고 있던 단단한 봉이 
		PRINTL 쪼그라들듯이 축소되었다…….
		PRINTW  
		PRINTFORML 조금 더 기다리자, %CALLNAME:ARG%의 육봉은 모습을 감추고 
		IF ABL:ARG:0 > 3
			PRINT 이제까지의 조교로 충분히 개발된 
		ELSEIF ABL:0 > 1
			PRINT 조금씩 쾌락의 맛을 깨우치기 시작한 
		ELSE
			PRINT 아직, 이거다 싶을 만큼 쾌락을 맛본 적 없는 
		ENDIF
		PRINTL 클리토리스로 돌아와 있었다…….
		PRINTW  
		PRINTFORMW ＜%조사처리(CALLNAME:ARG,"는")% 〔후타나리〕를 잃었다＞
		IF ABL:ARG:14 >= 5
			PRINTFORMW %조사처리(CALLNAME:ARG,"는")% 자신의 보지를 서운하게 바라보고 있다…….
		ELSEIF ABL:14 > 2
			PRINTFORMW %조사처리(CALLNAME:ARG,"는")% 조금 아쉽다는 듯이 이쪽을 보고 있다…….
		ELSE
			PRINTFORMW %조사처리(CALLNAME:ARG,"는")% 시원하다는 표정을 짓고 있다…….
		ENDIF
	ENDIF
	TALENT:ARG:121 = 0
	CALL CHANGE_FUTA_COMMON, ARG
	CALL SELF_KOJO, ARG, 311, 1
ELSE
	IF ARG == MASTER
		PRINTFORMW ＜%조사처리(CALLNAME:ARG,"는")% 〔후타나리〕를 얻었다＞
	ELSE
		CALL SELF_KOJO, ARG, 311, 2
		PRINTFORML 약을 \@(ABL:ARG:0 > 2) ? 마신 # 억지로 마신\@ %조사처리(CALLNAME:ARG,"는")% 보지에 아픔과 동시에 맹렬히 타오르는 듯한 감각에 휩싸였다…….
		PRINTW  
		IF ABL:ARG:37 == 5
			PRINT 더 기다리지 못하겠다는 듯 꿈지럭대고 있던 
		ELSEIF ABL:ARG:37 > 2
			PRINT 그 뜨거움조차 쾌감으로 느끼며 교성을 지르던 
		ELSE
			PRINT 필사적으로 고통을 참던 
		ENDIF
		PRINTFORM %조사처리(CALLNAME:ARG,"가")% 
		SIF ABL:ARG:37 < 3
			PRINT 고대해 마지 않았다는 듯 
		PRINTL 자신의 보지를 바라보자, 
		PRINTW 클리토리스가 서서히 비대화를 시작하고 있었다…….
		PRINTFORML 더더욱 비대화를 계속하던 %CALLNAME:ARG%의 클리토리스는 
		PRINTL 결국에는 훌륭한 육봉으로 모습을 바꾸었다…….
		PRINTW
		PRINTFORMW ＜%조사처리(CALLNAME:ARG,"를")% 〔후타나리〕를 얻었다＞
		IF ABL:ARG:37 == 5
			PRINTFORMW %조사처리(CALLNAME:ARG,"는")% 자신의 육봉을 보고 얼이 빠진 것 같다…….
		ELSEIF ABL:ARG:37 > 2
			PRINTFORMW %조사처리(CALLNAME:ARG,"는")% 다리 사이에 느껴지는 위화감을 즐기고 있다…….
		ELSE
			PRINTFORMW %조사처리(CALLNAME:ARG,"는")% 다리 사이의 위화감에 곤혹스러워 하고 있다…….
		ENDIF
	ENDIF
	TALENT:ARG:121 = 1
	CALL CHANGE_FUTA_COMMON, ARG
	CALL SELF_KOJO, ARG, 311, 3
ENDIF

;陰核肥大消去処理
MAXBASE:ARG:7 = BASE:ARG:7
BASE:ARG:7 = 0
TALENT:ARG:1 = 0
SIF CFLAG:ARG:37 & 16
	CFLAG:ARG:37 -= 16
;身体測定フラグを寝せる(次回身体測定を行うため)
CFLAG:ARG:8 = 0
RETURN 1

;-------------------------------------------------
;ふたたま薬
;-------------------------------------------------
@USE_DRUG_12, ARG, ARG:1
PRINTFORMW %조사처리(CALLNAME:MASTER,"는")% \@(ARG == MASTER) ? 〔후타타마약〕을 먹었다. # %CALLNAME:ARG%에게 [후타타마약]을 먹였다.\@
IF TALENT:ARG:121 == 2
	SIF ARG != MASTER
		CALL SELF_KOJO, ARG, 312, 0
	PRINTFORMW ＜%조사처리(CALLNAME:ARG,"는")% 불알이 사라졌다＞
	TALENT:ARG:121 = 1
	SIF ARG != MASTER
		CALL SELF_KOJO, ARG, 312, 1
ELSE
	SIF ARG != MASTER
		CALL SELF_KOJO, ARG, 312, 2
	PRINTFORMW ＜%조사처리(CALLNAME:ARG,"는")% 불알이 생겼다＞
	TALENT:ARG:121 = 2
	SIF ARG != MASTER
		CALL SELF_KOJO, ARG, 312, 3
ENDIF
RETURN 1

;-------------------------------------------------
;性露錠(陰核成長)
;-------------------------------------------------
@USE_DRUG_13, ARG, ARG:1
PRINTFORMW %조사처리(CALLNAME:MASTER,"는")% %CALLNAME:ARG%에게 〔성로정〕을 삼키게 했다.
PRINTFORMW ＜%CALLNAME:ARG%의 음핵이 약간 부풀어 오른 느낌이 들었다……＞
BASE:ARG:7 += (BASE:ARG:7 == 0) ? 1 # RAND(1, 10)
;成長による特殊処理
SIF BASE:ARG:7 >= 150 && TALENT:ARG:1 == 0
	CALL HUGE_CLITORIS, ARG
SIF BASE:ARG:7 > 200 && (CFLAG:ARG:37 & 4) == 0
	CALL GIGATORIS, ARG

CALL SELF_KOJO, ARG, 313
RETURN 1

;-------------------------------------------------
;根素勃乳
;-------------------------------------------------
@USE_DRUG_14, ARG, ARG:1
PRINTFORMW %조사처리(CALLNAME:MASTER,"는")% %CALLNAME:ARG%에게 〔근소발유〕를 넘겼다.
PRINTFORMW ＜%CALLNAME:ARG%의 [이상한 뿌리]는 기능을 되찾았다＞
;[不思議な根]の機能復活
TALENT:ARG:122 = 1

CALL SELF_KOJO, ARG, 314
RETURN 1

;-------------------------------------------------
;豊胸剤(爆乳化)
;-------------------------------------------------
@USE_DRUG_20, ARG, ARG:1
PRINTFORMW %조사처리(CALLNAME:MASTER,"는")% \@(ARG == MASTER) ? [풍유제]를 먹었다. # %CALLNAME:ARG%에게 [풍유제]를 먹였다.\@
SIF ARG != MASTER
	CALL SELF_KOJO, ARG, 320, 0
IF TALENT:ARG:110
	PRINTFORMW ＜%CALLNAME:ARG%의 가슴이 약간이지만 부풀었다＞
	TALENT:ARG:110 = 0
	TALENT:ARG:111 = 1
ELSEIF TALENT:ARG:111
	PRINTFORMW ＜%CALLNAME:ARG%의 가슴이 커졌다＞
	TALENT:ARG:111 = 0
ELSEIF TALENT:ARG:112
	PRINTFORMW ＜%CALLNAME:ARG%의 가슴이 몸에 안 어울릴 만큼이나 커졌다＞
	TALENT:ARG:112 = 0
	TALENT:ARG:113 = 1
ELSE
	PRINTFORMW ＜%CALLNAME:ARG%의 가슴이 터질 것 같은 크기로 화했다＞
	TALENT:ARG:112 = 1
ENDIF
SIF ARG != MASTER
	CALL SELF_KOJO, ARG, 320, 1
;身体測定フラグを寝せる(次回身体測定を行うため)
CFLAG:ARG:8 = 0
RETURN 1

;-------------------------------------------------
;縮胸剤(虚乳化)
;-------------------------------------------------
@USE_DRUG_21, ARG, ARG:1
PRINTFORMW %조사처리(CALLNAME:MASTER,"는")% \@(ARG == MASTER) ? [축유제]를 먹었다. # %CALLNAME:ARG%에게 [축유제]를 먹였다.\@
SIF ARG != MASTER
	CALL SELF_KOJO, ARG, 321, 0
IF TALENT:ARG:113
	PRINTFORMW ＜%CALLNAME:ARG%의 실로 풍만하던 가슴이 한 단계 작아졌다＞
	PRINTFORMW %조사처리(CALLNAME:ARG,"는")% 약간 아쉽다는 듯한 얼굴을 했다.
	TALENT:ARG:113 = 0
	TALENT:ARG:112 = 1
ELSEIF TALENT:ARG:112
	PRINTFORMW ＜%CALLNAME:ARG%의 가슴이 보통 크기가 되었다＞
	PRINTFORMW %조사처리(CALLNAME:ARG,"는")% 조금 쇼크를 받은 것 같다.
	TALENT:ARG:112 = 0
ELSEIF TALENT:ARG:111
	PRINTFORMW ＜%CALLNAME:ARG%의 가슴이 극단적으로 작아져버렸다＞
	PRINTFORMW %조사처리(CALLNAME:ARG,"는")% 아연해 하고 있다.
	TALENT:ARG:111 = 0
	TALENT:ARG:110 = 1
	TALENT:ARG:134 = 0
	MAXBASE:ARG:3 = 0
ELSE
	PRINTFORMW ＜%CALLNAME:ARG%의 가슴이 작아졌다＞
	PRINTFORMW %조사처리(CALLNAME:ARG,"는")% 쇼크를 받은 것 같다.
	TALENT:ARG:111 = 1
ENDIF
SIF ARG != MASTER
	CALL SELF_KOJO, ARG, 321, 1
;身体測定フラグを寝せる(次回身体測定を行うため)
CFLAG:ARG:8 = 0
RETURN 1

;-------------------------------------------------
;止乳剤(母乳治療)
;-------------------------------------------------
@USE_DRUG_22, ARG, ARG:1
PRINTFORMW %조사처리(CALLNAME:MASTER,"는")% \@(ARG == MASTER) ? [지유제]를 먹었다. # %CALLNAME:ARG%에게 [지유제]를 먹였다.\@
SIF ARG != MASTER
	CALL SELF_KOJO, ARG, 322, 0
PRINTFORMW ＜%조사처리(CALLNAME:ARG,"는")% 모유가 나오지 않게 되었다＞
TALENT:ARG:134 = 0
MAXBASE:ARG:3 = 0
SIF ARG != MASTER
	CALL SELF_KOJO, ARG, 322, 1
RETURN 1

;-------------------------------------------------
;尿漏れ改善薬(お漏らし治療)
;-------------------------------------------------
@USE_DRUG_23, ARG, ARG:1
PRINTFORMW %조사처리(CALLNAME:MASTER,"는")% \@(ARG == MASTER) ? [실금개선약]을 먹었다. # %CALLNAME:ARG%에게 [실금개선약]을 먹였다.\@
SIF ARG != MASTER
	CALL SELF_KOJO, ARG, 323, 0
PRINTFORMW ＜%조사처리(CALLNAME:ARG,"는")% [오줌싸개 버릇]이 사라졌다＞
TALENT:ARG:45 = 0
CALL CALC_GAUGE4_SLAVE, ARG, 1
CFLAG:ARG:31 = 0
SIF ARG != MASTER
	CALL SELF_KOJO, ARG, 323, 1
RETURN 1

;-------------------------------------------------
;記憶消去薬
;-------------------------------------------------
@USE_DRUG_24, ARG, ARG:1
PRINTFORML ＜%CALLNAME:ARG%의 기억을 지우겠습니까?＞
PRINTW ※한 번 실행하면 원래대로 돌아올 수 없습니다.
DRAWLINE
PRINTL [0] - 지금은 그만둔다
PRINTL [1] - 기억을 지운다
DRAWLINE
$INPUT_LOOP
INPUT
IF RESULT == 1
	CALL ERASE_MEMORY, ARG
ELSEIF RESULT == 0
	MONEY += ARG:1
	RETURN 0
ELSE
	GOTO INPUT_LOOP
ENDIF
RETURN 1

;-------------------------------------------------
;人魚姫の薬(人魚の人間化)
;-------------------------------------------------
@USE_DRUG_26, ARG, ARG:1
PRINTFORML ＜%조사처리(CALLNAME:ARG,"를")% 인간으로 만드시겠습니까?＞
PRINTW ※두 번 다시 되돌아갈 수 없게 됩니다! 잘 생각해주세요!
DRAWLINE
PRINTL [0] - 지금은 그만둔다
PRINTFORML [1] - 인간으로 만든다
DRAWLINE
$INPUT_LOOP
INPUT
IF RESULT == 1
	CALL MERMAID_EVENT, ARG
ELSEIF RESULT == 0
	RETURN 0
ELSE
	GOTO INPUT_LOOP
ENDIF
RETURN 1


;-------------------------------------------------
;赤の飴玉(処女膜再生)
;-------------------------------------------------
@USE_DRUG_30, ARG, ARG:1
PRINTFORMW %조사처리(CALLNAME:MASTER,"는")% \@(ARG == MASTER) ? 〔빨간 사탕〕을 먹었다. # %CALLNAME:ARG%에게 [빨간 사탕]을 먹였다.\@
SIF ARG != MASTER
	CALL SELF_KOJO, ARG, 330, 0
PRINTFORMW ＜%조사처리(CALLNAME:ARG,"는")% 재생처녀가 되었다.＞
TALENT:ARG:0 = 2
SIF ARG != MASTER
	CALL SELF_KOJO, ARG, 330, 1
RETURN 1

;-------------------------------------------------
;青の飴玉(母乳体質)
;-------------------------------------------------
@USE_DRUG_31, ARG, ARG:1
PRINTFORMW %조사처리(CALLNAME:MASTER,"는")% \@(ARG == MASTER) ? 〔파란 사탕〕을 먹었다. # %CALLNAME:ARG%에게 〔파란 사탕]을 먹였다.\@
SIF ARG != MASTER
	CALL SELF_KOJO, ARG, 331, 0
PRINTFORMW ＜%조사처리(CALLNAME:ARG,"는")% 모유가 나오기 시작했다＞
TALENT:ARG:134 = 1
CALL CALC_GAUGE3_SLAVE, ARG, 1
SIF ARG != MASTER
	CALL SELF_KOJO, ARG, 331, 1
RETURN 1

;-------------------------------------------------
;黄の飴玉(産卵体質)
;-------------------------------------------------
@USE_DRUG_32, ARG, ARG:1
;ABL:1をみる
LOCAL = 175 * (1 + ABL:ARG:1)

;EXP:0をみる
IF EXP:ARG:0 < EXPLV:1
	TIMES LOCAL , 0.20
	LOCAL:1 = 3000
ELSEIF EXP:ARG:0 < EXPLV:2
	TIMES LOCAL , 0.60
	LOCAL:1 = 2000
ELSEIF EXP:ARG:0 < EXPLV:3
	TIMES LOCAL , 1.00
	LOCAL:1 = 1500
ELSEIF EXP:ARG:0 < EXPLV:4
	TIMES LOCAL , 1.20
	LOCAL:1 = 1000
ELSEIF EXP:ARG:0 < EXPLV:5
	TIMES LOCAL , 1.40
	LOCAL:1 = 500
ELSE
	TIMES LOCAL , 1.60
	LOCAL:1 = 100
ENDIF

;EXP:51をみる
IF EXP:ARG:51 < EXPLV:2
	TIMES LOCAL , 0.80
	TIMES LOCAL:1 , 1.50
ELSEIF EXP:ARG:51 < EXPLV:4
	TIMES LOCAL , 1.00
	TIMES LOCAL:1 , 1.00
ELSEIF EXP:ARG:51 < EXPLV:5
	TIMES LOCAL , 1.20
	TIMES LOCAL:1 , 0.80
ELSE
	TIMES LOCAL , 1.50
	TIMES LOCAL:1 , 0.60
ENDIF
IF ARG == MASTER
	PRINTFORML %조사처리(CALLNAME:MASTER,"는")% 〔노란 사탕〕을 먹었다.
	PRINTW  
	PRINTFORMW 노란 사탕을 먹은 %조사처리(CALLNAME:MASTER,"는")% 몸 안에서 열이 나는 걸 느꼈다.
	PRINTFORMW 갑자기 %CALLNAME:MASTER%의 배가 순식간에 임산부처럼 부풀어 올랐다.
	PRINTFORML 그 후 %조사처리(CALLNAME:MASTER,"는")% 괴로워 했지만 
	PRINTFORMW 잠시 뒤 질에서 알이 양수와 함께 나왔다.
ELSE
	PRINTFORML 〔노란 사탕〕을 %CALLNAME:ARG%에게 \@(ABL:ARG:10 < 3) ? 억지로 먹였다 # 먹였다 \@…….	
	PRINTW  
	PRINTFORMW 노란 사탕을 먹은 %조사처리(CALLNAME:ARG,"는")% 몸이 이상하다고 호소했다.
	PRINTFORMW 잠시 뒤 %CALLNAME:ARG%의 배가 부풀어 올라서 임신한 것처럼 보였다.
	PRINTFORML %조사처리(CALLNAME:ARG,"는")% 고통스러운 표정을 짓다가 
	PRINTFORMW %조사처리(CALLNAME:MASTER,"가")% 다리를 M자로 펼치자 질에서 알이 양수와 함께 나왔다.
	CALL SELF_KOJO, ARG, 332
	PRINTFORMW 알을 낳은 %CALLNAME:ARG%의 배는 원래 크기로 돌아왔다.
ENDIF

IF TALENT:ARG:0
	CALL SETCOLOR_TORIKOMODE, "RED", 3
	PRINTL 【처녀 상실】
	RESETCOLOR
	IF CFLAG:ARG:500 == 0
		CFLAG:ARG:500 = 1007
		CSTR:ARG:5 = %CALLNAME:ARG%
	ENDIF
	TALENT:ARG:0 = 0
	TIMES LOCAL:1 , 2.50
ENDIF
;Ｖ経験
CALL COMMON_UP_EXP, ARG, 40, 5, 0, 1
;Ｖ拡張経験
CALL COMMON_UP_EXP, ARG, 32, 1, 0, 1
;薬物経験
CALL COMMON_UP_EXP, ARG, 40, 1, 0, 1
;異常経験
SIF EXP:ARG:51 == 0 && (CFLAG:ARG:20 & 1024) == 0
	CALL COMMON_UP_EXP, ARG, 50, 1, 0, 1
;産卵経験
CALL COMMON_UP_EXP, ARG, 51, 1, 0, 1
;快Ｖの珠
CALL COMMON_UP_JUEL, ARG, 1, LOCAL, 0, 1
;屈服の珠
CALL COMMON_UP_JUEL, ARG, 14, ((LOCAL+LOCAL:1)/2), 0, 1
;苦痛の珠
CALL COMMON_UP_JUEL, ARG, 16, LOCAL:1, 0, 1
;恐怖の珠
CALL COMMON_UP_JUEL, ARG, 17, (LOCAL/3), 1, 1

;元[卵生]かそうでないかを判断
IF CFLAG:ARG:20 & 1024
	PRINTFORMW ＜%조사처리(CALLNAME:ARG,"는")% 난생 체질로 돌아왔다＞
	TALENT:ARG:135 = 1
ELSE
	PRINTFORMW ＜%조사처리(CALLNAME:ARG,"는")% 난생 체질이 되어버렸다＞
	TALENT:ARG:136 = 1
ENDIF

;元々[卵生]ではなく, 異常経験5以下かつ妄信, 親愛, 相愛, 服従, 傀儡, 精神崩壊, 壊造人格なしだと狂気が付く
IF (CFLAG:ARG:20 & 1024) == 0 && EXP:ARG:50 < 6 && TALENT:ARG:151 == 0 && TALENT:ARG:152 == 0 && TALENT:ARG:153 == 0 && TALENT:ARG:158 == 0 && TALENT:ARG:160 == 0 && TALENT:ARG:167 == 0 && TALENT:ARG:168 == 0 && TALENT:ARG:169 == 0 && ARG != MASTER
	PRINTFORMW %조사처리(CALLNAME:ARG,"는")% 변화의 충격으로 미쳐 버렸다.
	TALENT:ARG:158 = 1
	CFLAG:ARG:12 |= 1
	CFLAG:ARG:17 |= 2
ENDIF

;入手した卵を展示室に
IF ARG != MASTER
	TFLAG:128 = 1
	SIF FLAG:12 & 8
		CALL GET_EGG, ARG
ENDIF
PRINTL 
PRINTFORML %조사처리(CALLNAME:ARG,"는")% 체력을 많이 소모한 모양이다.
PRINTFORMW 잠깐 \@(ARG == MASTER) ? 쉬는 게 # 쉬게 하는게\@ 좋을 것 같다.
BASE:ARG:0 = 100
TFLAG:128 = 0
RETURN 1

;-------------------------------------------------
;녹색 사탕(로리화)
;-------------------------------------------------
@USE_DRUG_33, ARG, ARG:1
IF ARG == MASTER
	PRINTFORML %조사처리(CALLNAME:MASTER,"는")% [녹색 사탕]을 먹었다.
ELSE
	PRINTFORML 〔녹색 사탕〕을 %CALLNAME:ARG%에게 \@(ABL:ARG:10 < 3) ? 억지로 먹였다 # 먹였다 \@.
ENDIF
PRINTW  
PRINTW  
IF TALENT:ARG:129
	PRINTFORML 비교적 큰 편이던 %CALLNAME:ARG%의 모습이 서서히 작아지기 시작하더니 
	PRINTL 최종적으로는 표준적인 체구라 할 수 있을 만큼까지 작아졌다…….
	PRINTW  
	PRINTFORMW ＜%CALLNAME:ARG%의 체격이 조금 줄어들었다＞
	TALENT:ARG:129 = 0
ELSE
	SIF ARG != MASTER
		PRINTFORM %CALLNAME:MASTER%의 눈에 비치는 
	PRINTFORML %CALLNAME:ARG%의 모습이 점점 더 작아지더니만, 
	PRINTL 최종적으론 아직 어린 모습이 남아있는 아이 정도 크기까지 줄어들었다…….
	PRINTW  
	PRINTFORMW ＜%조사처리(CALLNAME:ARG,"는")% 어린아이가 되었다＞
	TALENT:ARG:128 = 1
ENDIF
SIF ARG != MASTER
	CALL SELF_KOJO, ARG, 333
;범용 대사용 플래그
CFLAG:ARG:43 = 0
CALL COMMON_UP_EXP, ARG, 40, 1, 0
IF (CFLAG:ARG:12 & 8192) == 0
	CALL COMMON_UP_EXP, ARG, 50, 1, 0
	CFLAG:ARG:12 |= 8192
ENDIF
;身体測定フラグを寝せる(次回身体測定を行うため)
CFLAG:ARG:8 = 0
RETURN 1

;-------------------------------------------------
;白の飴玉(妖精化)
;-------------------------------------------------
@USE_DRUG_34, ARG, ARG:1
IF ARG == MASTER
	PRINTFORML ＜%조사처리(CALLNAME:ARG,"가")% 요정이 되겠습니까?＞
	PRINTFORM ※다시 되돌릴 수 
ELSE
	PRINTFORML ＜%조사처리(CALLNAME:ARG,"를")% 요정화시키겠습니까?＞
	PRINTFORM ※다시 되돌릴 수 
ENDIF
PRINTW 없으므로 신중하게 판단하세요!
DRAWLINE
PRINTL [0] - 그만둔다
IF ARG == MASTER
	PRINTL [1] - 요정이 된다
ELSE
	PRINTL [1] - 요정화시킨다
ENDIF
DRAWLINE
$INPUT_LOOP
INPUT
IF RESULT == 1
	CALL FAIRY_EVENT, ARG
ELSEIF RESULT == 0
	RETURN 0
ELSE
	GOTO INPUT_LOOP
ENDIF
RETURN 1

;-------------------------------------------------
;黒の飴玉(ＥＸ化)
;-------------------------------------------------
@USE_DRUG_35, ARG, ARG:1
PRINTFORML ＜%조사처리(CALLNAME:ARG,"를")% EX화 시키겠습니까?＞
PRINTW ※한 번 실행하면 돌아올 수 없습니다.
DRAWLINE
PRINTL [0] - 그만둔다
PRINTL [1] - EX화시킨다
DRAWLINE
$INPUT_LOOP
INPUT
IF RESULT == 1
	CALL EX_EVENT, ARG
ELSEIF RESULT == 0
	RETURN 0
ELSE
	GOTO INPUT_LOOP
ENDIF
RETURN 1

;-------------------------------------------------
;灰の飴玉(性格反転)
;-------------------------------------------------
@USE_DRUG_36, ARG, ARG:1
PRINTFORML ＜%CALLNAME:ARG%의 성격을 반전시키겠습니까?＞
PRINTW ※단 부작용으로 체력·기력의 최대치가 감소합니다
DRAWLINE
PRINTL [0] - 지금은 그만둔다
PRINTL [1] - 반전시킨다
DRAWLINE
$INPUT_LOOP
INPUT
IF RESULT == 1
	CALL REVERSE_CHARACTER, ARG
ELSEIF RESULT == 0
	RETURN 0
ELSE
	GOTO INPUT_LOOP
ENDIF
RETURN 1

;-------------------------------------------------
;금빛 사탕(강제 성장)
;-------------------------------------------------
@USE_DRUG_37, ARG, ARG:1
IF ARG == MASTER
	PRINTFORML %조사처리(CALLNAME:MASTER,"는")% 〔금빛 사탕〕을 먹었다.
ELSE
	PRINTFORML 〔금빛 사탕〕을 %CALLNAME:ARG%에게 \@(ABL:ARG:10 < 3) ? 억지로 먹였다 # 먹였다 \@.
ENDIF
PRINTW  
PRINTW  
IF TALENT:ARG:127
	PRINTFORML 손바닥 위에 들어올 만큼이나 작았던 %CALLNAME:ARG%의 몸이 쑥쑥 커지더니만 
	PRINTL 다시 보자, 인간 어린아이 크기만큼이나 성장해 있었다…….
	PRINTW  
	PRINTFORMW ＜%CALLNAME:ARG%의 몸이 커졌다＞
	TALENT:ARG:127 = 0
	TALENT:ARG:128 = 1
ELSEIF TALENT:ARG:128
	PRINTFORML %CALLNAME:ARG%의 어딜 봐도 어리단 느낌이었던 몸이 점점 더 자라나더니 
	PRINTL 최종적으론 보통 어른 크기의 체격이 되어 있었다…….
	PRINTW  
	PRINTFORMW ＜%CALLNAME:ARG%의 몸은 성장했다＞
	TALENT:ARG:128 = 0
ELSE
	SIF ARG != MASTER
		PRINTFORM %CALLNAME:MASTER%의 눈에 비치는 
	PRINTFORML %CALLNAME:ARG%의 모습이 점점 커지기 시작하더니만, 
	IF TALENT:MASTER:129
		PRINTL 정신이 들고 보니, 자신과 어깨를 나란히 할 만큼이나 자라 있었다…….
	ELSEIF TALENT:MASTER:128
		PRINTFORML 문득 올려다보니, %CALLNAME:ARG%의 얼굴이 굉장히 높은 위치에 있는 듯이 느껴졌다…….
	ELSE
		PRINTFORML 어느새 올려다보지 않으면%CALLNAME:ARG%의 얼굴을 볼 수 없게 되어버렸다…….
	ENDIF
	PRINTW  
	PRINTFORMW ＜%조사처리(CALLNAME:ARG,"는")% 거인이 되었다＞
	TALENT:ARG:129 = 1
ENDIF
SIF ARG != MASTER
	CALL SELF_KOJO, ARG, 337
;범용 대사용 플래그
CFLAG:ARG:43 = 0
CALL COMMON_UP_EXP, ARG, 40, 1, 0
IF (CFLAG:ARG:20 & 256) == 0
	CALL COMMON_UP_EXP, ARG, 50, 1, 0
	CFLAG:ARG:20 |= 256
ENDIF
;身体測定フラグを寝せる(次回身体測定を行うため)
CFLAG:ARG:8 = 0
RETURN 1

;-------------------------------------------------
;은빛 사탕(산란 체질 해제 · 난생 → 태생으로)
;-------------------------------------------------
@USE_DRUG_38, ARG, ARG:1
IF ARG == MASTER
	PRINTFORML %조사처리(CALLNAME:MASTER,"는")% 〔은빛 사탕〕을 먹었다…….
	PRINTL 
	PRINTFORML 은빛 사탕을 먹은 %조사처리(CALLNAME:MASTER,"는")% 몸 속 깊은 데서 열기를 느끼다가, 
	PRINTW 갑자기 하복부에 위화감을 느꼈다.
	PRINTFORML 몸 속에서 무언가가 변해가는 기묘한 감각에 %조사처리(CALLNAME:MASTER,"는")% 다소 기분이 나빠졌지만, 
	PRINTFORMW 그렇게 잠시 있으니 감각은 진정되었고, %조사처리(CALLNAME:MASTER,"는")% 자신의 암컷으로서의 기능이 사람의 그것으로 변했다는 것을 본능적으로 깨달았다.
ELSE
	PRINTFORML 〔은빛 사탕〕을 %CALLNAME:ARG%에게 \@(ABL:ARG:10 < 3) ? 억지로 먹였다 # 먹였다 \@.
	PRINTL 
	PRINTFORML 은빛 사탕을 먹은 %조사처리(CALLNAME:ARG,"는")% 몸의 변화를 호소했지만, 
	PRINTFORML 그 감각은 불쾌하거나 결코 격한 게 아니었고, 
	PRINTFORMW 그렇게 잠시 있다 보니 파도가 물러나듯이 진정된 것 같다.
	PRINTL 
	;消去対象素質が[卵生]か[産卵体質]かで分岐.
	;[卵生]なら無理矢理胎生にされたことへのショックを, 
	;でない場合は本来の妊娠形態にもどったことでそれぞれ反応を.
	IF TALENT:ARG:136
		;卵をにんっしんっしても狂気が付着しない条件あたりを境に少しメッセージを変化.
		;具体的には異常経験5以上か[妄信][親愛][相愛]のいずれかがあれば, ヒトの子を孕めるようになったのを喜ぶ.というか期待する感じに
		;処理と分岐は[卵生]持ちでも同じ.
		IF EXP:ARG:50 > 4 || (TALENT:ARG:151 || TALENT:ARG:152 || TALENT:ARG:153)
			PRINTFORML %조사처리(CALLNAME:ARG,"는")% 자신의 암컷으로서의 기능이 본래대로 돌아왔다는 걸 본능적으로 깨닫고는, 
			PRINTFORMW 미소와 함께 가만히 복부를 쓰다듬으며, %CALLNAME:MASTER%의 아이를 밸 기대에 뺨을 붉게 물들였다…….
		ELSE
			PRINTFORML %조사처리(CALLNAME:ARG,"는")% 자신의 암컷으로서의 기능이 본래의 그것으로 돌아왔다는 걸 본능적으로 깨닫고는, 
			PRINTFORML %CALLNAME:MASTER%의 아이를 정말로 임신하게 되지 않을지, 암컷으로서의 본능적 쾌락과 이성 사이에서
			PRINTFORMW 잠시 동안 떨고 있었다…….
		ENDIF
	ELSE
		IF EXP:ARG:50 > 4 || (TALENT:ARG:151 || TALENT:ARG:152 || TALENT:ARG:153)
			PRINTFORML %조사처리(CALLNAME:ARG,"는")% 자신의 암컷으로서의 기능이 인간의 것이 되었다는 걸 본능적으로 깨달았지만 
			PRINTFORMW 오히려 미소지으며 가만히 복부를 쓰다듬더니 %CALLNAME:MASTER%의 아이를 밸 기대에 뺨을 붉게 물들였다…….
		ELSE
			PRINTFORML %조사처리(CALLNAME:ARG,"는")% 자신의 암컷으로서의 기능이 사람의 것으로 변했다는 걸 본능적으로 깨닫고는 
			PRINTFORMW 본래 종으로서의 모습마저 범해진다는 것에 창백해졌다…….
		ENDIF
	ENDIF
ENDIF

;卵生・産卵体質別々に削除処理
;元々卵生のキャラはCFLAG:20の1024を立てることで初回の異常経験入手処理の判別と〔黄の飴玉〕で[卵生]に戻るかどうかを判別するのに使用
IF TALENT:ARG:135
	TALENT:ARG:135 = 0
	PRINTW  
	PRINTFORMW ＜%조사처리(CALLNAME:ARG,"는")% [난생]을 잃었다＞
	IF (CFLAG:ARG:20 & 1024) == 0
		CALL COMMON_UP_EXP, ARG, 50, 1, 0
		CFLAG:ARG:20 |= 1024
	ENDIF
ELSE
	TALENT:ARG:136 = 0
	PRINTW  
	PRINTFORMW ＜%조사처리(CALLNAME:ARG,"는")% [산란체질]을 잃었다＞
ENDIF

SIF ARG != MASTER
	CALL SELF_KOJO, ARG, 338

RETURN 1

;-------------------------------------------------
;무지개빛 사탕(성전환)
;-------------------------------------------------
@USE_DRUG_39, ARG, ARG:1
;性別再反転時（元に戻る時）に必要な情報の保存
;ここらへんはYM用性別反転コマンドから借用
;初回時に元の性別をCFLAG:4995に保存
;処女の情報をCFLAG:4996に保存
;胸の大きさをCFLAG:4997に保存
;ふたなりの情報をCFLAG:4998に保存
;母乳体質の情報をCFLAG:4999に保存

IF CFLAG:ARG:4995 == 0
	IF TALENT:ARG:120
		CFLAG:ARG:4995 = 1
		;男が初めてTSしたら処女に決まってんだろJK
		CFLAG:ARG:4996 = 1
		;元が男の場合は胸のサイズをランダムで決定
		;ただし, 小柄・小人体型はそこから1サイズ小さく, 巨躯は1サイズ大きくする
		LOCAL:1 = RAND:100
		IF LOCAL:1 < 5
			CFLAG:ARG:4997 = 0
		ELSEIF LOCAL:1 < 25
			CFLAG:ARG:4997 = 1
		ELSEIF LOCAL:1 > 95 - (TALENT:ARG:129 * 20 + (TALENT:ARG:127  + TALENT:ARG:128) * 5)
			CFLAG:ARG:4997 = 4
		ELSEIF LOCAL:1 > 75
			CFLAG:ARG:4997 = 3
		ELSE
			CFLAG:ARG:4997 = 2
		ENDIF
		CFLAG:ARG:4997 = LIMIT(CFLAG:ARG:4997 + (TALENT:ARG:129 - TALENT:ARG:128 - TALENT:ARG:127), 0, 4)
	ELSE
		CFLAG:ARG:4995 = 2
	ENDIF
ENDIF

IF TFLAG:228 == 0
	IF ARG == MASTER
		PRINTFORML %조사처리(CALLNAME:MASTER,"는")% 〔무지개빛 사탕〕을 먹었다.
	ELSE
		PRINTFORML 〔무지개빛 사탕〕을 %CALLNAME:ARG%에게 \@(ABL:ARG:10 < 3) ? 억지로 먹였다 # 먹였다 \@.
	ENDIF
ELSEIF TFLAG:228 >= 2
	PRINTFORML %CALLNAME:MASTER%의 몸이 변하기 시작한다.
ELSEIF TFLAG:228
	PRINTFORML %조사처리(CALLNAME:ARG,"는")% %CALLNAME:MASTER%의 눈 앞에서 몸이 변하기 시작했다.
ENDIF
PRINTW  
PRINTW  
IF TALENT:ARG:120
	IF ARG != MASTER
		IF TALENT:ARG:129
			PRINTFORML %CALLNAME:ARG%의 큰 실루엣이 점점 둥글어지더니 
			PRINTL 두꺼운 가슴근육이 점차 유방처럼 변해간다.
		ELSEIF TALENT:ARG:128 || TALENT:ARG:127
			PRINTFORML %CALLNAME:ARG%의 소년 같은 몸매가 점점 변하더니 
			PRINTL 얇은 가슴과 엉덩이에 살이 붙기 시작한다.
		ELSE
			PRINTFORML %CALLNAME:ARG%의 땅딸막한 몸이 점점 수축하더니 
			PRINTL 가슴과 엉덩이가 점점 커지기 시작했다.
		ENDIF
		PRINTFORML 눈을 비비고 다시보니까, %조사처리(CALLNAME:ARG,"는")% 완전히 여자가 되었다.
	ELSE
		PRINTFORML %조사처리(CALLNAME:MASTER,"는")% 갑자기 몸 전체가 빛으로 휩싸이는 듯한 감각을 느꼈다.
		PRINTW 그러더니 의식이 희미해지고, 마치 빛의 바다를 표류하는 듯한 부유감에 휩싸였다…….
		PRINTL 조금 지나, 몸을 휘감던 빛이 소실된 뒤에 몸을 확인하니
		PRINTFORML %CALLNAME:MASTER%의 몸은 완전히 여자가 되어 있었다…….
	ENDIF
	PRINTW  
	PRINTFORMW ＜%조사처리(CALLNAME:ARG,"는")% 여자가 되었다＞
	;女性化の処理
	TALENT:ARG:0 = CFLAG:ARG:4996
	TALENT:ARG:120 = 0
	TALENT:ARG:121 = CFLAG:ARG:4998
	;[早漏], [遅漏]関係の保存
	CALL CHANGE_SORO_COMMON, ARG
	IF TALENT:ARG:121
		MAXBASE:ARG:2 = CALC_GAUGE2_COMMON(ARG)
	ELSE
		MAXBASE:ARG:2 = 0
	ENDIF
	TALENT:ARG:134 = CFLAG:ARG:4999
	SIF TALENT:ARG:134
		CALL CALC_GAUGE3_SLAVE, ARG, 1
	IF CFLAG:ARG:4997 == 0
		TALENT:ARG:110 = 1
	ELSEIF CFLAG:ARG:4997 == 1
		TALENT:ARG:111 = 1
	ELSEIF CFLAG:ARG:4997 == 3
		TALENT:ARG:112 = 1
	ELSEIF CFLAG:ARG:4997 == 4
		TALENT:ARG:113 = 1
	ENDIF
ELSE
	IF ARG != MASTER
		PRINTFORML %CALLNAME:ARG%의 가슴과 엉덩이가 작아지기 시작한다.
		IF TALENT:ARG:129
			PRINTL 원래 크던 몸집이 더욱더 커지는 것 같다.
			PRINTFORML 주변을 보자, %조사처리(CALLNAME:ARG,"는")% 완전히 거인남자가 되었다.
		ELSEIF TALENT:ARG:128 || TALENT:ARG:127
			PRINTL 적절히 근육이 붙은 몸이 되어간다.
			PRINTFORML 정신을 차려보니,%조사처리(CALLNAME:ARG,"는")% 소년이 되었다.
		ELSE
			PRINTL 목이 굵어지고 몸이 땅딸막해져간다.
			PRINTFORML 다시 보니까, %조사처리(CALLNAME:ARG,"는")% 남자가 되었다.
		ENDIF
	ELSE
		PRINTFORML %조사처리(CALLNAME:MASTER,"는")% 갑자기 몸 전체가 빛으로 휩싸이는 듯한 감각을 느꼈다.
		PRINTW 그러더니 의식이 희미해지고, 마치 빛의 바다를 표류하는 듯한 부유감에 휩싸였다…….
		PRINTL 조금 지나, 몸을 휘감던 빛이 소실된 뒤에 몸을 확인하니, 
		PRINTFORML %CALLNAME:MASTER%의 몸은 완전히 남자가 되어 있었다…….
	ENDIF
	PRINTW  
	PRINTFORMW ＜%조사처리(CALLNAME:ARG,"는")% 남자가 되었다＞
	;男性化の処理
	CFLAG:ARG:4996 = TALENT:ARG:0
	CFLAG:ARG:4998 = TALENT:ARG:121
	CFLAG:ARG:4999 = TALENT:ARG:134
	IF TALENT:ARG:110
		CFLAG:ARG:4997 = 0
	ELSEIF TALENT:ARG:111
		CFLAG:ARG:4997 = 1
	ELSEIF TALENT:ARG:112
		CFLAG:ARG:4997 = 3
	ELSEIF TALENT:ARG:113
		CFLAG:ARG:4997 = 4
	ELSE
		CFLAG:ARG:4997 = 2
	ENDIF
	TALENT:ARG:0 = 0
	TALENT:ARG:110 = 0
	TALENT:ARG:111 = 0
	TALENT:ARG:112 = 0
	TALENT:ARG:113 = 0
	TALENT:ARG:120 = 1
	TALENT:ARG:121 = 0
	;[早漏], [遅漏]関係の再設定
	CALL CHANGE_SORO_COMMON, ARG
	MAXBASE:ARG:2 = CALC_GAUGE2_COMMON(ARG)
	TALENT:ARG:134 = 0
	MAXBASE:ARG:3 = 0
ENDIF
;범용 대사용 플래그
CFLAG:ARG:43 = 0
;身体測定フラグを寝せる(次回身体測定を行うため)
CFLAG:ARG:8 = 0

IF TFLAG:228 == 0
	SIF ARG != MASTER
		CALL SELF_KOJO, ARG, 339
	IF (CFLAG:ARG:20 & 512) == 0
		;異常経験
		CALL COMMON_UP_EXP, ARG, 50, 1, 0
		CFLAG:ARG:20 |= 512
	ENDIF
	;薬物経験
	CALL COMMON_UP_EXP, ARG, 40, 1, 1
ENDIF
RETURN 1

;-------------------------------------------------
;翠の飴玉(陰毛を生やす)
;-------------------------------------------------
@USE_DRUG_40, ARG, ARG:1
PRINTFORMW %조사처리(CALLNAME:MASTER,"는")% \@(ARG == MASTER) ? 〔비취색 사탕〕을 먹었다. # %CALLNAME:ARG%에게 〔비취색 사탕]을 먹였다.\@
SIF ARG != MASTER
	CALL SELF_KOJO, ARG, 340, 0
PRINTFORMW ＜%조사처리(CALLNAME:ARG,"는")% 음모가 나게 되었다＞
CFLAG:ARG:36 = 2
SIF ARG != MASTER
	CALL SELF_KOJO, ARG, 340, 1
RETURN 1

;-------------------------------------------------
;紺の飴玉(赤子の成長促進)　有精卵にも使える.
;-------------------------------------------------
@USE_DRUG_41, ARG, ARG:1
PRINTFORMW %조사처리(CALLNAME:MASTER,"는")% \@(ARG == MASTER) ? 〔남색 사탕〕을 먹었다. # %CALLNAME:ARG%에게 〔남색 사탕]을 먹였다.\@
SIF ARG != MASTER
	CALL SELF_KOJO, ARG, 341, 0
PRINTFORMW ＜%CALLNAME:ARG%의 뱃속이 뜨거워진 것 같다.
;効果量(デフォルトは10日早める.20以上に設定するとバグる可能性大)
EQUIP:ARG:1 = 10
SIF ARG != MASTER
	CALL SELF_KOJO, ARG, 341, 1
RETURN 1

;-------------------------------------------------
;桃の飴玉(助手引き継ぎ時にＥＸ化解除可能)
;-------------------------------------------------
@USE_DRUG_42, ARG, ARG:1
;一応主人服用時の地の文も用意(主人の解除の仕様は出来ていないため使用できないようにしている)
PRINTFORMW %조사처리(CALLNAME:MASTER,"는")% \@(ARG == MASTER) ? 〔복숭아색 사탕〕을 먹었다. # %CALLNAME:ARG%에게 〔복숭아색 사탕]을 먹였다.\@
SIF ARG != MASTER
	CALL SELF_KOJO, ARG, 342, 0
PRINTFORMW ＜%조사처리(CALLNAME:ARG,"는")% 말로 표현하기 힘든 이상한 느낌을 받는다＞
;周回を跨ぐ時に効果を発揮
EQUIP:ARG:0 = 1
SIF ARG != MASTER
	CALL SELF_KOJO, ARG, 342, 1
RETURN 1

;-------------------------------------------------
;鈍の飴玉(ミニ化, ～30㎝あたり)
;-------------------------------------------------
@USE_DRUG_43, ARG, ARG:1
IF ARG == MASTER
	PRINTFORML %조사처리(CALLNAME:MASTER,"는")% 〔쥐색 사탕〕을 먹었다.
ELSE
	PRINTFORML 〔쥐색 사탕〕을 %CALLNAME:ARG%에게 \@(ABL:ARG:10 < 3) ? 억지로 먹였다 # 먹였다 \@.
ENDIF
PRINTW  
PRINTW  
SIF ARG != MASTER
	PRINTFORM %CALLNAME:MASTER%의 시계가 이상하다.
PRINTFORML %CALLNAME:ARG%의 모습이 작아지더니 
PRINTL 원래 작았던 덩치가 더더욱 작아져 손바닥 위에 올려둘 정도로 줄었다.
PRINTW  
PRINTFORMW ＜%CALLNAME:ARG%의 몸이 줄어들었다＞
TALENT:ARG:127 = 1
;念のため[巨躯]は消去
TALENT:ARG:129 = 0
SIF ARG != MASTER
	CALL SELF_KOJO, ARG, 343
;범용 대사용 플래그
CFLAG:ARG:43 = 0
;身体測定フラグを寝せる(次回身体測定を行うため)
CFLAG:ARG:8 = 0
;薬物経験
CALL COMMON_UP_EXP, ARG, 40, 1, 1
IF (CFLAG:ARG:20 & 65536) == 0
	;異常経験
	CALL COMMON_UP_EXP, ARG, 50, 1, 0
	CFLAG:ARG:20 |= 65536
ENDIF
RETURN 1
