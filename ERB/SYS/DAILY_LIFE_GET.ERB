;≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡
;일상 이벤트의 각종 판정/취득 함수
;≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡
;-------------------------------------------------
;일상 이벤트에 등장할 수 있는지를 판정
;-------------------------------------------------
@ABLE_DAILY_LIFE(ARG, ARG:1)
#FUNCTION
;MASTER는 불가
SIF ARG == MASTER
	RETURNF 0
;실종 중에는 불가
SIF CFLAG:ARG:4
	RETURNF 0

;포란중에는 포란에 전념해주세요 ※ 어디까지나 잠정적인 처치로 합니다
;미욱한 제가 간단히 구상한 것이지만, 포란 중인 딸을, 모성 · 고무 · 헌신적을 보유한 아이, 사이좋은 아이 같은 애들이,
;혹은 MASTER가 부친이라면 MASTER 본인이 격려하거나, 시중을 들어준다는 등의 내용의 전용 이벤트를
;장래에 별도로 설치하는 것도 가능하지 않을까요
SIF TALENT:ARG:143
	RETURNF 0
;일상 강화 OFF라면 호감도0 불가
SIF (FLAG:12 & 128) == 0 && CFLAG:ARG:2 == 0
	RETURNF 0
;일상 강화라도 조교한 적 없으면 불가(에러 터짐 대책이므로 필수)
SIF CFLAG:ARG:1610 == 0
	RETURNF 0
;수면제로 자고 있으면 불가(배제 가능)
SIF EQUIP:ARG:14 && !GETBIT(ARG:1, 0)
	RETURNF 0
;일상 이벤트를 저해하는 낙인용 장비를 달고 있을 경우엔 불가
SIF (CFLAG:ARG:42 & 2) || (CFLAG:ARG:42 & 8)
	RETURNF 0
;전부 통과했으면 ok
RETURNF 1

;-------------------------------------------------
;요정인가 아닌가
;-------------------------------------------------
@GET_DAILY_LIFE_FAIRY(ARG)
#FUNCTION
RETURNF NO:ARG == 4 || NO:ARG == 5 || NO:ARG == 15 || NO:ARG == 45 || NO:ARG == 46 || NO:ARG == 47 || NO:ARG == 56 || TALENT:ARG:200

;-------------------------------------------------
;마법사인가 아닌가
;-------------------------------------------------
@GET_DAILY_LIFE_MAGICIAN(ARG)
#FUNCTION
RETURNF NO:ARG == 2 || NO:ARG == 8 || NO:ARG == 14 || NO:ARG == 51 || NO:ARG == 59 || NO:ARG == 60 || NO:ARG == 66 || NO:ARG == 77 || NO:ARG == 103 || NO:ARG == 116 || NO:ARG == 117 || NO:ARG == 213 || TALENT:ARG:197

;-------------------------------------------------
;책을 읽을 것 같은 사람
;-------------------------------------------------
@GET_DAILY_LIFE_READER(ARG)
#FUNCTION
RETURNF TALENT:ARG:14 || TALENT:ARG:55 || NO:ARG == 7 || NO:ARG == 8 || NO:ARG == 14 || NO:ARG == 26 || NO:ARG == 29 || NO:ARG == 68 || NO:ARG == 108 || NO:ARG == 176

;-------------------------------------------------
;카메라를 쓸 수 있는가 없는가
;-------------------------------------------------
@GET_DAILY_LIFE_CAMERA(ARG)
#FUNCTION
RETURNF NO:ARG == 32 || NO:ARG == 40 || NO:ARG == 42 || NO:ARG == 49 || NO:ARG == 50 || NO:ARG == 79 || TALENT:ARG:195 || TALENT:ARG:196 || TALENT:ARG:189

;-------------------------------------------------
;본직 메이드인가 아닌가
;-------------------------------------------------
@GET_DAILY_LIFE_MAID(ARG)
#FUNCTION
RETURNF NO:ARG == 9 || NO:ARG == 118 || NO:ARG == 128 || NO:ARG == 151 || NO:ARG == 161

;-------------------------------------------------
;교직 경험자라기보다 그렇게 보이는 사람
;-------------------------------------------------
@GET_DAILY_LIFE_TUTOR(ARG)
#FUNCTION
RETURNF NO:ARG == 26 || NO:ARG == 108

;-------------------------------------------------
;평소 일본 전통복 차림인 사람
;-------------------------------------------------
@GET_DAILY_LIFE_KIMONO(ARG)
#FUNCTION
;유유코, 코마치, 아큐, 코스즈, 마을사람, 신묘마루
RETURNF NO:ARG == 20 || NO:ARG == 35 || NO:ARG == 48 || NO:ARG == 88 || NO:ARG == 171 || NO:ARG == 226

;-------------------------------------------------
;차이나 드레스가 어울릴 것 같은 사람
;-------------------------------------------------
@GET_DAILY_LIFE_CHINESEDRESS(ARG)
#FUNCTION
;메이링, 리 메이링
RETURNF NO:ARG == 6 || NO:ARG == 204

;-------------------------------------------------
;평상시 세일러복인 사람
;-------------------------------------------------
@GET_DAILY_LIFE_SAILOR(ARG)
#FUNCTION
RETURNF NO:ARG == 75 || NO:ARG == 107

;-------------------------------------------------
;바니 슈츠가 어울릴 것 같은 사람
;-------------------------------------------------
@GET_DAILY_LIFE_BUNNY(ARG)
#FUNCTION
RETURNF NO:ARG == 27 || NO:ARG == 28 || NO:ARG == 55 || NO:ARG == 170 || NO:ARG == 184 || NO:ARG == 203 || (CFLAG:ARG:3217 + CFLAG:ARG:3218 > 49)

;-------------------------------------------------
;무녀(1) 및 무녀의 가계(2)
;-------------------------------------------------
@GET_DAILY_LIFE_MIKO(ARG)
#FUNCTION
;레이무, 사나에
IF NO:ARG == 1 || NO:ARG == 42
	RETURNF 1
ELSEIF NO:ARG == 149 && (CFLAG:ARG:3100 == 1 || CFLAG:ARG:3103 == 1 || CFLAG:ARG:3100 == 42 || CFLAG:ARG:3103 == 42 || TALENT:ARG:218 || TALENT:ARG:219)
	RETURNF 2
ELSE
	RETURNF 0
ENDIF

;-------------------------------------------------
;자매 판정
;-------------------------------------------------
@GET_DAILY_LIFE_SISTER(ARG, ARG:1)
#FUNCTION
LOCAL = ARG:1
;스칼렛 자매
SIF (NO:ARG == 10 || NO:ARG == 11) && (NO:LOCAL == 10 || NO:LOCAL == 11)
	RETURNF 1

;프리즘리버 자매
SIF (NO:ARG == 16 || NO:ARG == 17 || NO:ARG == 18 || NO:ARG == 98) && (NO:LOCAL == 16 || NO:LOCAL == 17 || NO:LOCAL == 18 || NO:LOCAL == 98)
	RETURNF 1

;아키 자매
SIF (NO:ARG == 37 || NO:ARG == 38) && (NO:LOCAL == 37 || NO:LOCAL == 38)
	RETURNF 1

;와타츠키 자매
SIF (NO:ARG == 53 || NO:ARG == 54) && (NO:LOCAL == 53 || NO:LOCAL == 54)
	RETURNF 1

;코메이지 자매
SIF (NO:ARG == 61 || NO:ARG == 64) && (NO:LOCAL == 61 || NO:LOCAL == 64)
	RETURNF 1

;무게츠/겐게츠
SIF (NO:ARG == 112 || NO:ARG == 113) && (NO:LOCAL == 112 || NO:LOCAL == 113)
	RETURNF 1

;츠쿠모 자매
SIF (NO:ARG == 223 || NO:ARG == 224) && (NO:LOCAL == 223 || NO:LOCAL == 224)
	RETURNF 1

;누군가의 딸이며 부모를 공유하고 있으면 자매로 친다
IF NO:ARG == 149 && NO:LOCAL == 149
	SIF CFLAG:ARG:3107 == CFLAG:LOCAL:3107 || CFLAG:ARG:3106 == CFLAG:LOCAL:3106 || CFLAG:ARG:3107 == CFLAG:LOCAL:3106 || CFLAG:ARG:3106 == CFLAG:LOCAL:3107
		RETURNF 1
ENDIF

RETURNF 0

;-------------------------------------------------
;만취 보정
;-------------------------------------------------
@GET_DAILY_LIFE_DRINK_RATE(ARG, ARG:1)
#FUNCTION
;오니거나 황혼의 괴물
IF TALENT:ARG:214 || TALENT:ARG:221
	RETURNF ARG:1 / 4
;인형이거나 캇파
ELSEIF TALENT:ARG:208 || TALENT:ARG:210
	RETURNF ARG:1 * 3 / 4
ELSE
	RETURNF ARG:1
ENDIF

;-------------------------------------------------
;체력이 많은가 적은가
;-------------------------------------------------
@GET_DAILY_LIFE_VIT(ARG)
#FUNCTION
;体力が低め(最大体力が5000以上の場合は20%
;1200以下の場合は60%
;そうでなければ1000が目安)の時は体力が少ないとする
;※体力上昇イベントや"*.sav"、"Chara*.csv"で最大体力をいじった際を想定
SELECTCASE MAXBASE:ARG:0
	CASE IS >= 5000
		LOCAL:1 = MAXBASE:ARG:0 / 5
	CASE IS <= 1200
		LOCAL:1 = MAXBASE:ARG:0 / 5 * 3
	CASEELSE
		LOCAL:1 = 1000
ENDSELECT
RETURNF BASE:ARG:0 <= LOCAL:1

;-------------------------------------------------
;경계를 조작할 수 있는가를 체크
;-------------------------------------------------
@ABLE_DAILY_LIFE_JUEL(ARG, ARG:1)
#FUNCTION
;ぶぶ漬け
SIF ARG < 0 || ARG >= CHARANUM || ARG:1 < 0 || ARG:1 >= 8
	RETURNF 0
	
;ARG:1==7の否定以外は閾値を下回ればok
RETURNF ARG:1 != 7 ? GET_THRESHOLD_DAILY_LIFE_JUEL(EXP:ARG:98) > JUEL:ARG:(ARG:1+11) # JUEL:ARG:100 >= 1000

;-------------------------------------------------
;경계 조작의 역치
;-------------------------------------------------
@GET_THRESHOLD_DAILY_LIFE_JUEL(ARG)
#FUNCTION
SELECTCASE ARG
	CASE IS < 100
		RETURNF 2000
	CASE 100 TO 199
		RETURNF 5000
	CASE 200 TO 299
		RETURNF 10000
	CASEELSE
		RETURNF 30000
ENDSELECT

;-------------------------------------------------
;일상 이벤트에 등장할 수 있는 캐릭터 숫자를 반환한다
;-------------------------------------------------
@GET_ABLE_DAILY_LIFE
#FUNCTION
;人数をリセット
LOCAL:1 = 0
FOR LOCAL, 0, CHARANUM
	;日常イベントに登場出来る
	IF ABLE_DAILY_LIFE(LOCAL)
		;人数を加算
		++LOCAL:1
	ENDIF
NEXT
RETURNF LOCAL:1

;-------------------------------------------------
;40종류 이상 모은 수집품의 품목수를 취득
;-------------------------------------------------
@DAILY_LIFE_GET_COLLECTED_GOODS
#FUNCTION
;品目数をリセット
LOCAL:1 = 0
FOR LOCAL, 501, 513
	;40以上なら加算
	SIF FLAG:LOCAL >= 40
		LOCAL:1 += 1
NEXT
RETURNF LOCAL:1

;-------------------------------------------------
;몇 년째의 기념일인지를 취득
;-------------------------------------------------
@GET_DAILY_LIFE_ANNIVERSARY_YEAR, ARG, ARG:1
;ぶぶ漬け
SIF ARG < 0 || ARG >= CHARANUM
	RETURN -1

;ARG:1で振り分け
SELECTCASE ARG:1
	;購入
	CASE 1
		LOCAL = 3111
	;連行
	CASE 2
		LOCAL = 3112
	;誕生
	CASE 3
		LOCAL = 3110
	;契約
	CASE 4
		LOCAL = 3114
	;結婚
	CASE 5
		LOCAL = 3113
	;陥落
	CASE 6
		LOCAL = 3115
	;妊娠
	CASE 7
		LOCAL = 3116
	;助手
	CASE 8
		LOCAL = 3117
	;ぶぶ漬け
	CASEELSE
		RETURN -1
ENDSELECT

;記念日取得
CALL CALENDAR_CALC, CFLAG:ARG:LOCAL
;退避
LOCAL:1 = DAY:4
;元に戻す
CALL CALENDAR_CALC, DAY
;差を返す
RETURN DAY:4 - LOCAL:1

;-------------------------------------------------
;부모를 취득
;-------------------------------------------------
@GET_DAILY_LIFE_PARENTS(ARG)
LOCAL:2 = 1
FOR LOCAL:1, 3105, 3108
	LOCAL = FINDCHARA(CFLAG:3109, CFLAG:ARG:(LOCAL:1))
	IF ABLE_DAILY_LIFE(LOCAL,1)
		RETURN LOCAL:2
	ENDIF
	++LOCAL:2
NEXT
RETURN 0

;-------------------------------------------------
;다 같이 바캉스에 참가할 수 있는지 아닌지를 판정
;-------------------------------------------------
@ABLE_DAILY_LIFE_ENJOY_VACATION(ARG)
#FUNCTION
;日常イベントに登場できないとダメ
SIF !ABLE_DAILY_LIFE(ARG)
	RETURNF 0
;体力気力が低いのはダメ
SIF GET_DAILY_LIFE_VIT(ARG)
	RETURNF 0
;昼間の場合は吸血鬼持ちは特注日傘がないとダメ
SIF TIME == 0 && TALENT:ARG:205 && ITEM:48 == 0 && NOITEM == 0
	RETURNF 0
;反発刻印か恐怖刻印持ちもダメ
SIF MARK:ARG:8 || MARK:ARG:9
	RETURNF 0
;相愛、親愛はOK
SIF TALENT:ARG:153 || TALENT:ARG:152
	RETURNF 1
;恋慕は条件付きでOK(従順が５以上で好感度が7500以上か、主人が謎の魅力持ちで好感度が3000以上である)
SIF TALENT:ARG:150 && ABL:ARG:10 >= 5 && ((TALENT:MASTER:92 && CFLAG:ARG:2 >= 3000) || CFLAG:ARG:2 >= 7500)
	RETURNF 1
;여기까지 와버렸으면 불가
RETURNF 0

;-------------------------------------------------
;상담을 받아줄 것 같은 멤버, 
;-------------------------------------------------
@GET_DAILY_LIFE_ADVISER(ARG)

;명련사 멤버 · 교육자 · 모성 보유자 ※ 다른 후보 · 보류: 운잔(대화 가능하나?), 유유코, 란, 유카리, 에이린, 에이키 님, 히나, 사나에, 카나코, 스와코, 유기, 사토리, 린노스케, 요우키, 신키
#FUNCTION
RETURNF NO:ARG == 71 || NO:ARG == 73 || NO:ARG == 75 || NO:ARG == 76 || NO:ARG == 77 || TALENT:ARG:126 || TALENT:ARG:142

;-------------------------------------------------
;봉래인이 되기 위한 조건 체크
;-------------------------------------------------
@CONDITION_BECAUSE_OF_BECOMING_HOURAIJIN, ARG,ARG:1

;当たり前ですが、物凄く条件を厳しくしました
;過去に蓬莱の薬を製造した事を、自責し後悔しているであろう永琳に……彼女に再び禁忌を犯す事を決意させる為、
;生半可な条件では彼女に対し申し訳ないと感じたので
;また"蓬莱の薬"とは、"輝夜の永遠の能力を使って作られた"とされているので、
;彼女の協力も不可欠です(永琳よりは若干条件は緩いですが)
;尚、種族に関しては今回試作と言う事もあり、明らかに駄目だろうと感じたものだけ弾きました(既に不死状態、死者、生物じゃない等……一応、機械も様子見です)

;条件1:MASTERが禁断の知識と調合知識を所持、技巧Lv5以上、機械・霊体・人形・蓬莱人・呪精・神霊・死神・不死刻印の全てが無い、10回以上クリア
SIF !(TALENT:MASTER:56 && TALENT:MASTER:55 && ABL:MASTER:12 >= 5 && !(MARK:MASTER:10 > 0 || TALENT:MASTER:48 || TALENT:MASTER:49 || TALENT:MASTER:206 || TALENT:MASTER:207 || TALENT:MASTER:208 || TALENT:MASTER:222 || TALENT:MASTER:224) && FLAG:6 >= 10)
	RETURN 0

;条件2:永琳が相愛である、好感度10万以上、愛情経験15000以上、恐怖・反発刻印無し(前段階で弾き済み)、MASTERとの相性が500以上、種族改造されていない、引き継ぎキャラである事、前述の要件全てを満たしている
SIF !(TALENT:ARG:153 && CFLAG:ARG:2 >= 100000 && EXP:ARG:21 >= 15000 && RELATION:ARG:(NO:MASTER) >= 500 && !CFLAG:ARG:29 && CFLAG:ARG:18)
	RETURN 0

;条件3:輝夜が親愛又は相愛である、好感度5万以上、愛情経験10000以上、恐怖・反発刻印無し、MASTERとの相性が200以上、種族改造されていない、前述の要件全てを満たしている
SIF !((TALENT:(ARG:1):152 || TALENT:(ARG:1):153) && CFLAG:(ARG:1):2 >= 50000 && EXP:(ARG:1):21 >= 10000 && MARK:(ARG:1):8 < 1 && MARK:(ARG:1):9 < 1 && RELATION:(ARG:1):(NO:MASTER) >= 200 && !CFLAG:(ARG:1):29)
	RETURN 0

;蓬莱人になる為のイベントを発生可能とする
RETURN 1

;-------------------------------------------------
;색시가 봉래인이 되는 것을 지원하는 조건 체크
;-------------------------------------------------
@CONDITION_BECAUSE_OF_BECOMING_OF_BRIDE_HOURAIJIN, ARG, ARG:1, ARG:2

;こちらも同様に物凄く条件を厳しくしました

;条件1:MASTERが禁断の知識と調合知識を所持、技巧Lv5以上、蓬莱人である事、10回以上クリア
SIF !(TALENT:MASTER:56 && TALENT:MASTER:55 && ABL:MASTER:12 >= 5 && TALENT:MASTER:49 && FLAG:6 >= 10)
	RETURN 0

;条件2:永琳が親愛又は相愛である、好感度10万以上、愛情経験15000以上、恐怖・反発刻印無し、MASTERとの相性が500以上、対象との相性が110以上、種族改造されていない、前述の要件全てを満たしている
SIF !((TALENT:(ARG:1):152 || TALENT:(ARG:1):153) && CFLAG:(ARG:1):2 >= 100000 && EXP:(ARG:1):21 >= 15000 && MARK:(ARG:1):8 < 1 && MARK:(ARG:1):9 < 1 && RELATION:(ARG:1):(NO:MASTER) >= 500 && RELATION:(ARG:1):(NO:ARG) >= 110 && !CFLAG:(ARG:1):29)
	RETURN 0

;条件3:輝夜が親愛又は相愛である、好感度10万以上、愛情経験15000以上、恐怖・反発刻印無し、MASTERとの相性が500以上、対象との相性が110以上、種族改造されていない、前述の要件全てを満たしている
SIF !((TALENT:(ARG:2):152 || TALENT:(ARG:2):153) && CFLAG:(ARG:2):2 >= 100000 && EXP:(ARG:2):21 >= 15000 && MARK:(ARG:2):8 < 1 && MARK:(ARG:2):9 < 1 && RELATION:(ARG:2):(NO:MASTER) >= 500 && RELATION:(ARG:2):(NO:ARG) >= 110 && !CFLAG:(ARG:2):29)
	RETURN 0

;条件4:対象が相愛である、好感度10万以上、愛情経験20000以上、恐怖・反発刻印無し、MASTERとの相性が500以上、引き継ぎキャラである事、種族改造されていない、前述の要件全てを満たしている
SIF !(TALENT:ARG:153 && CFLAG:ARG:2 >= 100000 && EXP:ARG:21 >= 20000 && MARK:ARG:8 < 1 && MARK:ARG:9 < 1 && RELATION:ARG:(NO:MASTER) >= 500 && !CFLAG:ARG:29 && CFLAG:ARG:18)
	RETURN 0

;購入可能な奴隷の人数の算出 ※前述の売却不能フラグに関連して、
;※ 詰み状態回避の為、キャラ購入が可能でないとイベント発生できないようにしておきます
LOCAL:5 = CALC_SLAVE_CAPACITY()

;条件5:ARGが蓬莱人・呪精・不死刻印・機械・霊体・人形・神霊・死神を持っていない事、永琳・輝夜との相性が110以上、キャラ購入が可能である事
SIF TALENT:ARG:49 || TALENT:ARG:48 || MARK:ARG:10 > 0 || TALENT:ARG:206 || TALENT:ARG:207 || TALENT:ARG:208 || TALENT:ARG:222 || TALENT:ARG:224 || RELATION:ARG:(NO:(ARG:1)) < 110 || RELATION:ARG:(NO:(ARG:2)) < 110 || CHARANUM >= LOCAL:5
	RETURN 0

;색시를 봉래인으로 만드는 이벤트를 발생 가능하게 한다
RETURN 1
