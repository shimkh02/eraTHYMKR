;≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡
;eratohoYM Mk-3用 口上テンプレート
;227をキャラ番号に置換して使用してください(霊夢なら1、蓮子なら49、魅魔なら100)。
;
;このファイルでは助手引き継ぎ処理が関係する口上用関数が置いてあります。
;≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡
;==============================================================
;○○の特殊引き継ぎ処理
;==============================================================
;通常の引き継ぎ処理は行われなくなるので注意
;コピペによって資質リセットや好感度リセットは行われます。
;わからない部分には触らないことを推奨。

;通常のリセット処理関数 @ASSI_STATE_TAKEOVER, ARGをほぼコピペ。
;ARGには壊造人格や即落ちを取得させたかがビット演算で入っている。
;■助手引き継ぎではTARGETではなくASSIを使うので、フラグ等を見る場合はCFLAG:(TARGET、省略可):0ではなくCFLAG:ASSI:0と書く。
@KOJO_227_EVENT_ASSI_TAKEOVER, ARG

;■誤動作防止用処理
;■使用時には↓の「JUMP」が書かれた1行をコメントアウトするか、削除してください。
JUMP ASSI_STATE_TAKEOVER, ARG

;==================================================
;------ここから処理内容------

;■普通にPRINTとかもできます。
;その場合はキャラ名を何らかの形で書くことを推奨します。
;==================================================
;PRINTFORMW 特殊な引き継ぎ処理テンプレート

;助手を経験したことを示す
ISASSI:ASSI = 1
;引き継いだキャラであることを判別するためのフラグ
CFLAG:ASSI:18 = 1
;称号リセット
NICKNAME:ASSI = %CSVNICKNAME (NO:ASSI , 0)%
;館にいた時の状況
CSTR:ASSI:3 = 過去において%CALLNAME:MASTER%の調教を受けた

;妊娠・抱卵中・乳母でなく育児中の場合は減っている体力を戻す
IF TALENT:ASSI:140 || TALENT:ASSI:143 || (TALENT:ASSI:141 && CFLAG:ASSI:79 == 0)
	MAXBASE:ASSI:0 += 500
	CALL RESET_ASSI_PREGNANT, ASSI
;懐卵時の体力の最大値低下を戻す処理
ELSEIF TALENT:ASSI:145
	MAXBASE:ASSI:0 += 500
ENDIF

;特定のゲージをリセット
REPEAT 4
	MAXBASE:ASSI:(COUNT+4) = 0
	BASE:ASSI:(COUNT+4) = 0
REND
;居場所のフラグリセット
CFLAG:ASSI:4 = 0
;恐怖・反発刻印取得済み判定リセット
MARK:ASSI:96 = 0
MARK:ASSI:97 = 0
;薬物刻印1Lv分減少
MARK:ASSI:3 = MAX(MARK:ASSI:3 - 1, 0)

;特定の素質をリセット
FOR LOCAL, 140, 180
	;壊造人格、即落ちは引き継ぎで取得させていない場合はリセット
	IF (LOCAL == 169 && (ARG & 2) == 0) || (LOCAL == 179 && (ARG & 1) == 0)
		TALENT:ASSI:LOCAL = 0
	;妊娠、育児中、母性、抱卵中、寄生、懐卵はリセット(バグ防ぐため)
	ELSEIF (LOCAL >= 140 && LOCAL <= 143) || LOCAL == 145 || LOCAL == 146
		TALENT:ASSI:LOCAL = 0
	;恋慕系、服従系、淫乱系もリセット(特に恋慕、淫乱、服従は堕とした人数のカウントに影響されるため)
	ELSEIF (LOCAL >= 150 && LOCAL < 157) || (LOCAL >= 159 && LOCAL < 169) || (LOCAL >= 170 && LOCAL < 179)
		TALENT:ASSI:LOCAL = 0
	ENDIF
NEXT
TALENT:ASSI:65 = 0
TALENT:ASSI:998 = 0

;CFLAGをリセット
FOR LOCAL, 0, 100
	;売却不可フラグ、ＶＡ異常経験変動フラグリセット
	IF LOCAL == 3 || LOCAL == 11
		CFLAG:ASSI:LOCAL = 0
	;嫉妬、放尿レベル、媚薬中毒、鈴蘭の毒、陰毛、触手インプラント、烙印用装備フラグはリセット
	ELSEIF (LOCAL >= 30 && LOCAL <= 33) || LOCAL == 36 || LOCAL == 41 || LOCAL == 42
		CFLAG:ASSI:LOCAL = 0
	;汎用口上、逆口上の人格再設定用
	ELSEIF LOCAL == 43 || LOCAL == 44
		CFLAG:ASSI:LOCAL = 0
	;最後の調教を受けた日と最後に助手を外された日をリセット（しないと表示が不自然になる）
	ELSEIF LOCAL == 46 || LOCAL == 47
		CFLAG:ASSI:LOCAL = 0
	;部位開発度、強制精飲絶頂フラグはリセット
	ELSEIF (LOCAL >= 60 && LOCAL <= 64)
		CFLAG:ASSI:LOCAL = 0
	;妊娠出産系フラグも全部リセット
	ELSEIF LOCAL >= 70 && LOCAL <= 79
		CFLAG:ASSI:LOCAL = 0
	ENDIF
NEXT
FOR LOCAL, 1000, 10000
	;娘に関するフラグを保存
	SIF LOCAL >= 3100 && LOCAL < 3110
		CONTINUE
	;種族遺伝子に関するフラグを保存
	SIF LOCAL >= 3200 && LOCAL < 3400
		CONTINUE
	;性別反転した時のもとの素質に関するフラグを保存
	SIF LOCAL >= 4995 && LOCAL < 5000
		CONTINUE

	;■ここでやっと特別な処理
	;CONTINUEで0を入れる処理を指定の番号だけ飛ばす
	;例・CFLAGの7990,7991, ... , 7999は引き継ぎ
	SIF LOCAL >= 7990 && LOCAL < 8000
		CONTINUE

	CFLAG:ASSI:LOCAL = 0
NEXT

;EQUIPをリセットする
VARSET EQUIP:ASSI:0, 0, 0, 15

;好感度は減少させた状態で引き継ぎ
;最低は50(引き継ぐ時点で1000以上はあると見込んでいるため)
;素質が変動する700、1000、2000、5000を境界に引き継ぎ率が激減するように
LOCAL:1 = CFLAG:ASSI:2 / 20
LOCAL:1 = TABLE_CALC2(LOCAL:1, 100, 90)
LOCAL:1 = TABLE_CALC2(LOCAL:1, 250, 80)
LOCAL:1 = TABLE_CALC2(LOCAL:1, 500, 75)
LOCAL:1 = TABLE_CALC2(LOCAL:1, 700, 66)
LOCAL:1 = TABLE_CALC2(LOCAL:1, 1000, 50)
LOCAL:1 = TABLE_CALC2(LOCAL:1, 2000, 33)
LOCAL:1 = TABLE_CALC2(LOCAL:1, 5000, 25)
CFLAG:ASSI:2 = LOCAL:1
