;=============================================================================
;순종
;=============================================================================
@ABLUP10
DRAWLINE
SIF ABL:10 >= 5
	JUMP ABLUP_MAX

;ＬＶ４から５に上げるときは異常経験必要（素質：[臆病][素直][妄信][淫乱]なら無視できる）
SIF COST10(4)
	PRINTFORML %EXPNAME:50% 있음, 그리고 

PRINTFORM [0] - %PALAMNAME:17%의 구슬×{COST10(0)}……
CALL SHOW_ISABLUP_COMMON, CAN10(0)

PRINTFORM [1] - %PALAMNAME:12%의 구슬×{COST10(1)}……
CALL SHOW_ISABLUP_COMMON, CAN10(1)

IF COST10(2) > 0
	PRINTFORM [2] - %PALAMNAME:13%의 구슬×{COST10(2)}……
	CALL SHOW_ISABLUP_COMMON, CAN10(2)
ENDIF

IF COST10(3) > 0
	PRINTFORM [3] - %PALAMNAME:14%의 구슬×{COST10(3)}……
	CALL SHOW_ISABLUP_COMMON, CAN10(3)
ENDIF
PRINTL [100] - 돌아간다
$INPUT_LOOP
INPUT
IF RESULT == 100
	RETURN 0
ELSEIF RESULT < 0 || RESULT > 100
	GOTO INPUT_LOOP
ELSEIF RESULT >= 0 && RESULT <= 3 && CAN10(RESULT) != 0
	PRINTL 조건을 만족하고 있지 않습니다.
	RESTART
ELSEIF RESULT == 0
	JUEL:17 -= COST10(0)
ELSEIF RESULT == 1
	JUEL:12 -= COST10(1)
ELSEIF RESULT == 2
	JUEL:13 -= COST10(2)
ELSEIF RESULT == 3
	JUEL:14 -= COST10(3)
ELSE
	GOTO INPUT_LOOP
ENDIF

ABL:10 += 1
PRINTFORMW %ABLNAME:10%의 레벨이 %조사처리(ABL:10,"가")% 되었습니다.

;--------------------------------------------------
;レベルアップに必要な珠計算、レベルアップ予告処理
;--------------------------------------------------
@ISABLUP10
SIF ABL:10 >= 5
	RETURN 0

CALLF CALC_COST10, "CALC"

;条件別にＯＫかダメかを記録する
RETURN CAN_ABLUP10("CHECK")

@COST10(ARG)
#FUNCTION
RETURNF CALC_COST10("GET", ARG)

@CALC_COST10(ARGS, ARG)
#FUNCTION
SIF ARGS == "GET"
	RETURNF LOCAL:ARG
;必要な条件の算出
VARSET LOCAL, 0

IF ABL:10 == 0
	LOCAL:0 = (TALENT:10) ? 7 # 10
	LOCAL:1 = 10
	LOCAL:2 = 300
	LOCAL:3 = 200
ELSEIF ABL:10 == 1
	LOCAL:0 = (TALENT:10) ? 90 # 150
	LOCAL:1 = 100
	LOCAL:2 = 1000
	LOCAL:3 = 1200
ELSEIF ABL:10 == 2
	LOCAL:0 = (TALENT:10) ? 500 # 1000
	LOCAL:1 = 800
	LOCAL:2 = 2000
	LOCAL:3 = 3000
ELSEIF ABL:10 == 3
	LOCAL:0 = (TALENT:10) ? 1200 # 3000
	LOCAL:1 = 3000
	LOCAL:2 = 0
	LOCAL:3 = 0
	;一線越えない
	IF TALENT:27
		TIMES LOCAL:0 , 2.00
		TIMES LOCAL:1 , 2.00
	ENDIF
ELSEIF ABL:10 == 4
	LOCAL:0 = (TALENT:10) ? 2400 # 8000
	LOCAL:1 = 15000
	LOCAL:2 = 0
	LOCAL:3 = 0
	;一線越えない
	IF TALENT:27
		TIMES LOCAL:0 , 3.00
		TIMES LOCAL:1 , 3.00
	ENDIF
ENDIF

;反抗的
IF TALENT:11
	TIMES LOCAL:0 , 1.50
	TIMES LOCAL:1 , 1.50
	TIMES LOCAL:3 , 1.50
;素直
ELSEIF TALENT:13
	TIMES LOCAL:0 , 0.50
	TIMES LOCAL:1 , 0.50
	TIMES LOCAL:3 , 0.50
ENDIF
;気丈
IF TALENT:12
	TIMES LOCAL:0 , 3.00
	TIMES LOCAL:1 , 2.00
	TIMES LOCAL:2 , 1.50
	TIMES LOCAL:3 , 1.50
ENDIF
;生意気
IF TALENT:15
	TIMES LOCAL:0 , 1.50
	TIMES LOCAL:1 , 2.00
	TIMES LOCAL:3 , 1.50
ENDIF
;恋慕
SIF TALENT:150
	TIMES LOCAL:1 , 0.50
;淫乱
IF TALENT:170
	TIMES LOCAL:0 , 0.80
	TIMES LOCAL:1 , 0.80
	TIMES LOCAL:2 , 0.80
	TIMES LOCAL:3 , 0.80
ENDIF
;人形
IF TALENT:208
	TIMES LOCAL:0 , 0.80
	TIMES LOCAL:1 , 0.80
	TIMES LOCAL:2 , 0.80
	TIMES LOCAL:3 , 0.80
ENDIF

;ＬＶ４から５に上げるときは異常経験必要（素質：[臆病][素直][妄信][淫乱]なら無視できる）
SIF ABL:10 == 4 && TALENT:10 == 0 && TALENT:13 == 0 && TALENT:151 == 0 && TALENT:170 == 0
	LOCAL:4 = 1

RETURNF 0

@CAN10(ARG)
#FUNCTION
RETURNF CAN_ABLUP10("GET", ARG)

@CAN_ABLUP10(ARGS, ARG)
#FUNCTION
SIF ARGS == "GET"
	RETURNF LOCAL:ARG
VARSET LOCAL, 0
;異常経験が必要な場合
IF EXP:50 < COST10(4)
	LOCAL:0 |= 2
	LOCAL:1 |= 2
	LOCAL:2 |= 2
	LOCAL:3 |= 2
ENDIF

;恐怖의 구슬で上げる
SIF JUEL:17 < COST10(0)
	LOCAL:0 |= 1

;恭順의 구슬で上げる
SIF JUEL:12 < COST10(1)
	LOCAL:1 |= 1

;欲情의 구슬で上げる
IF COST10(2) > 0
	SIF JUEL:13 < COST10(2)
		LOCAL:2 |= 1
ELSE
	LOCAL:2 = 256
ENDIF

;屈服의 구슬で上げる
IF COST10(3) > 0
	SIF JUEL:14 < COST10(3)
		LOCAL:3 |= 1
ELSE
	LOCAL:3 = 256
ENDIF

RETURNF (LOCAL:0 == 0 || LOCAL:1 == 0 || LOCAL:2 == 0 || LOCAL:3 == 0)
