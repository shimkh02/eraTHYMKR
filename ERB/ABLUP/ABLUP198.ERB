;=============================================================================
;촉수사역
;=============================================================================
;基本主人専用能力。通常の能力上昇と違い, 「触手의 구슬」, 촉수에너지, 자금が必要
;また촉수산재물수(触手部屋に取る取り込み含む)が必要な場合もある
;「触手의 구슬」は奴隷の触手中毒LVを上昇させたり生贄に捧げたりすることで入手できる
@ABLUP198
DRAWLINE
SIF ABL:MASTER:98 >= 5
	JUMP ABLUP_MAX

;技巧が必要
PRINTFORML %ABLNAME:12% {ABL:MASTER:98 + 1}LV 이상(현재:{ABL:MASTER:12}LV), 그리고 

PRINTFORM [0] - %PALAMNAME:198%의 구슬×{COST198(0)}, 촉수 에너지 {COST198(1)}τ, 촉수 산제물 수 {COST198(2)} 이상(현재:{FLAG:33}명), 자금 {COST198(3)}원 이상……
CALL SHOW_ISABLUP_COMMON, CAN198(0)

PRINTFORM [1] - %PALAMNAME:198%의 구슬×{COST198(0)}, 촉수 에너지{COST198(1)}τ, 촉수 산제물 수 누계{COST198(2) * 4} 이상(현재:{FLAG:99}명), 자금 {COST198(3)}원 이상……
CALL SHOW_ISABLUP_COMMON, CAN198(1)

PRINTL [100] - 돌아가기
$INPUT_LOOP
INPUT
IF RESULT == 100
	RETURN 0
ELSEIF (RESULT == 0 || RESULT == 1) && CAN198(RESULT) != 0
	PRINTL 조건을 만족하지 못한 상태입니다.
	RESTART
ELSEIF RESULT == 0 || RESULT == 1
	JUEL:MASTER:198 -= COST198(0)
	BASE:MASTER:6 -= COST198(1)
	MONEY -= COST198(3)
ELSE
	GOTO INPUT_LOOP
ENDIF

ABL:MASTER:98 += 1
PRINTFORMW %CALLNAME:MASTER%의 %ABLNAME:98% 레벨이 %조사처리(ABL:MASTER:98,"가")% 되었습니다.

;--------------------------------------------------
;レベルアップに必要な珠計算, レベルアップ予告処理
;--------------------------------------------------
@ISABLUP198
SIF ABL:MASTER:98 >= 5
	RETURN 0

CALLF CALC_COST198, "CALC"

;条件別にＯＫかダメかを記録する
RETURN CAN_ABLUP198("CHECK")

@COST198(ARG)
#FUNCTION
RETURNF CALC_COST198("GET", ARG)

@CALC_COST198(ARGS, ARG)
#FUNCTION
SIF ARGS == "GET"
	RETURNF LOCAL:ARG
;必要な条件の算出
VARSET LOCAL, 0

IF ABL:MASTER:98 == 0
	LOCAL:0 = 500
	LOCAL:1 = 2000
	LOCAL:2 = 0
	LOCAL:3 = 7000
ELSEIF ABL:MASTER:98 == 1
	LOCAL:0 = 2000
	LOCAL:1 = 5000
	LOCAL:2 = 1
	LOCAL:3 = 20000
ELSEIF ABL:MASTER:98 == 2
	LOCAL:0 = 5000
	LOCAL:1 = 12000
	LOCAL:2 = 4
	LOCAL:3 = 80000
ELSEIF ABL:MASTER:98 == 3
	LOCAL:0 = 15000
	LOCAL:1 = 25000
	LOCAL:2 = 20
	LOCAL:3 = 100000
	;一線越えない
	IF TALENT:MASTER:27
		LOCAL:0 *= 2
		LOCAL:1 *= 2
		LOCAL:2 *= 2
	ENDIF
ELSEIF ABL:MASTER:98 == 4
	LOCAL:0 = 60000
	LOCAL:1 = 75000
	LOCAL:2 = 50
	LOCAL:3 = 250000
	;一線越えない
	IF TALENT:MASTER:27
		LOCAL:0 *= 3
		LOCAL:1 *= 3
		LOCAL:2 *= 3
	ENDIF
ENDIF

;臆病
IF TALENT:MASTER:10
	TIMES LOCAL:0 , 2.50
	TIMES LOCAL:1 , 2.50
	TIMES LOCAL:2 , 0.75
ENDIF
;倒錯的
IF TALENT:MASTER:80
	TIMES LOCAL:0 , 0.75
	TIMES LOCAL:1 , 0.75
	TIMES LOCAL:2 , 0.75
ENDIF
;中毒しやすい
IF TALENT:MASTER:84
	TIMES LOCAL:0 , 0.50
	TIMES LOCAL:1 , 0.50
;中毒しにくい
ELSEIF TALENT:MASTER:85
	TIMES LOCAL:0 , 1.50
	TIMES LOCAL:1 , 1.50
ENDIF
;魅力
IF TALENT:MASTER:90
	TIMES LOCAL:0 , 0.75
	TIMES LOCAL:1 , 0.75
ENDIF
;魅惑
IF TALENT:MASTER:91
	TIMES LOCAL:0 , 0.50
	TIMES LOCAL:1 , 0.50
ENDIF
;謎の魅力
IF TALENT:MASTER:92
	TIMES LOCAL:0 , 0.75
	TIMES LOCAL:1 , 0.75
ENDIF
;狂気
IF TALENT:MASTER:158
	TIMES LOCAL:0 , 0.25
	TIMES LOCAL:1 , 0.25
	TIMES LOCAL:2 , 1.50
ENDIF
;禁断の知識＋調合知識＋妖精知識
IF TALENT:MASTER:55 && TALENT:MASTER:56 && TALENT:MASTER:57
	TIMES LOCAL:0 , 0.50
	TIMES LOCAL:1 , 0.50
ENDIF

RETURNF 0

@CAN198(ARG)
#FUNCTION
RETURNF CAN_ABLUP198("GET", ARG)

@CAN_ABLUP198(ARGS, ARG)
#FUNCTION
SIF ARGS == "GET"
	RETURNF LOCAL:ARG
VARSET LOCAL, 0
;技巧が必要
IF ABL:MASTER:12 < ABL:MASTER:98 + 1
	LOCAL:0 |= 2
	LOCAL:1 |= 2
ENDIF

;触手의 구슬で上げる
IF JUEL:MASTER:198 < COST198(0)
	LOCAL:0 |= 1
	LOCAL:1 |= 1
ENDIF

;촉수에너지が必要
IF BASE:MASTER:6 < COST198(1)
	LOCAL:0 |= 16
	LOCAL:1 |= 16
ENDIF
;触手生贄が必要
SIF FLAG:33 < COST198(2)
	LOCAL:0 |= 32
;触手生贄(累計)が必要
SIF FLAG:99 < COST198(2) * 4
	LOCAL:1 |= 32
;お金が必要
IF MONEY < COST198(3)
	LOCAL:0 |= 64
	LOCAL:1 |= 64
ENDIF

RETURNF (LOCAL:0 == 0 || LOCAL:1 == 0)
