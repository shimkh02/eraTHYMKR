;=============================================================================
;가창 기능
;=============================================================================
@ABLUP72
SIF (FLAG:12 & 16) == 0 || (ITEM:52 == 0 && NOITEM == 0)
	RETURN 0
DRAWLINE
SIF ABL:72 >= 5
	JUMP ABLUP_MAX

PRINTFORM [0] - %PALAMNAME:11%의 구슬×{COST72(0)}, %EXPNAME:73% {COST72(1)} 이상……
CALL SHOW_ISABLUP_COMMON, CAN72(0)

PRINTL [100] - 돌아간다
$INPUT_LOOP
INPUT
IF RESULT == 100
	RETURN 0
ELSEIF RESULT == 0 && CAN72(0) != 0
	PRINTL 조건을 만족하고 있지 않습니다.
	RESTART
ELSEIF RESULT == 0
	JUEL:11 -= COST72(0)
ELSE
	GOTO INPUT_LOOP
ENDIF

ABL:72 += 1
PRINTFORMW %ABLNAME:72%의 레벨이 %조사처리(ABL:72,"가")% 되었습니다.

;--------------------------------------------------
;レベルアップに必要な珠計算、レベルアップ予告処理
;--------------------------------------------------
@ISABLUP72
SIF ABL:72 >= 5
	RETURN 0

CALLF CALC_COST72, "CALC"

;条件別にＯＫかダメかを記録する
RETURN CAN_ABLUP72("CHECK")

@COST72(ARG)
#FUNCTION
RETURNF CALC_COST72("GET", ARG)

@CALC_COST72(ARGS, ARG)
#FUNCTION
SIF ARGS == "GET"
	RETURNF LOCAL:ARG
;必要な条件の算出
VARSET LOCAL, 0

IF ABL:72 == 0
	LOCAL:0 = 50
	LOCAL:1 = 10
ELSEIF ABL:72 == 1
	LOCAL:0 = (EXP:73 >= EXPLV:3) ? 35 # 100
	LOCAL:1 = 25
ELSEIF ABL:72 == 2
	LOCAL:0 = (EXP:73 >= EXPLV:4) ? 200 # 250
	LOCAL:1 = 40
ELSEIF ABL:72 == 3
	LOCAL:0 = (EXP:73 >= EXPLV:5) ? 1700 # 2000
	LOCAL:1 = 80
ELSEIF ABL:72 == 4
	LOCAL:0 = (EXP:73 >= EXPLV:5) ? 15000 # 30000
	LOCAL:1 = 100
ENDIF

;露出癖が高いと必要珠数と経験が軽減される
IF ABL:14 == 1
	TIMES LOCAL:0 , 0.90
	TIMES LOCAL:1 , 1.00
ELSEIF ABL:14 == 2
	TIMES LOCAL:0 , 0.80
	TIMES LOCAL:1 , 0.90
ELSEIF ABL:14 == 3
	TIMES LOCAL:0 , 0.75
	TIMES LOCAL:1 , 0.80
ELSEIF ABL:14 == 4
	TIMES LOCAL:0 , 0.70
	TIMES LOCAL:1 , 0.70
ELSEIF ABL:14 >= 5
	TIMES LOCAL:0 , 0.60
	TIMES LOCAL:1 , 0.66
ENDIF

;臆病
IF TALENT:10
	TIMES LOCAL:0 , 1.10
	TIMES LOCAL:1 , 1.20
ENDIF
;目立ちたがり
IF TALENT:28
	TIMES LOCAL:0 , 0.90
	TIMES LOCAL:1 , 0.80
ENDIF
;習得早い
IF TALENT:50
	TIMES LOCAL:0 , 0.80
	TIMES LOCAL:1 , 0.75
;習得遅い
ELSEIF TALENT:51
	TIMES LOCAL:0 , 1.20
	TIMES LOCAL:1 , 1.10
ENDIF

;音感
IF TALENT:191
	TIMES LOCAL:0 , 0.90
	TIMES LOCAL:1 , 0.90
;音痴
ELSEIF TALENT:190
	TIMES LOCAL:0 , 1.10
	TIMES LOCAL:1 , 1.10
ENDIF
;喘息、求聞持
IF TALENT:47 || TALENT:216
	TIMES LOCAL:0 , 1.20
	TIMES LOCAL:1 , 1.20
ENDIF

RETURNF 0

@CAN72(ARG)
#FUNCTION
RETURNF CAN_ABLUP72("GET", ARG)

@CAN_ABLUP72(ARGS, ARG)
#FUNCTION
SIF ARGS == "GET"
	RETURNF LOCAL:ARG
VARSET LOCAL, 0
;習得의 구슬で上げる
SIF JUEL:11 < COST72(0)
	LOCAL:0 |= 1
;歌唱経験が必要
SIF EXP:73 < COST72(1)
	LOCAL:0 |= 2

RETURNF (LOCAL:0 == 0)
