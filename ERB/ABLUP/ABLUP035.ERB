;=============================================================================
;촉수중독
;=============================================================================
@ABLUP35
DRAWLINE
IF (FLAG:15 & 4096) == 0
	RETURN 0
ELSEIF ABL:35 >= 5
	JUMP ABLUP_MAX
ENDIF

;上げるときは異常経験必要（素質：[中毒しやすい][淫乱]なら無視できる）
SIF COST35(4)
	PRINTFORM %EXPNAME:50% {COST35(4)} 이상, 그리고 
;欲望が必要
PRINTFORML %ABLNAME:11% {ABL:35 + 1}LV 이상, 그리고 

PRINTFORM [0] - %PALAMNAME:31%의 구슬×{COST35(0)}, %PALAMNAME:13%의 구슬×{COST35(1)}, %EXPNAME:53% {COST35(2)} 이상……
CALL SHOW_ISABLUP_COMMON, CAN35(0)

PRINTFORM [1] - %PALAMNAME:31%의 구슬×{COST35(0)}, %PALAMNAME:17%의 구슬×{COST35(3)}, %EXPNAME:53% {COST35(2)} 이상……
CALL SHOW_ISABLUP_COMMON, CAN35(1)

PRINTL [100] - 돌아간다
$INPUT_LOOP
INPUT
IF RESULT == 100
	RETURN 0
ELSEIF (RESULT == 0 || RESULT == 1) && CAN35(RESULT) != 0
	PRINTL 조건을 만족하고 있지 않습니다.
	RESTART
ELSEIF RESULT == 0
	JUEL:31 -= COST35(0)
	JUEL:13 -= COST35(1)
ELSEIF RESULT == 1
	JUEL:31 -= COST35(0)
	JUEL:17 -= COST35(3)
ELSE
	GOTO INPUT_LOOP
ENDIF

ABL:35 += 1
PRINTFORMW %ABLNAME:35%의 레벨이 %조사처리(ABL:35,"가")% 되었습니다.

;奴隷の触手中毒を上昇させると主人に一定量触手의 구슬が入る様になる
IF ABL:35 == 1
	LOCAL:0 = 100
ELSEIF ABL:35 == 2
	LOCAL:0 = 500
ELSEIF ABL:35 == 3
	LOCAL:0 = 1000
ELSEIF ABL:35 == 4
	LOCAL:0 = 3000
ELSEIF ABL:35 >= 5
	LOCAL:0 = 6000
ENDIF
CALL COMMON_UP_JUEL, MASTER, 198, (LOCAL:0), 1

;--------------------------------------------------
;レベルアップに必要な珠計算, レベルアップ予告処理
;--------------------------------------------------
@ISABLUP35
SIF ABL:35 >= 5
	RETURN 0

CALLF CALC_COST35, "CALC"

;条件別にＯＫかダメかを記録する
RETURN CAN_ABLUP35("CHECK")

@COST35(ARG)
#FUNCTION
RETURNF CALC_COST35("GET", ARG)

@CALC_COST35(ARGS, ARG)
#FUNCTION
SIF ARGS == "GET"
	RETURNF LOCAL:ARG
;必要な条件の算出
VARSET LOCAL, 0

IF ABL:35 == 0
	LOCAL:0 = 4000
	LOCAL:1 = 15000
	LOCAL:2 = 100
ELSEIF ABL:35 == 1
	LOCAL:0 = 10000
	LOCAL:1 = 30000
	LOCAL:2 = 250
ELSEIF ABL:35 == 2
	LOCAL:0 = 22000
	LOCAL:1 = 70000
	LOCAL:2 = 750
ELSEIF ABL:35 == 3
	LOCAL:0 = 50000
	LOCAL:1 = 150000
	LOCAL:2 = 1500
	;一線越えない
	IF TALENT:27
		TIMES LOCAL:0 , 2.00
		TIMES LOCAL:1 , 2.00
		TIMES LOCAL:2 , 2.00
	ENDIF
ELSEIF ABL:35 == 4
	LOCAL:0 = 80000
	LOCAL:1 = 360000
	LOCAL:2 = 3000
	;一線越えない
	IF TALENT:27
		TIMES LOCAL:0 , 3.00
		TIMES LOCAL:1 , 3.00
		TIMES LOCAL:2 , 3.00
	ENDIF
ENDIF

;自制心
IF TALENT:20
	TIMES LOCAL:0 , 2.50
	TIMES LOCAL:1 , 2.50
	TIMES LOCAL:2 , 1.50
ENDIF
;禁断の知識
IF TALENT:56
	TIMES LOCAL:0 , 0.80
	TIMES LOCAL:1 , 0.80
	TIMES LOCAL:2 , 0.80
ENDIF
;快感に素直
IF TALENT:70
	TIMES LOCAL:0 , 0.75
	TIMES LOCAL:1 , 0.75
;快感の否定
ELSEIF TALENT:71
	TIMES LOCAL:0 , 1.75
	TIMES LOCAL:1 , 1.75
ENDIF
;倒錯的
IF TALENT:80
	TIMES LOCAL:0 , 0.75
	TIMES LOCAL:1 , 0.75
	TIMES LOCAL:2 , 0.75
ENDIF
;中毒しやすい
IF TALENT:84
	TIMES LOCAL:0 , 0.50
	TIMES LOCAL:1 , 0.50
	TIMES LOCAL:2 , 0.50
;中毒しにくい
ELSEIF TALENT:85
	TIMES LOCAL:0 , 1.50
	TIMES LOCAL:1 , 1.50
	TIMES LOCAL:2 , 1.50
ENDIF
;[中毒扇動]有効
IF TFLAG:995 == 1
	TIMES LOCAL:0 , 0.80
	TIMES LOCAL:1 , 0.80
	TIMES LOCAL:2 , 0.80
ENDIF
;懐卵
IF TALENT:145
	TIMES LOCAL:0 , 0.90
	TIMES LOCAL:1 , 0.90
	TIMES LOCAL:2 , 0.90
ENDIF
;寄生
IF TALENT:146
	TIMES LOCAL:0 , 0.80
	TIMES LOCAL:1 , 0.80
	TIMES LOCAL:2 , 0.80
ENDIF
;相愛・隷属(心が主人に向いているので触手になびきにくい)
IF TALENT:153 || TALENT:162
	TIMES LOCAL:0 , 1.80
	TIMES LOCAL:1 , 1.80
	TIMES LOCAL:2 , 1.50
ENDIF
;狂気
IF TALENT:158
	TIMES LOCAL:0 , 0.50
	TIMES LOCAL:1 , 0.50
	TIMES LOCAL:2 , 0.50
ENDIF

;触手インプラント
IF CFLAG:41
	TIMES LOCAL:0 , 0.75
	TIMES LOCAL:1 , 0.75
	TIMES LOCAL:2 , 0.75
ENDIF

LOCAL:3 = LOCAL:1 / 20

;上げるときは異常経験必要（素質：[中毒しやすい][淫乱]なら無視できる）
IF TALENT:84 == 0 && TALENT:170 == 0
	IF ABL:35 <= 2
		LOCAL:4 = ABL:35 + 1
	ELSEIF ABL:35 == 3
		LOCAL:4 = 5
	ELSE
		LOCAL:4 = 10
	ENDIF
ENDIF

RETURNF 0

@CAN35(ARG)
#FUNCTION
RETURNF CAN_ABLUP35("GET", ARG)

@CAN_ABLUP35(ARGS, ARG)
#FUNCTION
SIF ARGS == "GET"
	RETURNF LOCAL:ARG
VARSET LOCAL, 0
;異常経験が必要な場合
IF EXP:50 < COST35(4)
	LOCAL:0 |= 2
	LOCAL:1 |= 2
ENDIF

;触手経験が必要
IF EXP:53 < COST35(2)
	LOCAL:0 |= 2
	LOCAL:1 |= 2
ENDIF

;欲望が必要
IF ABL:11 < ABL:35 + 1
	LOCAL:0 |= 4
	LOCAL:1 |= 4
ENDIF

;侵蝕의 구슬で上げる
IF JUEL:31 < COST35(0)
	LOCAL:0 |= 1
	LOCAL:1 |= 1
ENDIF
;欲情의 구슬で上げる
SIF JUEL:13 < COST35(1)
	LOCAL:0 |= 1
;恐怖의 구슬で上げる
SIF JUEL:17 < COST35(3)
	LOCAL:1 |= 1

RETURNF (LOCAL:0 == 0 || LOCAL:1 == 0)
