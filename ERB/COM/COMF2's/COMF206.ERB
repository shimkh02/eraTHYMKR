;=============================================================================
;젖 스팽킹
;=============================================================================
;………………………………………………
;실행 판정
;………………………………………………
@COM_ABLE206
;개별 설정 커맨드 필터
SIF DITEMTYPE:999:206 > 0
	RETURN 0
;Ｂ계 확장 커맨드를 유효로 하지 않으면 불가
SIF (FLAG:15 & 8) == 0
	RETURN 0
;남자라면 안 됨
SIF TALENT:120
	RETURN 0
;순종5미만 · 레즈끼4미만의 ASSI일 경우, 조교 대상이 동성이라면 실행할 수 없다(새드거나 새드끼Lv3이상, 또는 망신이나 복종이 있다면 ＯＫ)
IF ASSIPLAY
	SIF (ABL:ASSI:10 < 5 || (TALENT:120 == 0 && TALENT:ASSI:120 == 0 && ABL:ASSI:22 < 4)) && TALENT:ASSI:81 == 0 && ABL:ASSI:20 < 3 && TALENT:ASSI:151 == 0 && TALENT:ASSI:160 == 0
		RETURN 0
ENDIF
;훈도시와 알몸 와이셔츠 외의 코스프레 중에는 안 됨
SIF TEQUIP:59 > 2 && TEQUIP:59 != 11 && TEQUIP:59 != 17
	RETURN 0
RETURN 1

;--------------------------------------------------
;커맨드 본체
;--------------------------------------------------
@COM206
PRINTL 젖 스팽킹
TSTR:0 = 젖 스팽킹
CALL TRAIN_MESSAGE_COM206

DOWNBASE:0 += 70
DOWNBASE:1 += 45

SOURCE:12 = 200
SOURCE:31 = 500

;ABL:B감각을 본다
IF ABL:3 == 0
	SOURCE:3 = 10
ELSEIF ABL:3 == 1
	SOURCE:3 = 40
ELSEIF ABL:3 == 2
	SOURCE:3 = 300
ELSEIF ABL:3 == 3
	SOURCE:3 = 800
ELSEIF ABL:3 == 4
	SOURCE:3 = 1700
ELSE
	SOURCE:3 = 2500
ENDIF

;모유 체질 때는, ABL:분유중독을 본다
IF TALENT:134
	IF ABL:34 == 0
		SOURCE:24 += 0
		TIMES SOURCE:3 , 1.00
		TIMES SOURCE:31, 1.00
	ELSEIF ABL:34 == 1
		SOURCE:24 += 50
		TIMES SOURCE:3 , 1.05
		TIMES SOURCE:31, 1.10
	ELSEIF ABL:34 == 2
		SOURCE:24 += 200
		TIMES SOURCE:3 , 1.10
		TIMES SOURCE:31, 1.20
	ELSEIF ABL:34 == 3
		SOURCE:24 += 600
		TIMES SOURCE:3 , 1.20
		TIMES SOURCE:31, 1.30
	ELSEIF ABL:34 == 4
		SOURCE:24 += 1200
		TIMES SOURCE:3 , 1.25
		TIMES SOURCE:31, 1.80
	ELSE
		SOURCE:24 += 2000
		TIMES SOURCE:3 , 1.50
		TIMES SOURCE:31, 2.50
	ENDIF
ENDIF

;PALAM:고통을 본다
IF PALAM:16 < PALAMLV:1
	SOURCE:20 = 250
ELSEIF PALAM:16 < PALAMLV:2
	SOURCE:20 = 400
ELSEIF PALAM:16 < PALAMLV:3
	SOURCE:20 = 800
ELSEIF PALAM:16 < PALAMLV:4
	SOURCE:20 = 1050
ELSE
	SOURCE:20 = 1200
ENDIF

;노예의 B⇔조교자의 손가락의 불결이 이동
STAIN:5 |= STAIN:PLAYER:1
STAIN:PLAYER:1 |= STAIN:5

SIF TALENT:120 == 0 && TALENT:PLAYER:120 == 0
	TCVAR:12 += 2

;주인 경험 플래그
SIF ASSIPLAY == 0 && ABL:21 >= 1
	TFLAG:50 += 1
;SM교육 경험 플래그
SIF ASSIPLAY
	TFLAG:55 = 1

RETURN 1

;--------------------------------------------------
;조교시 메시지
;--------------------------------------------------
@TRAIN_MESSAGE_COM206
CALL KOJO_TRAIN_MESSAGE_COM
SIF FLAG:10 == 0 || CFLAG:99 == 1 || RESULT != 0
	RETURN 0

PRINTL
PRINTFORM %조사선택(CALLNAME:PLAYER, "는")% %CALLNAME:TARGET%의 
IF (TALENT:111 || TALENT:110)
	IF TALENT:111
		PRINT 간신히 부풀어 오른 게 느껴지는 정도의 
	ELSEIF TALENT:110
		PRINT 부푼 곳 하나 없이 평평한 
	ENDIF
	PRINTW 가슴의 정점을 강하게 손톱으로 튕겼다….
ELSE
	IF TALENT:112
		PRINT 풍만한 유방을 흔들어, 
	ELSEIF TALENT:113
		PRINT 너무나도 풍부한 유방을 흔들리게 해 
	ELSE
		PRINT 유방을 
	ENDIF
	PRINTW 튕기듯이 손바닥으로 때렸다….
ENDIF
