;≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡
;커맨드 실행 판정에서의 범용 처리
;
;≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡
;=============================================================================
;성별, 체격, 체질에 기인하는 실행 판정
;=============================================================================
;--------------------------------------------------
;대상의 성별이 ♂인가 판정
;--------------------------------------------------
@COM_ABLE_CHECK_MALE(ARG)
#FUNCTION

RETURNF (TALENT:ARG:120) ? 1 # 0

;--------------------------------------------------
;대상의 성별이 ♀인가 판정
;--------------------------------------------------
@COM_ABLE_CHECK_FEMALE(ARG)
#FUNCTION

RETURNF (TALENT:ARG:120) ? 0 # 1

;-------------------------------------------------
;대상의 [처녀]의 판정
;-------------------------------------------------
;ARG＝조교 대상, ARG:1＝조교자
@COM_ABLE_VIRGIN(ARG, ARG:1)
#FUNCTION

;조교자가 《새드끼》가 LV5 이상 또는 [광기]면 ＯＫ
SIF ABL:(ARG:1):20 >= 5 || TALENT:(ARG:1):158
	RETURNF 1
;조교자가 주인 이외고 《순종》이 LV5 이상이거나 [바이]면 ＯＫ
SIF ARG:1 != MASTER && (ABL:(ARG:1):10 > 4 || TALENT:(ARG:1):44)
	RETURNF 1
;조교자가 주인이고 대상이 [연모][음란][붕괴] 중 어느 하나라면 ＯＫ
SIF ARG:1 == MASTER && (TALENT:ARG:6 || TALENT:ARG:7 || TALENT:ARG:8)
	RETURNF 1
;조교자가 주인에다 조교 대상의 《운활》이 충분하거나 《순종》이 LV3 이상이면 ＯＫ
SIF ARG:1 == MASTER && (PALAM:ARG:10 > PALAMLV:2 || ABL:ARG:10 >= 3)
	RETURNF 1
;조교자가 주인이고 대상의 《순종》이 LV3 미만일 때, 주인의 《새드끼》가 LV3 아싱이며 대상을 긴박하고 있으면 ＯＫ
SIF ARG:1 == MASTER && ABL:ARG:10 < 3 && ABL:(ARG:1):20 >= 3 && TEQUIP:ARG:40
	RETURNF 1

RETURNF 0

;=============================================================================
;그 외의 현상에 기인하는 실행 판정
;=============================================================================
;-------------------------------------------------
;윤활 부족의 체크
;-------------------------------------------------
;ARG＝조교 대상, ARG:1＝조교자
@COM_ABLE_SLICK(ARG, ARG:1)
#FUNCTION

;《윤활》이 충분하다
SIF PALAM:ARG:10 > PALAMLV:2
	RETURNF 1
;조교자가 주인이면 ＯＫ
SIF ARG:1 == MASTER
	RETURNF 1
;조교자의 《순종》이 LV3 이상이거나 《새드끼》가 LV3 이상 또는 [새드] 또는 [광기]면 ＯＫ
SIF ABL:(ARG:1):10 > 2 || ABL:(ARG:1):20 > 2 || TALENT:(ARG:1):81 || TALENT:(ARG:1):158
	RETURNF 1
;조교자와 대상이 동성이고 조교자의 《레즈끼》가 LV3 이상이거나 《BL끼》가 LV3 이상이면 ＯＫ
SIF !(TALENT:ARG:120 ^^ TALENT:(ARG:1):120) && (ABL:(ARG:1):22 > 2 || ABL:(ARG:1):23 > 2)
	RETURNF 1

RETURNF 0

;--------------------------------------------------
;특정 부위의 불결 체크
;--------------------------------------------------
;ARG＝대상, ARG:1＝대상의 부위, ARG:2＝판정에서 튕기는 불결의 종류(비트 연산)
@COM_ABLE_STAIN(ARG, ARG:1, ARG:2)
#FUNCTION

;애액
SIF STAIN:ARG:(ARG:1) & 1 && (ARG:2 & 1) == 0
	RETURNF 0
;페니스
SIF STAIN:ARG:(ARG:1) & 2 && (ARG:2 & 2) == 0
	RETURNF 0
;정액
SIF STAIN:ARG:(ARG:1) & 4 && (ARG:2 & 4) == 0
	RETURNF 0
;애널
SIF STAIN:ARG:(ARG:1) & 8 && (ARG:2 & 8) == 0
	RETURNF 0
;모유
SIF STAIN:ARG:(ARG:1) & 16 && (ARG:2 & 16) == 0
	RETURNF 0
;파과의 피
SIF STAIN:ARG:(ARG:1) & 32 && (ARG:2 & 32) == 0
	RETURNF 0
;점액
SIF STAIN:ARG:(ARG:1) & 64 && (ARG:2 & 64) == 0
	RETURNF 0

RETURNF 1

;-------------------------------------------------
;하드한 커맨드 사용을 위한 판정
;-------------------------------------------------
;ARG＝조교자、ARG:1＝커맨드의 하드함 수준
@COM_ABLE_HARDTRAINING(ARG, ARG:1)
#FUNCTION

IF ARG:1 == 2
	;조수 조교에서 [복종][새드][왕Ｓ][광기] 전부 미취득일 때, 《순종》 5 미만에다 《새드끼》 5 미만이면 실행할 수 없다
	SIF ASSIPLAY && ABL:ARG:10 < 5 && ABL:ARG:20 < 5 && TALENT:ARG:81 == 0 && TALENT:ARG:158 == 0 && TALENT:ARG:160 == 0 && TALENT:ARG:177 == 0
		RETURNF 0
ELSEIF ARG:1 == 1
	;조수 조교에서 《윤활》 부족일 때, [망신][복종][새드][왕Ｓ][광기] 전부 미취득에다 《순종》 4 미만에다 《새드끼》 4 미만이면 실행할 수 없다
	SIF ASSIPLAY && PALAM:10 < PALAMLV:2 && ABL:ARG:10 < 4 && ABL:ARG:20 < 4 && TALENT:ARG:81 == 0 && TALENT:ARG:151 == 0 && TALENT:ARG:158 == 0 && TALENT:ARG:160 == 0 && TALENT:ARG:177 == 0
		RETURNF 0
ELSE
	;조수 조교에서 조교자와 대상이 동성이고 조교자의 《레즈끼》(《BL끼》)가 LV4 미만에다 [망신][복종] 모두 미취득일 때, 《순종》 3 미만에다 《새드끼》 3 미만이면 실행할 수 없다
	SIF ASSIPLAY && !(TALENT:ARG:120 ^^ TALENT:120) && ABL:ARG:22 < 4 && ABL:ARG:23 < 4 && ABL:ARG:10 < 4 && ABL:ARG:20 < 4 && TALENT:ARG:151 == 0 && TALENT:ARG:160 == 0
		RETURNF 0
ENDIF

RETURNF 1

;-------------------------------------------------
;하드한 커맨드 사용당하기 위한 판정
;-------------------------------------------------
;ARG＝조교자, ARG:1＝커맨드의 하드함 수준
@COM_ABLE_GAIN_HARDTRAINING(ARG, ARG:1)
#FUNCTION

IF ARG:1 == 2
	;조수 조교에서 [복종][마조][왕Ｍ][광기] 전부 미취득일 때, 《순종》 5 미만에다 《마조끼》 5 미만이면 실행할 수 없다
	SIF ASSIPLAY && ABL:ARG:10 < 5 && ABL:ARG:21 < 5 && TALENT:ARG:82 == 0 && TALENT:ARG:158 == 0 && TALENT:ARG:160 == 0 && TALENT:ARG:178 == 0
		RETURNF 0
ELSEIF ARG:1 == 1
	;조수 조교에서 [망신][복종][마조][왕Ｍ][광기] 전부 미취득일 때, 《순종》 4 미만에다 《마조끼》 4 미만이면 실행할 수 없다
	SIF ASSIPLAY && ABL:ARG:10 < 4 && ABL:ARG:21 < 4 && TALENT:ARG:82 == 0 && TALENT:ARG:151 == 0 && TALENT:ARG:158 == 0 && TALENT:ARG:160 == 0 && TALENT:ARG:178 == 0
		RETURNF 0
ELSE
	;조수 조교에서 [망신][복종][마조][왕Ｍ][광기] 전부 미취득일 때, 《순종》 3 미만에다 《마조끼》 3 미만이면 실행할 수 없다
	SIF ASSIPLAY && ABL:ARG:10 < 3 && ABL:ARG:21 < 3 && TALENT:ARG:82 == 0 && TALENT:ARG:151 == 0 && TALENT:ARG:158 == 0 && TALENT:ARG:160 == 0 && TALENT:ARG:178 == 0
		RETURNF 0
ENDIF

RETURNF 1
