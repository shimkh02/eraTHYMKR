;=============================================================================
;69
;=============================================================================
;………………………………………………
;실행 판정
;………………………………………………
;더미 함수(커맨드를 직접 선택할 수는 없음)
@COM_ABLE602
RETURN 0

@EXCOM_ABLE602
;性器が汚れているとMASTERの場合シックスナインはできない(조교자가 汚れ無視か愛液, 母乳のよごれならＯＫ)
SIF STAIN:3 >= 2 && (STAIN:3 != 16 && STAIN:3 != 17) && ASSIPLAY == 0 && TALENT:MASTER:64 == 0
	RETURN 0
;性器が汚れており, 汚臭敏感で순종4미만의 ASSI일 경우シックスナインはできない(조교자가 汚れ無視か愛液, 母乳のよごれならＯＫ)
IF STAIN:3 >= 2 && (STAIN:3 != 16 && STAIN:3 != 17) && ASSIPLAY
	SIF ABL:ASSI:10 <= 3 && TALENT:ASSI:62 && TALENT:ASSI:64 == 0
		RETURN 0
ENDIF
;욕망3이하 · 레즈끼3이하의 ASSI일 경우シックスナインはできない(소악마, 相性良好ならＯＫ)
IF ASSIPLAY
	SIF (ABL:ASSI:11 < 4 || ((TALENT:ASSI:120 && ABL:ASSI:23 < 4) || (TALENT:ASSI:120 == 0 && ABL:ASSI:22 < 4))) && TALENT:ASSI:93 == 0 && RELATION:(NO:PLAYER) < 150
		RETURN 0
ELSE
;조교자가 助手でないなら순종3이상, 奉仕精神3이상 필요
	SIF ABL:10 < 3 || ABL:13 < 3
		RETURN 0
ENDIF
;주인이 [금단의 지식]持ちじゃないとき, 体格差が2段階以内か조교자가 [금단의 지식]持ちじゃないとダメ
SIF TALENT:MASTER:56 == 0 && COMMON_BODYSIZE_CHECK(PLAYER, TARGET) == 0
	RETURN 0
;조교자가 고양이혀일 경우 고양이 펠라 패치 유효가 아니면 불가
SIF TALENT:PLAYER:54 && (FLAG:14 & 16) == 0
	RETURN 0
;対象が猫舌だと猫フェラパッチ有効でないとダメ
SIF TALENT:54 && (FLAG:14 & 16) == 0
	RETURN 0
;조교자가 고양이혀일 경우 조교자에게 기교4 필요
SIF TALENT:PLAYER:54 && ABL:PLAYER:12 < 4
	RETURN 0
;猫舌だと기교5 · 奉仕精神4 · 순종3が필요
SIF TALENT:54 && (ABL:12 < 5 || ABL:13 < 4 || ABL:10 < 3)
	RETURN 0
;Ｃ계 장착구 사용 중에는 불가
SIF TEQUIP:30 || TEQUIP:31
	RETURN 0
;수면 중에는 불가
SIF EQUIP:14
	RETURN 0
;실신 중에는 불가
SIF TFLAG:899 > 0
	RETURN 0
;쿠스코 삽입 중에는 불가
SIF TEQUIP:21
	RETURN 0
;밧줄 사용 중에는 불가
SIF TEQUIP:40
	RETURN 0
;볼 개그 사용 중에는 불가
SIF TEQUIP:42
	RETURN 0
;승마 중에는 불가
SIF TEQUIP:43
	RETURN 0
;욕실 플레이 중에는 플레이 매트가 없으면 불가
SIF TEQUIP:53 && ITEM:42 == 0 && NOITEM == 0
	RETURN 0
;샤워 중에는 불가
SIF TEQUIP:54
	RETURN 0
;○○목욕탕 입욕 중에는 불가
SIF TEQUIP:55
	RETURN 0
;기저귀 플레이 중에는 불가
SIF TEQUIP:58
	RETURN 0
;조수가 훈도시 착용 중일 경우 기교4이상 필수
SIF ASSI >= 0 && ASSIPLAY && TEQUIP:ASSI:59 == 17 && ABL:12 < 4
	RETURN 0
;훈도시 착용 중일 경우 조교자의 기교4이상 필수
SIF TEQUIP:59 == 17 && ABL:PLAYER:12 < 4
	RETURN 0
;나체정식 중에는 불가
SIF TEQUIP:60
	RETURN 0
;시간정지 중에는 무리
SIF TEQUIP:71
	RETURN 0
;촉수 조교 중에는 불가
SIF TEQUIP:90
	RETURN 0
RETURN 1

;--------------------------------------------------
;커맨드 본체
;--------------------------------------------------
@COM602
PRINTL 식스나인

;-------------------------------------------------
;실행할 수 있을까의 판정
;-------------------------------------------------
IF TFLAG:699 == 0
	CALL COM_ORDER, 602
	;실행할 수 없다
	SIF RESULT == 0
		RETURN 0
ENDIF

;-------------------------------------------------
;실행 결정
;-------------------------------------------------
SELECTCOM = 602
TSTR:0 = 69
CALL TRAIN_MESSAGE_COM602

;-------------------------------------------------
;사정 게이지 체크(후타나리·남자의 경우)
;-------------------------------------------------
LOCAL = 0

;ABL:기교를 본다
IF ABL:12 == 0
	LOCAL = 1200
ELSEIF ABL:12 == 1
	LOCAL = 1700
ELSEIF ABL:12 == 2
	LOCAL = 2300
ELSEIF ABL:12 == 3
	LOCAL = 3000
ELSEIF ABL:12 == 4
	LOCAL = 3600
ELSE
	LOCAL = 4200
ENDIF

;순종을 본다
LOCAL = EJACULATIONCHECK_ABL_10_1(TARGET, LOCAL)
;봉사 정신을 본다
LOCAL = EJACULATIONCHECK_ABL_13_1(TARGET, LOCAL)
;정액 중독을 본다
LOCAL = EJACULATIONCHECK_ABL_31_1(TARGET, LOCAL)

;TALENT:혀사용을 본다
SIF TALENT:52
	TIMES LOCAL , 2.00
;TALENT:고양이혀을 본다
SIF TALENT:54
	TIMES LOCAL , 0.70

;조교자의 C감각을 본다
LOCAL = EJACULATIONCHECK_ABL_0_1(PLAYER, LOCAL)

SIF TALENT:PLAYER:120 || TALENT:PLAYER:121
	BASE:PLAYER:2 += LOCAL

;-------------------------------------------------
;소스의 계산
;-------------------------------------------------
DOWNBASE:0 += 20
DOWNBASE:1 += 160

SOURCE:19 = 1000
SOURCE:12 = 1400
SOURCE:22 = 1300
SOURCE:31 = 800

;위 쪽으로 계산한 더러운 데이터
SOURCE:30 = TFLAG:242*80 + 50

;ABL:C감각을 본다
IF ABL:0 == 0
	SOURCE:0 = 40
ELSEIF ABL:0 == 1
	SOURCE:0 = 160
ELSEIF ABL:0 == 2
	SOURCE:0 = 700
ELSEIF ABL:0 == 3
	SOURCE:0 = 1500
ELSEIF ABL:0 == 4
	SOURCE:0 = 2400
ELSE
	SOURCE:0 = 3300
ENDIF

;조교자의 TALENT:혀사용을 본다
IF TALENT:PLAYER:52
	TIMES SOURCE:0 , 2.00
	SOURCE:17 += SOURCE:0/20
ENDIF

;ABL:봉사 정신을 본다
IF ABL:13 == 0
	SOURCE:15 = 620
	SOURCE:16 = 150
	TIMES SOURCE:30 , 4.00
ELSEIF ABL:13 == 1
	SOURCE:15 = 700
	SOURCE:16 = 300
	TIMES SOURCE:30 , 2.50
ELSEIF ABL:13 == 2
	SOURCE:15 = 820
	SOURCE:16 = 600
	TIMES SOURCE:30 , 1.50
ELSEIF ABL:13 == 3
	SOURCE:15 = 940
	SOURCE:16 = 900
	TIMES SOURCE:30 , 1.00
ELSEIF ABL:13 == 4
	SOURCE:15 = 1100
	SOURCE:16 = 1500
	TIMES SOURCE:30 , 0.50
ELSE
	SOURCE:15 = 1260
	SOURCE:16 = 2200
	TIMES SOURCE:30 , 0.10
ENDIF

;ABL:기교를 본다
IF ABL:12 == 0
	TIMES SOURCE:15 , 0.50
	TIMES SOURCE:16 , 0.50
ELSEIF ABL:12 == 1
	TIMES SOURCE:15 , 0.80
	TIMES SOURCE:16 , 0.80
ELSEIF ABL:12 == 2
	TIMES SOURCE:15 , 1.00
	TIMES SOURCE:16 , 1.00
ELSEIF ABL:12 == 3
	TIMES SOURCE:15 , 1.50
	TIMES SOURCE:16 , 1.50
ELSEIF ABL:12 == 4
	TIMES SOURCE:15 , 2.50
	TIMES SOURCE:16 , 2.50
ELSE
	TIMES SOURCE:15 , 4.00
	TIMES SOURCE:16 , 4.00
ENDIF

;조교자의 ABL:C감각을 본다
IF ABL:PLAYER:0 == 0
	TIMES SOURCE:15 , 0.80
	TIMES SOURCE:16 , 0.50
ELSEIF ABL:PLAYER:0 == 1
	TIMES SOURCE:15 , 0.90
	TIMES SOURCE:16 , 0.70
ELSEIF ABL:PLAYER:0 == 2
	TIMES SOURCE:15 , 1.00
	TIMES SOURCE:16 , 1.00
ELSEIF ABL:PLAYER:0 == 3
	TIMES SOURCE:15 , 1.10
	TIMES SOURCE:16 , 1.20
ELSEIF ABL:PLAYER:0 == 4
	TIMES SOURCE:15 , 1.20
	TIMES SOURCE:16 , 1.40
ELSE
	TIMES SOURCE:15 , 1.30
	TIMES SOURCE:16 , 1.70
ENDIF

IF PREVCOM == 180
	;ABL:A감각을 본다
	IF ABL:2 == 0
		SOURCE:2 = 5
	ELSEIF ABL:2 == 1
		SOURCE:2 = 50
	ELSEIF ABL:2 == 2
		SOURCE:2 = 200
	ELSEIF ABL:2 == 3
		SOURCE:2 = 500
	ELSEIF ABL:2 == 4
		SOURCE:2 = 1000
	ELSE
		SOURCE:2 = 1800
	ENDIF
ENDIF

;-------------------------------------------------
;사정 체크
;-------------------------------------------------
TFLAG:91 = SAMEN_CHECK(PLAYER)

;사정시의 처리
IF TFLAG:91 >= 1
	;커맨드 마다 고유의 처리는 이 아래에 쓴다
	TIMES SOURCE:15 , 3.00

	;ABL:정액 중독을 본다
	IF ABL:31 == 0
		SOURCE:24 = 0
		TIMES SOURCE:16 , 2.00
		TIMES SOURCE:22, 2.00
	ELSEIF ABL:31 == 1
		SOURCE:24 = 200
		TIMES SOURCE:16 , 2.50
		TIMES SOURCE:22, 1.60
	ELSEIF ABL:31 == 2
		SOURCE:24 = 500
		TIMES SOURCE:16 , 3.00
		TIMES SOURCE:22, 1.00
	ELSEIF ABL:31 == 3
		SOURCE:24 = 1200
		TIMES SOURCE:16 , 4.50
		TIMES SOURCE:22, 0.70
	ELSEIF ABL:31 == 4
		SOURCE:24 = 2500
		TIMES SOURCE:16 , 6.00
		TIMES SOURCE:22, 0.40
	ELSE
		SOURCE:24 = 5000
		TIMES SOURCE:16 , 8.00
		TIMES SOURCE:22, 0.10
	ENDIF

	;사정처 타입의 설정
	TFLAG:703 = 4
	;사정처 선택의 가능·불가
	TFLAG:704 = 0
	;범용적인 처리는 이 함수로 실시한다
	;(사정 게이지의 재계산이나 사정 경험의 상승과 공통 부분의 표시)
	CALL SAMEN_SHOOT
ENDIF

;사정에 관계없이 행해지는 처리는 여기로부터
;조교자가 남자나 후타나리라면 펠라 경험
SIF TALENT:PLAYER:120 || TALENT:PLAYER:121
	TCVAR:22 += 1
;노예가 남자나 후타나리라면 조교자에게 펠라 경험
SIF TALENT:120 || TALENT:121
	TCVAR:PLAYER:22 += 1

;노예의 입⇔조교자의 V의 불결이 이동
STAIN:0 |= STAIN:PLAYER:3
STAIN:PLAYER:3 |= STAIN:0
;노예의 V⇔조교자의 입의 불결이 이동
STAIN:3 |= STAIN:PLAYER:0
STAIN:PLAYER:0 |= STAIN:3

IF TALENT:120 == 0 && TALENT:PLAYER:120 == 0
	TCVAR:12 += 8
ELSEIF TALENT:120 && TALENT:PLAYER:120
	TCVAR:13 += 8
ENDIF

SIF PLAYER == MASTER
	TCVAR:41 += 3

;봉사 쾌락 경험 입수 판정
TFLAG:100 |= 1
;동일 커맨드 연속 실행 패널티 회피
TFLAG:96 = 1
RETURN 1

;--------------------------------------------------
;명령 거부의 메시지
;--------------------------------------------------
@DENIAL_MESSAGE_COM_602
CALL KOJO_DENIAL_MESSAGE_COM
SIF FLAG:10 == 0 || CFLAG:99 == 1 || RESULT != 0
	RETURN 0

;--------------------------------------------------
;조교시 메시지
;--------------------------------------------------
@TRAIN_MESSAGE_COM602
CALL KOJO_TRAIN_MESSAGE_COM
SIF FLAG:10 == 0 || CFLAG:99 == 1 || RESULT != 0
	RETURN 0

PRINTL
PRINTFORM %조사선택(CALLNAME:TARGET, "와")% 
SIF ASSIPLAY
	PRINTFORM %조사선택(CALLNAME:ASSI, "는")% 
PRINTW 서로의 성기를 빨았다….

;-------------------------------------------------
;고유의 실행 판정
;-------------------------------------------------
@COM_ORDER_602, ARG
LOCAL:99 = ARG
;ABL:욕망
IF ABL:11
	SIF LOCAL:99
		RESULTS = %RESULTS% + 
	LOCAL:99 = ABL:11
	TFLAG:240 += LOCAL:99 * 2
	RESULTS = %RESULTS% %ABLNAME:11%LV{LOCAL:99}({LOCAL:99 * 3})
ENDIF
;ABL:BL기분＆TALENT:남자
IF TALENT:PLAYER:120 && TALENT:120
	SIF LOCAL:99
		RESULTS = %RESULTS% - 
	;↓의 LOCAL:99의 기준(자위 5, 펠라/A애무 강제 10)
	LOCAL:99 = 12
	TFLAG:240 -= LOCAL:99
	RESULTS = %RESULTS% %TALENTNAME:120%같은 종류({LOCAL:99})
	IF ABL:23
		LOCAL:99 = ABL:23
		TFLAG:240 += LOCAL:99 * 4
		RESULTS = %RESULTS% + %ABLNAME:23%LV{LOCAL:99}({LOCAL:99 * 4})
	ENDIF
ENDIF
;ABL:봉사 정신
IF ABL:13
	SIF LOCAL:99
		RESULTS = %RESULTS% + 
	LOCAL:99 = ABL:13
	TFLAG:240 += LOCAL:99 * 4
	RESULTS = %RESULTS% %ABLNAME:13%LV{LOCAL:99}({LOCAL:99 * 4})
ENDIF

;쾌락 각인
IF MARK:1
	SIF LOCAL:99
		RESULTS = %RESULTS% + 
	LOCAL:99 = MARK:1
	TFLAG:240 += LOCAL:99 * 3
	RESULTS = %RESULTS% %MARKNAME:1%LV{LOCAL:99}({LOCAL:99 * 3})
ENDIF

;PALAM:욕정
GETPALAMLV PALAM:13, 5
LOCAL:2 = RESULT
IF LOCAL:2
	SIF LOCAL:99
		RESULTS = %RESULTS% + 
	LOCAL:99 = LOCAL:2 * 2
	TFLAG:240 += LOCAL:99
	RESULTS = %RESULTS% %PALAMNAME:13%LV{LOCAL:2}({LOCAL:99})
ENDIF

;부끄럼쟁이
IF TALENT:35
	RESULTS = %RESULTS% - 
	LOCAL:99 = 3
	TFLAG:240 -= LOCAL:99
	RESULTS = %RESULTS% %TALENTNAME:35%({LOCAL:99})
ENDIF
;악취 둔감
IF TALENT:61
	SIF LOCAL:99
		RESULTS = %RESULTS% + 
	LOCAL:99 = 3
	TFLAG:240 += LOCAL:99
	RESULTS = %RESULTS% %TALENTNAME:61%({LOCAL:99})
;악취 민감
ELSEIF TALENT:62
	RESULTS = %RESULTS% - 
	LOCAL:99 = 3
	TFLAG:240 -= LOCAL:99
	RESULTS = %RESULTS% %TALENTNAME:62%({LOCAL:99})
ENDIF
;헌신적
IF TALENT:63
	SIF LOCAL:99
		RESULTS = %RESULTS% + 
	LOCAL:99 = 6
	TFLAG:240 += LOCAL:99
	RESULTS = %RESULTS% %TALENTNAME:63%({LOCAL:99})
ENDIF
;쾌감에 솔직
IF TALENT:70
	SIF LOCAL:99
		RESULTS = %RESULTS% + 
	LOCAL:99 = 3
	TFLAG:240 += LOCAL:99
	RESULTS = %RESULTS% %TALENTNAME:70%({LOCAL:99})
;쾌감의 부정
ELSEIF TALENT:71
	RESULTS = %RESULTS% - 
	LOCAL:99 = 3
	TFLAG:240 -= LOCAL:99
	RESULTS = %RESULTS% %TALENTNAME:71%({LOCAL:99})
ENDIF
;연모
IF TALENT:150 && ASSIPLAY == 0
	SIF LOCAL:99
		RESULTS = %RESULTS% + 
	LOCAL:99 = 5
	TFLAG:240 += LOCAL:99
	RESULTS = %RESULTS% %TALENTNAME:150%({LOCAL:99})
ENDIF

;미약
IF TEQUIP:11
	SIF LOCAL:99
		RESULTS = %RESULTS% + 
	LOCAL:99 = 8
	TFLAG:240 += LOCAL:99
	RESULTS = %RESULTS% %ITEMNAME:61%({LOCAL:99})
ENDIF

;조교자의 V의 더러움
TFLAG:242 = COMORDER_STAIN_CALC(3, 0)

;서로 불결
IF TFLAG:242
	RESULTS = %RESULTS% - 
	TFLAG:240 -= TFLAG:242
	RESULTS = %RESULTS% 서로 불결
	;악취 둔감
	IF TALENT:61
		RESULTS = %RESULTS%, %TALENTNAME:61%
	ELSEIF TALENT:62
		RESULTS = %RESULTS%, %TALENTNAME:62%
	ENDIF
	RESULTS = %RESULTS%({TFLAG:242})
	LOCAL:99 = 1
ENDIF

TFLAG:241 = 33
