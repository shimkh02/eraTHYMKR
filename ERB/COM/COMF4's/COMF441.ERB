;=============================================================================
;반령을 삽입한다 (앞)
;=============================================================================
;………………………………………………
;실행 판정
;………………………………………………
@COM_ABLE441
;개별 설정 커맨드 필터
SIF DITEMTYPE:999:441 > 0
	RETURN 0
;캐릭터 전용 커맨드를 유효로 하지 않으면 불가
SIF (FLAG:15 & 16384) == 0
	RETURN 0
;Ｖ조교 필터가 오프되어 있지 않으면 불가
SIF FLAG:2 & 1
	RETURN 0
;一度も반령を삽입(前)をしたことが無い場合, 이상계 조교 필터가 오프되어 있지 않으면 불가
SIF FLAG:2 & 4 && (CFLAG:21 & 32) == 0
	RETURN 0
;조교자가 요우무か요우키でないとダメ
SIF NO:PLAYER != 19 && NO:PLAYER != 70
	RETURN 0
;조교자가 요우무の場合は반령이 調教可能キャラとして存在し, 主人や助手, 조교 대상以外でないとダメ
IF NO:PLAYER == 19
	;반령이 현재 조교 가능
	LOCAL:1 = 0
	FOR LOCAL, 0, CHARANUM
		;主人や助手, 조교 대상は除外
		SIF LOCAL == MASTER || LOCAL == ASSI || LOCAL == TARGET
			CONTINUE
		;妊娠していたら除外
		SIF TALENT:LOCAL:140 == 1
			CONTINUE
		;失踪中などこの場にいないなら除外
		SIF CFLAG:LOCAL:4
			CONTINUE
		;反発刻印もしくは恐怖刻印３がある場合は除外(拒絶する)
		SIF MARK:LOCAL:8 > 2 || MARK:LOCAL:9
			CONTINUE
		;반령だったらLOCAL:1に+1
		SIF NO:LOCAL == 65
			LOCAL:1 += 1
	NEXT
	SIF LOCAL:1 == 0
		RETURN 0
;조교자가 요우키の場合は반령(요우키)が調教可能キャラとして存在し, 主人や助手, 조교 대상以外でないとダメ
ELSEIF NO:PLAYER == 70
	;반령(요우키)が현재 조교 가능
	LOCAL:1 = 0
	FOR LOCAL, 0, CHARANUM
		;主人や助手, 조교 대상は除外
		SIF LOCAL == MASTER || LOCAL == ASSI || LOCAL == TARGET
			CONTINUE
		;妊娠していたら除外
		SIF TALENT:LOCAL:140 == 1
			CONTINUE
		;失踪中などこの場にいないなら除外
		SIF CFLAG:LOCAL:4
			CONTINUE
		;反発刻印もしくは恐怖刻印３がある場合は除外(拒絶する)
		SIF MARK:LOCAL:8 > 2 || MARK:LOCAL:9
			CONTINUE
		;반령(요우키)だったらLOCAL:1に+1
		SIF NO:LOCAL == 99
			LOCAL:1 += 1
	NEXT
	SIF LOCAL:1 == 0
		RETURN 0
ENDIF
;남자라면 안 됨
SIF TALENT:120
	RETURN 0
;조교 대상이 소인체형のとき主人か조교자가 금단의 지식 보유자가 아니면 불가
IF TALENT:127 && TALENT:MASTER:56 == 0
	SIF TALENT:PLAYER:56 == 0
		RETURN 0
ENDIF
;처녀で, 순종5미만의 ASSI일 경우できない(새드, または망신か복종ならＯＫ)
IF TALENT:0 == 1 && ASSIPLAY
	SIF ABL:ASSI:10 < 5 && TALENT:ASSI:81 == 0 && TALENT:ASSI:151 == 0 && TALENT:ASSI:160 == 0
		RETURN 0
ENDIF
;윤활 부족이고, 순종5미만의 場合できない(새드, または망신か복종ならＯＫ)
IF PALAM:10 < PALAMLV:2 && ASSIPLAY
	SIF ABL:ASSI:10 < 5 && TALENT:ASSI:81 == 0 && TALENT:ASSI:151 == 0 && TALENT:ASSI:160 == 0
		RETURN 0
ENDIF
;Ｖ계 장착구 사용 중에는 불가
SIF TEQUIP:20 || TEQUIP:21
	RETURN 0
;승마 중에는 불가
SIF TEQUIP:43
	RETURN 0
;욕실 플레이 중에는 플레이 매트가 없으면 불가
SIF TEQUIP:53 && ITEM:42 == 0 && NOITEM == 0
	RETURN 0
;샤워 중에는 불가
SIF TEQUIP:54
	RETURN 0
;○○목욕탕 입욕 중에는 불가
SIF TEQUIP:55
	RETURN 0
;기저귀 플레이 중에는 불가
SIF TEQUIP:58
	RETURN 0
;조수가 훈도시 착용 중일 경우에는 両方に기교4이상 필수
SIF ASSI >= 0 && ASSIPLAY && TEQUIP:ASSI:59 == 17 && (ABL:PLAYER:12 < 4 || ABL:12 < 4)
	RETURN 0
;훈도시 착용 중일 경우는 안 됨
SIF TEQUIP:59 == 17
	RETURN 0
;나체정식 중에는 불가
SIF TEQUIP:60
	RETURN 0
;촉수 조교 중에는 불가
SIF TEQUIP:90
	RETURN 0
RETURN 1

;--------------------------------------------------
;커맨드 본체
;--------------------------------------------------
@COM441
PRINTL 반령을 삽입한다 (앞)
TSTR:0 = 반령을 삽입(전)
CALL TRAIN_MESSAGE_COM441

;V경험을 수반하는 커맨드의 플래그(처녀막 재생 대응)
TFLAG:1 = 1

;-------------------------------------------------
;소스의 계산
;-------------------------------------------------
DOWNBASE:0 += 120
DOWNBASE:1 += 350

SOURCE:31 = 400

;ABL:V감각을 본다
IF ABL:1 == 0
	SOURCE:1 = 20
ELSEIF ABL:1 == 1
	SOURCE:1 = 50
ELSEIF ABL:1 == 2
	SOURCE:1 = 100
ELSEIF ABL:1 == 3
	SOURCE:1 = 1700
ELSEIF ABL:1 == 4
	SOURCE:1 = 2200
ELSE
	SOURCE:1 = 3500
ENDIF

;EXP:V경험을 본다
IF EXP:0 < EXPLV:1
	TIMES SOURCE:1 , 0.05
	SOURCE:20 = 7500
ELSEIF EXP:0 < EXPLV:2
	TIMES SOURCE:1 , 0.30
	SOURCE:20 = 4900
ELSEIF EXP:0 < EXPLV:3
	TIMES SOURCE:1 , 0.70
	SOURCE:20 = 1200
ELSEIF EXP:0 < EXPLV:4
	TIMES SOURCE:1 , 1.00
	SOURCE:20 = 30
ELSEIF EXP:0 < EXPLV:5
	TIMES SOURCE:1 , 2.30
	SOURCE:20 = 0
ELSE
	TIMES SOURCE:1 , 4.20
	SOURCE:20 = 0
ENDIF

;PALAM:윤활을 본다
IF PALAM:10 < PALAMLV:1
	TIMES SOURCE:1 , 0.10
	SOURCE:20 += 2900
	TIMES SOURCE:20 , 3.00
ELSEIF PALAM:10 < PALAMLV:2
	TIMES SOURCE:1 , 0.40
	SOURCE:20 += 750
	TIMES SOURCE:20 , 1.00
ELSEIF PALAM:10 < PALAMLV:3
	TIMES SOURCE:1 , 1.00
	TIMES SOURCE:20 , 0.50
ELSEIF PALAM:10 < PALAMLV:4
	TIMES SOURCE:1 , 1.40
	TIMES SOURCE:20 , 0.20
ELSEIF PALAM:10 >= PALAMLV:4
	TIMES SOURCE:1 , 1.80
	TIMES SOURCE:20 , 0.10
ENDIF

;작은 몸집 체형
SIF TALENT:128
	TIMES SOURCE:20 , 2.00

;정조관념
IF TALENT:30
	IF TALENT:0 == 1
		TIMES SOURCE:11 , 0.60
		SOURCE:32 = 10000
	ELSE
		TIMES SOURCE:11 , 0.60
		SOURCE:32 = 1000
	ENDIF
;정조무구애
ELSEIF TALENT:31
	IF TALENT:0 == 1
		TIMES SOURCE:11 , 0.60
		SOURCE:32 = 300
	ENDIF
ELSE
	SIF TALENT:0 == 1
		SOURCE:32 = 3000
ENDIF

;PALAM:욕정을 본다
IF PALAM:13 < PALAMLV:1
	TIMES SOURCE:1 , 0.60
	TIMES SOURCE:11 , 0.30
ELSEIF PALAM:13 < PALAMLV:2
	TIMES SOURCE:1 , 0.80
	TIMES SOURCE:11 , 0.60
ELSEIF PALAM:13 < PALAMLV:3
	TIMES SOURCE:1 , 1.00
	TIMES SOURCE:11 , 1.00
ELSEIF PALAM:13 < PALAMLV:4
	TIMES SOURCE:1 , 1.20
	TIMES SOURCE:11 , 1.50
ELSEIF PALAM:13 >= PALAMLV:4
	TIMES SOURCE:1 , 1.50
	TIMES SOURCE:11 , 1.80
ENDIF

;ABL:순종을 본다
IF ABL:10 == 0
	TIMES SOURCE:1 , 0.50
	TIMES SOURCE:11 , 0.60
	TIMES SOURCE:32 , 2.00
ELSEIF ABL:10 == 1
	TIMES SOURCE:1 , 0.80
	TIMES SOURCE:11 , 0.80
	TIMES SOURCE:32 , 1.50
ELSEIF ABL:10 == 2
	TIMES SOURCE:1 , 1.00
	TIMES SOURCE:11 , 1.00
	TIMES SOURCE:32 , 1.00
ELSEIF ABL:10 == 3
	TIMES SOURCE:1 , 1.30
	TIMES SOURCE:11 , 1.20
	TIMES SOURCE:32 , 0.80
ELSEIF ABL:10 == 4
	TIMES SOURCE:1 , 1.60
	TIMES SOURCE:11 , 1.40
	TIMES SOURCE:32 , 0.60
ELSE
	TIMES SOURCE:1 , 2.00
	TIMES SOURCE:11 , 1.60
	TIMES SOURCE:32 , 0.30
ENDIF

TCVAR:0 += 2
IF (CFLAG:21 & 32) == 0
	TCVAR:50 += 1
	CFLAG:21 |= 32
ENDIF
RETURN 1

;--------------------------------------------------
;조교시 메시지
;--------------------------------------------------
@TRAIN_MESSAGE_COM441
CALL KOJO_TRAIN_MESSAGE_COM
SIF FLAG:10 == 0 || CFLAG:99 == 1 || RESULT != 0
	RETURN 0

PRINTL
IF PREVCOM != 440 && PREVCOM != 441 && PREVCOM != 442 && PREVCOM != 443 && PREVCOM != 444
	IF NO:PLAYER == 70 
		PRINTFORML %조사선택(CALLNAME:MASTER, "는")% 반령을 불러냈다.
	ELSE
		PRINTFORML %조사선택(CALLNAME:MASTER, "는")% 반령을 불러냈다.
	ENDIF
ENDIF
IF TALENT:172 || TALENT:180
	PRINTFORML %조사선택(CALLNAME:PLAYER, "는")% 반령을 %CALLNAME:TARGET%의 질에 삽입해 완급을 조절하며 넣고 뺐다.
	PRINTFORML %조사선택(CALLNAME:TARGET, "는")% 그 이상한 질에의 쾌감에 곧바로 순응해, 격렬하게 허덕이기 시작했다.
	PRINTFORMW %CALLNAME:PLAYER%도 치밀어 오르는 쾌감에 취해 있다….
ELSE
	PRINTFORML %조사선택(CALLNAME:PLAYER, "는")% 반령을 %CALLNAME:TARGET%의 질에 삽입해 완급을 조절하며 넣고 뺐다.
	PRINTFORMW %CALLNAME:TARGET%뿐만이 아니라, %CALLNAME:PLAYER%도 흥분해 버렸다….
ENDIF
