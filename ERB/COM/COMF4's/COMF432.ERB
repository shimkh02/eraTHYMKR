;=============================================================================
;고드름 고문
;=============================================================================
;………………………………………………
;실행 판정
;………………………………………………
@COM_ABLE432
;개별 설정 커맨드 필터
SIF DITEMTYPE:999:432 > 0
	RETURN 0
;캐릭터 전용 커맨드를 유효로 하지 않으면 불가
SIF (FLAG:15 & 16384) == 0
	RETURN 0
;Ａ조교 필터가 오프되어 있지 않으면 불가
SIF FLAG:2 & 2
	RETURN 0
;승마 중에는 불가
SIF TEQUIP:43
	RETURN 0
;촉수 조교 중에는 불가
SIF TEQUIP:90
	RETURN 0
;해제는 언제든 가능
SIF TEQUIP:26
	RETURN 1
;조교자가 チルノじゃないとダメ
SIF NO:PLAYER != 5
	RETURN 0
;기교3이상이 아니면 쓸 수 없다
SIF ABL:PLAYER:12 < 3
	RETURN 0
;潤滑不足の場合氷柱責めはできない(새드または새드끼Lv3이상, 또는 망신이나 복종이 있다면 ＯＫ)
SIF PALAM:10 < PALAMLV:1 && TALENT:PLAYER:81 == 0 && ABL:PLAYER:20 < 3 && TALENT:ASSI:151 == 0 && TALENT:ASSI:160 == 0
	RETURN 0
;그 조교 중에 관장 플레이＋배설이 있었을 경우, 조교자가 汚れ無視もしくは汚臭鈍感を持たないなら実行できない
SIF TFLAG:140 & 256 && TALENT:PLAYER:61 == 0 && TALENT:PLAYER:64 == 0
	RETURN 0
;Ａ계 장착구 사용 중에는 불가
SIF TEQUIP:25 || TEQUIP:26
	RETURN 0
;熱湯シャワー中はダメ
SIF TEQUIP:54 == 3
	RETURN 0
;○○목욕탕 입욕 중에는 불가
SIF TEQUIP:55
	RETURN 0
;기저귀 플레이 중에는 불가
SIF TEQUIP:58
	RETURN 0
;훈도시 착용 중일 경우는 안 됨
SIF TEQUIP:59 == 17
	RETURN 0
;나체정식 중에는 불가
SIF TEQUIP:60
	RETURN 0
RETURN 1

;--------------------------------------------------
;커맨드 본체
;--------------------------------------------------
@COM432
PRINTL 고드름 고문
TSTR:0 = 고드름 고문
CALL TRAIN_MESSAGE_COM432

DOWNBASE:0 += 100
DOWNBASE:1 += 200

SOURCE:19 += 250

;ABL:A감각을 본다
IF ABL:2 == 0
	SOURCE:2 = 50
	SOURCE:22 = 100
ELSEIF ABL:2 == 1
	SOURCE:2 = 100
	SOURCE:22 = 300
ELSEIF ABL:2 == 2
	SOURCE:2 = 250
	SOURCE:22 = 600
ELSEIF ABL:2 == 3
	SOURCE:2 = 500
	SOURCE:22 = 1200
ELSEIF ABL:2 == 4
	SOURCE:2 = 1000
	SOURCE:22 = 2000
ELSE
	SOURCE:2 = 2000
	SOURCE:22 = 2700
ENDIF

;EXP:A경험을 본다
IF EXP:1 < EXPLV:1
	TIMES SOURCE:2 , 0.50
	SOURCE:20 = 3000
ELSEIF EXP:1 < EXPLV:2
	TIMES SOURCE:2 , 1.00
	SOURCE:20 = 1700
ELSEIF EXP:1 < EXPLV:3
	TIMES SOURCE:2 , 1.10
	SOURCE:20 = 600
ELSEIF EXP:1 < EXPLV:4
	TIMES SOURCE:2 , 1.20
	SOURCE:20 = 100
ELSEIF EXP:1 < EXPLV:5
	TIMES SOURCE:2 , 1.40
	SOURCE:20 = 50
ELSE
	TIMES SOURCE:2 , 1.60
	SOURCE:20 = 10
ENDIF

;PALAM:윤활을 본다
IF PALAM:10 < PALAMLV:1
	TIMES SOURCE:2 , 0.40
	SOURCE:20 += 2000
ELSEIF PALAM:10 < PALAMLV:2
	TIMES SOURCE:2 , 0.80
	SOURCE:20 += 1200
ELSEIF PALAM:10 < PALAMLV:3
	TIMES SOURCE:2 , 1.00
	SOURCE:20 += 700
ELSEIF PALAM:10 < PALAMLV:4
	TIMES SOURCE:2 , 1.40
	SOURCE:20 += 500
ELSE
	TIMES SOURCE:2 , 1.80
	SOURCE:20 += 200
ENDIF

;PALAM:욕정을 본다
IF PALAM:13 < PALAMLV:1
	TIMES SOURCE:2 , 0.80
ELSEIF PALAM:13 < PALAMLV:2
	TIMES SOURCE:2 , 0.90
ELSEIF PALAM:13 < PALAMLV:3
	TIMES SOURCE:2 , 1.00
ELSEIF PALAM:13 < PALAMLV:4
	TIMES SOURCE:2 , 1.10
ELSE
	TIMES SOURCE:2 , 1.20
ENDIF

;작은 몸집 체형
IF TALENT:128
	TIMES SOURCE:20 , 2.00
	SIF TALENT:130 || TALENT:131
		TIMES SOURCE:2 , 2.00
	SIF NO:TARGET == 44 || NO:TARGET == 66 || NO:TARGET == 67
		TIMES SOURCE:2 , 1.20
ENDIF

;A민감, 둔감을 본다
;쾌A자체의 체크는 다음에 정리해 준다
IF TALENT:105
	TIMES SOURCE:20 , 1.50
	TIMES SOURCE:22 , 1.50
	TIMES SOURCE:31 , 1.50
ELSEIF TALENT:104
	TIMES SOURCE:20 , 0.60
	TIMES SOURCE:22 , 0.60
	TIMES SOURCE:31 , 0.60
ENDIF

SIF TALENT:0 == 1 && TALENT:30
	SOURCE:22 /= 3

;고드름의 착탈
IF TEQUIP:26
	TIMES SOURCE:2 , 2.80
	LOCAL = MAX (TEQUIP:26 - 1, 0)
	TCVAR:1 += 2
	SIF LOCAL > 0
		TCVAR:33 += LOCAL
	TEQUIP:26 = 0
ELSE
	TEQUIP:26 = 1
	LOCAL = 0
	TIMES SOURCE:2 , 0.80
	IF TFLAG:171 == 0 && EXP:33 >= 10 && EXP:1 >= 50
		LOCAL += 1
	ELSEIF TFLAG:171 > 0 && TFLAG:171 <= 5
		LOCAL += 2
		TEQUIP:26 += 1
	ELSEIF TFLAG:171 > 5 && TFLAG:171 <= 10
		LOCAL += 3
		TEQUIP:26 += 2
	ELSEIF TFLAG:171 > 10
		LOCAL += 4
		TEQUIP:26 += 3
	ENDIF
	TCVAR:1 += 1
	SIF LOCAL > 0
		TCVAR:33 += LOCAL
	TFLAG:171 += 1
ENDIF

IF TALENT:120 == 0 && TALENT:PLAYER:120 == 0
	TCVAR:12 += 1
ELSEIF TALENT:120 && TALENT:PLAYER:120
	TCVAR:13 += 1
ENDIF

RETURN 1

;-------------------------------------------------
;고드름 삽입중
;-------------------------------------------------
@EQUIP_COM432
IF RAND:4 == 0
	TEQUIP:26 -= 1
	PRINTFORMW %CALLNAME:TARGET%의 항문에 박혀있는 고드름이 녹기 시작했다….
	IF TEQUIP:26 <= 0
		TEQUIP:26 = 0
		PRINTFORMW %CALLNAME:TARGET%의 항문에 박혀있던 고드름은 완전하게 녹아 버렸다….
		RETURN 1
	ENDIF
ENDIF

IF TEQUIP:26 == 1 || TEQUIP:26 == 2
	PRINTL <고드름 삽입 중>
ELSEIF TEQUIP:26 == 3
	PRINTL <초대형 고드름 삽입 중>
ELSEIF TEQUIP:26 >= 4
	PRINTL <얼음 덩어리 삽입 중>
ENDIF

LOCAL:3 = MAX(TEQUIP:26 / 2, 1)

DOWNBASE:0 += 25 * LOCAL:3
DOWNBASE:1 += 50 * LOCAL:3

;ABL:A감각을 본다
IF ABL:2 == 0
	LOCAL = 20
	LOCAL:1 = 50
ELSEIF ABL:2 == 1
	LOCAL = 40
	LOCAL:1 = 100
ELSEIF ABL:2 == 2
	LOCAL = 60
	LOCAL:1 = 300
ELSEIF ABL:2 == 3
	LOCAL = 100
	LOCAL:1 = 600
ELSEIF ABL:2 == 4
	LOCAL = 200
	LOCAL:1 = 1200
ELSE
	LOCAL = 400
	LOCAL:1 = 1500
ENDIF

;EXP:A경험을 본다
IF EXP:1 < EXPLV:1
	TIMES LOCAL , 0.50
	LOCAL:2 = 3000
ELSEIF EXP:1 < EXPLV:2
	TIMES LOCAL , 1.00
	LOCAL:2 = 1700
ELSEIF EXP:1 < EXPLV:3
	TIMES LOCAL , 1.10
	LOCAL:2 = 600
ELSEIF EXP:1 < EXPLV:4
	TIMES LOCAL , 1.20
	LOCAL:2 = 100
ELSEIF EXP:1 < EXPLV:5
	TIMES LOCAL , 1.40
	LOCAL:2 = 50
ELSE
	TIMES LOCAL , 1.60
	LOCAL:2 = 10
ENDIF

;PALAM:윤활을 본다
IF PALAM:10 < PALAMLV:1
	TIMES LOCAL , 0.40
	LOCAL:2 += 800
ELSEIF PALAM:10 < PALAMLV:2
	TIMES LOCAL , 0.80
	LOCAL:2 += 500
ELSEIF PALAM:10 < PALAMLV:3
	TIMES LOCAL , 1.00
	LOCAL:2 += 300
ELSEIF PALAM:10 < PALAMLV:4
	TIMES LOCAL , 1.40
	LOCAL:2 += 120
ELSE
	TIMES LOCAL , 1.80
	LOCAL:2 += 100
ENDIF

;PALAM:욕정을 본다
IF PALAM:13 < PALAMLV:1
	TIMES LOCAL , 0.80
ELSEIF PALAM:13 < PALAMLV:2
	TIMES LOCAL , 0.90
ELSEIF PALAM:13 < PALAMLV:3
	TIMES LOCAL , 1.00
ELSEIF PALAM:13 < PALAMLV:4
	TIMES LOCAL , 1.10
ELSE
	TIMES LOCAL , 1.20
ENDIF

;ABL:순종을 본다
IF ABL:10 == 0
	TIMES LOCAL , 0.80
ELSEIF ABL:10 == 1
	TIMES LOCAL , 0.90
ELSEIF ABL:10 == 2
	TIMES LOCAL , 1.00
ELSEIF ABL:10 == 3
	TIMES LOCAL , 1.10
ELSEIF ABL:10 == 4
	TIMES LOCAL , 1.20
ELSE
	TIMES LOCAL , 1.30
ENDIF

;작은 몸집 체형
SIF TALENT:128
	TIMES LOCAL:2 , 1.50
;샹하이 인형, 호라이 인형, 케로 보정
SIF NO:TARGET == 44 || NO:TARGET == 66 || NO:TARGET == 67
	TIMES LOCAL , 1.20

LOCAL *= LOCAL:3

;A민감, 둔감을 본다
;쾌A자체의 체크는 다음에 정리해 준다
IF TALENT:105
	TIMES LOCAL:2 , 1.50
	TIMES LOCAL:1 , 1.50
ELSEIF TALENT:104
	TIMES LOCAL:2 , 0.60
	TIMES LOCAL:1 , 0.60
ENDIF

;처녀로 정조관념
SIF TALENT:0 == 1 && TALENT:30
	LOCAL:1 /= 3

SOURCE:2 += LOCAL
SOURCE:22 += LOCAL:1
SOURCE:20 += LOCAL:2

TCVAR:1 += 1
;촉수 경험 플래그
SIF TEQUIP:90
	TFLAG:90 += 1

RETURN 1

;--------------------------------------------------
;조교시 메시지
;--------------------------------------------------
@TRAIN_MESSAGE_COM432
CALL KOJO_TRAIN_MESSAGE_COM
SIF FLAG:10 == 0 || CFLAG:99 == 1 || RESULT != 0
	RETURN 0

PRINTL
IF TEQUIP:26
	PRINTFORML %조사선택(CALLNAME:PLAYER, "는")% %CALLNAME:TARGET%의 항문에서 고드름을 잡고, 단번에 뽑아냈다….
ELSE
	PRINTFORM %조사선택(CALLNAME:PLAYER, "는")% 손바닥 위에 
	IF TFLAG:171 == 0 && EXP:1 < 1
		PRINTFORML 아직 충분히 개발되어 있지 않은 %CALLNAME:TARGET%의 항문에도
		PRINT 충분히 들어갈 정도로 작은 얼음 조각을 
	ELSEIF TFLAG:171 == 0 && EXP:33 >= 10 && EXP:1 >= 50
		PRINTFORM 충분히 개발된 %CALLNAME:TARGET%의 항문에도 
		PRINT 쾌감을 줄 수 있도록 형태를 정돈한 고드름을 
	ELSEIF TFLAG:171 > 0 && TFLAG:171 <= 5
		PRINT 조금 전보다 크고 굵은 고드름을 
	ELSEIF TFLAG:171 > 5 && TFLAG:171 <= 10
		PRINT 더욱 더 크고 굵은 고드름을 
	ELSEIF TFLAG:171 > 10
		PRINT 이미 얼음 덩어리라고 밖엔 부를 수 없는 물건을 
	ELSE
		PRINT 적당한 크기의 고드름을 
	ENDIF
	PRINTFORML 만들고,
	PRINTFORML 그대로 %CALLNAME:TARGET%의 항문에 쑤셔 넣었다….
	PRINTL
	PRINTFORML %조사선택(CALLNAME:TARGET, "는")% 엉덩이의 이물감, 확장감, 그리고 차가움에 고뇌의 표정을 짓고 있다….
ENDIF
PRINTW
