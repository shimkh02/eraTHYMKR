;=============================================================================
;수레바퀴
;=============================================================================
;………………………………………………
;실행 판정
;………………………………………………
@COM_ABLE430
;개별 설정 커맨드 필터
SIF DITEMTYPE:999:430 > 0
	RETURN 0
;캐릭터 전용 커맨드를 유효로 하지 않으면 불가
SIF (FLAG:15 & 16384) == 0
	RETURN 0
;조교 대상이 雛じゃないとダメ
IF NO:TARGET == 39
	;雛が처녀の時, Ｖ조교 필터가 오프되어 있지 않으면 불가
	SIF TALENT:0 && FLAG:2 & 1
		RETURN 0
	;Ｖ경험レベルが0일 경우, 이상계 조교 필터가 오프되어 있지 않으면 불가
	SIF FLAG:2 & 4 && EXP:0 < EXPLV:1
		RETURN 0
	;남자라면 안 됨
	SIF TALENT:120
		RETURN 0
;もしくは조교자가 雛の場合, 別条件をみる
ELSEIF NO:PLAYER == 39
	CALL EXCOM_ABLE651
	IF RESULT == 0
		RETURN 0
	ELSE
		RETURN 1
	ENDIF
ELSE
	RETURN 0
ENDIF
;주인이 [금단의 지식]持ちじゃないとき, 体格差が2段階以内か조교자가 [금단의 지식]持ちじゃないとダメ
SIF TALENT:MASTER:56 == 0 && COMMON_BODYSIZE_CHECK(PLAYER, TARGET) == 0
	RETURN 0
;조교자에게 페니스가 없으면 불가
SIF TALENT:PLAYER:1 == 0 && TALENT:PLAYER:120 == 0 && TALENT:PLAYER:121 == 0
	RETURN 0
;수면 중에는 불가
SIF EQUIP:14
	RETURN 0
;실신 중에는 불가
SIF TFLAG:899 > 0
	RETURN 0
;Ｖ계 장착구 사용 중에는 불가
SIF TEQUIP:20 || TEQUIP:21
	RETURN 0
;Ａ계 장착구 사용 중에는 불가
SIF TEQUIP:25 || TEQUIP:26
	RETURN 0
;緊縛中は不可
SIF TEQUIP:40
	RETURN 0
;승마 중에는 불가
SIF TEQUIP:43
	RETURN 0
;욕실 플레이 중에는 플레이 매트가 없으면 불가
SIF TEQUIP:53 && ITEM:42 == 0 && NOITEM == 0
	RETURN 0
;샤워 중에는 불가
SIF TEQUIP:54
	RETURN 0
;○○목욕탕 입욕 중에는 불가
SIF TEQUIP:55
	RETURN 0
;기저귀 플레이 중에는 불가
SIF TEQUIP:58
	RETURN 0
;조수가 훈도시 착용 중일 경우에는 불가
SIF ASSI >= 0 && ASSIPLAY && TEQUIP:ASSI:59 == 17
	RETURN 0
;훈도시 착용 중일 경우는 안 됨
SIF TEQUIP:59 == 17
	RETURN 0
;나체정식 중에는 불가
SIF TEQUIP:60
	RETURN 0
;촉수 조교 중에는 불가
SIF TEQUIP:90
	RETURN 0
RETURN 1

;--------------------------------------------------
;커맨드 본체
;--------------------------------------------------
;기승위·역강간을 기본으로 작성
@COM430
;조교자가 병아리 때는 별효과
IF NO:PLAYER == 39
	CALL EXCOM_ABLE651
	SIF RESULT == 1
		JUMP COM651
ENDIF

PRINTL 수레바퀴
;-------------------------------------------------
;실행할 수 있을까의 판정
;-------------------------------------------------
IF TFLAG:699 == 0
	CALL COM_ORDER
	;실행할 수 없다
	SIF RESULT == 0
		RETURN 0
ENDIF

;-------------------------------------------------
;실행 결정
;-------------------------------------------------
TSTR:0 = 수레바퀴
CALL TRAIN_MESSAGE_COM430

;V경험을 수반하는 커맨드의 플래그(처녀막 재생 대응)
TFLAG:1 = 1
SIF TALENT:0 && TALENT:150 && ASSIPLAY == 0
	TFLAG:2 = TALENT:0

;-------------------------------------------------
;사정 게이지 체크
;-------------------------------------------------
LOCAL = 0

;ABL:기교를 본다
IF ABL:12 == 0
	LOCAL = 1000
ELSEIF ABL:12 == 1
	LOCAL = 1300
ELSEIF ABL:12 == 2
	LOCAL = 1700
ELSEIF ABL:12 == 3
	LOCAL = 2200
ELSEIF ABL:12 == 4
	LOCAL = 3000
ELSE
	LOCAL = 4500
ENDIF

;윤활을 본다
LOCAL = EJACULATIONCHECK_PALAM_10_3(TARGET, LOCAL)
;욕정을 본다
LOCAL = EJACULATIONCHECK_PALAM_13_1(TARGET, LOCAL)
;순종을 본다
LOCAL = EJACULATIONCHECK_ABL_10_1(TARGET, LOCAL)
;욕망을 본다
LOCAL = EJACULATIONCHECK_ABL_11_1(TARGET, LOCAL)
;봉사 정신을 본다
LOCAL = EJACULATIONCHECK_ABL_13_2(TARGET, LOCAL)

;조교자의 C감각을 본다
LOCAL = EJACULATIONCHECK_ABL_0_1(PLAYER, LOCAL)

;EXP:V경험을 본다
;V경험이 많아지면(자) 조금 내린다
IF EXP:0 < EXPLV:1
	TIMES LOCAL , 1.50
	IF ASSIPLAY && TALENT:PLAYER:120 == 0
		TCVAR:50 += 2
	ELSE
		TCVAR:50 += 1
	ENDIF
ELSEIF EXP:0 < EXPLV:2
	TIMES LOCAL , 1.00
ELSEIF EXP:0 < EXPLV:3
	TIMES LOCAL , 0.90
ELSEIF EXP:0 < EXPLV:4
	TIMES LOCAL , 0.80
ELSEIF EXP:0 < EXPLV:5
	TIMES LOCAL , 0.70
ELSE
	TIMES LOCAL , 0.60
ENDIF

SIF TALENT:PLAYER:120 || TALENT:PLAYER:121
	BASE:PLAYER:2 += LOCAL

;-------------------------------------------------
;소스의 계산
;-------------------------------------------------
DOWNBASE:0 += 120
DOWNBASE:1 += 180

SOURCE:12 = 1000

;위 쪽으로 계산한 더러운 데이터
SOURCE:30 = TFLAG:242*12 + 80

;ABL:봉사 정신을 본다
IF ABL:13 == 0
	SOURCE:15 = 150
	SOURCE:16 = 20
	SOURCE:31 = 300
	TIMES SOURCE:30 , 4.00
ELSEIF ABL:13 == 1
	SOURCE:15 = 200
	SOURCE:16 = 100
	SOURCE:31 = 100
	TIMES SOURCE:30 , 2.50
ELSEIF ABL:13 == 2
	SOURCE:15 = 330
	SOURCE:16 = 500
	SOURCE:31 = 30
	TIMES SOURCE:30 , 1.50
ELSEIF ABL:13 == 3
	SOURCE:15 = 500
	SOURCE:16 = 900
	SOURCE:31 = 0
	TIMES SOURCE:30 , 1.00
ELSEIF ABL:13 == 4
	SOURCE:15 = 650
	SOURCE:16 = 1800
	SOURCE:31 = 0
	TIMES SOURCE:30 , 0.50
ELSE
	SOURCE:15 = 800
	SOURCE:16 = 2500
	SOURCE:31 = 0
	TIMES SOURCE:30 , 0.10
ENDIF

;ABL:V감각을 본다
IF ABL:1 == 0
	SOURCE:1 = 30
	SOURCE:11 = 20
	TIMES SOURCE:15 , 0.50
ELSEIF ABL:1 == 1
	SOURCE:1 = 90
	SOURCE:11 = 70
	TIMES SOURCE:15 , 0.80
ELSEIF ABL:1 == 2
	SOURCE:1 = 300
	SOURCE:11 = 120
	TIMES SOURCE:15 , 1.00
ELSEIF ABL:1 == 3
	SOURCE:1 = 600
	SOURCE:11 = 190
	TIMES SOURCE:15 , 1.20
ELSEIF ABL:1 == 4
	SOURCE:1 = 900
	SOURCE:11 = 330
	TIMES SOURCE:15 , 1.50
ELSE
	SOURCE:1 = 1800
	SOURCE:11 = 500
	TIMES SOURCE:15 , 2.00
ENDIF

;EXP:V경험을 본다
IF EXP:0 < EXPLV:1
	TIMES SOURCE:1 , 0.20
	SOURCE:20 = 10000
	SOURCE:12 = 12000
	SOURCE:31 = 140000
ELSEIF EXP:0 < EXPLV:2
	TIMES SOURCE:1 , 0.60
	SOURCE:20 = 640
ELSEIF EXP:0 < EXPLV:3
	TIMES SOURCE:1 , 1.00
	SOURCE:20 = 80
ELSEIF EXP:0 < EXPLV:4
	TIMES SOURCE:1 , 1.20
	SOURCE:20 = 15
ELSEIF EXP:0 < EXPLV:5
	TIMES SOURCE:1 , 1.40
ELSE
	TIMES SOURCE:1 , 1.50
ENDIF

;PALAM:윤활
IF PALAM:10 < PALAMLV:1
	TIMES SOURCE:1 , 0.20
	SOURCE:20 += 1800
	TIMES SOURCE:20 , 3.00
	SOURCE:12 += 1800
ELSEIF PALAM:10 < PALAMLV:2
	TIMES SOURCE:1 , 0.60
	SOURCE:20 += 700
	TIMES SOURCE:20 , 1.00
	SOURCE:12 += 600
ELSEIF PALAM:10 < PALAMLV:3
	TIMES SOURCE:1 , 1.00
	TIMES SOURCE:20 , 0.50
	SOURCE:12 += 80
ELSEIF PALAM:10 < PALAMLV:4
	TIMES SOURCE:1 , 1.30
	TIMES SOURCE:20 , 0.30
ELSEIF PALAM:10 < PALAMLV:5
	TIMES SOURCE:1 , 1.60
	TIMES SOURCE:20 , 0.20
ELSE
	TIMES SOURCE:1 , 2.00
	TIMES SOURCE:20 , 0.10
ENDIF

;작은 몸집 체형
SIF TALENT:128
	TIMES SOURCE:20 , 2.00

;정조관념
IF TALENT:30
	IF TALENT:0 == 1
		TIMES SOURCE:11 , 0.60
		TIMES SOURCE:31, 15.00
		SOURCE:32 = 17000
	ELSE
		TIMES SOURCE:11 , 0.60
		TIMES SOURCE:31, 15.00
		SOURCE:32 = 1500
	ENDIF
;정조무구애
ELSEIF TALENT:31
	IF TALENT:0 == 1
		TIMES SOURCE:31, 0.50
		SOURCE:32 = 450
	ELSE
		TIMES SOURCE:31, 0.50
	ENDIF
ELSE
	SIF TALENT:0 == 1
		SOURCE:32 = 4500
ENDIF

;PALAM:욕정
IF PALAM:13 < PALAMLV:1
	TIMES SOURCE:1 , 0.60
	TIMES SOURCE:11 , 0.30
ELSEIF PALAM:13 < PALAMLV:2
	TIMES SOURCE:1 , 0.80
	TIMES SOURCE:11 , 0.60
ELSEIF PALAM:13 < PALAMLV:3
	TIMES SOURCE:1 , 1.00
	TIMES SOURCE:11 , 1.00
ELSEIF PALAM:13 < PALAMLV:4
	TIMES SOURCE:1 , 1.20
	TIMES SOURCE:11 , 1.50
ELSEIF PALAM:13 < PALAMLV:5
	TIMES SOURCE:1 , 1.40
	TIMES SOURCE:11 , 1.80
ELSE
	TIMES SOURCE:1 , 1.60
	TIMES SOURCE:11 , 2.20
ENDIF

;ABL:순종을 본다
IF ABL:10 == 0
	TIMES SOURCE:1 , 0.50
	TIMES SOURCE:11 , 0.60
	TIMES SOURCE:32 , 2.00
	SOURCE:22 += 1000
ELSEIF ABL:10 == 1
	TIMES SOURCE:1 , 0.80
	TIMES SOURCE:11 , 0.80
	TIMES SOURCE:32 , 1.50
	SOURCE:22 += 700
ELSEIF ABL:10 == 2
	TIMES SOURCE:1 , 1.00
	TIMES SOURCE:11 , 1.00
	TIMES SOURCE:32 , 1.00
	SOURCE:22 += 400
ELSEIF ABL:10 == 3
	TIMES SOURCE:1 , 1.30
	TIMES SOURCE:11 , 1.20
	TIMES SOURCE:32 , 0.80
	SOURCE:22 += 150
ELSEIF ABL:10 == 4
	TIMES SOURCE:1 , 1.60
	TIMES SOURCE:11 , 1.40
	TIMES SOURCE:32 , 0.60
	SOURCE:22 += 60
ELSE
	TIMES SOURCE:1 , 2.00
	TIMES SOURCE:11 , 1.60
	TIMES SOURCE:32 , 0.30
	SOURCE:22 += 0
ENDIF

;ABL:기교를 본다
IF ABL:12 == 0
	TIMES SOURCE:1 , 0.50
	TIMES SOURCE:11 , 0.60
ELSEIF ABL:12 == 1
	TIMES SOURCE:1 , 0.80
	TIMES SOURCE:11 , 0.80
ELSEIF ABL:12 == 2
	TIMES SOURCE:1 , 1.00
	TIMES SOURCE:11 , 1.00
ELSEIF ABL:12 == 3
	TIMES SOURCE:1 , 1.30
	TIMES SOURCE:11 , 1.20
ELSEIF ABL:12 == 4
	TIMES SOURCE:1 , 1.60
	TIMES SOURCE:11 , 1.40
ELSE
	TIMES SOURCE:1 , 2.00
	TIMES SOURCE:11 , 1.60
ENDIF

TCVAR:0 += 1
IF PLAYER == MASTER
	TCVAR:42 += 2
	SIF TALENT:MASTER:120 || TALENT:MASTER:121 || TALENT:MASTER:1
		CFLAG:173 += 2
ENDIF

;-------------------------------------------------
;사정 체크
;-------------------------------------------------
TFLAG:91 = SAMEN_CHECK(PLAYER)

;사정시의 처리
IF TFLAG:91 >= 1
	;커맨드 마다 고유의 처리는 이 아래에 쓴다
	TIMES SOURCE:15 , 3.00
	TIMES SOURCE:16 , 5.00
	TIMES SOURCE:22 , 1.50

	;ABL:정액 중독을 본다
	IF ABL:31 == 0
		SOURCE:24 = 0
	ELSEIF ABL:31 == 1
		SOURCE:24 = 300
	ELSEIF ABL:31 == 2
		SOURCE:24 = 1000
	ELSEIF ABL:31 == 3
		SOURCE:24 = 1500
	ELSEIF ABL:31 == 4
		SOURCE:24 = 2200
	ELSE
		SOURCE:24 = 1000 + (ABL:31 * 400)
	ENDIF
	;사정처 타입의 설정
	TFLAG:703 = 1
	;사정처 선택의 가능·불가
	TFLAG:704 = 1
	;범용적인 처리는 이 함수로 실시한다
	;(사정 게이지의 재계산이나 사정 경험의 상승과 공통 부분의 표시)
	CALL SAMEN_SHOOT
ENDIF

;사정에 관계없이 행해지는 처리는 여기로부터
STAIN:3 |= STAIN:PLAYER:2
STAIN:PLAYER:2 |= STAIN:3

SIF TALENT:120 == 0 && TALENT:PLAYER:120 == 0
	TCVAR:12 += 7

;주인 경험 플래그
SIF ASSIPLAY == 0
	TFLAG:50 += (ABL:1 >= 3) ?  3 # 2

;조교자가 후타나리
SIF TALENT:PLAYER:121
	SOURCE:22 /= 2
;봉사 쾌락 경험 입수 판정
TFLAG:100 |= 1

;전회와 이번 조교자가 같아, 전회의 조교가 기승위이며, 뽑지 않는 플래그가 있는 경우
IF TFLAG:102 && PREVCOM == 24 && ((ASSIPLAY && TFLAG:133) || (ASSIPLAY == 0 && TFLAG:133 == 0))
	SIF TFLAG:101
		TFLAG:101 = 2
;전회와 이번 조교자가 같아, 전회의 조교가 수레 가마이며, 뽑지 않는 플래그가 있는 경우
ELSEIF TFLAG:102 && PREVCOM == 430 && ((ASSIPLAY && TFLAG:133) || (ASSIPLAY == 0 && TFLAG:133 == 0))
	SIF TFLAG:101
		TFLAG:101 = 2
;같이 전회의 조교가 정상위나 후배위인 경우
ELSEIF TFLAG:102 && (PREVCOM == 20 || PREVCOM == 21) && ((ASSIPLAY && TFLAG:133) || (ASSIPLAY == 0 && TFLAG:133 == 0))
	SIF TFLAG:101
		TFLAG:101 = 2
ENDIF
;처녀 상실의 후에 페니스를 뽑고 있을까
TFLAG:101 = (TFLAG:101 == 2) ?  1 # 0
RETURN 1

;--------------------------------------------------
;명령 거부의 메시지
;--------------------------------------------------
@DENIAL_MESSAGE_COM_430
CALL KOJO_DENIAL_MESSAGE_COM
SIF FLAG:10 == 0 || CFLAG:99 == 1 || RESULT != 0
	RETURN 0

PRINTL
IF TEQUIP:68
	SIF PALAM:0 >= (PALAMLV:4 - 1) ||  PALAM:1 >= (PALAMLV:4 - 1) ||  PALAM:2 >= (PALAMLV:4 - 1) ||  PALAM:10 >= (PALAMLV:4 - 1)
		PRINT 당장이라도 가버릴 것 같으면서도, 
	PRINTFORMW %조사선택(CALLNAME:TARGET, "는")% 명령을 거부했다….
ENDIF

;--------------------------------------------------
;조교시 메시지
;--------------------------------------------------
@TRAIN_MESSAGE_COM430
CALL KOJO_TRAIN_MESSAGE_COM
SIF FLAG:10 == 0 || CFLAG:99 == 1 || RESULT != 0
	RETURN 0

PRINTL
;애태우기플레이중에 자위계·기승위계·조수를 범하게 한다
IF TEQUIP:68
	SIF PALAM:0 >= (PALAMLV:4 - 1) ||  PALAM:1 >= (PALAMLV:4 - 1) ||  PALAM:2 >= (PALAMLV:4 - 1) ||  PALAM:10 >= (PALAMLV:4 - 1)
		PRINT 당장이라도 가버릴 것 같아하길래, 
	PRINTFORML %조사선택(CALLNAME:TARGET, "가")% 원하는 대로 하게 했다….
ENDIF
;전회와 이번 조교자가 같아, 전회의 조교가 기승위이며, 뽑지 않는 플래그가 있는 경우
IF TFLAG:102 && PREVCOM == 24 && ((ASSIPLAY && TFLAG:133) || (ASSIPLAY == 0 && TFLAG:133 == 0))
	PRINTFORML %조사선택(CALLNAME:TARGET, "는")% 삽입된 채로 페니스를 단단히 조여 몸을 돌리기 시작했다….
;전회와 이번 조교자가 같아, 전회의 조교가 수레 가마이며, 뽑지 않는 플래그가 있는 경우
ELSEIF TFLAG:102 && PREVCOM == 430 && ((ASSIPLAY && TFLAG:133) || (ASSIPLAY == 0 && TFLAG:133 == 0))
	PRINTFORML %조사선택(CALLNAME:TARGET, "는")% 삽입된 채로 페니스를 단단히 조여 다시 빙빙 돌기 시작했다….
;같이 전회의 조교가 정상위나 후배위인 경우
ELSEIF TFLAG:102 && (PREVCOM == 20 || PREVCOM == 21) && ((ASSIPLAY && TFLAG:133) || (ASSIPLAY == 0 && TFLAG:133 == 0))
	PRINTFORML %조사선택(CALLNAME:PLAYER, "는")% 삽입한 채로 누워, %조사선택(CALLNAME:TARGET, "를")% 허리에 앉히고
	PRINTFORML 아래에서 밀어 올리면서 %CALLNAME:TARGET%에게 스스로 돌도록 명령했다….
;같이 전회의 조교가 대면좌위나 배면좌위인 경우
ELSEIF TFLAG:102 && (PREVCOM == 22 || PREVCOM == 23) && ((ASSIPLAY && TFLAG:133) || (ASSIPLAY == 0 && TFLAG:133 == 0))
	PRINTFORML %조사선택(CALLNAME:PLAYER, "는")% 삽입한 채로 누워, 아래로부터 허리를 밀어 올리면서 회전하도록 명령했다….
ELSE
	PRINTFORML %조사선택(CALLNAME:TARGET, "는")% %CALLNAME:PLAYER%의 위에 걸쳐, 천천히 앉아 왔다….
	PRINTFORML %CALLNAME:PLAYER%의 물건을 완전하게 집어넣자, 그것을 기점으로 %조사선택(CALLNAME:TARGET, "는")% 빙글빙글 돌기 시작했다.
ENDIF
PRINTFORML %조사선택(CALLNAME:TARGET, "가")% 몸을 돌릴 때에 두 명의 성기는 서로 스쳐, 기묘한 쾌감을 낳고 있다….
IF EXP:0 >= EXPLV:3
	PRINTL
	PRINTFORM %조사선택(CALLNAME:TARGET, "는")% 몸이 방향을 바꿀 때마다 
	SIF TALENT:112 || TALENT:113
		PRINT 크게 가슴을 흔들면서 
	PRINTL 교성을 지르고 있다….
ENDIF
PRINTW

;-------------------------------------------------
;고유의 실행 판정
;-------------------------------------------------
@COM_ORDER_430, ARG
LOCAL:99 = ARG
;ABL:욕망
IF ABL:11
	SIF LOCAL:99
		RESULTS = %RESULTS% + 
	LOCAL:99 = ABL:11
	TFLAG:240 += LOCAL:99 * 3
	RESULTS = %RESULTS% %ABLNAME:11%LV{LOCAL:99}({LOCAL:99 * 3})
ENDIF
;ABL:V감각
IF ABL:1
	SIF LOCAL:99
		RESULTS = %RESULTS% + 
	LOCAL:99 = ABL:1
	TFLAG:240 += LOCAL:99 * 2
	RESULTS = %RESULTS% %ABLNAME:1%LV{LOCAL:99}({LOCAL:99 * 2})
ENDIF
;ABL:봉사 정신
IF ABL:13
	SIF LOCAL:99
		RESULTS = %RESULTS% + 
	LOCAL:99 = ABL:13
	TFLAG:240 += LOCAL:99 * 4
	RESULTS = %RESULTS% %ABLNAME:13%LV{LOCAL:99}({LOCAL:99 * 4})
ENDIF
;ABL:정액 중독
IF ABL:31
	SIF LOCAL:99
		RESULTS = %RESULTS% + 
	LOCAL:99 = ABL:31
	TFLAG:240 += LOCAL:99
	RESULTS = %RESULTS% %ABLNAME:31%LV{LOCAL:99}({LOCAL:99})
ENDIF

;쾌락 각인
IF MARK:1
	SIF LOCAL:99
		RESULTS = %RESULTS% + 
	LOCAL:99 = MARK:1
	TFLAG:240 += LOCAL:99 * 3
	RESULTS = %RESULTS% %MARKNAME:1%LV{LOCAL:99}({LOCAL:99 * 3})
ENDIF

;PALAM:윤활(의 부족)
IF PALAM:10 < PALAMLV:3
	RESULTS = %RESULTS% - 
	LOCAL:99 = 5
	TFLAG:240 -= LOCAL:99
	RESULTS = %RESULTS% 윤활 부족({LOCAL:99})
ENDIF
;PALAM:욕정
GETPALAMLV PALAM:13, 5
LOCAL:2 = RESULT
IF LOCAL:2
	SIF LOCAL:99
		RESULTS = %RESULTS% + 
	LOCAL:99 = LOCAL:2 * 3
	TFLAG:240 += LOCAL:99
	RESULTS = %RESULTS% %PALAMNAME:13%LV{LOCAL:2}({LOCAL:99})
ENDIF

;부끄럼쟁이
IF TALENT:35
	RESULTS = %RESULTS% - 
	LOCAL:99 = 2
	TFLAG:240 -= LOCAL:99
	RESULTS = %RESULTS% %TALENTNAME:35%({LOCAL:99})
ENDIF
;남자 싫다
IF TALENT:37 && TALENT:PLAYER:120
	RESULTS = %RESULTS% - 
	LOCAL:99 = 12
	TFLAG:240 -= LOCAL:99
	RESULTS = %RESULTS% %TALENTNAME:37%({LOCAL:99})
;여자 증오
ELSEIF TALENT:38 && TALENT:PLAYER:120 == 0
	RESULTS = %RESULTS% - 
	LOCAL:99 = 12
	TFLAG:240 -= LOCAL:99
	RESULTS = %RESULTS% %TALENTNAME:38%({LOCAL:99})
ENDIF
;쾌감의 부정
IF TALENT:71
	RESULTS = %RESULTS% - 
	LOCAL:99 = 5
	TFLAG:240 -= LOCAL:99
	RESULTS = %RESULTS% %TALENTNAME:71%({LOCAL:99})
ENDIF
;연모
IF TALENT:150 && ASSIPLAY == 0
	SIF LOCAL:99
		RESULTS = %RESULTS% + 
	LOCAL:99 = 5
	TFLAG:240 += LOCAL:99
	RESULTS = %RESULTS% %TALENTNAME:150%({LOCAL:99})
ENDIF
;처녀
IF TALENT:0 == 1
	RESULTS = %RESULTS% - 
	LOCAL:99 = 20
	TFLAG:240 -= LOCAL:99
	RESULTS = %RESULTS% %TALENTNAME:0%({LOCAL:99})
ELSEIF EXP:0 < EXPLV:2
	RESULTS = %RESULTS% - 
	LOCAL:99 = 5
	TFLAG:240 -= LOCAL:99
	RESULTS = %RESULTS% %EXPNAME:0%부족({LOCAL:99})
ENDIF

;조교자가 후타나리
IF TALENT:PLAYER:121
	SIF LOCAL:99
		RESULTS = %RESULTS% + 
	LOCAL:99 = 4
	TFLAG:240 += LOCAL:99
	RESULTS = %RESULTS% %조사처리(CALLNAME:PLAYER,"가")% %TALENTNAME:121%({LOCAL:99})
ENDIF

;미약
IF TEQUIP:11
	SIF LOCAL:99
		RESULTS = %RESULTS% + 
	LOCAL:99 = 6
	TFLAG:240 += LOCAL:99
	RESULTS = %RESULTS% %ITEMNAME:61%({LOCAL:99})
ENDIF

;조교자의 P의 더러움
TFLAG:242 = COMORDER_STAIN_CALC(2, 0)
;수레 가마에서는 더러움의 영향이 적은
TFLAG:242 /= 3

;서로 불결
IF TFLAG:242
	RESULTS = %RESULTS% - 
	TFLAG:240 -= TFLAG:242
	RESULTS = %RESULTS% 서로 불결
	;악취 둔감
	IF TALENT:61
		RESULTS = %RESULTS%, %TALENTNAME:61%
	ELSEIF TALENT:62
		RESULTS = %RESULTS%, %TALENTNAME:62%
	ENDIF
	RESULTS = %RESULTS%({TFLAG:242})
	LOCAL:99 = 1
ENDIF

TFLAG:241 = 40
