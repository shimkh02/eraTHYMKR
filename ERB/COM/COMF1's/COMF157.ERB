;=============================================================================
;바기나벌룬
;=============================================================================
;………………………………………………
;실행 판정
;………………………………………………
@COM_ABLE157
;개별 설정 커맨드 필터
SIF DITEMTYPE:999:157 > 0
	RETURN 0
;Ｖ계 확장 커맨드를 유효로 하지 않으면 불가
SIF (FLAG:15 & 2) == 0
	RETURN 0
;Ｖ조교 필터가 오프되어 있지 않으면 불가
SIF FLAG:2 & 1
	RETURN 0
;대상의 Ｖ확장경험이 0일 때 이상계 조교 필터가 오프되어 있지 않으면 불가
SIF FLAG:2 & 4 && EXP:32 == 0
	RETURN 0
;승마 중에는 불가
SIF TEQUIP:43
	RETURN 0
;기저귀 플레이 중에는 불가
SIF TEQUIP:58
	RETURN 0
;훈도시 착용 중일 경우는 안 됨
SIF TEQUIP:59 == 17
	RETURN 0
;촉수 조교 중에는 불가
SIF TEQUIP:90
	RETURN 0
;해제는 언제든 가능
SIF TEQUIP:20 == 6
	RETURN 1
;아이템을 가지고 있거나, 아이템 없음 설정이 아니면 불가
SIF ITEM:5 == 0 && NOITEM == 0
	RETURN 0
;처녀면 불가
SIF TALENT:0 == 1
	RETURN 0
;남자라면 안 됨
SIF TALENT:120
	RETURN 0
;조교 대상이 소인체형일 경우에는 주인이 금단의 지식 보유자가 아니면 불가
SIF TALENT:127 && TALENT:MASTER:56 == 0
	RETURN 0
;기계면 불가
SIF TALENT:206
	RETURN 0
;학교수영복 플레이 중에는 불가
SIF TEQUIP:59 == 2
	RETURN 0
;쿠스코 삽입 중에는 불가
SIF TEQUIP:21
	RETURN 0
;그밖의 Ｖ계 장착구 사용 중에는 불가
SIF TEQUIP:20
	RETURN 0
;한증탕과 암반욕 외의 ○○ 욕탕 입욕 중에는 불가
SIF TEQUIP:55 && TEQUIP:55 != 12 && TEQUIP:55 != 15
	RETURN 0
RETURN 1

;--------------------------------------------------
;커맨드 본체
;--------------------------------------------------
@COM157
TSTR:7 = 바기나 벌룬
IF FLAG:1100 & 1
	;技師パッチ有効なら特殊な名前チェック
	FLAG:1110 = 5
	CALL NAMECHECK_CUSTOMTOOL
	IF TSTR:9 == "바기나벌룬"
		TSTR:7 = 벌룬
		TSTR:9 = 에어벌룬
	ENDIF
ELSE
	TSTR:8 = %TSTR:7%
	TSTR:9 = 에어벌룬
	TSTR:7 = 벌룬
ENDIF
PRINTFORML %TSTR:8%
TSTR:0 = 보지 벌룬
CALL TRAIN_MESSAGE_COM157

;V경험을 수반하는 커맨드의 플래그(처녀막 재생 대응)
TFLAG:1 = 1

;-------------------------------------------------
;소스의 계산
;-------------------------------------------------
DOWNBASE:0 += 140
DOWNBASE:1 += 190

SOURCE:12 = 300
SOURCE:20 = 400
SOURCE:24 = 500
SOURCE:31 = 900

;ABL:V감각을 본다
IF ABL:1 == 0
	SOURCE:1 = 10
	SOURCE:22 = 40
ELSEIF ABL:1 == 1
	SOURCE:1 = 30
	SOURCE:22 = 200
ELSEIF ABL:1 == 2
	SOURCE:1 = 300
	SOURCE:22 = 400
ELSEIF ABL:1 == 3
	SOURCE:1 = 600
	SOURCE:22 = 800
ELSEIF ABL:1 == 4
	SOURCE:1 = 900
	SOURCE:22 = 1300
ELSE
	SOURCE:1 = 1200
	SOURCE:22 = 2500
ENDIF

;EXP:V경험을 본다
IF EXP:0 < EXPLV:1
	TIMES SOURCE:1 , 0.05
	SOURCE:20 += 26000
ELSEIF EXP:0 < EXPLV:2
	TIMES SOURCE:1 , 0.20
	SOURCE:20 += 6000
ELSEIF EXP:0 < EXPLV:3
	TIMES SOURCE:1 , 0.50
	SOURCE:20 += 2000
ELSEIF EXP:0 < EXPLV:4
	TIMES SOURCE:1 , 1.00
	SOURCE:20 += 1200
ELSEIF EXP:0 < EXPLV:5
	TIMES SOURCE:1 , 1.40
	SOURCE:20 += 600
ELSE
	TIMES SOURCE:1 , 1.60
	SOURCE:20 += 200
ENDIF

;PALAM:윤활을 본다
IF PALAM:10 < PALAMLV:1
	TIMES SOURCE:1 , 0.10
	SOURCE:20 += 12000
ELSEIF PALAM:10 < PALAMLV:2
	TIMES SOURCE:1 , 0.40
	SOURCE:20 += 3400
ELSEIF PALAM:10 < PALAMLV:3
	TIMES SOURCE:1 , 1.00
	TIMES SOURCE:20 , 0.50
ELSEIF PALAM:10 < PALAMLV:4
	TIMES SOURCE:1 , 1.40
	TIMES SOURCE:20 , 0.20
ELSE
	TIMES SOURCE:1 , 1.80
	TIMES SOURCE:20 , 0.10
ENDIF

;PALAM:욕정을 본다
IF PALAM:13 < PALAMLV:1
	TIMES SOURCE:1 , 0.60
	TIMES SOURCE:22 , 0.60
ELSEIF PALAM:13 < PALAMLV:2
	TIMES SOURCE:1 , 0.80
	TIMES SOURCE:22 , 0.80
ELSEIF PALAM:13 < PALAMLV:3
	TIMES SOURCE:1 , 1.00
	TIMES SOURCE:22 , 1.00
ELSEIF PALAM:13 < PALAMLV:4
	TIMES SOURCE:1 , 1.20
	TIMES SOURCE:22 , 1.20
ELSE
	TIMES SOURCE:1 , 1.40
	TIMES SOURCE:22 , 1.40
ENDIF

;작은 몸집 체형
SIF TALENT:128
	TIMES SOURCE:20 , 2.00

;V민감, 둔감을 본다
;쾌V자체의 체크는 다음에 정리해 준다
IF TALENT:103
	TIMES SOURCE:20 , 1.50
	TIMES SOURCE:22 , 1.50
	TIMES SOURCE:31 , 1.50
ELSEIF TALENT:102
	TIMES SOURCE:20 , 0.60
	TIMES SOURCE:22 , 0.60
	TIMES SOURCE:31 , 0.60
ENDIF

;처녀로 정조관념
SIF TALENT:0 == 1 && TALENT:30
	SOURCE:22 /= 3

IF FLAG:1100 & 1
	;技師パッチ有効なら道具使用時ソースチェックを呼ぶ
	;FLAG 1110:道具番号 1111:改善タイプ 1112:SOURCEの実値 
	FLAG:1110 = 5
	FLAG:1111 = 1
	FLAG:1112 = SOURCE:1
	CALL SOURCECHECK_CUSTOMTOOL
	SOURCE:1 = FLAG:1120
	FLAG:1112 = SOURCE:12
	CALL SOURCECHECK_CUSTOMTOOL
	SOURCE:12 = FLAG:1120
	FLAG:1112 = SOURCE:22
	CALL SOURCECHECK_CUSTOMTOOL
	SOURCE:22 = FLAG:1120
	FLAG:1111 = 2
	FLAG:1112 = SOURCE:20
	CALL SOURCECHECK_CUSTOMTOOL
	SOURCE:20 = FLAG:1120
ENDIF

TCVAR:0 += 5
SIF EXP:32 == 0 
	TCVAR:50 += 1
TCVAR:32 += 1

;커맨드 속성：도구(조교자)
TFLAG:98 = 1

;확장 벌룬의 착탈
IF TEQUIP:20
	TEQUIP:20 = 0
ELSE
	TEQUIP:20 = 6
ENDIF
RETURN 1

;--------------------------------------------------
;보지 벌룬 삽입중
;--------------------------------------------------
@EQUIP_COM157
TSTR:7 = 바기나 벌룬
IF FLAG:1100 & 1
	;技師パッチ有効なら特殊な名前チェック
	FLAG:1110 = 5
	CALL NAMECHECK_CUSTOMTOOL
ENDIF
PRINTFORML <%TSTR:7% 삽입 중>

DOWNBASE:0 += 45
DOWNBASE:1 += 95

;ABL:V감각을 본다
IF ABL:1 == 0
	LOCAL = 10
	LOCAL:1 = 40
	SOURCE:11 += 20
ELSEIF ABL:1 == 1
	LOCAL = 20
	LOCAL:1 = 200
	SOURCE:11 += 40
ELSEIF ABL:1 == 2
	LOCAL = 200
	LOCAL:1 = 400
	SOURCE:11 += 140
ELSEIF ABL:1 == 3
	LOCAL = 400
	LOCAL:1 = 800
	SOURCE:11 += 280
ELSEIF ABL:1 == 4
	LOCAL = 700
	LOCAL:1 = 1300
	SOURCE:11 += 550
ELSE
	LOCAL = 1000
	LOCAL:1 = 2500
	SOURCE:11 += 950
ENDIF

;EXP:V경험을 본다
IF EXP:0 < EXPLV:1
	TIMES LOCAL , 0.05
	LOCAL:2 = 26000
ELSEIF EXP:0 < EXPLV:2
	TIMES LOCAL , 0.20
	LOCAL:2 = 6000
ELSEIF EXP:0 < EXPLV:3
	TIMES LOCAL , 0.50
	LOCAL:2 = 2000
ELSEIF EXP:0 < EXPLV:4
	TIMES LOCAL , 1.00
	LOCAL:2 = 1200
ELSEIF EXP:0 < EXPLV:5
	TIMES LOCAL , 1.40
	LOCAL:2 = 600
ELSE
	TIMES LOCAL , 1.60
	LOCAL:2 = 200
ENDIF

;PALAM:욕정을 본다
IF PALAM:13 < PALAMLV:1
	TIMES LOCAL , 0.60
	TIMES LOCAL:1 , 0.60
ELSEIF PALAM:13 < PALAMLV:2
	TIMES LOCAL , 0.80
	TIMES LOCAL:1 , 0.80
ELSEIF PALAM:13 < PALAMLV:3
	TIMES LOCAL , 1.00
	TIMES LOCAL:1 , 1.00
ELSEIF PALAM:13 < PALAMLV:4
	TIMES LOCAL , 1.20
	TIMES LOCAL:1 , 1.20
ELSE
	TIMES LOCAL , 1.40
	TIMES LOCAL:1 , 1.40
ENDIF

;작은 몸집 체형
SIF TALENT:128
	TIMES LOCAL:2 , 2.00

;V민감, 둔감을 본다
;쾌V자체의 체크는 다음에 정리해 준다
IF TALENT:103
	TIMES LOCAL , 1.50
	TIMES LOCAL:1 , 1.50
	TIMES LOCAL:2 , 1.50
ELSEIF TALENT:102
	TIMES LOCAL , 0.60
	TIMES LOCAL:1 , 0.60
	TIMES LOCAL:2 , 0.60
ENDIF

;처녀로 정조관념
SIF TALENT:0 == 1 && TALENT:30
	LOCAL:1 /= 3

LOCAL:3 = LOCAL:1
IF FLAG:1100 & 1
	;技師パッチ有効なら道具使用時ソースチェックを呼ぶ
	;FLAG 1110:道具番号 1111:改善タイプ 1112:SOURCEの実値 
	FLAG:1110 = 5
	FLAG:1111 = 1
	FLAG:1112 = LOCAL
	CALL SOURCECHECK_CUSTOMTOOL
	LOCAL = FLAG:1120
	FLAG:1112 = LOCAL:1
	CALL SOURCECHECK_CUSTOMTOOL
	LOCAL:1 = FLAG:1120
	FLAG:1111 = 3
	FLAG:1112 = LOCAL:2
	CALL SOURCECHECK_CUSTOMTOOL
	LOCAL:2 = FLAG:1120
ENDIF

SOURCE:1 += LOCAL
SOURCE:22 += LOCAL:1
SOURCE:20 += LOCAL:2
SOURCE:31 += LOCAL:3

TCVAR:0 += 3
TCVAR:32 += 1

IF TALENT:120 == 0 && TALENT:PLAYER:120 == 0
	TCVAR:12 += 1
ELSEIF TALENT:120 && TALENT:PLAYER:120
	TCVAR:13 += 1
ENDIF

RETURN 1

;--------------------------------------------------
;조교시 메시지
;--------------------------------------------------
@TRAIN_MESSAGE_COM157
CALL KOJO_TRAIN_MESSAGE_COM
SIF FLAG:10 == 0 || CFLAG:99 == 1 || RESULT != 0
	RETURN 0

PRINTL
IF TEQUIP:20
	PRINTFORML %조사선택(CALLNAME:PLAYER, "는")% \@(MARK:9 > 1) ? 굴욕을 참고 있는 # \@ %CALLNAME:TARGET%의 벌려진 질로부터 %조사선택(TSTR:7, "를")% 뽑아 주었다.
	IF ABL:1 == 0
		PRINTFORMW %CALLNAME:TARGET%의 질은 천천히 닫혀 간다….
	ELSEIF ABL:1 == 1
		PRINTFORMW %CALLNAME:TARGET%의 질은 벌름거리면서 꾸물꾸물 천천히 닫혀 간다….
	ELSEIF ABL:1 == 2
		PRINTFORMW %CALLNAME:TARGET%의 질은 뻐끔 입을 열고 있다….
	ELSEIF ABL:1 == 3
		PRINTFORMW %CALLNAME:TARGET%의 질은 벌려진 채로 좀처럼 돌아오지 않는 것 같다….
	ELSEIF ABL:1 == 4
		PRINTFORMW %CALLNAME:TARGET%의 질로부터 자궁이 훤히 들여다 보인다….
	ELSE
		PRINTFORMW %조사선택(CALLNAME:TARGET, "는")% 자신의 질을 손가락으로 펼치며 황홀해 하고 있다….
	ENDIF
ELSE
	IF MARK:9 > 1
		PRINTFORML %조사선택(CALLNAME:PLAYER, "는")% 싫어서 버둥대는 %조사선택(CALLNAME:TARGET, "를")% 억눌렀다.
		PRINTFORM 그리고, %CALLNAME:TARGET%의 
		SIF PALAM:10 >= PALAMLV:4
			PRINT 흠뻑 젖어있는 
		PRINTFORMW 질에 %조사선택(TSTR:9, "를")% 삽입해, 공기를 집어넣었다….
	ELSE
		PRINTFORM %조사선택(CALLNAME:PLAYER, "는")% %CALLNAME:TARGET%의 
		SIF PALAM:10 >= PALAMLV:4
			PRINT 흠뻑 젖어있는 
		PRINTFORMW 질에 %조사선택(TSTR:9, "를")% 삽입해, 공기를 집어넣었다….
	ENDIF
ENDIF
