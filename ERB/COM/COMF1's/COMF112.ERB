;=============================================================================
;제모플레이
;=============================================================================
;………………………………………………
;실행 판정
;………………………………………………
@COM_ABLE112
;개별 설정 커맨드 필터
SIF DITEMTYPE:999:112 > 0
	RETURN 0
;수치 플레이계 확장 커맨드를 유효로 하지 않으면 불가
SIF (FLAG:15 & 512) == 0
	RETURN 0
;[후타나리]에다 조교자의 기교가 3미만이면 불가
SIF TALENT:121 && ABL:PLAYER:12 < 3
	RETURN 0
;[パイパン]だと不可, 生えそろっていないと無理
SIF TALENT:65 || (CFLAG:36 && BASE:5 < 250)
	RETURN 0
;テキストより[작은 체형]はデフォでツルツル。[소인체형]もそれに準じる
SIF (TALENT:128 || TALENT:127) && CFLAG:36 == 0
	RETURN 0
;Ｖ계 장착구 사용 중에는 불가
SIF TEQUIP:20 || TEQUIP:21
	RETURN 0
;오나홀 · 전극 오나홀 사용 중에는 불가
SIF TEQUIP:30 == 2 || TEQUIP:30 == 6
	RETURN 0
;귀갑 묶기일 때는 무리
SIF TEQUIP:40 == 5
	RETURN 0
;승마 중에는 불가
SIF TEQUIP:43
	RETURN 0
;욕실 플레이 중에는 플레이 매트가 없으면 불가
SIF TEQUIP:53 && ITEM:42 == 0 && NOITEM == 0
	RETURN 0
;샤워 중에는 불가
SIF TEQUIP:54
	RETURN 0
;○○목욕탕 입욕 중에는 불가
SIF TEQUIP:55
	RETURN 0
;기저귀 플레이 중에는 불가
SIF TEQUIP:58
	RETURN 0
;훈도시 착용 중일 경우는 안 됨
SIF TEQUIP:59 == 17
	RETURN 0
;학교수영복 플레이 중에는 불가
SIF TEQUIP:59 == 2
	RETURN 0
;시간정지 중에는 무리
SIF TEQUIP:71
	RETURN 0
;촉수 조교 중에는 불가
SIF TEQUIP:90
	RETURN 0
;시체는 안됨
SIF TALENT:226 == 1
	RETURN 0
RETURN 1

;--------------------------------------------------
;커맨드 본체
;--------------------------------------------------
@COM112
PRINTL 제모플레이
TSTR:0 = 제모플레이
CALL TRAIN_MESSAGE_COM112

DOWNBASE:0 += 20
DOWNBASE:1 += 60

LOCAL:1 = 500
SOURCE:16 = 750
SOURCE:24 = 500
SOURCE:10 = 800
SOURCE:12 = 1000
SOURCE:31 = 1200

;PALAM:윤활을 본다
IF PALAM:10 < PALAMLV:1
	SOURCE:20 = 600
ELSEIF PALAM:10 < PALAMLV:2
	SOURCE:20 = 250
ELSEIF PALAM:10 < PALAMLV:3
	SOURCE:20 = 100
ELSEIF PALAM:10 < PALAMLV:4
	SOURCE:20 = 30
ENDIF

;ABL:노출벽을 본다
IF ABL:14 == 0
	TIMES LOCAL:1 , 0.80
ELSEIF ABL:14 == 1
	TIMES LOCAL:1 , 1.00
ELSEIF ABL:14 == 2
	TIMES LOCAL:1 , 1.20
ELSEIF ABL:14 == 3
	TIMES LOCAL:1 , 1.50
ELSEIF ABL:14 == 4
	TIMES LOCAL:1 , 2.00
ELSE
	TIMES LOCAL:1 , 2.60
ENDIF

;ABL:마조끼를 본다
IF ABL:21 == 0
	TIMES LOCAL:1 , 0.80
ELSEIF ABL:21 == 1
	TIMES LOCAL:1 , 1.00
ELSEIF ABL:21 == 2
	TIMES LOCAL:1 , 1.20
ELSEIF ABL:21 == 3
	TIMES LOCAL:1 , 1.40
ELSEIF ABL:21 == 4
	TIMES LOCAL:1 , 1.70
ELSE
	TIMES LOCAL:1 , 2.00
ENDIF

SOURCE:24 += LOCAL:1
SOURCE:18 += LOCAL:1
SOURCE:12 += LOCAL:1

;작은 몸집 체형, 소인 체형 이외
SIF TALENT:128 == 0 && TALENT:127 == 0
	TIMES SOURCE:12 , 2.50

IF TALENT:65 == 0
	PRINTL
	PRINTFORMW %조사선택(CALLNAME:TARGET, "는")% [%TALENTNAME:65%]가 되었다.
	TALENT:65 = 1
ENDIF
;탈모 상태 이외의 경우는 나중에 나 오게 된다
SIF CFLAG:36 != 1
	CFLAG:36 = 2
;털이 나 있는 경우는 반들반들이 된다
BASE:5 = 0

;음모 입수 이벤트
SIF FLAG:12 & 8 && (TFLAG:996 & 64) == 0 
	CALL SWEET_POTATO_PUBLIC_HAIR

;노출 쾌락 경험 입수 판정
TFLAG:100 |= 2

RETURN 1

;--------------------------------------------------
;조교시 메시지
;--------------------------------------------------
@TRAIN_MESSAGE_COM112
CALL KOJO_TRAIN_MESSAGE_COM
SIF FLAG:10 == 0 || CFLAG:99 == 1 || RESULT != 0
	RETURN 0

PRINTL
PRINTFORML %조사선택(CALLNAME:PLAYER, "는")% %CALLNAME:TARGET%의 음모를 말끔하게 깎았다.
;수면중과 실신중은 반응할 수 없다.
IF EQUIP:14 == 0 && TFLAG:899 <= 0
	PRINTFORM %조사선택(CALLNAME:TARGET, "는")% 
	IF ABL:14 < 2
		PRINT 얼굴을 새빨갛게 물들이고 
		IF TEQUIP:40
			PRINTFORMW %조사선택(CALLNAME:PLAYER, "와")% 시선을 맞추지 않게 돌리고 있다….
		ELSE
			PRINTW 손으로 반들반들해진 성기를 숨기려 하고 있다….
		ENDIF
	ELSEIF ABL:14 == 2
		PRINTW 부끄럽게도 축축히 젖어버린 자신의 성기를 바라보고 있다….
	ELSEIF ABL:14 == 3
		PRINTW 조금 얼굴을 숙이고 있다….
	ELSEIF ABL:14 == 4
		PRINTW 성기가 가림 없이 보여진다는 것에 부끄러움을 느끼면서도, 어딘지 모르게 즐기고 있는 것 같다….
	ELSE
		PRINTW 바람이 성기를 어루만지는 감촉을 즐기고 있는 것 같다….
	ENDIF
ELSE
	PRINTFORMW %조사선택(CALLNAME:TARGET, "는")% 의식을 잃은 채로, 반들반들해진 된 성기로 %CALLNAME:MASTER%\@ (ASSI >= 0) ? 들 # \@의 눈을 즐겁게 하고 있다….
ENDIF
