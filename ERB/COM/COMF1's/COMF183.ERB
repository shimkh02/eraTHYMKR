;=============================================================================
;엉덩이 스마타
;=============================================================================
;………………………………………………
;실행 판정
;………………………………………………
@COM_ABLE183
;개별 설정 커맨드 필터
SIF DITEMTYPE:999:183 > 0
	RETURN 0
;Ａ계 확장 커맨드를 유효로 하지 않으면 불가
SIF (FLAG:15 & 4) == 0
	RETURN 0
;그 조교 중에 관장 플레이＋배설이 있었을 경우, 조교자가 汚れ無視もしくは汚臭鈍感を持たないなら実行できない
SIF TFLAG:140 & 256 && TALENT:PLAYER:61 == 0 && TALENT:PLAYER:64 == 0
	RETURN 0
;조교자에게 페니스가 없으면 불가
SIF EXIST_BAR(PLAYER) == 0
	RETURN 0
;주인이 [금단의 지식]持ちじゃないとき, 体格差が2段階以内か조교자가 [금단의 지식]持ちじゃないとダメ
SIF TALENT:MASTER:56 == 0 && COMMON_BODYSIZE_CHECK(PLAYER, TARGET) == 0
	RETURN 0
;수면 중에는 불가
SIF EQUIP:14
	RETURN 0
;패닉 중에는 불가
SIF TFLAG:108 > 0
	RETURN 0
;실신 중에는 불가
SIF TFLAG:899 > 0
	RETURN 0
;Ｖ계 장착구 사용 중에는 불가
SIF TEQUIP:20 || TEQUIP:21
	RETURN 0
;Ａ계 장착구 사용 중에는 불가
SIF TEQUIP:25 || TEQUIP:26
	RETURN 0
;밧줄 사용 중에는 불가
SIF TEQUIP:40
	RETURN 0
;승마 중에는 불가
SIF TEQUIP:43
	RETURN 0
;욕실 플레이 중에는 플레이 매트가 없으면 불가
SIF TEQUIP:53 && ITEM:42 == 0 && NOITEM == 0
	RETURN 0
;한증탕과 암반욕 외의 ○○ 욕탕 입욕 중에는 불가
SIF TEQUIP:55 && TEQUIP:55 != 12 && TEQUIP:55 != 15
	RETURN 0
;기저귀 플레이 중에는 불가
SIF TEQUIP:58
	RETURN 0
;조수가 훈도시 착용 중일 경우에는 불가
SIF ASSI >= 0 && ASSIPLAY && TEQUIP:ASSI:59 == 17
	RETURN 0
;훈도시 착용 중일 경우는 안 됨
SIF TEQUIP:59 == 17
	RETURN 0
;나체정식 중에는 불가
SIF TEQUIP:60
	RETURN 0
;시간정지 중에는 무리
SIF TEQUIP:71
	RETURN 0
;촉수 조교중일 시 주인의 촉수사역Lv2 이하는 안 됨
SIF TEQUIP:90 && ABL:MASTER:98 <= 2
	RETURN 0
RETURN 1

;--------------------------------------------------
;커맨드 본체
;--------------------------------------------------
;기승위의 체위로부터 엉덩이의 균열로 켜게 한 여성이 자지의 리근에 손을 더해 준다. 엉덩이 호된 훈련과 닮아 있는 것 같아 조금 다르다
@COM183
PRINTL 엉덩이 스마타

;-------------------------------------------------
;실행할 수 있을까의 판정
;-------------------------------------------------
IF TFLAG:699 == 0
	CALL COM_ORDER
	;실행할 수 없다
	SIF RESULT == 0
		RETURN 0
ENDIF

;-------------------------------------------------
;실행 결정
;-------------------------------------------------
TSTR:0 = 엉덩이 스마타
CALL TRAIN_MESSAGE_COM183

;-------------------------------------------------
;사정 게이지 체크
;-------------------------------------------------
LOCAL = 0

;ABL:기교를 본다
IF ABL:12 == 0
	LOCAL = 400
ELSEIF ABL:12 == 1
	LOCAL = 880
ELSEIF ABL:12 == 2
	LOCAL = 1600
ELSEIF ABL:12 == 3
	LOCAL = 2400
ELSEIF ABL:12 == 4
	LOCAL = 3120
ELSE
	LOCAL = 3680
ENDIF

;윤활을 본다
LOCAL = EJACULATIONCHECK_PALAM_10_3(TARGET, LOCAL)
;순종을 본다
LOCAL = EJACULATIONCHECK_ABL_10_1(TARGET, LOCAL)
;봉사 정신을 본다
LOCAL = EJACULATIONCHECK_ABL_13_1(TARGET, LOCAL)

;조교자의 C감각을 본다
LOCAL = EJACULATIONCHECK_ABL_0_1(PLAYER, LOCAL)

SIF TALENT:PLAYER:120 || TALENT:PLAYER:121
	BASE:PLAYER:2 += LOCAL

;-------------------------------------------------
;소스의 계산
;-------------------------------------------------
DOWNBASE:0 += 50
DOWNBASE:1 += 135

SOURCE:12 = 900
SOURCE:14 = 2000
SOURCE:22 = 600
SOURCE:31 = 450

;위 쪽으로 계산한 더러운 데이터
SOURCE:30 = TFLAG:242*10 + 60

;ABL:봉사 정신을 본다
IF ABL:13 == 0
	SOURCE:15 = 200
	SOURCE:16 = 50
	TIMES SOURCE:30 , 4.00
ELSEIF ABL:13 == 1
	SOURCE:15 = 220
	SOURCE:16 = 90
	TIMES SOURCE:30 , 2.50
ELSEIF ABL:13 == 2
	SOURCE:15 = 300
	SOURCE:16 = 125
	TIMES SOURCE:30 , 1.50
ELSEIF ABL:13 == 3
	SOURCE:15 = 425
	SOURCE:16 = 225
	TIMES SOURCE:30 , 1.00
ELSEIF ABL:13 == 4
	SOURCE:15 = 500
	SOURCE:16 = 360
	TIMES SOURCE:30 , 0.50
ELSE
	SOURCE:15 = 650
	SOURCE:16 = 550
	TIMES SOURCE:30 , 0.10
ENDIF

;ABL:기교를 본다
IF ABL:12 == 0
	TIMES SOURCE:15 , 0.70
	TIMES SOURCE:16 , 0.70
ELSEIF ABL:12 == 1
	TIMES SOURCE:15 , 0.90
	TIMES SOURCE:16 , 0.90
ELSEIF ABL:12 == 2
	TIMES SOURCE:15 , 1.00
	TIMES SOURCE:16 , 1.00
ELSEIF ABL:12 == 3
	TIMES SOURCE:15 , 1.20
	TIMES SOURCE:16 , 1.20
ELSEIF ABL:12 == 4
	TIMES SOURCE:15 , 1.40
	TIMES SOURCE:16 , 1.40
ELSE
	TIMES SOURCE:15 , 1.60
	TIMES SOURCE:16 , 1.60
ENDIF

;ABL:A감각을 본다
IF ABL:2 == 0
	SOURCE:2 = 10
	SOURCE:22 += 1800
ELSEIF ABL:2 == 1
	SOURCE:2 = 37
	SOURCE:22 += 1850
ELSEIF ABL:2 == 2
	SOURCE:2 = 150
	SOURCE:22 += 1900
ELSEIF ABL:2 == 3
	SOURCE:2 = 350
	SOURCE:22 += 2150
ELSEIF ABL:2 == 4
	SOURCE:2 = 550
	SOURCE:22 += 2500
ELSE
	SOURCE:2 = 750
	SOURCE:22 += 3000
ENDIF

;PALAM:윤활
IF PALAM:10 < PALAMLV:1
	TIMES SOURCE:2 , 0.30
	TIMES SOURCE:15 , 0.60
ELSEIF PALAM:10 < PALAMLV:2
	TIMES SOURCE:2 , 0.60
	TIMES SOURCE:15 , 0.80
ELSEIF PALAM:10 < PALAMLV:3
	TIMES SOURCE:2 , 1.00
	TIMES SOURCE:15 , 1.00
ELSEIF PALAM:10 < PALAMLV:4
	TIMES SOURCE:2 , 1.50
	TIMES SOURCE:15 , 1.20
ELSEIF PALAM:10 < PALAMLV:5
	TIMES SOURCE:2 , 2.00
	TIMES SOURCE:15 , 1.40
ELSE
	TIMES SOURCE:2 , 2.50
	TIMES SOURCE:15 , 1.60
ENDIF

;-------------------------------------------------
;사정 체크
;-------------------------------------------------
TFLAG:91 = SAMEN_CHECK(PLAYER)

;사정시의 처리
IF TFLAG:91 >= 1
	;커맨드 마다 고유의 처리는 이 아래에 쓴다
	TIMES SOURCE:15 , 2.00

	;ABL:정액 중독을 본다
	IF ABL:31 == 0
		SOURCE:24 = 0
		TIMES SOURCE:16 , 1.50
		TIMES SOURCE:22, 1.40
	ELSEIF ABL:31 == 1
		SOURCE:24 = 200
		TIMES SOURCE:16 , 2.00
		TIMES SOURCE:22, 1.00
	ELSEIF ABL:31 == 2
		SOURCE:24 = 400
		TIMES SOURCE:16 , 2.50
		TIMES SOURCE:22, 0.80
	ELSEIF ABL:31 == 3
		SOURCE:24 = 700
		TIMES SOURCE:16 , 3.00
		TIMES SOURCE:22, 0.50
	ELSEIF ABL:31 == 4
		SOURCE:24 = 1000
		TIMES SOURCE:16 , 4.00
		TIMES SOURCE:22, 0.20
	ELSE
		SOURCE:24 = 1500
		TIMES SOURCE:16 , 5.00
		TIMES SOURCE:22, 0.00
	ENDIF

	;사정처 타입의 설정
	TFLAG:703 = 13
	;사정처 선택의 가능·불가
	TFLAG:704 = 0
	;범용적인 처리는 이 함수로 실시한다
	;(사정 게이지의 재계산이나 사정 경험의 상승과 공통 부분의 표시)
	CALL SAMEN_SHOOT
ENDIF

;사정에 관계없이 행해지는 처리는 여기로부터
;노예의 A⇔조교자의 P의 불결이 이동
STAIN:4 |= STAIN:PLAYER:2
STAIN:PLAYER:2 |= STAIN:4

IF TALENT:120 == 0 && TALENT:PLAYER:120 == 0
	TCVAR:12 += 7
ELSEIF TALENT:120 && TALENT:PLAYER:120
	TCVAR:13 += 7
ENDIF

SIF PLAYER == MASTER
	TCVAR:41 += 1

;주인 경험 플래그
SIF ASSIPLAY == 0
	TFLAG:50 += 1
;봉사 쾌락 경험 입수 판정
TFLAG:100 |= 1

;조교자가 후타나리
SIF TALENT:PLAYER:121
	SOURCE:22 /= 2

RETURN 1

;--------------------------------------------------
;명령 거부의 메시지
;--------------------------------------------------
@DENIAL_MESSAGE_COM_183
CALL KOJO_DENIAL_MESSAGE_COM
SIF FLAG:10 == 0 || CFLAG:99 == 1 || RESULT != 0
	RETURN 0

PRINTL
PRINTFORML %CALLNAME:TARGET%에게 기승위 자세로 엉덩이의 균열에 끼워 봉사하라고 명령했지만 거절당했다.
PRINTW 좀 더 조교가 필요할 것 같다.

;--------------------------------------------------
;조교시 메시지
;--------------------------------------------------
@TRAIN_MESSAGE_COM183
CALL KOJO_TRAIN_MESSAGE_COM
SIF FLAG:10 == 0 || CFLAG:99 == 1 || RESULT != 0
	RETURN 0

PRINTL
PRINTFORM %조사선택(CALLNAME:TARGET, "는")% %CALLNAME:PLAYER%에게 두 다리를 벌리고 올라타 페니스를 엉덩이의 틈새로 물고, 
IF TALENT:150 && TALENT:170 && PALAM:13 > PALAMLV:5
	PRINT 참을 수 없다는 듯한 표정을 하곤 
ELSEIF TALENT:150 && PALAM:13 > PALAMLV:5
	PRINT 당장 녹을 것 같은 표정을 지으면서 
ELSEIF TALENT:170 && PALAM:13 > PALAMLV:5
	PRINT 기다려 마지않았다는 모습으로 
ELSEIF PALAM:13 > PALAMLV:4
	PRINT 입가에 군침을 흘리며 
ELSEIF TALENT:150 && TALENT:170
	PRINT 도취돼버린 눈동자로 바라보며 
ELSEIF TALENT:150
	PRINT 행복한 표정을 지으면서 
ELSEIF TALENT:170
	PRINT 음부를 도발적으로 보여주며 
ELSEIF TALENT:36
	PRINT 대담하게 허리를 움직이면서 
ELSEIF TALENT:22
	PRINT 희미하게 상기한 얼굴로 
ELSE
	PRINT 부끄러운 듯이 
ENDIF

PRINTW 페니스의 뒤편을 손으로 잡고, 엉덩이의 균열에 비벼 문지르고 있다….

;-------------------------------------------------
;고유의 실행 판정
;-------------------------------------------------
@COM_ORDER_183, ARG
LOCAL:99 = ARG
;ABL:욕망
IF ABL:11
	SIF LOCAL:99
		RESULTS = %RESULTS% + 
	LOCAL:99 = ABL:11
	TFLAG:240 += LOCAL:99 * 2
	RESULTS = %RESULTS% %ABLNAME:11%LV{LOCAL:99}({LOCAL:99 * 2})
ENDIF
;ABL:봉사 정신
IF ABL:13
	SIF LOCAL:99
		RESULTS = %RESULTS% + 
	LOCAL:99 = ABL:13
	TFLAG:240 += LOCAL:99 * 4
	RESULTS = %RESULTS% %ABLNAME:13%LV{LOCAL:99}({LOCAL:99 * 4})
ENDIF
;ABL:정액 중독
IF ABL:31
	SIF LOCAL:99
		RESULTS = %RESULTS% + 
	LOCAL:99 = ABL:31
	TFLAG:240 += LOCAL:99
	RESULTS = %RESULTS% %ABLNAME:31%LV{LOCAL:99}({LOCAL:99})
ENDIF

;쾌락 각인
IF MARK:1
	SIF LOCAL:99
		RESULTS = %RESULTS% + 
	LOCAL:99 = MARK:1
	TFLAG:240 += LOCAL:99 * 2
	RESULTS = %RESULTS% %MARKNAME:1%LV{LOCAL:99}({LOCAL:99 * 2})
ENDIF

;PALAM:욕정
GETPALAMLV PALAM:13, 5
LOCAL:2 = RESULT
IF LOCAL:2
	SIF LOCAL:99
		RESULTS = %RESULTS% + 
	LOCAL:99 = LOCAL:2 * 3
	TFLAG:240 += LOCAL:99
	RESULTS = %RESULTS% %PALAMNAME:13%LV{LOCAL:2}({LOCAL:99})
ENDIF

;부끄럼쟁이
IF TALENT:35
	RESULTS = %RESULTS% - 
	LOCAL:99 = 1
	TFLAG:240 -= LOCAL:99
	RESULTS = %RESULTS% %TALENTNAME:35%({LOCAL:99})
ENDIF
;남자 싫다
IF TALENT:37 && TALENT:PLAYER:120
	RESULTS = %RESULTS% - 
	LOCAL:99 = 10
	TFLAG:240 -= LOCAL:99
	RESULTS = %RESULTS% %TALENTNAME:37%({LOCAL:99})
;여자 증오
ELSEIF TALENT:38 && TALENT:PLAYER:120 == 0
	RESULTS = %RESULTS% - 
	LOCAL:99 = 10
	TFLAG:240 -= LOCAL:99
	RESULTS = %RESULTS% %TALENTNAME:38%({LOCAL:99})
ENDIF
;쾌감의 부정
IF TALENT:71
	RESULTS = %RESULTS% - 
	LOCAL:99 = 4
	TFLAG:240 -= LOCAL:99
	RESULTS = %RESULTS% %TALENTNAME:71%({LOCAL:99})
ENDIF
;연모
IF TALENT:150 && ASSIPLAY == 0
	SIF LOCAL:99
		RESULTS = %RESULTS% + 
	LOCAL:99 = 4
	TFLAG:240 += LOCAL:99
	RESULTS = %RESULTS% %TALENTNAME:150%({LOCAL:99})
ENDIF

;조교자가 후타나리
IF TALENT:PLAYER:121
	SIF LOCAL:99
		RESULTS = %RESULTS% + 
	LOCAL:99 = 8
	TFLAG:240 += LOCAL:99
	RESULTS = %RESULTS% %조사처리(CALLNAME:PLAYER,"가")% %TALENTNAME:121%({LOCAL:99})
ENDIF

;미약
IF TEQUIP:11
	SIF LOCAL:99
		RESULTS = %RESULTS% + 
	LOCAL:99 = 6
	TFLAG:240 += LOCAL:99
	RESULTS = %RESULTS% %ITEMNAME:61%({LOCAL:99})
ENDIF

;조교자의 P의 더러움
TFLAG:242 = COMORDER_STAIN_CALC(2, 0)
;엉덩이 스마타에서는 더러움의 영향이 적은
TFLAG:242 /= 3

;서로 불결
IF TFLAG:242
	RESULTS = %RESULTS% - 
	TFLAG:240 -= TFLAG:242
	RESULTS = %RESULTS% 서로 불결
	;악취 둔감
	IF TALENT:61
		RESULTS = %RESULTS%, %TALENTNAME:61%
	ELSEIF TALENT:62
		RESULTS = %RESULTS%, %TALENTNAME:62%
	ENDIF
	RESULTS = %RESULTS%({TFLAG:242})
	LOCAL:99 = 1
ENDIF

TFLAG:241 = 22
