;=============================================================================
;벌룬카테터
;=============================================================================
;………………………………………………
;실행 판정
;………………………………………………
@COM_ABLE143
;개별 설정 커맨드 필터
SIF DITEMTYPE:999:143 > 0
	RETURN 0
;Ｃ계 확장 커맨드를 유효로 하지 않으면 불가
SIF (FLAG:15 & 1) == 0
	RETURN 0
;요도개발경험이 1이하일 경우, 이상계 조교 필터가 오프되어 있지 않으면 불가
SIF FLAG:2 & 4 && EXP:46 <= 1
	RETURN 0
;승마 중에는 불가
SIF TEQUIP:43
	RETURN 0
;기저귀 플레이 중에는 불가
SIF TEQUIP:58
	RETURN 0
;훈도시 착용 중일 경우는 안 됨
SIF TEQUIP:59 == 17
	RETURN 0
;촉수 조교 중에는 불가
SIF TEQUIP:90
	RETURN 0
;해제는 언제든 가능
SIF TEQUIP:32 == 3
	RETURN 1
;아이템을 가지고 있거나, 아이템 없음 설정이 아니면 불가
SIF ITEM:27 == 0 && NOITEM == 0
	RETURN 0
;남자일 경우, 기교3이상이 아니면 쓸 수 없다
SIF TALENT:120 && ABL:12 < 3
	RETURN 0
;ASSI일 경우, 기교4이상이 아니면 쓸 수 없다。
IF ASSIPLAY
	SIF ABL:ASSI:12 < 4
		RETURN 0
ENDIF
;조교 대상이 소인체형일 경우에는 주인이 금단의 지식 보유자가 아니면 불가
SIF TALENT:127 && TALENT:MASTER:56 == 0
	RETURN 0
;체력이 1000미만으로는 장착 불가능
SIF BASE:0 < 1000
	RETURN 0
;기력이 500미만일 때는 불가
SIF BASE:1 < 500
	RETURN 0
;Ｕ확장경험 적으면 안 됨
SIF EXP:34 < 5
	RETURN 0
;쿠스코 사용 중에는 불가
SIF TEQUIP:21
	RETURN 0
;그밖의 Ｕ계 장착구 사용 중에는 불가
SIF (TEQUIP:32 && TEQUIP:32 != 3)
	RETURN 0
;남자일 경우, Ｃ계 장착구 사용 중에는 불가
SIF (TEQUIP:30 || TEQUIP:31) && TALENT:120
	RETURN 0
;욕실 플레이 중에는 플레이 매트가 없으면 불가
SIF TEQUIP:53 && ITEM:42 == 0 && NOITEM == 0
	RETURN 0
;샤워 중에는 불가
SIF TEQUIP:54
	RETURN 0
;○○목욕탕 입욕 중에는 불가
SIF TEQUIP:55
	RETURN 0
;학교수영복 플레이 중에는 불가
SIF TEQUIP:59 == 2
	RETURN 0
;나체정식 중에는 불가
SIF TEQUIP:60
	RETURN 0
RETURN 1

;--------------------------------------------------
;커맨드 본체
;--------------------------------------------------
;확장 벌룬을 마개조
@COM143
TSTR:7 = 벌룬카테터
IF FLAG:1100 & 1
	;技師パッチ有効なら特殊な名前チェック
	FLAG:1110 = 27
	CALL NAMECHECK_CUSTOMTOOL
	IF TSTR:8 == ITEMNAME:27
		TSTR:8 = 벌룬카테터
	ENDIF 
ELSE
	TSTR:8 = %TSTR:7%
	TSTR:9 = %TSTR:7%
ENDIF
PRINTFORML %TSTR:8%
TSTR:0 = 벌룬카테터
CALL TRAIN_MESSAGE_COM143

;-------------------------------------------------
;소스의 계산
;-------------------------------------------------
LOCAL:1 = 160

;EXP:U확장 경험을 본다
IF EXP:34 < EXPLV:1
	TIMES LOCAL:1 , 3.00
ELSEIF EXP:34 < EXPLV:2
	TIMES LOCAL:1 , 2.50
ELSEIF EXP:34 < EXPLV:3
	TIMES LOCAL:1 , 2.00
ELSEIF EXP:34 < EXPLV:4
	TIMES LOCAL:1 , 1.00
ELSEIF EXP:34 < EXPLV:5
	TIMES LOCAL:1 , 0.80
ELSE
	TIMES LOCAL:1 , 0.60
ENDIF

;겁쟁이
SIF TALENT:10
	TIMES LOCAL:1 , 2.00
;감정결여
SIF TALENT:22
	TIMES LOCAL:1 , 0.60
;작은 몸집 체형
SIF TALENT:128
	TIMES LOCAL:1 , 2.00

DOWNBASE:0 += LOCAL:1
DOWNBASE:1 += LOCAL:1 * 2

SOURCE:12 = 800
SOURCE:20 = 400
SOURCE:24 = 500
SOURCE:31 = 1000

;ABL:C감각을 본다
IF ABL:0 == 0
	SOURCE:0 = 80
	SOURCE:22 = 300
ELSEIF ABL:0 == 1
	SOURCE:0 = 250
	SOURCE:22 = 800
ELSEIF ABL:0 == 2
	SOURCE:0 = 600
	SOURCE:22 = 1400
ELSEIF ABL:0 == 3
	SOURCE:0 = 1000
	SOURCE:22 = 1800
ELSEIF ABL:0 == 4
	SOURCE:0 = 1300
	SOURCE:22 = 2100
ELSE
	SOURCE:0 = 1700
	SOURCE:22 = 2400
ENDIF

;ABL:마조끼를 본다
IF ABL:21 == 0
	SOURCE:20 += 2000
	SOURCE:22 += 200
	SOURCE:30 = 1000
	SOURCE:31 += 1000
	SOURCE:32 = 2000
ELSEIF ABL:21 == 1
	SOURCE:20 += 1600
	SOURCE:22 += 500
	SOURCE:30 = 2000
	SOURCE:31 += 1000
	SOURCE:32 = 1000
ELSEIF ABL:21 == 2
	SOURCE:20 += 1200
	SOURCE:22 += 800
	SOURCE:30 = 1000
	SOURCE:31 += 1000
	SOURCE:32 = 500
ELSEIF ABL:21 == 3
	SOURCE:20 += 800
	SOURCE:22 += 1200
	SOURCE:30 = 500
	SOURCE:31 += 500
	SOURCE:32 = 100
ELSEIF ABL:21 == 4
	SOURCE:20 += 600
	SOURCE:22 += 1500
	SOURCE:30 = 300
	SOURCE:31 += 100
	SOURCE:32 = 0
ELSE
	SOURCE:20 += 400
	SOURCE:22 += 2000
	SOURCE:30 = 150
	SOURCE:31 += 50
	SOURCE:32 = 0
ENDIF

;PALAM:윤활을 본다
IF PALAM:10 < PALAMLV:1
	TIMES SOURCE:0 , 0.40
	SOURCE:20 += 1600
ELSEIF PALAM:10 < PALAMLV:2
	TIMES SOURCE:0 , 0.80
	SOURCE:20 += 1000
ELSEIF PALAM:10 < PALAMLV:3
	TIMES SOURCE:0 , 1.00
	SOURCE:20 += 600
ELSEIF PALAM:10 < PALAMLV:4
	TIMES SOURCE:0 , 1.40
	SOURCE:20 += 250
ELSE
	TIMES SOURCE:0 , 1.80
	SOURCE:20 += 120
ENDIF

;PALAM:욕정을 본다
IF PALAM:13 < PALAMLV:1
	TIMES SOURCE:0 , 0.80
	TIMES SOURCE:22 , 0.60
ELSEIF PALAM:13 < PALAMLV:2
	TIMES SOURCE:0 , 0.90
	TIMES SOURCE:22 , 0.80
ELSEIF PALAM:13 < PALAMLV:3
	TIMES SOURCE:0 , 1.00
	TIMES SOURCE:22 , 1.00
ELSEIF PALAM:13 < PALAMLV:4
	TIMES SOURCE:0 , 1.10
	TIMES SOURCE:22 , 1.20
ELSE
	TIMES SOURCE:0 , 1.20
	TIMES SOURCE:22 , 1.40
ENDIF

;EXP:요도 개발 경험을 본다
IF EXP:46 < EXPLV:1
	TIMES SOURCE:0 , 0.20
	SOURCE:20 += 8000
ELSEIF EXP:46 < EXPLV:2
	TIMES SOURCE:0 , 0.60
	SOURCE:20 += 3600
ELSEIF EXP:46 < EXPLV:3
	TIMES SOURCE:0 , 1.00
	SOURCE:20 += 1200
ELSEIF EXP:46 < EXPLV:4
	TIMES SOURCE:0 , 1.20
	SOURCE:20 += 300
ELSEIF EXP:46 < EXPLV:5
	TIMES SOURCE:0 , 1.40
	SOURCE:20 += 30
ELSE
	TIMES SOURCE:0 , 1.60
	SOURCE:20 += 0
ENDIF

;ABL:순종을 본다
IF ABL:10 == 0
	TIMES SOURCE:0 , 0.80
ELSEIF ABL:10 == 1
	TIMES SOURCE:0 , 0.90
ELSEIF ABL:10 == 2
	TIMES SOURCE:0 , 1.00
ELSEIF ABL:10 == 3
	TIMES SOURCE:0 , 1.10
ELSEIF ABL:10 == 4
	TIMES SOURCE:0 , 1.20
ELSE
	TIMES SOURCE:0 , 1.30
ENDIF

;남자
SIF TALENT:120
	TIMES SOURCE:20 , 4.50
;작은 몸집 체형
SIF TALENT:128
	TIMES SOURCE:20 , 2.00
;요도광
SIF TALENT:175
	TIMES SOURCE:0 , 1.20

;C민감, 둔감을 본다
;쾌C자체의 체크는 다음에 정리해 준다
IF TALENT:101
	TIMES SOURCE:20 , 1.50
	TIMES SOURCE:22 , 1.50
	TIMES SOURCE:31 , 1.50
ELSEIF TALENT:100
	TIMES SOURCE:20 , 0.60
	TIMES SOURCE:22 , 0.60
	TIMES SOURCE:31 , 0.60
ENDIF

;처녀로 정조관념
SIF TALENT:0 == 1 && TALENT:30
	SOURCE:22 /= 3

IF FLAG:1100 & 1
	;技師パッチ有効なら道具使用時ソースチェックを呼ぶ
	;FLAG 1110:道具番号 1111:改善タイプ 1112:SOURCEの実値 
	FLAG:1110 = 27
	FLAG:1111 = 1
	FLAG:1112 = SOURCE:0
	CALL SOURCECHECK_CUSTOMTOOL
	SOURCE:0 = FLAG:1120
	FLAG:1112 = SOURCE:12
	CALL SOURCECHECK_CUSTOMTOOL
	SOURCE:12 = FLAG:1120
	FLAG:1112 = SOURCE:22
	CALL SOURCECHECK_CUSTOMTOOL
	SOURCE:22 = FLAG:1120
	FLAG:1111 = 2
	FLAG:1112 = SOURCE:20
	CALL SOURCECHECK_CUSTOMTOOL
	SOURCE:20 = FLAG:1120
ENDIF

;벌룬카테터의 착탈
IF TEQUIP:32 == 3
	TEQUIP:32 = 0
	LOCAL:2 = 2
	IF TEQUIP:12
		LOCAL:2 *= 2
		TEQUIP:12 = 0
	ENDIF
ELSE
	TEQUIP:32 = 3
	LOCAL:2 = 1
ENDIF

SIF EXP:46 <= 5
	TCVAR:50 += 1
;방뇨 레벨 증가
CFLAG:31 += LOCAL:2
SIF BASE:4
	BASE:4 = 0
TCVAR:46 += LOCAL:2
TCVAR:34 += 1

;커맨드 속성：도구(조교자)
TFLAG:98 = 1
;촉수 경험 플래그
TFLAG:90 = 0
RETURN 1

;--------------------------------------------------
;벌룬카테터 삽입중
;--------------------------------------------------
@EQUIP_COM143
TSTR:7 = 벌룬카테터
IF FLAG:1100 & 1
	;技師パッチ有効なら特殊な名前チェック
	FLAG:1110 = 27
	CALL NAMECHECK_CUSTOMTOOL
ENDIF
PRINTFORML <%TSTR:7% 삽입 중>

LOCAL:3 = 80

;EXP:U확장 경험을 본다
IF EXP:34 < EXPLV:1
	TIMES LOCAL:3 , 3.00
ELSEIF EXP:34 < EXPLV:2
	TIMES LOCAL:3 , 2.50
ELSEIF EXP:34 < EXPLV:3
	TIMES LOCAL:3 , 2.00
ELSEIF EXP:34 < EXPLV:4
	TIMES LOCAL:3 , 1.00
ELSEIF EXP:34 < EXPLV:5
	TIMES LOCAL:3 , 0.80
ELSE
	TIMES LOCAL:3 , 0.60
ENDIF	

;겁쟁이
SIF TALENT:10
	TIMES LOCAL:3 , 2.00
;감정결여
SIF TALENT:22
	TIMES LOCAL:3 , 0.60

DOWNBASE:0 += LOCAL:3
DOWNBASE:1 += LOCAL:3 * 2

;ABL:C감각을 본다
IF ABL:0 == 0
	LOCAL = 80
	LOCAL:1 = 300
ELSEIF ABL:0 == 1
	LOCAL = 250
	LOCAL:1 = 800
ELSEIF ABL:0 == 2
	LOCAL = 600
	LOCAL:1 = 1400
ELSEIF ABL:0 == 3
	LOCAL = 1000
	LOCAL:1 = 1800
ELSEIF ABL:0 == 4
	LOCAL = 1300
	LOCAL:1 = 2100
ELSE
	LOCAL = 1700
	LOCAL:1 = 2400
ENDIF

;EXP:요도 경험을 본다
IF EXP:46 < EXPLV:1
	TIMES LOCAL , 0.50
	LOCAL:2 = 2000
ELSEIF EXP:46 < EXPLV:2
	TIMES LOCAL , 1.00
	LOCAL:2 = 300
ELSEIF EXP:46 < EXPLV:3
	TIMES LOCAL , 1.10
	LOCAL:2 = 50
ELSEIF EXP:46 < EXPLV:4
	TIMES LOCAL , 1.20
	LOCAL:2 = 10
ELSEIF EXP:46 < EXPLV:5
	TIMES LOCAL , 1.40
	LOCAL:2 = 0
ELSE
	TIMES LOCAL , 1.60
	LOCAL:2 = 0
ENDIF

;PALAM:윤활을 본다
IF PALAM:10 < PALAMLV:1
	TIMES LOCAL , 0.40
	LOCAL:2 += 800
ELSEIF PALAM:10 < PALAMLV:2
	TIMES LOCAL , 0.80
	LOCAL:2 += 500
ELSEIF PALAM:10 < PALAMLV:3
	TIMES LOCAL , 1.00
	LOCAL:2 += 300
ELSEIF PALAM:10 < PALAMLV:4
	TIMES LOCAL , 1.40
	LOCAL:2 += 120
ELSE
	TIMES LOCAL , 1.80
	LOCAL:2 += 100
ENDIF

;PALAM:욕정을 본다
IF PALAM:13 < PALAMLV:1
	TIMES LOCAL , 0.80
ELSEIF PALAM:13 < PALAMLV:2
	TIMES LOCAL , 0.90
ELSEIF PALAM:13 < PALAMLV:3
	TIMES LOCAL , 1.00
ELSEIF PALAM:13 < PALAMLV:4
	TIMES LOCAL , 1.10
ELSE
	TIMES LOCAL , 1.20
ENDIF

;ABL:순종을 본다
IF ABL:10 == 0
	TIMES LOCAL , 0.80
ELSEIF ABL:10 == 1
	TIMES LOCAL , 0.90
ELSEIF ABL:10 == 2
	TIMES LOCAL , 1.00
ELSEIF ABL:10 == 3
	TIMES LOCAL , 1.10
ELSEIF ABL:10 == 4
	TIMES LOCAL , 1.20
ELSE
	TIMES LOCAL , 1.30
ENDIF

;남자
SIF TALENT:120
	TIMES LOCAL:2 , 4.50
;작은 몸집 체형
SIF TALENT:128
	TIMES LOCAL:2 , 2.00
;요도광
SIF TALENT:175
	TIMES LOCAL , 1.20

;C민감, 둔감을 본다
;쾌C자체의 체크는 다음에 정리해 준다
IF TALENT:101
	TIMES LOCAL , 1.50
	TIMES LOCAL:1 , 1.50
	TIMES LOCAL:2 , 1.50
ELSEIF TALENT:100
	TIMES LOCAL , 0.60
	TIMES LOCAL:1 , 0.60
	TIMES LOCAL:2 , 0.60
ENDIF

;처녀로 정조관념
SIF TALENT:0 == 1 && TALENT:30
	LOCAL:1 /= 3

LOCAL:3 = LOCAL:1
IF FLAG:1100 & 1
	;技師パッチ有効なら道具使用時ソースチェックを呼ぶ
	;FLAG 1110:道具番号 1111:改善タイプ 1112:SOURCEの実値 
	FLAG:1110 = 27
	FLAG:1111 = 1
	FLAG:1112 = LOCAL
	CALL SOURCECHECK_CUSTOMTOOL
	LOCAL = FLAG:1120
	FLAG:1112 = LOCAL:1
	CALL SOURCECHECK_CUSTOMTOOL
	LOCAL:1 = FLAG:1120
	FLAG:1111 = 3
	FLAG:1112 = LOCAL:2
	CALL SOURCECHECK_CUSTOMTOOL
	LOCAL:2 = FLAG:1120
ENDIF

SOURCE:0 += LOCAL
SOURCE:22 += LOCAL:1
SOURCE:20 += LOCAL:2
SOURCE:31 += LOCAL:3

;벌룬카테터 삽입중 때는 강제적으로 뇨의를 개최되는 효과도(=방뇨 레벨 증가)
IF TEQUIP:12
	CFLAG:31 += 2
ELSE
	CFLAG:31 += 1
ENDIF

CALL EQUIP_MESSAGE_COM143

TCVAR:46 += 3
TCVAR:34 += 1

IF TALENT:120 == 0 && TALENT:PLAYER:120 == 0
	TCVAR:12 += 1
ELSEIF TALENT:120 && TALENT:PLAYER:120
	TCVAR:13 += 1
ENDIF
;촉수 경험 플래그
SIF TEQUIP:90
	TFLAG:90 += 2
RETURN 1

;--------------------------------------------------
;조교시 메시지
;--------------------------------------------------
@TRAIN_MESSAGE_COM143
CALL KOJO_TRAIN_MESSAGE_COM
SIF FLAG:10 == 0 || CFLAG:99 == 1 || RESULT != 0
	RETURN 0

PRINTL
IF TEQUIP:32 == 3
	PRINTFORML %CALLNAME:TARGET%의 넓혀진 요도로부터 %조사선택(TSTR:7, "를")% 뽑았다….
	IF EXP:34 < EXPLV:1
		PRINTFORMW %CALLNAME:TARGET%의 요도는 천천히 닫혀간다.
	ELSEIF EXP:34 < EXPLV:2
		PRINTFORMW %CALLNAME:TARGET%의 요도는 벌름거리면서 천천히 닫혀간다.
	ELSEIF EXP:34 < EXPLV:3
		PRINTFORMW %CALLNAME:TARGET%의 요도는 넓혀져 입을 열고 있다.
	ELSEIF EXP:34 < EXPLV:4
		PRINTFORMW %CALLNAME:TARGET%의 요도는 넓혀진 채로 좀처럼 돌아오지 않는 것 같다.
	ELSE
		PRINTFORMW %CALLNAME:TARGET%의 요도로부터 방광 안쪽이 훤히 보이게 되었다.
	ENDIF
ELSE
	PRINTFORMW %CALLNAME:TARGET%의 요도에 %조사선택(TSTR:9, "를")% 끼워, 바람을 불어넣었다….
ENDIF

;--------------------------------------------------
;장착시 메시지
;--------------------------------------------------
@EQUIP_MESSAGE_COM143
CALL KOJO_EQUIP_MESSAGE_COM
SIF FLAG:10 == 0 || CFLAG:99 == 1 || RESULT != 0
	RETURN 0

PRINTL
PRINTFORML %조사선택(CALLNAME:TARGET, "는")% 요도에 %조사선택(TSTR:9, "를")% 넣은 채로 계속 괴롭혀지고 있다….
PRINTL
IF ABL:21 == 0
	PRINTFORML %조사선택(CALLNAME:TARGET, "는")% 요도에 들어있는 이물을 받아들일 수 없는지, 고뇌의 표정을 짓고 있다.
ELSEIF ABL:21 == 1
	PRINTFORML %조사선택(CALLNAME:TARGET, "는")% 질끈 눈을 감고 결사적으로 오줌을 참고 있다.
ELSEIF ABL:21 == 2
	PRINTFORML %조사선택(CALLNAME:TARGET, "는")% 오줌을 참으려 얼굴을 붉게 불들이고 허벅지를 모으고 있다.
ELSEIF ABL:21 == 3
	PRINTFORML %조사선택(CALLNAME:TARGET, "는")% 하복부의 격통에 시달리면서도, 때때로 황홀한 표정을 짓고 있다.
ELSEIF ABL:21 >= 5 && TALENT:45
	PRINTFORML %조사선택(CALLNAME:TARGET, "는")% 요도의 자극을 마음껏 만끽하고 있다.
	PRINTFORML %CALLNAME:TARGET%의 입은 쾌감으로 헤 벌려지고 \@(TALENT:120) ? 거기에서 # 거기와 질구에서\@ 끈적한 군침이 방울져 떨어지고 있다.
ELSE
	PRINTFORML %조사선택(CALLNAME:TARGET, "는")% 요의와 요도통을 쾌감으로 느끼고 있는지, 넋을 잃은 표정을 하고 있다….
ENDIF
WAIT
