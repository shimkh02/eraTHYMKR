;=============================================================================
;바기나전극
;=============================================================================
;………………………………………………
;실행 판정
;………………………………………………
@COM_ABLE155
;개별 설정 커맨드 필터
SIF DITEMTYPE:999:155 > 0
	RETURN 0
;해제는 언제든 가능
SIF TEQUIP:20 == 4
	RETURN 1
;Ｖ계 확장 커맨드를 유효로 하지 않으면 불가
SIF (FLAG:15 & 2) == 0
	RETURN 0
;Ｖ조교 필터가 오프되어 있지 않으면 불가
SIF FLAG:2 & 1
	RETURN 0
;승마 중에는 불가
SIF TEQUIP:43
	RETURN 0
;기저귀 플레이 중에는 불가
SIF TEQUIP:58
	RETURN 0
;훈도시 착용 중일 경우는 안 됨
SIF TEQUIP:59 == 17
	RETURN 0
;촉수 조교 중에는 불가
SIF TEQUIP:90
	RETURN 0
;아이템을 가지고 있거나, 아이템 없음 설정이 아니면 불가
SIF ITEM:7 == 0 && NOITEM == 0
	RETURN 0
;처녀면 불가
SIF TALENT:0 == 1
	RETURN 0
;남자라면 안 됨
SIF TALENT:120
	RETURN 0
;조교 대상이 소인체형일 경우에는 주인이 금단의 지식 보유자가 아니면 불가
SIF TALENT:127 && TALENT:MASTER:56 == 0
	RETURN 0
;기계면 불가
SIF TALENT:206
	RETURN 0
;윤활 부족이고, 순종4미만 · 레즈끼4미만의 ASSI일 경우, 조교 대상이 동성이라면 실행할 수 없다(새드거나 새드끼Lv3이상, 또는 망신이나 복종이 있다면 ＯＫ)
IF PALAM:10 < PALAMLV:2 && ASSIPLAY
	SIF (ABL:ASSI:10 < 4 || (TALENT:120 == 0 && TALENT:ASSI:120 == 0 && ABL:ASSI:22 < 4)) && TALENT:ASSI:81 == 0 && ABL:ASSI:20 < 3 && TALENT:ASSI:151 == 0 && TALENT:ASSI:160 == 0
		RETURN 0
ENDIF
;그밖의 Ｖ계 장착구 사용 중에는 불가
SIF TEQUIP:20
	RETURN 0
;쿠스코 삽입 중에는 불가
SIF TEQUIP:21
	RETURN 0
;욕실 플레이 중, ○○ 욕탕 입욕 중에는 불가
SIF TEQUIP:53 || TEQUIP:55
	RETURN 0
;샤워 중에는 불가
SIF TEQUIP:54
	RETURN 0
;학교수영복 플레이 중에는 불가
SIF TEQUIP:59 == 2
	RETURN 0
;나체정식 중에는 불가
SIF TEQUIP:60
	RETURN 0
RETURN 1

;--------------------------------------------------
;커맨드 본체
;--------------------------------------------------
@COM155
TSTR:7 = 전극
IF FLAG:1100 & 1
	;技師パッチ有効なら特殊な名前チェック
	FLAG:1110 = 7
	CALL NAMECHECK_CUSTOMTOOL
	IF (TSTR:8 != "전극") && (TSTR:8 != ITEMNAME:7)
		PRINTFORML %TSTR:8% 삽입
	ELSE
		PRINTL 바기나 전극
	ENDIF
ELSE
	TSTR:9 = %TSTR:7%
	PRINTL 바기나 전극
ENDIF
TSTR:0 = 보지 전극
CALL TRAIN_MESSAGE_COM155

;V경험을 수반하는 커맨드의 플래그(처녀막 재생 대응)
TFLAG:1 = 1

DOWNBASE:0 += 90
DOWNBASE:1 += 140

SOURCE:31 = 1100

;ABL:V감각을 본다
IF ABL:1 == 0
	SOURCE:1 = 200
	SOURCE:20 = 800
	SOURCE:22 = 400
ELSEIF ABL:1 == 1
	SOURCE:1 = 500
	SOURCE:20 = 800
	SOURCE:22 = 700
ELSEIF ABL:1 == 2
	SOURCE:1 = 900
	SOURCE:20 = 800
	SOURCE:22 = 1150
ELSEIF ABL:1 == 3
	SOURCE:1 = 1700
	SOURCE:20 = 1000
	SOURCE:22 = 1750
ELSEIF ABL:1 == 4
	SOURCE:1 = 2200
	SOURCE:20 = 1200
	SOURCE:22 = 3000
ELSE
	SOURCE:1 = 3400
	SOURCE:20 = 1400
	SOURCE:22 = 4500
ENDIF

;EXP:V경험을 본다
IF EXP:0 < EXPLV:1
	TIMES SOURCE:1 , 0.20
	SOURCE:20 += 5500
ELSEIF EXP:0 < EXPLV:2
	TIMES SOURCE:1 , 0.60
	SOURCE:20 += 300
ELSEIF EXP:0 < EXPLV:3
	TIMES SOURCE:1 , 1.00
	SOURCE:20 += 50
ELSEIF EXP:0 < EXPLV:4
	TIMES SOURCE:1 , 1.20
	SOURCE:20 += 10
ELSEIF EXP:0 < EXPLV:5
	TIMES SOURCE:1 , 1.40
	SOURCE:20 += 0
ELSE
	TIMES SOURCE:1 , 1.60
	SOURCE:20 += 0
ENDIF

;PALAM:윤활을 본다
IF PALAM:10 < PALAMLV:1
	TIMES SOURCE:1 , 0.10
	SOURCE:20 += 1000
	TIMES SOURCE:20 , 3.00
ELSEIF PALAM:10 < PALAMLV:2
	TIMES SOURCE:1 , 0.40
	SOURCE:20 += 400
	TIMES SOURCE:20 , 1.00
ELSEIF PALAM:10 < PALAMLV:3
	TIMES SOURCE:1 , 1.00
	TIMES SOURCE:20 , 0.50
ELSEIF PALAM:10 < PALAMLV:4
	TIMES SOURCE:1 , 1.40
	TIMES SOURCE:20 , 0.20
ELSE
	TIMES SOURCE:1 , 1.80
	TIMES SOURCE:20 , 0.10
ENDIF

;PALAM:욕정을 본다
IF PALAM:13 < PALAMLV:1
	TIMES SOURCE:1 , 0.80
ELSEIF PALAM:13 < PALAMLV:2
	TIMES SOURCE:1 , 0.90
ELSEIF PALAM:13 < PALAMLV:3
	TIMES SOURCE:1 , 1.00
ELSEIF PALAM:13 < PALAMLV:4
	TIMES SOURCE:1 , 1.10
ELSE
	TIMES SOURCE:1 , 1.20
ENDIF

;ABL:순종을 본다
IF ABL:10 == 0
	TIMES SOURCE:1 , 0.80
ELSEIF ABL:10 == 1
	TIMES SOURCE:1 , 0.90
ELSEIF ABL:10 == 2
	TIMES SOURCE:1 , 1.00
ELSEIF ABL:10 == 3
	TIMES SOURCE:1 , 1.10
ELSEIF ABL:10 == 4
	TIMES SOURCE:1 , 1.20
ELSE
	TIMES SOURCE:1 , 1.30
ENDIF

;작은 몸집 체형
SIF TALENT:128
	TIMES SOURCE:20 , 2.00

;V민감, 둔감을 본다
;쾌V자체의 체크는 다음에 정리해 준다
IF TALENT:103
	TIMES SOURCE:20 , 1.50
	TIMES SOURCE:22 , 1.50
	TIMES SOURCE:31 , 1.50
ELSEIF TALENT:102
	TIMES SOURCE:20 , 0.60
	TIMES SOURCE:22 , 0.60
	TIMES SOURCE:31 , 0.60
ENDIF

SIF TALENT:0 == 1 && TALENT:30
	SOURCE:22 /= 3

IF FLAG:1100 & 1
	;技師パッチ有効なら道具使用時ソースチェックを呼ぶ
	;FLAG 1110:道具番号 1111:改善タイプ 1112:SOURCEの実値 
	FLAG:1110 = 7
	FLAG:1111 = 1
	FLAG:1112 = SOURCE:1
	CALL SOURCECHECK_CUSTOMTOOL
	SOURCE:1 = FLAG:1120
	FLAG:1112 = SOURCE:22
	CALL SOURCECHECK_CUSTOMTOOL
	SOURCE:22 = FLAG:1120
	FLAG:1111 = 2
	FLAG:1112 = SOURCE:20
	CALL SOURCECHECK_CUSTOMTOOL
	SOURCE:20 = FLAG:1120
ENDIF

TCVAR:0 += 5

;커맨드 속성：도구(조교자)
TFLAG:98 = 1

;전극의 착탈
IF TEQUIP:20
	TEQUIP:20 = 0
ELSE
	TEQUIP:20 = 4
ENDIF
;촉수 경험 플래그
TFLAG:90 = 0
RETURN 1

;--------------------------------------------------
;보지 전극 삽입중
;--------------------------------------------------
@EQUIP_COM155
TSTR:7 = 바기나 전극
IF FLAG:1100 & 1
	;技師パッチ有効なら特殊な名前チェック
	FLAG:1110 = 7
	CALL NAMECHECK_CUSTOMTOOL
ENDIF
PRINTFORML <%TSTR:7% 삽입 중>

DOWNBASE:0 += 70
DOWNBASE:1 += 110

;ABL:V감각을 본다
IF ABL:1 == 0
	LOCAL = 250
	LOCAL:1 = 400
ELSEIF ABL:1 == 1
	LOCAL = 600
	LOCAL:1 = 700
ELSEIF ABL:1 == 2
	LOCAL = 1200
	LOCAL:1 = 1150
ELSEIF ABL:1 == 3
	LOCAL = 1900
	LOCAL:1 = 1750
ELSEIF ABL:1 == 4
	LOCAL = 2500
	LOCAL:1 = 3000
ELSE
	LOCAL = 3900
	LOCAL:1 = 4500
ENDIF

;EXP:V경험을 본다
;처녀는 있을 수 없다
IF EXP:0 < EXPLV:2
	TIMES LOCAL , 0.60
	LOCAL:2 = 150
ELSEIF EXP:0 < EXPLV:3
	TIMES LOCAL , 1.00
	LOCAL:2 = 20
ELSEIF EXP:0 < EXPLV:4
	TIMES LOCAL , 1.20
	LOCAL:2 = 0
ELSEIF EXP:0 < EXPLV:5
	TIMES LOCAL , 1.40
	LOCAL:2 = 0
ELSE
	TIMES LOCAL , 1.60
	LOCAL:2 = 0
ENDIF

;PALAM:윤활을 본다
IF PALAM:10 < PALAMLV:1
	TIMES LOCAL , 0.10
	LOCAL:2 += 400
ELSEIF PALAM:10 < PALAMLV:2
	TIMES LOCAL , 0.40
	LOCAL:2 += 150
ELSEIF PALAM:10 < PALAMLV:3
	TIMES LOCAL , 1.00
	TIMES LOCAL:2 , 0.50
ELSEIF PALAM:10 < PALAMLV:4
	TIMES LOCAL , 1.40
	TIMES LOCAL:2 , 0.20
ELSE
	TIMES LOCAL , 1.80
	TIMES LOCAL:2 , 0.10
ENDIF

;PALAM:욕정을 본다
IF PALAM:13 < PALAMLV:1
	TIMES LOCAL , 0.80
ELSEIF PALAM:13 < PALAMLV:2
	TIMES LOCAL , 0.90
ELSEIF PALAM:13 < PALAMLV:3
	TIMES LOCAL , 1.00
ELSEIF PALAM:13 < PALAMLV:4
	TIMES LOCAL , 1.10
ELSE
	TIMES LOCAL , 1.20
ENDIF

;ABL:순종을 본다
IF ABL:10 == 0
	TIMES LOCAL , 0.80
ELSEIF ABL:10 == 1
	TIMES LOCAL , 0.90
ELSEIF ABL:10 == 2
	TIMES LOCAL , 1.00
ELSEIF ABL:10 == 3
	TIMES LOCAL , 1.10
ELSEIF ABL:10 == 4
	TIMES LOCAL , 1.20
ELSE
	TIMES LOCAL , 1.30
ENDIF

;작은 몸집 체형
SIF TALENT:128
	TIMES LOCAL:2 , 2.00

;V민감, 둔감을 본다
;쾌V자체의 체크는 다음에 정리해 준다
IF TALENT:103
	TIMES LOCAL , 1.50
	TIMES LOCAL:1 , 1.50
	TIMES LOCAL:2 , 1.50
ELSEIF TALENT:102
	TIMES LOCAL , 0.60
	TIMES LOCAL:1 , 0.60
	TIMES LOCAL:2 , 0.60
ENDIF

IF FLAG:1100 & 1
	;技師パッチ有効なら道具使用時ソースチェックを呼ぶ
	;FLAG 1110:道具番号 1111:改善タイプ 1112:SOURCEの実値 
	FLAG:1110 = 7
	FLAG:1111 = 1
	FLAG:1112 = LOCAL
	CALL SOURCECHECK_CUSTOMTOOL
	LOCAL:0 = FLAG:1120
	FLAG:1112 = LOCAL:1
	CALL SOURCECHECK_CUSTOMTOOL
	LOCAL:1 = FLAG:1120
	FLAG:1111 = 3
	FLAG:1112 = LOCAL:2
	CALL SOURCECHECK_CUSTOMTOOL
	LOCAL:2 = FLAG:1120
ENDIF

SOURCE:1 += LOCAL
SOURCE:22 += LOCAL:1
SOURCE:20 += LOCAL:2
SOURCE:31 += 1100

TCVAR:0 += 5

IF TALENT:120 == 0 && TALENT:PLAYER:120 == 0
	TCVAR:12 += 1
ELSEIF TALENT:120 && TALENT:PLAYER:120
	TCVAR:13 += 1
ENDIF
;촉수 경험 플래그
SIF TEQUIP:90
	TFLAG:90 += 1

RETURN 1

;--------------------------------------------------
;조교시 메시지
;--------------------------------------------------
@TRAIN_MESSAGE_COM155
CALL KOJO_TRAIN_MESSAGE_COM
SIF FLAG:10 == 0 || CFLAG:99 == 1 || RESULT != 0
	RETURN 0

PRINTL
;전극의 착탈
IF TEQUIP:20
	IF MARK:9 > 1
		PRINTFORMW %조사선택(CALLNAME:PLAYER, "가")% 미처 손을 쓰기도 전에, %조사선택(CALLNAME:TARGET, "는")% 보지로부터 %조사선택(TSTR:7, "를")% 뽑아 던져버렸다….
	ELSE
		PRINTFORMW %조사선택(CALLNAME:PLAYER, "는")% %CALLNAME:TARGET%의 보지로부터 %조사선택(TSTR:7, "를")% 뽑아 주었다….
	ENDIF
ELSE
	IF MARK:9 > 1
		PRINTFORML %조사선택(CALLNAME:PLAYER, "는")% 싫어서 버둥대는 %조사선택(CALLNAME:TARGET, "를")% 억눌렀다.
		PRINTFORM 그리고, %CALLNAME:TARGET%의 
		SIF PALAM:10 >= PALAMLV:4
			PRINT 흠뻑 젖어있는 
		PRINTFORMW 질에 %조사선택(TSTR:9, "를")% 삽입해 스위치를 넣었다….
	ELSE
		PRINTFORM %조사선택(CALLNAME:PLAYER, "는")% %CALLNAME:TARGET%의 
		SIF PALAM:10 >= PALAMLV:4
			PRINT 흠뻑 젖어있는 
		PRINTFORMW 질에 %조사선택(TSTR:9, "를")% 삽입해 스위치를 넣었다….
	ENDIF
ENDIF
