;=============================================================================
;조수주인자위공연
;=============================================================================
;………………………………………………
;실행 판정
;………………………………………………
@COM_ABLE116
;개별 설정 커맨드 필터
SIF DITEMTYPE:999:116 > 0
	RETURN 0
;수치 플레이계 확장 커맨드를 유효로 하지 않으면 불가
SIF (FLAG:15 & 512) == 0
	RETURN 0
;조수가 없으면 불가
SIF ASSI < 0
	RETURN 0
;조수로는 안 됨
SIF ASSIPLAY
	RETURN 0
;野外もしくは公開自慰を行ったことがない場合の野外およびビデオ撮影中は, 이상계 조교 필터가 오프되어 있지 않으면 불가
SIF FLAG:2 & 4 && (CFLAG:ASSI:21 & 1) == 0 && (TEQUIP:50 || TEQUIP:52)
	RETURN 0
;수면 중에는 불가
SIF EQUIP:14
	RETURN 0
;실신 중에는 불가
SIF TFLAG:899 > 0
	RETURN 0
;조수가 훈도시 착용 중일 경우 助手に기교4이상 필수
SIF ASSI >= 0 && TEQUIP:ASSI:59 == 17 && ABL:ASSI:12 < 4
	RETURN 0
;眼隠しと耳栓両方つけていた場合は意味がないので実行できない
SIF TEQUIP:41 && TEQUIP:45
	RETURN 0
;욕실 플레이 중에는 플레이 매트가 없으면 불가
SIF TEQUIP:53 && ITEM:42 == 0 && NOITEM == 0
	RETURN 0
RETURN 1

;--------------------------------------------------
;커맨드 본체
;--------------------------------------------------
;자위보여주기 더블판
@COM116
;특정의 주인과 조수의 편성을 조사한다
CALL JUDGE_TRAIN_PAIR, MASTER, ASSI
IF TFLAG:175 == 1
	LOCALS = 홍마자매 경연에 의한
ELSEIF TFLAG:175 == 2
	LOCALS = 겨드랑이 무녀 두 명에 의한 신성한
ELSEIF TFLAG:175 == 3
	LOCALS = 가을의 소득을 축하하는 2기둥의
ELSEIF TFLAG:175 == 4
	LOCALS = 달 자매에 의한
ELSEIF TFLAG:175 == 5
	LOCALS = 비봉클럽판
ELSEIF TFLAG:175 == 6
	LOCALS = 춘고정, W릴리에 의한
ELSEIF TFLAG:175 == 7
	LOCALS = 사토리 자매에 의한 추잡한
ELSEIF TFLAG:175 == 8
	LOCALS = 몽환 자매에 의한 환상적인
ELSEIF TFLAG:175 == 9
	LOCALS = 유유유 경연에 의한
ELSEIF TFLAG:175 == 10 || TFLAG:175 == 11 || TFLAG:175 == 12
	LOCALS = 소령자매에 의한
ELSE
	LOCALS = 
ENDIF

PRINTFORML %LOCALS% 조수주인자위공연
TSTR:0 = %LOCALS% 조수주인자위공연
CALL TRAIN_MESSAGE_COM116

;-------------------------------------------------
;사정 게이지 체크(주인)
;-------------------------------------------------
LOCAL = 0

;주인의 기교를 본다
IF ABL:MASTER:12 == 0
	LOCAL = 800
ELSEIF ABL:MASTER:12 == 1
	LOCAL = 1600
ELSEIF ABL:MASTER:12 == 2
	LOCAL = 2400
ELSEIF ABL:MASTER:12 == 3
	LOCAL = 3200
ELSEIF ABL:MASTER:12 == 4
	LOCAL = 4000
ELSE
	LOCAL = 4800
ENDIF

;주인의 C감각을 본다
LOCAL = EJACULATIONCHECK_ABL_0_2(MASTER, LOCAL)

SIF TALENT:MASTER:120 || TALENT:MASTER:121
	BASE:MASTER:2 += LOCAL

;-------------------------------------------------
;사정 게이지 체크(조수)
;-------------------------------------------------
LOCAL:1 = 0

;조수의 기교를 본다
IF ABL:ASSI:12 == 0
	LOCAL:1 = 800
ELSEIF ABL:ASSI:12 == 1
	LOCAL:1 = 1600
ELSEIF ABL:ASSI:12 == 2
	LOCAL:1 = 2400
ELSEIF ABL:ASSI:12 == 3
	LOCAL:1 = 3200
ELSEIF ABL:ASSI:12 == 4
	LOCAL:1 = 4000
ELSE
	LOCAL:1 = 4800
ENDIF

;조수의 C감각을 본다
LOCAL:1 = EJACULATIONCHECK_ABL_0_2(ASSI, LOCAL:1)

SIF TALENT:ASSI:120 || TALENT:ASSI:121
	BASE:ASSI:2 += LOCAL:1

;-------------------------------------------------
;소스의 계산
;-------------------------------------------------
DOWNBASE:0 += 5
DOWNBASE:1 += 15

SOURCE:12 = 500
SOURCE:30 = 100
SOURCE:31 = 600

;ABL:자위 중독을 본다
IF ABL:30 == 0
	SOURCE:15 = 50
	SOURCE:22 = 150
	TIMES SOURCE:30 , 4.00
ELSEIF ABL:30 == 1
	SOURCE:15 = 100
	SOURCE:22 = 120
	TIMES SOURCE:30 , 2.50
ELSEIF ABL:30 == 2
	SOURCE:15 = 150
	SOURCE:22 = 105
	TIMES SOURCE:30 , 1.50
ELSEIF ABL:30 == 3
	SOURCE:15 = 200
	SOURCE:22 = 90
	TIMES SOURCE:30 , 1.00
ELSEIF ABL:30 == 4
	SOURCE:15 = 250
	SOURCE:22 = 75
	TIMES SOURCE:30 , 0.50
ELSE
	SOURCE:15 = 300
	SOURCE:22 = 0
	TIMES SOURCE:30 , 0.10
ENDIF

;PALAM:욕정을 본다
IF PALAM:13 < PALAMLV:1
	SOURCE:18 = 37
ELSEIF PALAM:13 < PALAMLV:2
	SOURCE:18 = 75
ELSEIF PALAM:13 < PALAMLV:3
	SOURCE:18 = 150
ELSEIF PALAM:13 < PALAMLV:4
	SOURCE:18 = 300
ELSE
	SOURCE:18 = 600
ENDIF

;-------------------------------------------------
;독심의 경우 에로 있고 기분이 옮기는지 과시하고 효과 업(1.8배)
;-------------------------------------------------
;캐릭터 전용 커맨드가 온의 경우, 대상이[연모]와 마음의 눈을 닫지 않은[깨닫고]소유라면 변화
IF TALENT:213 == 1 && TALENT:150 && (FLAG:15 & 16384)
	TIMES SOURCE:15 , 1.80
	TIMES SOURCE:18 , 1.80
ENDIF

;-------------------------------------------------
;사정 체크(주인)
;-------------------------------------------------
TFLAG:91 = SAMEN_CHECK(MASTER)

;사정시의 처리
IF TFLAG:91 >= 1
	;커맨드 마다 고유의 처리는 이 아래에 쓴다
	TIMES SOURCE:15 , 3.00

	;ABL:정액 중독을 본다
	IF ABL:31 == 0
		SOURCE:24 = 0
		TIMES SOURCE:22, 2.00
	ELSEIF ABL:31 == 1
		SOURCE:24 = 200
		TIMES SOURCE:22, 1.60
	ELSEIF ABL:31 == 2
		SOURCE:24 = 600
		TIMES SOURCE:22, 1.00
	ELSEIF ABL:31 == 3
		SOURCE:24 = 1500
		TIMES SOURCE:22, 0.70
	ELSEIF ABL:31 == 4
		SOURCE:24 = 3000
		TIMES SOURCE:22, 0.40
	ELSE
		SOURCE:24 = 6000
		TIMES SOURCE:22, 0.10
	ENDIF

	;사정처 타입의 설정
	TFLAG:703 = 3
	;사정처 선택의 가능·불가
	TFLAG:704 = 1
	;사정자 판정용의 임시 변수
	TFLAG:93 = MASTER
	;범용적인 처리는 이 함수로 실시한다
	;(사정 게이지의 재계산이나 사정 경험의 상승과 공통 부분의 표시)
	CALL SAMEN_SHOOT
ENDIF

;사정에 관계없이 행해지는 처리는 여기로부터
;주인의 손가락⇔주인의 P의 불결이 이동
IF TALENT:MASTER:120 || TALENT:MASTER:121
	STAIN:MASTER:1 |= STAIN:MASTER:2
	STAIN:MASTER:2 |= STAIN:MASTER:1
;주인의 손가락⇔주인의 V의 불결이 이동
ELSE
	STAIN:MASTER:3 |= STAIN:MASTER:2
	STAIN:MASTER:2 |= STAIN:MASTER:3
ENDIF

;-------------------------------------------------
;사정 체크 2
;-------------------------------------------------
TFLAG:91 = SAMEN_CHECK(ASSI)

;사정시의 처리
IF TFLAG:91 >= 1
	;커맨드 마다 고유의 처리는 이 아래에 쓴다

	;ABL:정액 중독을 본다
	IF ABL:31 == 0
		SOURCE:24 += 0
		TIMES SOURCE:22, 2.00
	ELSEIF ABL:31 == 1
		SOURCE:24 += 200
		TIMES SOURCE:22, 1.60
	ELSEIF ABL:31 == 2
		SOURCE:24 += 600
		TIMES SOURCE:22, 1.00
	ELSEIF ABL:31 == 3
		SOURCE:24 += 1500
		TIMES SOURCE:22, 0.70
	ELSEIF ABL:31 == 4
		SOURCE:24 += 3000
		TIMES SOURCE:22, 0.40
	ELSE
		SOURCE:24 += 6000
		TIMES SOURCE:22, 0.10
	ENDIF

	;사정처 타입의 설정
	TFLAG:703 = 3
	;사정처 선택의 가능·불가
	TFLAG:704 = 0
	;사정자 판정용의 임시 변수
	TFLAG:93 = ASSI
	;범용적인 처리는 이 함수로 실시한다
	;(사정 게이지의 재계산이나 사정 경험의 상승과 공통 부분의 표시)
	CALL SAMEN_SHOOT
ENDIF

;조수의 손가락⇔조수의 P의 불결이 이동
IF TALENT:ASSI:120 || TALENT:ASSI:121
	STAIN:ASSI:1 |= STAIN:ASSI:2
	STAIN:ASSI:2 |= STAIN:ASSI:1
;조수의 손가락⇔조수의 V의 불결이 이동
ELSE
	STAIN:ASSI:3 |= STAIN:ASSI:2
	STAIN:ASSI:2 |= STAIN:ASSI:3
ENDIF

;주인이나 조수나 후타나리
SIF TALENT:MASTER:121 || TALENT:ASSI:121
	SOURCE:22 /= 2
;주인이나 조수도 후타나리
SIF TALENT:MASTER:121 && TALENT:ASSI:121
	SOURCE:22 /= 2

IF TALENT:120 == 0 && TALENT:MASTER:120 == 0 && TALENT:ASSI:120 == 0
	TCVAR:12 += 1
ELSEIF TALENT:120 && TALENT:MASTER:120 && TALENT:ASSI:120
	TCVAR:13 += 1
ENDIF

;자위 경험
TCVAR:MASTER:10 += 1
TCVAR:ASSI:10 += 1
;조수만 야외나 공개로 첫자위라면…, 이상 경험을 획득
IF (TEQUIP:50 || TEQUIP:52) && (CFLAG:ASSI:21 & 1) == 0
	TCVAR:ASSI:50 += 1
	CFLAG:ASSI:21 |= 4
ENDIF
RETURN 1

;--------------------------------------------------
;조교시 메시지
;--------------------------------------------------
@TRAIN_MESSAGE_COM116
CALL KOJO_TRAIN_MESSAGE_COM
SIF FLAG:10 == 0 || CFLAG:99 == 1 || RESULT != 0
	RETURN 0

PRINTL
;연모 득도함응에 과시하면(자) 득도함응도 대흥분
IF TALENT:213 == 1 && TALENT:150 && (FLAG:15 & 16384)
	PRINTFORML %조사선택(CALLNAME:MASTER, "와")% %조사선택(CALLNAME:ASSI, "는")% 각각 %CALLNAME:TARGET%에게 과시하듯이 눈앞에서 자위를 시작했다.
	PRINTFORMW 자위 행위로 흥분하고 있는 두 명의 욕정에 동화되어 %CALLNAME:TARGET%도 한층 더 격렬하게 욕정하고 있다….
;반발 Lv2 이상 소유이지만 욕정이 튀고 있는[해방][쾌감에 솔직]를 가지지 않는 비함락 노예 상대에게
ELSEIF MARK:9 > 1 && PALAM:13 >= PALAMLV:3 && TALENT:33 == 0 && TALENT:70 == 0 && TALENT:150 == 0 && TALENT:160 == 0 && TALENT:170 == 0
	PRINTFORML %조사선택(CALLNAME:MASTER, "와")% %조사선택(CALLNAME:ASSI, "는")% 각각 %CALLNAME:TARGET%에게 과시하듯이 눈앞에서 자위를 시작했다.
	PRINTFORML 자위 행위로 흥분하고 있는 두 명을, 불쾌하게 보고 있는 %조사선택(CALLNAME:TARGET, "지만")%,
	PRINTW 그 신체가 격렬하게 욕정하고 있는 것은 숨길 수 없었다….
;자위 중독 소유로[자위하기 쉬운]이 있어 공포 각인이 없는 경우
ELSEIF ABL:30 > 0 && TALENT:60 && MARK:8 <= 0
	PRINTFORML %조사선택(CALLNAME:MASTER, "와")% %조사선택(CALLNAME:ASSI, "는")% 각각 %CALLNAME:TARGET%에게 과시하듯이 눈앞에서 자위를 시작했다.
	PRINTFORMW %조사선택(CALLNAME:TARGET, "는")% 자위 행위로 흥분하고 있는 두 명을 부러운 듯이 보고 있다….
;[정신 붕괴]하고 있는 경우
ELSEIF TALENT:167
	PRINTFORML %조사선택(CALLNAME:MASTER, "와")% %조사선택(CALLNAME:ASSI, "는")% 각각 %CALLNAME:TARGET%에게 과시하듯이 눈앞에서 자위를 시작했다.
	PRINTFORMW 그러나, %조사선택(CALLNAME:TARGET, "는")% 그런 짓에 흥미가 없는 것 같다.
;주인이나 조수가 대상의 진짜의 부모인 경우(2세대 이상전은 고려하지 않는다)
ELSEIF (NO:TARGET == 148 || NO:TARGET == 149) && (CFLAG:3105 == CFLAG:MASTER:3109 || CFLAG:3105 == CFLAG:ASSI:3109 || CFLAG:3107 == CFLAG:MASTER:3109 || CFLAG:3107 == CFLAG:ASSI:3109)
	PRINTFORML %조사선택(CALLNAME:PLAYER, "와")% %조사선택(CALLNAME:PLAYER, "는")% 각각 %CALLNAME:TARGET%에게 과시하듯이 눈앞에서 자위를 시작했다.
	PRINTFORMW 친부모의 자위행위를 본 것에 %조사선택(CALLNAME:TARGET, "는")% 깊은 충격을 받으면서도 성적 흥분을 느끼고 있는 것 같았다….
;그 외
ELSE
	PRINTFORMW %조사선택(CALLNAME:MASTER, "와")% %조사선택(CALLNAME:ASSI, "는")% 각각 %CALLNAME:TARGET%에게 과시하듯이 눈 앞에서 자위를 시작했다….
ENDIF
