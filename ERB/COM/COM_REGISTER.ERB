;===========================================================
;	조교 메뉴 등록 패치 Emuera 대응판
;===========================================================
;변수의 설명-------------------------------------------------
;TFLAG:204…主に現在選択している調教コマンド番号の一時的な保存
;TFLAG:224…おねだり口上抑制フラグ
;FLAG:550…メニューの長さ
;FLAG:551～560…コマンド番号を保存
;STR:20…調教メニューの登録の際、行番号揃える為に一時的に使用、他で再使用OK

;함수의 설명-------------------------------------------------
;@COMSEQ_REGISTER          USERCOMから呼び出される調教メニュー登録
;@COMSEQ_SHOW              登録した調教メニューを表示
;@COMSEQ_TRAIN             USERCOMから調教メニュー実行
;@MULTI_COMABLE            コマンドが実行可能かどうか調べる
;@COMSEQSUB_PRINT_COMLIST  登録時のコマンドリスト表示
;@COMSEQSUB_TRAIN_DO       調教メニュー実行の実体

;-----------------------------------------------------------
;조교 메뉴의 등록
;파괴하는 변수 : TFLAG:204
;LOCAL:0 … 루프 변수
;LOCAL:1 … 반복 시 동작에 사용
;-----------------------------------------------------------
@COMSEQ_REGISTER
PRINTL 조교메뉴등록
VARSET LOCAL, 0
FOR LOCAL, 0, 10
	$INPUT_LOOP
	CALL COMSEQSUB_PRINT_COMLIST
	SIF FLAG:550 > 0
		PRINTLC [998] 리셋한다
	SIF LOCAL:0 > 0
		PRINTLC [999] 반복한다
	IF LOCAL:0 == 0 && FLAG:550 > 0
		PRINTLC [1000] 변경하지 않고 돌아감
	ELSEIF LOCAL:0 == 0
		PRINTLC [1000] 등록하지 않고 돌아감
	ELSE
		PRINTLC [1000] 등록한다
	ENDIF
	PRINTL 
	DRAWLINE
	CALL COMSEQ_SHOW
	PRINTFORML 메뉴 {LOCAL:0+1}
	INPUT
	IF RESULT == 1000 && LOCAL:0 == 0
		RETURN 0
	ELSEIF RESULT < 0 || RESULT > 999
		BREAK
	ENDIF

	;리셋의 처리
	IF RESULT == 998 && FLAG:550 > 0
		TFLAG:204 = 0
		LOCAL:0 = 0
		LOCAL:2 = 550
		$RESET_MENU_LOOP
		FLAG:(LOCAL:2) = 0
		LOCAL:2 += 1
		SIF LOCAL:2 <= 560
			GOTO RESET_MENU_LOOP
		GOTO INPUT_LOOP
	;반복의 처리
	ELSEIF RESULT == 999 && LOCAL:0 > 0
		;現時点で何回目まで登録してるかをとっておく
		LOCAL:1 = LOCAL:0
		;二重REPEATはできない
		$COMSEQ_REPEAT
		;総数が10に達したらBREAK
		SIF LOCAL:0 > 9
			BREAK
		LOCAL:2 = 551 + LOCAL:0
		LOCAL:3 = 551 + (LOCAL:0 % LOCAL:1)
		FLAG:(LOCAL:2) = FLAG:(LOCAL:3)
		FLAG:550 += 1
		LOCAL:0 += 1
		GOTO COMSEQ_REPEAT
	ENDIF

	;TFLAG:204に選んだ調教コマンドを代入
	TFLAG:204 = RESULT
	;TFLAG:204に選んだ調教コマンドが登録できるかをチェック
	CALL MULTI_COMABLE, TFLAG:204
	IF RESULT == 0
		PRINTL 등록할 수 없습니다.
		GOTO INPUT_LOOP
	ENDIF
	;登録できる番号が入力されたので登録
	LOCAL:1 = 551 + LOCAL:0
	FLAG:(LOCAL:1) = TFLAG:204
	SIF LOCAL:0 == 0
		FLAG:550 = 0
	FLAG:550 += 1
NEXT

PRINTW 조교 메뉴를 등록했습니다.
SIF RESULT == 999
	CALL COMSEQ_SHOW
TFLAG:204 = 0
RETURN 0

;-----------------------------------------------------------
;조교 메뉴의 표시
;-----------------------------------------------------------
@COMSEQ_SHOW
VARSET LOCAL, 0
PRINTL 현재 등록되어있는 조교 메뉴를 표시합니다.
REPEAT FLAG:550
	LOCAL = 551 + COUNT
	PRINTFORM %TRAINNAME:(FLAG:LOCAL)%
	;동 커맨드 연속 실행을 ×로 표시해본다
	LOCAL:1 = 1
	$TIMES_EXP_CHECK
	IF COUNT < FLAG:550 - 1
		LOCAL:2 = COUNT + 1
		LOCAL:3 = 551 + LOCAL:2
		IF FLAG:(LOCAL:3) == FLAG:LOCAL
			LOCAL:1 += 1
			COUNT += 1
			GOTO TIMES_EXP_CHECK
		ENDIF
	ENDIF
	SIF LOCAL:1 > 1
		PRINTFORM *{LOCAL:1}
	SIF COUNT < FLAG:550 - 1
		PRINT  → 
REND
PRINTL  

;-----------------------------------------------------------
;実行可能なコマンドのリストを表示する
;破壊する変数 : STR:20
;emueraのPRINTFORMCの仕様上複素数は対応できないため
;-----------------------------------------------------------
@COMSEQSUB_PRINT_COMLIST
FOR LOCAL, 0, 1000
	CALL MULTI_COMABLE, LOCAL
	SIF RESULT == 1
		;スペースとコマンド番号とTALENTNAMEを表示する
		PRINTFORMLC [{LOCAL, 3}] %TRAINNAME:LOCAL%
NEXT
PRINTL 
RETURN 0

;-----------------------------------------------------------
;与えられたコマンド番号の調教コマンドが実行可能かどうか調べる
;CSVからTRAINNAMEを取得してその文字列数が2以上(コメントアウトされていない)場合、
;COM_ABLEを呼び出す
;引数  ARG  : コマンド番号xxx
;返値  1 コマンド番号xxxの調教コマンドが実行可能
;      0 （封印されているとかそもそもそんなコマンドないとかで）不可能
;内部でCOM_ABLExxxを呼び出すので，色々と変数が破壊されるかもしれない
;-----------------------------------------------------------
@MULTI_COMABLE, ARG
;CSVにない調教コマンド名なら不可
IF STRLENS(TRAINNAME:ARG) == 0
	RETURN 0
;COM_ABLExxx로 분기
ELSE
	;調教メニュー実行中を表すTFLAGを設定する
	TFLAG:224 = 555
	TRYCALLFORM COM_ABLE{ARG}
	;調教メニュー実行中を表すTFLAGをリセットする
	TFLAG:224 = 0
	RETURN RESULT
ENDIF

;-----------------------------------------------------------
;조교 메뉴의 실행
;-----------------------------------------------------------
@COMSEQ_TRAIN
PRINTFORML 조교 메뉴를 실행합니다.
CALL COMSEQ_SHOW
;調教メニュー実行中を表すTFLAGを設定する
TFLAG:224 = 555
;メニューの長さ回繰り返す
LOCAL:1 = 0
;PREVCOMを待避させておく
LOCAL = PREVCOM
REPEAT FLAG:550
	CALLFORM COM_ABLE{FLAG:(551 + COUNT)}
	IF RESULT == 0
		LOCAL:1 = 1
		BREAK
	ENDIF
	SELECTCOM:(COUNT + 1) = FLAG:(551 + COUNT)
	PREVCOM = FLAG:(551 + COUNT)
REND
IF LOCAL:1 == 0
	CALLTRAIN FLAG:550
ELSE
	;조교 메뉴가 실행 불가능하므로 TFLAG를 리셋한다
	TFLAG:224 = 0
	PRINTL 실행 불가능한 커맨드가 있기 때문에 실행할 수 없습니다.
ENDIF
PREVCOM = LOCAL
RETURN 0

;-----------------------------------------------------------
;調教メニューの終了後に自動実行されるイベント関数
;破壊する変数 :TFLAG:224　というかこれをリセットする
;-----------------------------------------------------------
@CALLTRAINEND
;調教メニュー実行中を表すTFLAGをリセットする
TFLAG:224 = 0
