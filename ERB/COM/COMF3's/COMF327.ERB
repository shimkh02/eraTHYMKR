;=============================================================================
;손가락 츄파
;=============================================================================
;………………………………………………
;실행 판정
;………………………………………………
@COM_ABLE327
;개별 설정 커맨드 필터
SIF DITEMTYPE:999:327 > 0
	RETURN 0
;조교자奉仕 · ウフフ系커맨드를 유효로 하지 않으면 불가
SIF (FLAG:15 & 2048) == 0
	RETURN 0
;対象が猫舌だと猫フェラパッチ有効でないとダメ
SIF TALENT:54 && (FLAG:14 & 16) == 0
	RETURN 0
;猫舌だと기교3 · 奉仕精神3 · 순종2が필요
SIF TALENT:54 && (ABL:12 < 3 || ABL:13 < 3 || ABL:10 < 2)
	RETURN 0
;거미집에 걸려 있을 때는 불가
SIF TEQUIP:40 == 7
	RETURN 0
;볼 개그 사용 중에는 불가
SIF TEQUIP:42
	RETURN 0
;승마 중에는 불가
SIF TEQUIP:43
	RETURN 0
;나체정식 중에는 불가
SIF TEQUIP:60
	RETURN 0
;시간정지 중에는 무리
SIF TEQUIP:71
	RETURN 0
;촉수 조교중일 시 주인의 촉수사역Lv2 이하는 안 됨
SIF TEQUIP:90 && ABL:MASTER:98 <= 2
	RETURN 0
RETURN 1

;--------------------------------------------------
;커맨드 본체
;--------------------------------------------------
;독단과 편견으로 손가락 펠라를 손가락 츄파로 명칭 변경
@COM327
PRINTL 손가락 츄파

;-------------------------------------------------
;실행할 수 있을까의 판정
;-------------------------------------------------
IF TFLAG:699 == 0
	CALL COM_ORDER
	;실행할 수 없다
	SIF RESULT == 0
		RETURN 0
ENDIF

;-------------------------------------------------
;실행 결정
;-------------------------------------------------
TSTR:0 = 손가락 츄파
CALL TRAIN_MESSAGE_COM327

;-------------------------------------------------
;소스의 계산
;-------------------------------------------------
DOWNBASE:0 += 5
DOWNBASE:1 += 50

SOURCE:14 = 200
SOURCE:22 = 500
SOURCE:31 = 200

;위 쪽으로 계산한 더러운 데이터
SOURCE:30 = TFLAG:242*80 + 50

;ABL:봉사 정신을 본다
IF ABL:13 == 0
	SOURCE:15 = 210
	SOURCE:16 = 100
	TIMES SOURCE:30 , 4.00
ELSEIF ABL:13 == 1
	SOURCE:15 = 250
	SOURCE:16 = 200
	TIMES SOURCE:30 , 2.50
ELSEIF ABL:13 == 2
	SOURCE:15 = 290
	SOURCE:16 = 400
	TIMES SOURCE:30 , 1.50
ELSEIF ABL:13 == 3
	SOURCE:15 = 330
	SOURCE:16 = 600
	TIMES SOURCE:30 , 1.00
ELSEIF ABL:13 == 4
	SOURCE:15 = 370
	SOURCE:16 = 1000
	TIMES SOURCE:30 , 0.50
ELSE
	SOURCE:15 = 410
	SOURCE:16 = 1500
	TIMES SOURCE:30 , 0.10
ENDIF

;ABL:기교를 본다
IF ABL:12 == 0
	TIMES SOURCE:15 , 0.50
	TIMES SOURCE:16 , 0.50
ELSEIF ABL:12 == 1
	TIMES SOURCE:15 , 0.80
	TIMES SOURCE:16 , 0.80
ELSEIF ABL:12 == 2
	TIMES SOURCE:15 , 1.00
	TIMES SOURCE:16 , 1.00
ELSEIF ABL:12 == 3
	TIMES SOURCE:15 , 1.50
	TIMES SOURCE:16 , 1.50
ELSEIF ABL:12 == 4
	TIMES SOURCE:15 , 2.50
	TIMES SOURCE:16 , 2.50
ELSE
	TIMES SOURCE:15 , 4.00
	TIMES SOURCE:16 , 4.00
ENDIF

;[괴뢰]와[정신 붕괴]와[괴조인격]을 가지지 않고,[연모]소유라면 애정 경험이 가산
SIF TALENT:150 && TALENT:167 == 0 && TALENT:168 == 0 && TALENT:169 == 0
	TCVAR:21 += 1

TCVAR:22 += 1

;노예의 입⇔조교자의 손의 불결이 이동
STAIN:0 |= STAIN:PLAYER:1
STAIN:PLAYER:1 |= STAIN:0

IF TALENT:120 == 0 && TALENT:PLAYER:120 == 0
	TCVAR:12 += 1
ELSEIF TALENT:120 && TALENT:PLAYER:120
	TCVAR:13 += 1
ENDIF
;봉사 쾌락 경험 입수 판정
TFLAG:100 |= 1
RETURN 1

;--------------------------------------------------
;명령 거부의 메시지
;--------------------------------------------------
@DENIAL_MESSAGE_COM_327
CALL KOJO_DENIAL_MESSAGE_COM
SIF FLAG:10 == 0 || CFLAG:99 == 1 || RESULT != 0
	RETURN 0

PRINTL
PRINTFORML %CALLNAME:TARGET%에게 %CALLNAME:PLAYER%의 손가락을 빨라고 명령했지만 거절당했다.
PRINTW 아직도 조교가 부족한 것 같다.

;--------------------------------------------------
;조교시 메시지
;--------------------------------------------------
@TRAIN_MESSAGE_COM327
CALL KOJO_TRAIN_MESSAGE_COM
SIF FLAG:10 == 0 || CFLAG:99 == 1 || RESULT != 0
	RETURN 0

PRINTL
IF TFLAG:699 > 0
	PRINTFORM %조사선택(CALLNAME:MASTER, "가")% 아무것도 명령하지 않았는데도 %조사선택(CALLNAME:TARGET, "는")% 
	SIF TALENT:150
		PRINT 사랑에 빠진 것 같은 표정으로 
	PRINTFORMW %CALLNAME:PLAYER%의 손가락을 빨고 핥아 돌리기 시작했다….
ELSE
	PRINTFORM %조사선택(CALLNAME:TARGET, "는")% 
	SIF TALENT:150
		PRINT 사랑에 빠진 것 같은 표정으로 
	PRINTFORMW %CALLNAME:PLAYER%의 손가락을 빨고 핥아 돌리기 시작했다….
ENDIF

;-------------------------------------------------
;고유의 실행 판정
;-------------------------------------------------
@COM_ORDER_327, ARG
LOCAL:99 = ARG
;ABL:욕망
IF ABL:11
	SIF LOCAL:99
		RESULTS = %RESULTS% + 
	LOCAL:99 = ABL:11
	TFLAG:240 += LOCAL:99
	RESULTS = %RESULTS% %ABLNAME:11%LV{LOCAL:99}({LOCAL:99})
ENDIF
;ABL:봉사 정신
IF ABL:13
	SIF LOCAL:99
		RESULTS = %RESULTS% + 
	LOCAL:99 = ABL:13
	TFLAG:240 += LOCAL:99 * 4
	RESULTS = %RESULTS% %ABLNAME:13%LV{LOCAL:99}({LOCAL:99 * 4})
ENDIF

;쾌락 각인
IF MARK:1
	SIF LOCAL:99
		RESULTS = %RESULTS% + 
	LOCAL:99 = MARK:1
	TFLAG:240 += LOCAL:99 * 2
	RESULTS = %RESULTS% %MARKNAME:1%LV{LOCAL:99}({LOCAL:99 * 2})
ENDIF

;PALAM:욕정
GETPALAMLV PALAM:13, 5
LOCAL:2 = RESULT
IF LOCAL:2
	SIF LOCAL:99
		RESULTS = %RESULTS% + 
	LOCAL:99 = LOCAL:2
	TFLAG:240 += LOCAL:99
	RESULTS = %RESULTS% %PALAMNAME:13%LV{LOCAL:2}({LOCAL:99})
ENDIF

;부끄럼쟁이
IF TALENT:35
	RESULTS = %RESULTS% - 
	LOCAL:99 = 1
	TFLAG:240 -= LOCAL:99
	RESULTS = %RESULTS% %TALENTNAME:35%({LOCAL:99})
ENDIF
;악취 둔감
IF TALENT:61
	SIF LOCAL:99
		RESULTS = %RESULTS% + 
	LOCAL:99 = 3
	TFLAG:240 += LOCAL:99
	RESULTS = %RESULTS% %TALENTNAME:61%({LOCAL:99})
;악취 민감
ELSEIF TALENT:62
	RESULTS = %RESULTS% - 
	LOCAL:99 = 3
	TFLAG:240 -= LOCAL:99
	RESULTS = %RESULTS% %TALENTNAME:62%({LOCAL:99})
ENDIF
;헌신적
IF TALENT:63
	SIF LOCAL:99
		RESULTS = %RESULTS% + 
	LOCAL:99 = 6
	TFLAG:240 += LOCAL:99
	RESULTS = %RESULTS% %TALENTNAME:63%({LOCAL:99})
ENDIF
;쾌감의 부정
IF TALENT:71
	RESULTS = %RESULTS% - 
	LOCAL:99 = 1
	TFLAG:240 -= LOCAL:99
	RESULTS = %RESULTS% %TALENTNAME:71%({LOCAL:99})
ENDIF
;연모
IF TALENT:150 && ASSIPLAY == 0
	SIF LOCAL:99
		RESULTS = %RESULTS% + 
	LOCAL:99 = 5
	TFLAG:240 += LOCAL:99
	RESULTS = %RESULTS% %TALENTNAME:150%({LOCAL:99})
ENDIF

;조교자의 손의 더러움
TFLAG:242 = COMORDER_STAIN_CALC(1, 0)

;서로 불결
IF TFLAG:242
	RESULTS = %RESULTS% - 
	TFLAG:240 -= TFLAG:242
	RESULTS = %RESULTS% 서로 불결
	;악취 둔감
	IF TALENT:61
		RESULTS = %RESULTS%, %TALENTNAME:61%
	ELSEIF TALENT:62
		RESULTS = %RESULTS%, %TALENTNAME:62%
	ENDIF
	RESULTS = %RESULTS%({TFLAG:242})
	LOCAL:99 = 1
ENDIF

TFLAG:241 = 10
