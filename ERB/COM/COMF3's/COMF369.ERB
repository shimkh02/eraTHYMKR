;=============================================================================
;털 구슬 목욕
;=============================================================================
;………………………………………………
;실행 판정
;………………………………………………
@COM_ABLE369
;개별 설정 커맨드 필터
SIF DITEMTYPE:999:369 > 0
	RETURN 0
;特殊風呂系커맨드를 유효로 하지 않으면 불가
SIF (FLAG:15 & 1024) == 0
	RETURN 0
;風呂を改装していないとダメ
SIF (FLAG:74 & 512) == 0
	RETURN 0
;해제는 언제든 가능
SIF TEQUIP:55 == 11
	RETURN 1
;수면 중에는 불가
SIF EQUIP:14
	RETURN 0
;실신 중에는 불가
SIF TFLAG:899 > 0
	RETURN 0
;전극사용 중에는 불가
SIF TEQUIP:20 == 4 || TEQUIP:25 == 4 || TEQUIP:30 == 4 || TEQUIP:30 == 6 || TEQUIP:32 == 4 || TEQUIP:35 == 4
	RETURN 0
;거미집에 걸려 있을 때는 불가
SIF TEQUIP:40 == 7
	RETURN 0
;승마 중에는 불가
SIF TEQUIP:43
	RETURN 0
;야외면 안 됨
SIF TEQUIP:52
	RETURN 0
;욕실 플레이中じゃ無いとダメ
SIF TEQUIP:53 != 1
	RETURN 0
;샤워 중에는 불가
SIF TEQUIP:54
	RETURN 0
;그밖의 ○○ 욕탕 입욕 중에는 불가
SIF TEQUIP:55 && TEQUIP:55 != 11
	RETURN 0
;의사 선생님 플레이 중에는 불가
SIF TEQUIP:57
	RETURN 0
;기저귀 플레이 중에는 불가
SIF TEQUIP:58
	RETURN 0
;코스프레 중에는 불가
SIF TEQUIP:59 > 2
	RETURN 0
;알몸 앞치마 중에는 불가
SIF TEQUIP:59 == 1
	RETURN 0
;나체정식 중에는 불가
SIF TEQUIP:60
	RETURN 0
;시간정지 중에는 무리
SIF TEQUIP:71
	RETURN 0
;촉수 조교 중에는 불가
SIF TEQUIP:90
	RETURN 0
RETURN 1

;--------------------------------------------------
;커맨드 본체
;--------------------------------------------------
;모옥들됩니다
;입욕중은 상시 약한 애무 를 하고 있는 느낌에
@COM369
PRINTL 털 구슬 목욕
TSTR:0 = 털 구슬 목욕
CALL TRAIN_MESSAGE_COM369

IF TEQUIP:55 == 11
	DOWNBASE:0 += 5
	DOWNBASE:1 += 50
	TEQUIP:55 = 0
	RETURN 1
ENDIF

DOWNBASE:0 += 10
DOWNBASE:1 += 75

SOURCE:12 += 100
SOURCE:31 += 270

TEQUIP:55 = 11

RETURN 1

;--------------------------------------------------
;털겨울의 북서풍려입욕중
;--------------------------------------------------
;우리들, 모옥에도 거절해 대!
@EQUIP_COM369
PRINTDATAL
	DATA ＜털 구슬 목욕 중＞
	DATA ＜복실복실중＞
	DATA ＜푹신하게! 푹신하게!＞
ENDDATA
DOWNBASE:0 += 5
DOWNBASE:1 += 75

SOURCE:16 += 30
SOURCE:12 += 150
SOURCE:31 += 80

;ABL:C감각을 본다
IF ABL:0 == 0
	SOURCE:0 += 10
	SOURCE:21 += 25
ELSEIF ABL:0 == 1
	SOURCE:0 += 50
	SOURCE:21 += 50
ELSEIF ABL:0 == 2
	SOURCE:0 += 200
	SOURCE:21 += 80
ELSEIF ABL:0 == 3
	SOURCE:0 += 400
	SOURCE:21 += 100
ELSEIF ABL:0 == 4
	SOURCE:0 += 650
	SOURCE:21 += 140
ELSE
	SOURCE:0 += 1000
	SOURCE:21 += 200
ENDIF

;ABL:B감각을 본다
IF ABL:3 == 0
	SOURCE:3 += 5
	SOURCE:21 += 5
ELSEIF ABL:3 == 1
	SOURCE:3 += 25
	SOURCE:21 += 25
ELSEIF ABL:3 == 2
	SOURCE:3 += 80
	SOURCE:21 += 50
ELSEIF ABL:3 == 3
	SOURCE:3 += 160
	SOURCE:21 += 80
ELSEIF ABL:3 == 4
	SOURCE:3 += 380
	SOURCE:21 += 100
ELSE
	SOURCE:3 += 600
	SOURCE:21 += 150
ENDIF

RETURN 1

;--------------------------------------------------
;조교시 메시지
;--------------------------------------------------
@TRAIN_MESSAGE_COM369
CALL KOJO_TRAIN_MESSAGE_COM
SIF FLAG:10 == 0 || CFLAG:99 == 1 || RESULT != 0
	RETURN 0

PRINTL
IF TEQUIP:55 == 11
	PRINTFORMW 털 구슬에 파묻힌 %조사선택(CALLNAME:TARGET, "를")% 꺼냈다.
ELSE
	PRINTFORML 털 구슬들이 북적거리고 있는 욕탕에 %조사선택(CALLNAME:TARGET, "를")% 빠뜨렸다.
	PRINTFORMW 호기심을 느낀 털 구슬들은 너나 할 것 없이 %CALLNAME:TARGET%의 몸에 달라붙었다….
ENDIF
