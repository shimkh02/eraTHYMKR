;=============================================================================
;다이빙대
;=============================================================================
;泳ぐと比べて　怖い　痛い　気力消費大　その代わり体力消費は控えめ
;達成感モリモリ
;高さを選べる。
;口上用にTFLAG:201を使用。(0=低, 1=中, 2=高)
;恐怖　　　　　　→（苦痛　トラウマ）　消費気力ガッツリ
;水に慣れてない　→（苦痛　トラウマ）　消費体力ガッツリ
;慣れてる　　　　→達成感大 主導権小
;LOCAL			→プラス効果基礎値　（達成感　主導権）
;LOCAL:1		→マイナス効果基礎値（苦痛　トラウマ）
;--------------------------------------------------
;다이빙대 실행 판정
;--------------------------------------------------
@COM_ABLE358
;개별 설정 커맨드 필터
SIF DITEMTYPE:999:358 > 0
	RETURN 0
;풀장과 다이빙대를 소유하고 있지 않으면 불가
SIF (FLAG:89 & 1) == 0 || (FLAG:89 & 4) == 0
	RETURN 0
;吸血鬼や動物耳には従順4がないと使用不可（プレイヤーがサドならＯＫ）
IF TALENT:205 || TALENT:116
	SIF ABL:10 < 4 && TALENT:PLAYER:81 == 0
		RETURN 0
ENDIF
;助手調教で[妄信][服従]もなく、調教対象と同性の場合、どちらかもしくは両方が脱いでいて《従順》3未満または《レズっ気》(《ＢＬっ気》)2未満だと実行できない
IF ASSIPLAY && !(TALENT:PLAYER:120 ^^ TALENT:120) && TALENT:PLAYER:151 == 0 && TALENT:PLAYER:160 == 0
	SIF ABL:PLAYER:10 < 3 || ABL:PLAYER:(22 + TALENT:120) < 2 && !(TEQUIP:59 && TEQUIP:ASSI:59)
		RETURN 0
ENDIF
;睡眠中は不可
SIF EQUIP:14
	RETURN 0
;パニック中は不可
SIF TFLAG:108 > 0
	RETURN 0
;失神中は不可
SIF TFLAG:899 > 0
	RETURN 0
;Ｖ系装着具使用中は不可
SIF TEQUIP:20 || TEQUIP:21
	RETURN 0
;Ａ系装着具使用中は不可
SIF TEQUIP:25 || TEQUIP:26
	RETURN 0
;Ｃ系装着具使用中は不可
SIF TEQUIP:30
	RETURN 0
;Ｂ系装着具使用中は不可
SIF TEQUIP:35 || TEQUIP:36
	RETURN 0
;電極使用中は不可
SIF TEQUIP:20 == 4 || TEQUIP:25 == 4 || TEQUIP:30 == 4 || TEQUIP:30 == 6 || TEQUIP:32 == 4 || TEQUIP:35 == 4
	RETURN 0
;プール中じゃないとダメ
SIF TEQUIP:53 != 2
	RETURN 0
;シャワー中はダメ
SIF TEQUIP:54
	RETURN 0
;○○風呂入浴中はダメ
SIF TEQUIP:55
	RETURN 0
;オムツプレイ中はダメ
SIF TEQUIP:58
	RETURN 0
;助手がスク水とビキニと褌以外のコスプレ中はだめ
SIF ASSIPLAY && TEQUIP:PLAYER:59 > 1 && TEQUIP:PLAYER:59 != 2 && TEQUIP:PLAYER:59 != 12 && TEQUIP:PLAYER:59 != 17
	RETURN 0
;ビキニと褌以外のコスプレ中はだめ
SIF TEQUIP:59 > 2 && TEQUIP:59 != 12 && TEQUIP:59 != 17
	RETURN 0
RETURN 1

;--------------------------------------------------
;커맨드 본체
;--------------------------------------------------
@COM358
PRINTL 다이빙대

;-------------------------------------------------
;실행할 수 있을까의 판정
;-------------------------------------------------
IF TFLAG:699 == 0
	CALL COM_ORDER
	;실행할 수 없다
	SIF RESULT == 0
		RETURN 0
ENDIF

TSTR:0 = 다이빙대
TFLAG:201 = 0
;다이빙대の高さの選択肢が出現。
;ソースを±50%で調整可能
CALL SELECT_TOBIKOMI_HEIGHT
CALL TRAIN_MESSAGE_COM358

DOWNBASE:0 += 40
DOWNBASE:1 += 80

LOCAL = 300
LOCAL:1 = 200

;高さ補正
IF TFLAG:201 == 0
	TIMES LOCAL , 0.50
	TIMES LOCAL:1 , 0.50
ELSEIF TFLAG:201 == 2
	TIMES LOCAL , 1.50
	TIMES LOCAL:1 , 1.50
ENDIF

;河童
SIF TALENT:210
	TIMES LOCAL:1 , 0.80
;気丈
SIF TALENT:12
	TIMES LOCAL:1 , 0.50
	
;PALAM:恐怖をみる
IF PALAM:17 < PALAMLV:1
	TIMES LOCAL:1 , 1.00
	TIMES DOWNBASE:1 , 1.00
ELSEIF PALAM:17 < PALAMLV:2
	TIMES LOCAL:1 , 1.10
	TIMES DOWNBASE:1 , 2.00
ELSEIF PALAM:17 < PALAMLV:3
	TIMES LOCAL:1 , 1.30
	TIMES DOWNBASE:1 , 3.00
ELSEIF PALAM:17 < PALAMLV:4
	TIMES LOCAL:1 , 1.60
	TIMES DOWNBASE:1 , 4.00
ELSE
	TIMES LOCAL:1 , 2.00
	TIMES DOWNBASE:1 , 5.00
ENDIF
;ABL:水泳技能をみる
IF ABL:74 == 0
	TIMES LOCAL , 1.00
	TIMES LOCAL:1 , 3.20
	TIMES DOWNBASE:0 , 6.00
ELSEIF ABL:74 == 1
	TIMES LOCAL , 2.00
	TIMES LOCAL:1 , 1.60
	TIMES DOWNBASE:0 , 5.00
ELSEIF ABL:74 == 2
	TIMES LOCAL , 4.00
	TIMES LOCAL:1 , 0.80
	TIMES DOWNBASE:0 , 4.00
ELSEIF ABL:74 == 3
	TIMES LOCAL , 8.00
	TIMES LOCAL:1 , 0.40
	TIMES DOWNBASE:0 , 3.00
ELSEIF ABL:74 == 4
	TIMES LOCAL , 16.00
	TIMES LOCAL:1 , 0.20
	TIMES DOWNBASE:0 , 2.00
ELSE
	TIMES LOCAL , 32.00
	TIMES LOCAL:1 , 0.10
	TIMES DOWNBASE:0 , 1.00
ENDIF

;達成感
SOURCE:16 += LOCAL
;主導権
SOURCE:14 += LOCAL /4
;痛み
SOURCE:20 += LOCAL:1 /2
;トラウマ
SOURCE:25 += LOCAL:1 /2
;逸脱
SOURCE:31 += LOCAL:1 /2

;動物耳の場合
IF TALENT:116
	TIMES SOURCE:31 , 1.60
	TIMES SOURCE:22 , 1.50
	TIMES SOURCE:32 , 2.00
ENDIF
;吸血鬼の場合
IF TALENT:205
	TIMES SOURCE:20 , 1.60
	TIMES SOURCE:22 , 2.00
	TIMES SOURCE:32 , 1.40
ENDIF

;飛び込んだ高さの水泳経験　調教者が教育者なら倍
TCVAR:78 += (TFLAG:201 + 1) * (TALENT:PLAYER:126 + 1)

;泳いだから汚れがリセット
RESET_STAIN TARGET

;潤滑が半分
PALAM:10 /= 2
PALAM:21 /= 2
RETURN 1

;--------------------------------------------------
;高さ選択
;--------------------------------------------------
@SELECT_TOBIKOMI_HEIGHT
PRINTL 어느 정도의 높이로부터 뛰어내리게 할까요……?
PRINTL [0] 낮음
PRINTL [1] 중간
PRINTL [2] 높음
$INPUT_LOOP
INPUT
IF RESULT == 0
	TFLAG:201 = 0
ELSEIF RESULT == 1
	TFLAG:201 = 1
ELSEIF RESULT == 2
	TFLAG:201 = 2
ELSE
	GOTO INPUT_LOOP
ENDIF

;--------------------------------------------------
;조교시 메시지
;--------------------------------------------------
@TRAIN_MESSAGE_COM358
CALL KOJO_TRAIN_MESSAGE_COM
PRINTFORMW %조사처리(CALLNAME:TARGET,"를")% 다이빙대에서 뛰어내리게 했다….
