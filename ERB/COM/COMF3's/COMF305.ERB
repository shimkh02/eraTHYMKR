;=============================================================================
;더블스마타
;=============================================================================
;………………………………………………
;실행 판정
;………………………………………………
@COM_ABLE305
;개별 설정 커맨드 필터
SIF DITEMTYPE:999:305 > 0
	RETURN 0
;조수가 없으면 불가
SIF ASSI < 0
	RETURN 0
;主人じゃないとダメ
SIF ASSIPLAY
	RETURN 0
;主人にペニスがないとダメ
SIF EXIST_BAR(MASTER) == 0
	RETURN 0
;주인이 【魅惑】か【謎の魅力】じゃないとダメ
SIF TALENT:MASTER:91 == 0 && TALENT:MASTER:92 == 0
	RETURN 0
;조수가 奉仕精神3이상, 기교4이상이 아니면 불가
SIF ABL:ASSI:12 < 4 || ABL:ASSI:13 < 3
	RETURN 0
;全員体のサイズが同程度か, 주인이 금단의 지식 보유자가 아니면 불가
IF TALENT:MASTER:56 == 0
	IF TALENT:MASTER:127
		SIF TALENT:127 == 0 || TALENT:ASSI:127 == 0
			RETURN 0
	ELSE
		SIF TALENT:127 || TALENT:ASSI:127
			RETURN 0
	ENDIF
ENDIF
;조교 대상か助手の両方, あるいはどちらかが肥大陰核かオトコか후타나리だとダメ
SIF EXIST_BAR(TARGET) || EXIST_BAR(ASSI)
	RETURN 0
;윤활이 높지 않으면 불가
SIF PALAM:10 < 2000
	RETURN 0
;수면 중에는 불가
SIF EQUIP:14
	RETURN 0
;실신 중에는 불가
SIF TFLAG:899 > 0
	RETURN 0
;패닉 중에는 불가
SIF TFLAG:108 > 0
	RETURN 0
;Ｖ계 장착구 사용 중에는 불가
SIF TEQUIP:20 || TEQUIP:21
	RETURN 0
;밧줄 사용 중에는 불가
SIF TEQUIP:40
	RETURN 0
;승마 중에는 불가
SIF TEQUIP:43
	RETURN 0
;욕실 플레이 중에는 플레이 매트가 없으면 불가
SIF TEQUIP:53 && ITEM:42 == 0 && NOITEM == 0
	RETURN 0
;한증탕과 암반욕 외의 ○○ 욕탕 입욕 중에는 불가
SIF TEQUIP:55 && TEQUIP:55 != 12 && TEQUIP:55 != 15
	RETURN 0
;기저귀 플레이 중에는 불가
SIF TEQUIP:58
	RETURN 0
;조수가 훈도시 착용 중일 경우에는 불가
SIF ASSI >= 0 && TEQUIP:ASSI:59 == 17
	RETURN 0
;훈도시 착용 중일 경우는 안 됨
SIF TEQUIP:59 == 17
	RETURN 0
;나체정식 중에는 불가
SIF TEQUIP:60
	RETURN 0
;시간정지 중에는 무리
SIF TEQUIP:71
	RETURN 0
;촉수 조교 중에는 불가
SIF TEQUIP:90
	RETURN 0
;인어는 안됨
SIF TALENT:229 == 1
	RETURN 0
RETURN 1

;--------------------------------------------------
;커맨드 본체
;--------------------------------------------------
@COM305
;캐릭터 전용 커맨드를 유효해 샌드위치 실행 가능의 판정이 있으면(자) 샌드위치 판정에
IF FLAG:15 & 16384 && TFLAG:157
	CALL JUDGE_TRAIN_PAIR, TARGET, ASSI
	;샌드위치 편성이 있는 경우만
	SIF TFLAG:175 != 0
		JUMP COM629
ENDIF

PRINTL 더블 스마타

;-------------------------------------------------
;실행할 수 있을까의 판정
;-------------------------------------------------
IF TFLAG:699 == 0
	CALL COM_ORDER, 305
	;실행할 수 없다
	SIF RESULT == 0
		RETURN 0
ENDIF

;-------------------------------------------------
;실행 결정
;-------------------------------------------------
SELECTCOM = 305
TSTR:0 = 둘이서 가랑이
CALL TRAIN_MESSAGE_COM305

;-------------------------------------------------
;사정 게이지 체크
;-------------------------------------------------
LOCAL = 0

;ABL:기교를 본다
IF ABL:12 == 0
	LOCAL = 500
ELSEIF ABL:12 == 1
	LOCAL = 1100
ELSEIF ABL:12 == 2
	LOCAL = 2000
ELSEIF ABL:12 == 3
	LOCAL = 3000
ELSEIF ABL:12 == 4
	LOCAL = 3900
ELSE
	LOCAL = 4600
ENDIF

;윤활을 본다
LOCAL = EJACULATIONCHECK_PALAM_10_5(TARGET, LOCAL)
;순종을 본다
LOCAL = EJACULATIONCHECK_ABL_10_1(TARGET, LOCAL)
;봉사 정신을 본다
LOCAL = EJACULATIONCHECK_ABL_13_1(TARGET, LOCAL)

;조교자의 C감각을 본다
LOCAL = EJACULATIONCHECK_ABL_0_1(PLAYER, LOCAL)

SIF TALENT:PLAYER:120 || TALENT:PLAYER:121
	BASE:PLAYER:2 += LOCAL

;-------------------------------------------------
;소스의 계산
;-------------------------------------------------
DOWNBASE:0 += 40
DOWNBASE:1 += 160

SOURCE:10 = 500
SOURCE:12 = 250
SOURCE:22 = 1500
SOURCE:31 = 600

;위 쪽으로 계산한 더러운 데이터
SOURCE:30 = TFLAG:242*10 + 60

;ABL:봉사 정신을 본다
IF ABL:13 == 0
	SOURCE:15 = 200
	SOURCE:16 = 100
	TIMES SOURCE:30 , 4.00
ELSEIF ABL:13 == 1
	SOURCE:15 = 250
	SOURCE:16 = 180
	TIMES SOURCE:30 , 2.50
ELSEIF ABL:13 == 2
	SOURCE:15 = 300
	SOURCE:16 = 250
	TIMES SOURCE:30 , 1.50
ELSEIF ABL:13 == 3
	SOURCE:15 = 350
	SOURCE:16 = 350
	TIMES SOURCE:30 , 1.00
ELSEIF ABL:13 == 4
	SOURCE:15 = 400
	SOURCE:16 = 500
	TIMES SOURCE:30 , 0.50
ELSE
	SOURCE:15 = 450
	SOURCE:16 = 800
	TIMES SOURCE:30 , 0.10
ENDIF

;ABL:기교를 본다
IF ABL:12 == 0
	TIMES SOURCE:15 , 0.70
	TIMES SOURCE:16 , 0.70
ELSEIF ABL:12 == 1
	TIMES SOURCE:15 , 0.90
	TIMES SOURCE:16 , 0.90
ELSEIF ABL:12 == 2
	TIMES SOURCE:15 , 1.00
	TIMES SOURCE:16 , 1.00
ELSEIF ABL:12 == 3
	TIMES SOURCE:15 , 1.20
	TIMES SOURCE:16 , 1.20
ELSEIF ABL:12 == 4
	TIMES SOURCE:15 , 1.40
	TIMES SOURCE:16 , 1.40
ELSE
	TIMES SOURCE:15 , 1.60
	TIMES SOURCE:16 , 1.60
ENDIF

;ABL:C감각을 본다
IF ABL:0 == 0
	SOURCE:0 = 20
	TIMES SOURCE:12 , 0.80
ELSEIF ABL:0 == 1
	SOURCE:0 = 80
	TIMES SOURCE:12 , 0.90
ELSEIF ABL:0 == 2
	SOURCE:0 = 350
	TIMES SOURCE:12 , 1.00
ELSEIF ABL:0 == 3
	SOURCE:0 = 750
	TIMES SOURCE:12 , 1.10
ELSEIF ABL:0 == 4
	SOURCE:0 = 1200
	TIMES SOURCE:12 , 1.20
ELSE
	SOURCE:0 = 1750
	TIMES SOURCE:12 , 1.30
ENDIF

;PALAM:윤활
IF PALAM:10 < PALAMLV:1
	TIMES SOURCE:0 , 0.30
	TIMES SOURCE:15 , 0.60
ELSEIF PALAM:10 < PALAMLV:2
	TIMES SOURCE:0 , 0.60
	TIMES SOURCE:15 , 0.80
ELSEIF PALAM:10 < PALAMLV:3
	TIMES SOURCE:0 , 1.00
	TIMES SOURCE:15 , 1.00
ELSEIF PALAM:10 < PALAMLV:4
	TIMES SOURCE:0 , 1.50
	TIMES SOURCE:15 , 1.20
ELSEIF PALAM:10 < PALAMLV:5
	TIMES SOURCE:0 , 2.00
	TIMES SOURCE:15 , 1.40
ELSE
	TIMES SOURCE:0 , 2.50
	TIMES SOURCE:15 , 1.60
ENDIF

;조수의 ABL:C감각을 본다
IF ABL:ASSI:0 == 0
	TIMES SOURCE:15 , 0.80
	TIMES SOURCE:16 , 0.50
ELSEIF ABL:ASSI:0 == 1
	TIMES SOURCE:15 , 0.90
	TIMES SOURCE:16 , 0.70
ELSEIF ABL:ASSI:0 == 2
	TIMES SOURCE:15 , 1.00
	TIMES SOURCE:16 , 1.00
ELSEIF ABL:ASSI:0 == 3
	TIMES SOURCE:15 , 1.10
	TIMES SOURCE:16 , 1.20
ELSEIF ABL:ASSI:0 == 4
	TIMES SOURCE:15 , 1.20
	TIMES SOURCE:16 , 1.40
ELSE
	TIMES SOURCE:15 , 1.30
	TIMES SOURCE:16 , 1.70
ENDIF

;-------------------------------------------------
;사정 체크
;-------------------------------------------------
TFLAG:91 = SAMEN_CHECK(MASTER)

;사정시의 처리
IF TFLAG:91 >= 1
	;커맨드 마다 고유의 처리는 이 아래에 쓴다
	TIMES SOURCE:15 , 2.00

	;ABL:정액 중독을 본다
	IF ABL:31 == 0
		SOURCE:24 = 0
		TIMES SOURCE:16 , 1.50
		TIMES SOURCE:22, 1.40
	ELSEIF ABL:31 == 1
		SOURCE:24 = 200
		TIMES SOURCE:16 , 2.00
		TIMES SOURCE:22, 1.00
	ELSEIF ABL:31 == 2
		SOURCE:24 = 400
		TIMES SOURCE:16 , 2.50
		TIMES SOURCE:22, 0.80
	ELSEIF ABL:31 == 3
		SOURCE:24 = 700
		TIMES SOURCE:16 , 3.00
		TIMES SOURCE:22, 0.50
	ELSEIF ABL:31 == 4
		SOURCE:24 = 1000
		TIMES SOURCE:16 , 4.00
		TIMES SOURCE:22, 0.20
	ELSE
		SOURCE:24 = 1500
		TIMES SOURCE:16 , 5.00
		TIMES SOURCE:22, 0.00
	ENDIF

	;사정처 타입의 설정
	TFLAG:703 = 5
	;사정처 선택의 가능·불가
	TFLAG:704 = 0
	;범용적인 처리는 이 함수로 실시한다
	;(사정 게이지의 재계산이나 사정 경험의 상승과 공통 부분의 표시)
	CALL SAMEN_SHOOT
ENDIF

;사정에 관계없이 행해지는 처리는 여기로부터
;노예의 V⇔조교자의 P의 불결이 이동
STAIN:3 |= STAIN:PLAYER:2
STAIN:PLAYER:2 |= STAIN:3

;조수의 V⇔조교자의 P의 불결이 이동
STAIN:ASSI:3 |= STAIN:PLAYER:2
STAIN:PLAYER:2 |= STAIN:ASSI:3

;노예의 V⇔조수의 V의 불결이 이동
STAIN:3 |= STAIN:ASSI:3
STAIN:ASSI:3 |= STAIN:3

IF TALENT:120 == 0 && TALENT:PLAYER:120 == 0
	TCVAR:12 += 7
ELSEIF TALENT:120 && TALENT:PLAYER:120
	TCVAR:13 += 7
ENDIF
IF PLAYER == MASTER
	TCVAR:41 += 1
	TCVAR:ASSI:41 += 1
ENDIF
;봉사 쾌락 경험 입수 판정
TFLAG:100 |= 1

;조교자가 후타나리
SIF TALENT:PLAYER:121
	SOURCE:22 /= 2

RETURN 1

;--------------------------------------------------
;명령 거부의 메시지
;--------------------------------------------------
@DENIAL_MESSAGE_COM_305
CALL KOJO_DENIAL_MESSAGE_COM
SIF FLAG:10 == 0 || CFLAG:99 == 1 || RESULT != 0
	RETURN 0

;--------------------------------------------------
;조교시 메시지
;--------------------------------------------------
@TRAIN_MESSAGE_COM305
CALL KOJO_TRAIN_MESSAGE_COM
SIF FLAG:10 == 0 || CFLAG:99 == 1 || RESULT != 0
	RETURN 0

PRINTL
IF (TALENT:152 || TALENT:153) && MARK:9 < 1 && (TALENT:ASSI:152 || TALENT:ASSI:153) && MARK:ASSI:9 < 1
	PRINTFORML %조사선택(CALLNAME:TARGET, "와")% %조사선택(CALLNAME:ASSI, "는")% 시합을 하듯이, 사랑하는 %조사선택(CALLNAME:MASTER, "를")% 위해서
	PRINTFORML %조사선택(CALLNAME:TARGET, "와")% %CALLNAME:ASSI%의 성기로 %CALLNAME:MASTER%의 페니스를 끼웠다.
	PRINTW 그리고, 사랑을 담아 함께 열심히 비벼 댔다….
ELSE
	PRINTFORMW %조사선택(CALLNAME:TARGET, "와")% %CALLNAME:ASSI%의 성기 사이에 페니스를 끼워, 둘이 함께 비벼댔다….
ENDIF

;-------------------------------------------------
;고유의 실행 판정
;-------------------------------------------------
@COM_ORDER_305, ARG
LOCAL:99 = ARG
;ABL:욕망
IF ABL:11
	SIF LOCAL:99
		RESULTS = %RESULTS% +
	LOCAL:99 = ABL:11
	TFLAG:240 += LOCAL:99 * 2
	RESULTS = %RESULTS% %ABLNAME:11%LV{LOCAL:99}({LOCAL:99 * 2})
ENDIF
;ABL:봉사 정신
IF ABL:13
	SIF LOCAL:99
		RESULTS = %RESULTS% +
	LOCAL:99 = ABL:13
	TFLAG:240 += LOCAL:99 * 4
	RESULTS = %RESULTS% %ABLNAME:13%LV{LOCAL:99}({LOCAL:99 * 4})
ENDIF
;ABL:정액 중독
IF ABL:31
	SIF LOCAL:99
		RESULTS = %RESULTS% +
	LOCAL:99 = ABL:31
	TFLAG:240 += LOCAL:99
	RESULTS = %RESULTS% %ABLNAME:31%LV{LOCAL:99}({LOCAL:99})
ENDIF

;쾌락 각인
IF MARK:1
	SIF LOCAL:99
		RESULTS = %RESULTS% +
	LOCAL:99 = MARK:1
	TFLAG:240 += LOCAL:99 * 2
	RESULTS = %RESULTS% %MARKNAME:1%LV{LOCAL:99}({LOCAL:99 * 2})
ENDIF

;PALAM:욕정
GETPALAMLV PALAM:13, 5
LOCAL:2 = RESULT
IF LOCAL:2
	SIF LOCAL:99
		RESULTS = %RESULTS% +
	LOCAL:99 = LOCAL:2 * 3
	TFLAG:240 += LOCAL:99
	RESULTS = %RESULTS% %PALAMNAME:13%LV{LOCAL:2}({LOCAL:99})
ENDIF

;부끄럼쟁이
IF TALENT:35
	RESULTS = %RESULTS% -
	LOCAL:99 = 1
	TFLAG:240 -= LOCAL:99
	RESULTS = %RESULTS% %TALENTNAME:35%({LOCAL:99})
ENDIF
;남자 싫다
IF TALENT:37 && TALENT:PLAYER:120
	RESULTS = %RESULTS% -
	LOCAL:99 = 7
	TFLAG:240 -= LOCAL:99
	RESULTS = %RESULTS% %TALENTNAME:37%({LOCAL:99})
;여자 증오
ELSEIF TALENT:38 && TALENT:PLAYER:120 == 0
	RESULTS = %RESULTS% -
	LOCAL:99 = 7
	TFLAG:240 -= LOCAL:99
	RESULTS = %RESULTS% %TALENTNAME:38%({LOCAL:99})
ENDIF
;쾌감의 부정
IF TALENT:71
	RESULTS = %RESULTS% -
	LOCAL:99 = 3
	TFLAG:240 -= LOCAL:99
	RESULTS = %RESULTS% %TALENTNAME:71%({LOCAL:99})
ENDIF
;연모
IF TALENT:150 && ASSIPLAY == 0
	SIF LOCAL:99
		RESULTS = %RESULTS% +
	LOCAL:99 = 3
	TFLAG:240 += LOCAL:99
	RESULTS = %RESULTS% %TALENTNAME:150%({LOCAL:99})
ENDIF

;조교자가 후타나리
IF TALENT:PLAYER:121
	SIF LOCAL:99
		RESULTS = %RESULTS% +
	LOCAL:99 = 8
	TFLAG:240 += LOCAL:99
	RESULTS = %RESULTS% %조사처리(CALLNAME:PLAYER,"가")% %TALENTNAME:121%({LOCAL:99})
ENDIF

;미약
IF TEQUIP:11
	SIF LOCAL:99
		RESULTS = %RESULTS% +
	LOCAL:99 = 6
	TFLAG:240 += LOCAL:99
	RESULTS = %RESULTS% %ITEMNAME:61%({LOCAL:99})
ENDIF

;조교자의 P의 더러움
TFLAG:242 = COMORDER_STAIN_CALC(2, 0)
;가랑이에서는 더러움의 영향이 적은
TFLAG:242 /= 3

;서로 불결
IF TFLAG:242
	RESULTS = %RESULTS% -
	TFLAG:240 -= TFLAG:242
	RESULTS = %RESULTS% 서로 불결
	;악취 둔감
	IF TALENT:61
		RESULTS = %RESULTS%, %TALENTNAME:61%
	ELSEIF TALENT:62
		RESULTS = %RESULTS%, %TALENTNAME:62%
	ENDIF
	RESULTS = %RESULTS%({TFLAG:242})
	LOCAL:99 = 1
ENDIF

TFLAG:241 = 25
;실행치가 75이상때는 샌드위치의 체크 온
TFLAG:157 = (TFLAG:240 >= 75) ?  1 # 0
