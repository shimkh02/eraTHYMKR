;=============================================================================
;학교수영복 플레이
;=============================================================================
;………………………………………………
;실행 판정
;………………………………………………
@COM_ABLE91
;개별 설정 커맨드 필터
SIF DITEMTYPE:999:91 > 0
	RETURN 0
;수치 플레이계 확장 커맨드를 유효하게 하지 않으면 안 됨
SIF (FLAG:15 & 512) == 0
	RETURN 0
;승마 중에는 불가
SIF TEQUIP:43
	RETURN 0
;촉수 조교 중에는 불가
SIF TEQUIP:90
	RETURN 0
;해제는 나체정식안 이외 배워 개에서도 가능
SIF TEQUIP:60 && TEQUIP:59 == 2
	RETURN 1
;아이템「스쿨 수영복」을 가지고 있는지, 아이템 없음 설정이 아니면 안 됨
SIF ITEM:45 == 0 && NOITEM == 0
	RETURN 0
;[남자]의 경우, 함락 하고 있지 않으면 안 됨
SIF TALENT:120 && TALENT:150 == 0 && TALENT:160 == 0 && TALENT:170 == 0
	RETURN 0
;순종이 3이상 없으면 안 됨
SIF ABL:10 < 3
	RETURN 0
;수면중은 불가
SIF EQUIP:14
	RETURN 0
;B계 장착도구 사용중은 불가
SIF TEQUIP:35 || TEQUIP:36
	RETURN 0
;긴박 중에는 불가
SIF TEQUIP:40
	RETURN 0
;아이마스크 장착 중에는 불가
SIF TEQUIP:41
	RETURN 0
;목욕탕 플레이중은 플레이 매트가 없으면 안 됨
SIF TEQUIP:53 && ITEM:42 == 0 && NOITEM == 0
	RETURN 0
;샤워 중에는 불가
SIF TEQUIP:54
	RETURN 0
;00목욕탕 입욕 중에는 불가
SIF TEQUIP:55
	RETURN 0
;의사 플레이 중에는 불가
SIF TEQUIP:57
	RETURN 0
;기저귀 플레이 중에는 불가
SIF TEQUIP:58
	RETURN 0
;알몸 앞치마 중에는 불가
SIF TEQUIP:59 == 1
	RETURN 0
;코스프레 중에는 불가
SIF TEQUIP:59 > 2
	RETURN 0
;나체정식 중에는 안 됨
SIF TEQUIP:60
	RETURN 0
RETURN 1

;--------------------------------------------------
;커맨드 본체
;--------------------------------------------------
@COM91
PRINTL 스쿠미즈 플레이

;-------------------------------------------------
;실행할 수 있는가 판정
;-------------------------------------------------
IF TFLAG:699 == 0
	CALL COM_ORDER
	;실행할 수 없다
	SIF RESULT == 0
		RETURN 0
ENDIF

;-------------------------------------------------
;실행 결정
;-------------------------------------------------
TSTR:0 = 스쿠미즈 플레이
CALL TRAIN_MESSAGE_COM091

;-------------------------------------------------
;소스의 계산
;-------------------------------------------------
DOWNBASE:0 += 5
DOWNBASE:1 += 40

LOCAL:1 = 500
LOCAL:2 = 500

;PALAM:욕정을 본다
IF PALAM:13 < PALAMLV:1
	TIMES LOCAL:1 , 0.80
ELSEIF PALAM:13 < PALAMLV:2
	TIMES LOCAL:1 , 0.90
ELSEIF PALAM:13 < PALAMLV:3
	TIMES LOCAL:1 , 1.00
ELSEIF PALAM:13 < PALAMLV:4
	TIMES LOCAL:1 , 1.10
ELSE
	TIMES LOCAL:1 , 1.20
ENDIF
;ABL:노출벽을 본다
IF ABL:14 == 0
	SOURCE:24 += 0
	SOURCE:19 += 0
	TIMES LOCAL:1, 1.00
ELSEIF ABL:14 == 1
	SOURCE:24 += 50
	SOURCE:19 += 50
	TIMES LOCAL:1, 1.20
ELSEIF ABL:14 == 2
	SOURCE:24 += 100
	SOURCE:19 += 100
	TIMES LOCAL:1, 1.40
ELSEIF ABL:14 == 3
	SOURCE:24 += 200
	SOURCE:19 += 200
	TIMES LOCAL:1, 1.60
ELSEIF ABL:14 == 4
	SOURCE:24 += 400
	SOURCE:19 += 400
	TIMES LOCAL:1, 2.00
ELSE
	SOURCE:24 += 800
	SOURCE:19 += 800
	TIMES LOCAL:1, 3.00
ENDIF
;ABL:마조끼를 본다
IF ABL:21 == 0
	TIMES LOCAL:1 , 0.80
ELSEIF ABL:21 == 1
	TIMES LOCAL:1 , 1.00
ELSEIF ABL:21 == 2
	TIMES LOCAL:1 , 1.30
ELSEIF ABL:21 == 3
	TIMES LOCAL:1 , 1.60
ELSEIF ABL:21 == 4
	TIMES LOCAL:1 , 2.00
ELSE
	TIMES LOCAL:1 , 3.00
ENDIF

;臆病
SIF TALENT:10
	TIMES LOCAL:2 , 2.00
;目立ちたがり
SIF TALENT:28
	TIMES LOCAL:1 , 1.50
;解放
SIF TALENT:33
	TIMES LOCAL:1 , 1.50
;부끄럼쟁이
SIF TALENT:35
	TIMES LOCAL:2 , 2.00
;巨乳
IF TALENT:112
	SOURCE:20 = 500
;爆乳
ELSEIF TALENT:113
	SOURCE:20 = 800
ENDIF
;小柄体型
IF TALENT:128
	TIMES LOCAL:1 , 1.50
	TIMES LOCAL:2 , 1.50
ENDIF
;유아유치유아퇴행
SIF TALENT:130 || TALENT:131 || TALENT:159
	TIMES LOCAL:1 , 2.00

SOURCE:12 += LOCAL:1
SOURCE:22 += LOCAL:1 /2
SOURCE:31 += LOCAL:2

;남자
IF TALENT:120
	SOURCE:20 += 1600
	SOURCE:28 += LOCAL:1 /2
	TIMES SOURCE:31 , 2.50
;絶壁
ELSEIF TALENT:110
	SOURCE:28 += LOCAL:1 /4
;貧乳
ELSEIF TALENT:111
	SOURCE:28 += LOCAL:1 /6
;普通以上のおっぱいの女
ELSE
	SOURCE:28 += LOCAL:1 /8
ENDIF

SOURCE:25 += LOCAL:2 /5
;유아유치유아퇴행
SIF TALENT:130 || TALENT:131 || TALENT:159
	SOURCE:25 += SOURCE:31/2

;スク水の着脱
IF TEQUIP:59 == 2
	TEQUIP:59 = 0
ELSE
	TEQUIP:59 = 2
	;調教対象のコスプレ経験
	CFLAG:50 += 1
ENDIF
RETURN 1

;-------------------------------------------------
;スク水플레이 중
;-------------------------------------------------
@EQUIP_COM91
PRINTL <스쿠미즈 플레이 중>

DOWNBASE:0 += 0
DOWNBASE:1 += 20

LOCAL = 500
LOCAL:1 = 500

;PALAM:욕정을 본다
IF PALAM:13 < PALAMLV:1
	TIMES LOCAL , 0.80
ELSEIF PALAM:13 < PALAMLV:2
	TIMES LOCAL , 0.90
ELSEIF PALAM:13 < PALAMLV:3
	TIMES LOCAL , 1.00
ELSEIF PALAM:13 < PALAMLV:4
	TIMES LOCAL , 1.10
ELSE
	TIMES LOCAL , 1.20
ENDIF
;ABL:노출벽을 본다
IF ABL:14 == 0
	SOURCE:24 += 0
	SOURCE:19 += 0
	TIMES LOCAL, 1.00
ELSEIF ABL:14 == 1
	SOURCE:24 += 50
	SOURCE:19 += 50
	TIMES LOCAL, 1.20
ELSEIF ABL:14 == 2
	SOURCE:24 += 100
	SOURCE:19 += 100
	TIMES LOCAL, 1.40
ELSEIF ABL:14 == 3
	SOURCE:24 += 200
	SOURCE:19 += 200
	TIMES LOCAL, 1.60
ELSEIF ABL:14 == 4
	SOURCE:24 += 400
	SOURCE:19 += 400
	TIMES LOCAL, 2.00
ELSE
	SOURCE:24 += 800
	SOURCE:19 += 800
	TIMES LOCAL, 3.00
ENDIF
;ABL:마조끼를 본다
IF ABL:21 == 0
	TIMES LOCAL , 0.80
ELSEIF ABL:21 == 1
	TIMES LOCAL , 1.00
ELSEIF ABL:21 == 2
	TIMES LOCAL , 1.30
ELSEIF ABL:21 == 3
	TIMES LOCAL , 1.60
ELSEIF ABL:21 == 4
	TIMES LOCAL , 2.00
ELSE
	TIMES LOCAL , 3.00
ENDIF

;臆病
SIF TALENT:10
	TIMES LOCAL:1 , 2.00
;目立ちたがり
SIF TALENT:28
	TIMES LOCAL , 1.50
;解放
SIF TALENT:33
	TIMES LOCAL , 1.50
;부끄럼쟁이
SIF TALENT:35
	TIMES LOCAL:1 , 2.00
;巨乳
IF TALENT:112
	SOURCE:20 += 250
;爆乳
ELSEIF TALENT:113
	SOURCE:20 += 400
ENDIF
;小柄体型
IF TALENT:128
	TIMES LOCAL , 1.50
	TIMES LOCAL:1 , 1.50
ENDIF
;유아유치유아퇴행
SIF TALENT:130 || TALENT:131 || TALENT:159
	TIMES LOCAL , 2.00

SOURCE:12 += LOCAL
SOURCE:22 += LOCAL/2
SOURCE:31 += LOCAL:1

;남자
IF TALENT:120
	SOURCE:20 += 800
	SOURCE:28 += LOCAL/2
	TIMES SOURCE:31 , 2.50
;絶壁
ELSEIF TALENT:110
	SOURCE:28 += LOCAL/4
;貧乳
ELSEIF TALENT:111
	SOURCE:28 += LOCAL/6
ELSE
	SOURCE:28 += LOCAL/8
ENDIF

SOURCE:25 += LOCAL:1 /5
;유아유치유아퇴행
SIF TALENT:130 || TALENT:131 || TALENT:159
	SOURCE:25 += LOCAL:1 /20

IF TALENT:120 == 0 && TALENT:PLAYER:120 == 0
	TCVAR:12 += 1
ELSEIF TALENT:120 && TALENT:PLAYER:120
	TCVAR:13 += 1
ENDIF

;露出快楽経験入手判定
TFLAG:100 |= 2
;주인 경험 플래그
SIF ASSIPLAY == 0 && ABL:14 >= 3
	TFLAG:50 += 1

RETURN 1

;--------------------------------------------------
;명령 거부 메시지
;--------------------------------------------------
@DENIAL_MESSAGE_COM_91
CALL KOJO_DENIAL_MESSAGE_COM
SIF FLAG:10 == 0 || CFLAG:99 == 1 || RESULT != 0
	RETURN 0

PRINTL
IF TEQUIP:59 == 2
	PRINTFORML %CALLNAME:TARGET%에게 입고 있는 학교 수영복을 벗도록 명령했지만 거부당했다.
	PRINTW 좀 더 조교가 필요하다.
ELSE
	PRINTFORML %CALLNAME:TARGET%에게 학교 수영복을 입도록 명령했지만 거부당했다.
	PRINTW 좀 더 조교가 필요하다.
ENDIF

;--------------------------------------------------
;조교시 메시지
;--------------------------------------------------
@TRAIN_MESSAGE_COM091
CALL KOJO_TRAIN_MESSAGE_COM
SIF FLAG:10 == 0 || CFLAG:99 == 1 || RESULT != 0
	RETURN 0

PRINTL
IF TEQUIP:59 == 2
	IF MARK:9 > 1
		PRINTFORMW %조사선택(CALLNAME:PLAYER, "가")% 다가오자, %조사선택(CALLNAME:TARGET, "는")% 입고 있던 수영복을 벗어서 던졌다.
	ELSEIF TALENT:153 && ASSIPLAY == 0
		PRINTFORML %조사선택(CALLNAME:PLAYER, "가")%, %조사선택(CALLNAME:TARGET, "가")% 입고 있는 수영복을 벗기자
		PRINTFORMW %조사선택(CALLNAME:TARGET, "는")% 약간 빨개진 얼굴을 하고 있다.
	ELSE
		PRINTFORMW %조사선택(CALLNAME:PLAYER, "는")%, %조사선택(CALLNAME:TARGET, "가")% 입고 있는 수영복을 벗게 했다….
	ENDIF
ELSE
	PRINTFORML %조사선택(CALLNAME:PLAYER, "는")%, %CALLNAME:TARGET%에게 학교 수영복을 입도록 명령했다.
	PRINTFORM %조사선택(CALLNAME:TARGET, "는")% 
	IF TALENT:35
		PRINTFORMW 얼굴을 새빨갛게 하면서 %CALLNAME:PLAYER%의 명령을 실행하고 있다….
	ELSEIF TALENT:110 || TALENT:111
		PRINTL 수영복을 입은 건 좋긴 하지만
		PRINTW 가슴이 납작해서 궁상스러운지 끊임없이 가슴을 가리려고 하고 있다.
	ELSEIF TALENT:112 || TALENT:113
		PRINTL 수영복을 입은 건 좋긴 하지만
		PRINTW 수영복이 조이는지 앞가슴 쪽을 자꾸 잡아 당기고 있다.
	ELSEIF TALENT:130 || TALENT:131
		PRINTW 수영복을 입고 아이처럼 까불며 떠들기 시작했다.
	ELSEIF TALENT:80 || TALENT:170
		PRINTL 처음에는 주저했지만
		PRINTW 새로운 쾌감에 눈을 뜬 건지 후다닥 갈아입었다.
	ELSEIF TALENT:153 || TALENT:162
		PRINTFORMW %조사선택(CALLNAME:MASTER, "가")% 온몸을 즐길 수 있듯이 빙글 돌면서 갈아 입었다.
	ELSEIF MARK:9 > 1
		PRINTW 노골적으로 혐오감을 띠면서 수영복을 갈아 입었다.
	ELSE
		PRINTW 기가 막힌 표정을 지으면서 수영복을 갈아 입었다.
	ENDIF
ENDIF

;-------------------------------------------------
;고유 실행 판정
;-------------------------------------------------
@COM_ORDER_91, ARG
LOCAL:99 = ARG
;ABL:욕망
IF ABL:11
	SIF LOCAL:99
		RESULTS = %RESULTS% + 
	LOCAL:99 = ABL:11
	TFLAG:240 += LOCAL:99 * 3
	RESULTS = %RESULTS% %ABLNAME:11%LV{LOCAL:99}({LOCAL:99 * 3})
ENDIF
;ABL:노출벽
IF ABL:14
	SIF LOCAL:99
		RESULTS = %RESULTS% + 
	LOCAL:99 = ABL:14
	TFLAG:240 += LOCAL:99 * 4
	RESULTS = %RESULTS% %ABLNAME:14%LV{LOCAL:99}({LOCAL:99 * 4})
ENDIF
;ABL:자위중독
IF ABL:30
	SIF LOCAL:99
		RESULTS = %RESULTS% + 
	LOCAL:99 = ABL:30
	TFLAG:240 += LOCAL:99 * 3
	RESULTS = %RESULTS% %ABLNAME:30%LV{LOCAL:99}({LOCAL:99 * 3})
ENDIF

;쾌락각인
IF MARK:1
	SIF LOCAL:99
		RESULTS = %RESULTS% + 
	LOCAL:99 = MARK:1
	TFLAG:240 += LOCAL:99 * 3
	RESULTS = %RESULTS% %MARKNAME:1%LV{LOCAL:99}({LOCAL:99 * 3})
ENDIF

;PALAM:욕정
GETPALAMLV PALAM:13, 5
LOCAL:2 = RESULT
IF LOCAL:2
	SIF LOCAL:99
		RESULTS = %RESULTS% + 
	LOCAL:99 = LOCAL:2 * 3
	TFLAG:240 += LOCAL:99
	RESULTS = %RESULTS% %PALAMNAME:13%LV{LOCAL:2}({LOCAL:99})
ENDIF

;자제심
IF TALENT:20
	RESULTS = %RESULTS% - 
	LOCAL:99 = 5
	TFLAG:240 -= LOCAL:99
	RESULTS = %RESULTS% %TALENTNAME:20%({LOCAL:99})
ENDIF
;부끄럼쟁이
IF TALENT:35
	RESULTS = %RESULTS% - 
	LOCAL:99 = 5
	TFLAG:240 -= LOCAL:99
	RESULTS = %RESULTS% %TALENTNAME:35%({LOCAL:99})
;恥薄い
ELSEIF TALENT:36
	SIF LOCAL:99
		RESULTS = %RESULTS% + 
	LOCAL:99 = 2
	TFLAG:240 += LOCAL:99
	RESULTS = %RESULTS% %TALENTNAME:36%({LOCAL:99})
ENDIF
;自慰しやすい
IF TALENT:60
	SIF LOCAL:99
		RESULTS = %RESULTS% + 
	LOCAL:99 = 5
	TFLAG:240 += LOCAL:99
	RESULTS = %RESULTS% %TALENTNAME:60%({LOCAL:99})
ENDIF
;쾌감에 솔직
IF TALENT:70
	SIF LOCAL:99
		RESULTS = %RESULTS% + 
	LOCAL:99 = 5
	TFLAG:240 += LOCAL:99
	RESULTS = %RESULTS% %TALENTNAME:70%({LOCAL:99})
;쾌감의 부정
ELSEIF TALENT:71
	RESULTS = %RESULTS% - 
	LOCAL:99 = 5
	TFLAG:240 -= LOCAL:99
	RESULTS = %RESULTS% %TALENTNAME:71%({LOCAL:99})
ENDIF
;남자
IF TALENT:120
	RESULTS = %RESULTS% - 
	LOCAL:99 = 15
	TFLAG:240 -= LOCAL:99
	RESULTS = %RESULTS% %TALENTNAME:120%({LOCAL:99})
ENDIF

;媚薬
IF TEQUIP:11
	SIF LOCAL:99
		RESULTS = %RESULTS% + 
	LOCAL:99 = 8
	TFLAG:240 += LOCAL:99
	RESULTS = %RESULTS% %ITEMNAME:61%({LOCAL:99})
ENDIF

;難易度上昇
;ビデオまたは野外で+10羞恥플레이 중で+7Ｃ系도구またはカテーテル系装着で＋3
;バイブ系やアナル系の도구で+5拡張系도구装着でさらに＋3
TFLAG:241 = 27
SIF TEQUIP:50 || TEQUIP:52
	TFLAG:241 += 10
SIF TEQUIP:56
	TFLAG:241 += 7
SIF TEQUIP:30 || TEQUIP:32
	TFLAG:241 += 3
SIF TEQUIP:20 || TEQUIP:21
	TFLAG:241 += 5
SIF TEQUIP:25 || TEQUIP:26
	TFLAG:241 += 5
SIF TEQUIP:20 == 2 || TEQUIP:20 == 5 || TEQUIP:20 == 6 || TEQUIP:20 == 8 || TEQUIP:25 == 2 || TEQUIP:25 == 5 || TEQUIP:25 == 6 || TEQUIP:25 == 8 || TEQUIP:26 > 2
	TFLAG:241 += 3
