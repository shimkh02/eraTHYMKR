;=============================================================================
;클리캡
;=============================================================================
;………………………………………………
;실행 판정
;………………………………………………
@COM_ABLE14
;개별 설정 커맨드 필터
SIF DITEMTYPE:999:14 > 0
	RETURN 0
;강력 아이템·커맨드 무효시는 안 됨
SIF (FLAG:13 & 2) == 0
	RETURN 0
;승마 중에는 불가
SIF TEQUIP:43
	RETURN 0
;한증과 암반 욕 이외의00목욕탕 입욕 중에는 불가
SIF TEQUIP:55 && TEQUIP:55 != 12 && TEQUIP:55 != 15
	RETURN 0
;기저귀 플레이 중에는 불가
SIF TEQUIP:58
	RETURN 0
;훈도시 착용중의 경우 조교자의 기교 4이상 필수
SIF TEQUIP:59 == 17 && ABL:PLAYER:12 < 4
	RETURN 0
;촉수 조교 중에는 불가
SIF TEQUIP:90
	RETURN 0
;해제는 언제라도 가능
SIF TEQUIP:30 == 1
	RETURN 1
;아이템을 가지고 있는지, 아이템 없음 설정이 아니면 안 됨
SIF ITEM:15 == 0 && NOITEM == 0
	RETURN 0
;조교 대상이 [소인 체형]의 경우는 주인이 [금단의 지식] 소유가 아니면 안 됨
SIF TALENT:127 && TALENT:MASTER:56 == 0
	RETURN 0
;대상으로 페니스가 있다면 안 됨
SIF EXIST_BAR(TARGET)
	RETURN 0
;ASSI의 경우, 기교 3이상이 아니면 사용할 수 없다.
IF ASSIPLAY
	SIF ABL:ASSI:12 < 3
		RETURN 0
ENDIF
;다른 C계 장착도구 사용중은 불가
SIF TEQUIP:30 && TEQUIP:30 != 1
	RETURN 0
;샤워 중에는 불가
SIF TEQUIP:54
	RETURN 0
RETURN 1

;--------------------------------------------------
;커맨드 본체
;--------------------------------------------------
;-------------------------------------------------------
;클리캡(SELECTCOM=14), 촉수 클리 고문(SELECTCOM=503)
;--------------------------------------------------------
;촉수 클리 고문은 COMF500.ERB로부터 JUMP 해 온다.
@COM14
IF TEQUIP:90
	PRINTL 촉수 클리 고문
	TSTR:0 = 촉수 클리 고문
ELSE
	TSTR:7 = 전동 클리캡
	IF FLAG:1100 & 1
		;技師パッチ有効なら特殊な名前チェック
		FLAG:1110 = 15
		CALL NAMECHECK_CUSTOMTOOL
	ELSE
		TSTR:8 = %TSTR:7%
		TSTR:9 = %TSTR:7%
	ENDIF
	PRINTFORML %TSTR:8%
	TSTR:0 = 전동 클리캡
ENDIF
CALL TRAIN_MESSAGE_COM14

DOWNBASE:0 += 10
DOWNBASE:1 += 80

SOURCE:12 = 120
SOURCE:31 = 70

;ABL:C감각을 본다
IF ABL:0 == 0
	SOURCE:0 = 200
ELSEIF ABL:0 == 1
	SOURCE:0 = 400
ELSEIF ABL:0 == 2
	SOURCE:0 = 900
ELSEIF ABL:0 == 3
	SOURCE:0 = 1600
ELSEIF ABL:0 == 4
	SOURCE:0 = 2400
ELSE
	SOURCE:0 = 3000
ENDIF

IF (FLAG:1100 & 1) && TEQUIP:90 == 0
	;技師パッチ有効なら道具使用時ソースチェックを呼ぶ
	;FLAG 1110:道具番号 1111:改善タイプ 1112:SOURCEの実値 
	FLAG:1110 = 15
	FLAG:1111 = 0
	FLAG:1112 = SOURCE:0
	CALL SOURCECHECK_CUSTOMTOOL
	SOURCE:0 = FLAG:1120
ENDIF

;클리캡의 착탈
IF TEQUIP:30
	TEQUIP:30 = 0
ELSE
	IF TEQUIP:90
		STAIN:3 |= 2
		STAIN:3 |= 4
	ENDIF
	TEQUIP:30 = 1
ENDIF

IF TALENT:120 == 0 && TALENT:PLAYER:120 == 0
	TCVAR:12 += 1
ELSEIF TALENT:120 && TALENT:PLAYER:120
	TCVAR:13 += 1
ENDIF
SIF PLAYER == MASTER && TEQUIP:30 == 1
	TCVAR:41 += 1

;커맨드 속성：촉수
IF TEQUIP:90
	TFLAG:98 = 8
;커맨드 속성：도구(조교자)
ELSE
	TFLAG:98 = 1
ENDIF
;촉수 경험 플래그
TFLAG:90 = 0
RETURN 1

;-------------------------------------------------
;클리캡 장비중
;-------------------------------------------------
@EQUIP_COM14
IF TEQUIP:90
	PRINTL <촉수클리고문 중>
ELSE
	TSTR:7 = 클리캡
	IF FLAG:1100 & 1
		;技師パッチ有効なら特殊な名前チェック
		FLAG:1110 = 15
		CALL NAMECHECK_CUSTOMTOOL
	ENDIF
	PRINTFORML <%TSTR:7% 장비 중>
ENDIF

DOWNBASE:0 += 5
DOWNBASE:1 += 20

;ABL:C감각을 본다
IF ABL:0 == 0
	LOCAL = 40
ELSEIF ABL:0 == 1
	LOCAL = 120
ELSEIF ABL:0 == 2
	LOCAL = 250
ELSEIF ABL:0 == 3
	LOCAL = 450
ELSEIF ABL:0 == 4
	LOCAL = 600
ELSE
	LOCAL = 750
ENDIF

;PALAM:욕정을 본다
IF PALAM:13 < PALAMLV:1
	TIMES LOCAL , 0.80
ELSEIF PALAM:13 < PALAMLV:2
	TIMES LOCAL , 0.90
ELSEIF PALAM:13 < PALAMLV:3
	TIMES LOCAL , 1.00
ELSEIF PALAM:13 < PALAMLV:4
	TIMES LOCAL , 1.10
ELSE
	TIMES LOCAL , 1.20
ENDIF

;ABL:순종을 본다
IF ABL:10 == 0
	TIMES LOCAL , 0.80
ELSEIF ABL:10 == 1
	TIMES LOCAL , 0.90
ELSEIF ABL:10 == 2
	TIMES LOCAL , 1.00
ELSEIF ABL:10 == 3
	TIMES LOCAL , 1.10
ELSEIF ABL:10 == 4
	TIMES LOCAL , 1.20
ELSE
	TIMES LOCAL , 1.30
ENDIF

IF (FLAG:1100 & 1) && TEQUIP:90 == 0
	;技師パッチ有効なら道具使用時ソースチェックを呼ぶ
	;FLAG 1110:道具番号 1111:改善タイプ 1112:SOURCEの実値 
	FLAG:1110 = 15
	FLAG:1111 = 0
	FLAG:1112 = LOCAL
	CALL SOURCECHECK_CUSTOMTOOL
	LOCAL = FLAG:1120
ENDIF

SOURCE:0 += LOCAL
SOURCE:12 += 50
SOURCE:18 += 50
;촉수 경험 플래그
SIF TEQUIP:90
	TFLAG:90 += 1

RETURN 1

;--------------------------------------------------
;조교시 메시지
;--------------------------------------------------
@TRAIN_MESSAGE_COM14
CALL KOJO_TRAIN_MESSAGE_COM
SIF FLAG:10 == 0 || CFLAG:99 == 1 || RESULT != 0
	RETURN 0

PRINTL
IF TEQUIP:90
	IF TEQUIP:30
		IF ABL:35 > 2
			PRINTFORML 촉수는 %CALLNAME:TARGET%의 클리 고문을 그만두었다.
			PRINTFORMW %조사선택(CALLNAME:TARGET, "는")% 아직 모자란다는 표정을 지었다….
		ELSEIF MARK:9 > 1
			PRINTFORML 촉수는 %CALLNAME:TARGET%의 클리 고문을 그만두었다.
			PRINTFORMW %조사선택(CALLNAME:TARGET, "는")% 촉수에게 더럽혀진 부분을 곤란한 표정으로 응시하고 있다….
		ELSE
			PRINTFORMW 촉수는 %CALLNAME:TARGET%의 클리 고문을 그만두었다.
		ENDIF
	ELSE
		IF ABL:35 > 2
			PRINTFORML 세세하게 나눠진 촉수가 %CALLNAME:TARGET%의 클리토리스를 쥐어짜기 시작하자, 
			PRINTFORMW %조사선택(CALLNAME:TARGET, "는")% 그 자극을 전신으로 느끼면서 요염한 \@(TEQUIP:42) ? 행동으로 # 소리를 내어 \@, %조사선택(CALLNAME:PLAYER, "를")% 기쁘게 했다….
		ELSEIF MARK:9 > 1
			PRINTFORML 세세하게 나눠진 촉수가 %CALLNAME:TARGET%의 클리토리스를 쥐어짜기 시작했다.
			PRINTFORMW %조사선택(CALLNAME:TARGET, "는")% 어떻게든 촉수를 피해보려고 몸을 꼬고 있다….
		ELSE
			PRINTFORMW 세세하게 나눠진 촉수가 %CALLNAME:TARGET%의 클리토리스를 쥐어짜기 시작했다.
		ENDIF
	ENDIF
ELSE
	IF TEQUIP:30
		IF MARK:9 > 1
			PRINTFORMW %조사선택(CALLNAME:PLAYER, "가")% 미처 손을 쓰기도 전에, %조사선택(CALLNAME:TARGET, "는")% %조사선택(TSTR:7, "를")% 벗어 던졌다….
		ELSEIF TALENT:153 && ASSIPLAY == 0
			PRINTFORML %조사선택(CALLNAME:PLAYER, "는")% %조사선택(CALLNAME:TARGET, "로")%부터 %조사선택(TSTR:7, "를")% 벗겼다.
			PRINTFORMW %조사선택(CALLNAME:TARGET, "는")% 다음엔 어디를 괴롭혀줄 지를 기대하며 %조사선택(CALLNAME:PLAYER, "를")% 뜨거운 시선으로 바라보고 있다….
		ELSE
			PRINTFORMW %조사선택(CALLNAME:PLAYER, "는")% %조사선택(CALLNAME:TARGET, "로")%부터 %조사선택(TSTR:7, "를")% 벗겼다….
		ENDIF
	ELSE
		IF MARK:9 > 1
			PRINTFORML %조사선택(CALLNAME:PLAYER, "는")% 거부하는 %조사선택(CALLNAME:TARGET, "를")% 억눌렀다.
			PRINTFORMW 그리고, %CALLNAME:TARGET%의 비부에 %조사선택(TSTR:9, "를")% 달았다….
		ELSE
			PRINTFORMW %조사선택(CALLNAME:PLAYER, "는")% %CALLNAME:TARGET%에게 %조사선택(TSTR:9, "를")% 달았다….
		ENDIF
	ENDIF
ENDIF
