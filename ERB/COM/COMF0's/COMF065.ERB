;=============================================================================
;현자의 피
;=============================================================================
;몇 커맨드 동안 절정이 발생하지 않게 되는 약을 먹인다.
;효과가 끝난 다음, 반동으로 절정을 제지당해 버린 부위의 쾌감 소스가 몇 차례에 걸쳐 크게 상승합니다.
;………………………………………………
;실행 판정
;………………………………………………
@COM_ABLE65
;개별 설정 커맨드 필터
SIF DITEMTYPE:999:65 > 0
	RETURN 0
;약품계 확장 커맨드를 유효하게 하지 않으면 안 됨
SIF (FLAG:15 & 32) == 0
	RETURN 0
;현자의 피를 가지고 있는지, 아이템 없음 설정이 아니면 안 됨
SIF ITEM:66 == 0 && NOITEM == 0
	RETURN 0
;약물내성이라면 안 됨
SIF TALENT:44
	RETURN 0
;기계라면 안 됨
SIF TALENT:206
	RETURN 0
;산란 촉진제를 사용하고 있으면(자) 사용할 수 없다
SIF TEQUIP:9
	RETURN 0
;효과 발휘중·반동 중에는 불가
SIF TEQUIP:13
	RETURN 0
;수면 중에는 불가
SIF EQUIP:14
	RETURN 0
;강제개구기 외에 다른 걸로 입이 막혀 있으면 불가
SIF TEQUIP:42 && TEQUIP:42 != 3
	RETURN 0
;촉수 조교 중에는 불가
SIF TEQUIP:90
	RETURN 0
RETURN 1

;--------------------------------------------------
;커맨드 본체
;--------------------------------------------------
@COM65
PRINTL 현자의 피
TSTR:0 = 현자의 피
CALL TRAIN_MESSAGE_COM065

DOWNBASE:0 += 20
DOWNBASE:1 += 100

SOURCE:26 = 2500
SOURCE:31 = 3000

IF NOITEM == 0
	ITEM:66 -= 1
	SIF FLAG:13 & 8 && MONEY:0 >= ITEMPRICE:66
		CALL FILL_PLURAL_ITEM, 66
ENDIF
TEQUIP:13 = 8

;약물 경험
TCVAR:40 += 3

RETURN 1

;--------------------------------------------------
;현자의 피 효과 발휘중
;--------------------------------------------------
@EQUIP_COM65
IF TEQUIP:13 > 2
	PRINTL <현자의 피 효과 발휘 중>
	TEQUIP:13 -= 1
ELSEIF TEQUIP:13 == 2
	PRINTL <현자의 피 반동 중>
	TEQUIP:13 = 1
	CALL EQUIP_MESSAGE_COM065
	CALL SELF_KOJO, TARGET, 2
ELSEIF TEQUIP:13 == 1
	PRINTL <현자의 피 반동 중>
ENDIF
RETURN 1

;-------------------------------------------------
;절정의 캔슬 처리
;-------------------------------------------------
;SOURCE.ERB의 절정 처리를 유용
@ECST_CANCEL
VARSET LOCAL, 0

;절정 C
IF UP:0 + PALAM:0 >= PALAMLV:7 * 2
	LOCAL:0 = 4
	DOWN:0 = (PALAMLV:4 * 2 - 1000)
ELSEIF UP:0 + PALAM:0 >= PALAMLV:4 * 2
	LOCAL:0 = 2
	DOWN:0 = (PALAMLV:4 * 2 - 1000)
ELSEIF UP:0 + PALAM:0 >= PALAMLV:4
	LOCAL:0 = 1
	PALAM:0 = 5000
	UP:0 = 0
ENDIF
;DOWN:1으로 내려도 절정 이상이라면
;그 값-1이 되도록(듯이) 조정(10000으로 절정이라면 9999)(감각 증폭 약효과중은 캔슬)
SIF UP:0 + PALAM:0 - DOWN:0 >= PALAMLV:4 && TEQUIP:7 == 0
	DOWN:0 = UP:0 + PALAM:0 - PALAMLV:4 + 1

;절정 V
IF UP:1 + PALAM:1 >= PALAMLV:7 * 2
	LOCAL:1 = 4
	DOWN:1 = (PALAMLV:4 * 2 - 1000)
ELSEIF UP:1 + PALAM:1 >= PALAMLV:4 * 2
	LOCAL:1 = 2
	DOWN:1 = (PALAMLV:4 * 2 - 1000)
ELSEIF UP:1 + PALAM:1 >= PALAMLV:4
	LOCAL:1 = 1
	DOWN:1 = (PALAMLV:4 - 1000)
ENDIF
;DOWN:1으로 내려도 절정 이상이라면
;그 값-1이 되도록(듯이) 조정(10000으로 절정이라면 9999)(감각 증폭 약효과중은 캔슬)
SIF UP:1 + PALAM:1 - DOWN:1 >= PALAMLV:4 && TEQUIP:7 == 0
	DOWN:1 = UP:1 + PALAM:1 - PALAMLV:4 + 1

;절정 A
IF UP:2 + PALAM:2 >= PALAMLV:7 * 2
	LOCAL:2 = 4
	DOWN:2 = (PALAMLV:4 * 2 - 1000)
ELSEIF UP:2 + PALAM:2 >= PALAMLV:4 * 2
	LOCAL:2 = 2
	DOWN:2 = (PALAMLV:4 * 2 - 1000)
ELSEIF UP:2 + PALAM:2 >= PALAMLV:4
	LOCAL:2 = 1
	DOWN:2 = (PALAMLV:4 - 1000)
ENDIF
;DOWN:2로 내려도 절정 이상이라면
;그 값-1이 되도록(듯이) 조정(10000으로 절정이라면 9999)(감각 증폭 약효과중은 캔슬)
SIF UP:2 + PALAM:2 - DOWN:2 >= PALAMLV:4 && TEQUIP:7 == 0
	DOWN:2 = UP:2 + PALAM:2 - PALAMLV:4 + 1

;절정 B
IF UP:3 + PALAM:3 >= PALAMLV:7 * 2
	LOCAL:3 = 4
	DOWN:3 = (PALAMLV:4 * 2 - 1000)
ELSEIF UP:3 + PALAM:3 >= PALAMLV:4 * 2
	LOCAL:3 = 2
	DOWN:3 = (PALAMLV:4 * 2 - 1000)
ELSEIF UP:3 + PALAM:3 >= PALAMLV:4
	LOCAL:3 = 1
	DOWN:3 = (PALAMLV:4 - 1000)
ENDIF
;DOWN:3으로 내려도 절정 이상이라면
;그 값-1이 되도록(듯이) 조정(10000으로 절정이라면 9999)(감각 증폭 약효과중은 캔슬)
SIF UP:3 + PALAM:3 - DOWN:3 >= PALAMLV:4 && TEQUIP:7 == 0
	DOWN:3 = UP:3 + PALAM:3 - PALAMLV:4 + 1
	
;제지당한 절정 회수를 TFLAG:110~TFLAG:113에 기록
TFLAG:110 += LOCAL:0
TFLAG:111 += LOCAL:1
TFLAG:112 += LOCAL:2
TFLAG:113 += LOCAL:3

RETURN 1

;-------------------------------------------------
;현자의 피의 부작용(조교중)
;-------------------------------------------------
;*사양 설명
;@ECST_CANCEL로 TFLAG:110~TFLAG:113에 기록해 온 절정 회수를 해방합니다. 내역은
;TFLAG:110 = C절정 처리)의 대신해, TFLAG:111 = V절정 처리)의 대신해,
;TFLAG:112 = A절정 처리)의 대신해, TFLAG:113 = B절정 처리)의 대신
;기록해 온 절정 회수 1회에 대해, 해당하는 부위의 쾌소스(UP:0, 1,2,14)를 2배로 합니다.
;1회의 커맨드로 하나의 부위에 대해 1회씩 이 배화 처리를 실시해,
;TFLAG:110~TFLAG:113의 합계가 0이 되었을 때, 반동이 해제됩니다.
@REACT_DRUG
;쾌C의 처리
IF TFLAG:110 > 0 && UP:0 > 0
	UP:0 *= 2
	TFLAG:110 -= 1
ENDIF
;쾌V의 처리
IF TFLAG:111 > 0 && UP:1 > 0
	UP:1 *= 2
	TFLAG:111 -= 1
ENDIF
;쾌A의 처리
IF TFLAG:112 > 0 && UP:2 > 0
	UP:2 *= 2
	TFLAG:112 -= 1
ENDIF
;쾌B의 처리
IF TFLAG:113 > 0 && UP:3 > 0
	UP:3 *= 2
	TFLAG:113 -= 1
ENDIF
;반동의 종료 체크
SIF TFLAG:110 + TFLAG:111 + TFLAG:112 + TFLAG:113 == 0
	TEQUIP:13 = 0

RETURN 1

;-------------------------------------------------
;현자의 피의 부작용(조교 종료 후 시)
;-------------------------------------------------
@REACT_DRUG_SELF
;현자의 피의 효과가 남은 채로 조교를 종료했을 경우의 처리
;통상시의 효과를 재현 할 수 있을 것 같지 않기 때문에 동시 다중 절정 했다……라고 하는 처리로 변경
;LOCAL:99는 다중 절정의 종류를 나타낸다(1의 경우는○절정, 2의 경우는○＋△절정, 4의 경우는 사중 절정……이 되고 있다)
VARSET LOCAL, 0

SIF TFLAG:110
	LOCAL:99 += 1
SIF TFLAG:111
	LOCAL:99 += 1
SIF TFLAG:112
	LOCAL:99 += 1
SIF TFLAG:113
	LOCAL:99 += 1

REPEAT 4
	LOCAL:COUNT = LOCAL:99 * TFLAG:(110 + COUNT)
REND

;절정 경험
CALL COMMON_UP_EXP, TARGET, 2, (TFLAG:110 + TFLAG:111 + TFLAG:112 + TFLAG:113), 0, 1
;쾌C의 주
CALL COMMON_UP_JUEL, TARGET, 0, (LOCAL:0*1000), 0, 1
;쾌V의 주
CALL COMMON_UP_JUEL, TARGET, 1, (LOCAL:1*1000), 0, 1
;쾌A의 주
CALL COMMON_UP_JUEL, TARGET, 2, (LOCAL:2*1000), 0, 1
;쾌B의 주
CALL COMMON_UP_JUEL, TARGET, 3, (LOCAL:3*1000), 0, 1
WAIT

;리셋
TFLAG:110 = 0
TFLAG:111 = 0
TFLAG:112 = 0
TFLAG:113 = 0
TEQUIP:13 = 0
VARSET LOCAL, 0

;부작용으로 체력이 1에
BASE:0 = LIMIT(BASE:0, 0, 1)

RETURN 1

;--------------------------------------------------
;조교시 메시지
;--------------------------------------------------
@TRAIN_MESSAGE_COM065
CALL KOJO_TRAIN_MESSAGE_COM
SIF FLAG:10 == 0 || CFLAG:99 == 1 || RESULT != 0
	RETURN 0

PRINTL
PRINTFORM %조사선택(CALLNAME:MASTER, "는")% %CALLNAME:TARGET%에게 
SIF ABL:PLAYER:12 >= 3
	PRINT 입으로 
SIF ABL:10 == 0
	PRINT 억지로 
PRINTL 현자의 피를 먹였다.
PRINTFORM %조사선택(CALLNAME:TARGET, "는")% 
IF ABL:10 > 3 && MARK:9 < 3
	PRINTFORML 곤혹한 표정을 지으며 능글능글 웃는 %조사선택(CALLNAME:MASTER, "를")% 불안하게 보고 있다.
ELSEIF MARK:9 >= 3
	PRINTFORML 경계심을 띠며 능글능글 웃는 %조사선택(CALLNAME:MASTER, "를")% 째려보고 있다.
ELSE
	PRINTFORML 놀란 표정을 지으며 능글능글 웃는 %조사선택(CALLNAME:MASTER, "를")% 보고 있다.
ENDIF
PRINTW

;--------------------------------------------------
;장착시 메시지
;--------------------------------------------------
@EQUIP_MESSAGE_COM065
CALL KOJO_EQUIP_MESSAGE_COM
SIF FLAG:10 == 0 || CFLAG:99 == 1 || RESULT != 0
	RETURN 0

PRINTL
PRINTFORML %CALLNAME:TARGET%의 현자의 피의 효과가 끊어진 것 같다.
PRINTFORMW 지금까지 모였던 쾌감이 단번에 몰려와, %조사선택(CALLNAME:TARGET, "는")% 몸을 비틀면서 허덕이고 있다.
