;=============================================================================
;좌약
;=============================================================================
;………………………………………………
;실행 판정
;………………………………………………
@COM_ABLE63
;개별 설정 커맨드 필터
SIF DITEMTYPE:999:63 > 0
	RETURN 0
;A조교 필터가 오프가 되지 않으면 안 됨
SIF FLAG:2 & 2
	RETURN 0
;아이템을 가지고 있는지, 아이템 없음 설정이 아니면 안 됨
SIF ITEM:63 == 0 && NOITEM == 0
	RETURN 0
;윤활 부족해, 순종 4 미만·레즈끼(BL끼) 4 미만의 ASSI의 경우, 조교 대상이 동성이라면 실행할 수 없다(새드나 새드끼 Lv3 이상, 또는 망신이나 복종이 있다면 OK)
IF PALAM:10 < PALAMLV:2 && ASSIPLAY
	SIF (ABL:ASSI:10 < 4 || ((TALENT:120 == 0 && TALENT:ASSI:120 == 0 && ABL:ASSI:22 < 4) || (TALENT:120 && TALENT:ASSI:120 && ABL:ASSI:23 < 4))) && TALENT:ASSI:81 == 0 && ABL:ASSI:20 < 3 && TALENT:ASSI:151 == 0 && TALENT:ASSI:160 == 0
		RETURN 0
ENDIF
;A계 장착도구 사용중은 불가
SIF TEQUIP:25 || TEQUIP:26
	RETURN 0
;승마 중에는 불가
SIF TEQUIP:43
	RETURN 0
;한증과 암반 욕 이외의00목욕탕 입욕 중에는 불가
SIF TEQUIP:55 && TEQUIP:55 != 12 && TEQUIP:55 != 15
	RETURN 0
;기저귀 플레이 중에는 불가
SIF TEQUIP:58
	RETURN 0
;훈도시 착용중의 경우 조교자의 기교 4이상 필수
SIF TEQUIP:59 == 17 && ABL:PLAYER:12 < 4
	RETURN 0
;학교수영복 플레이 중에는 불가
SIF TEQUIP:59 == 2
	RETURN 0
;촉수 조교 중에는 불가
SIF TEQUIP:90
	RETURN 0
RETURN 1

;--------------------------------------------------
;커맨드 본체
;--------------------------------------------------
;약독내성持ちだと効果が減弱(挿入時は除く)
@COM63
IF TEQUIP:15 & 1
	PRINTL 좌약 추가 삽입
	TSTR:0 = 좌약 추가 삽입
ELSE
	PRINTL 좌약 삽입
	TSTR:0 = 좌약 삽입
ENDIF

;調合知識があれば消費を抑える
IF TALENT:MASTER:55 || (ASSI >= 0 && TALENT:ASSI:55)
	DOWNBASE:0 += 5
	DOWNBASE:1 += 20
ELSE
	DOWNBASE:0 += 10
	DOWNBASE:1 += 50
ENDIF

SOURCE:12 = 350
SOURCE:31 = 225

;ABL:Ａ감각을 본다
IF ABL:2 == 0
	SOURCE:2 = 10
	SOURCE:22 = 50
ELSEIF ABL:2 == 1
	SOURCE:2 = 50
	SOURCE:22 = 75
ELSEIF ABL:2 == 2
	SOURCE:2 = 100
	SOURCE:22 = 150
ELSEIF ABL:2 == 3
	SOURCE:2 = 250
	SOURCE:22 = 300
ELSEIF ABL:2 == 4
	SOURCE:2 = 400
	SOURCE:22 = 500
ELSE
	SOURCE:2 = 750
	SOURCE:22 = 800
ENDIF

;EXP:Ａ경험을 본다
IF EXP:1 < EXPLV:1
	SOURCE:20 = 500
	TIMES SOURCE:2 , 0.20
	TIMES SOURCE:22 , 0.20
ELSEIF EXP:1 < EXPLV:2
	SOURCE:20 = 400
	TIMES SOURCE:2 , 0.50
	TIMES SOURCE:22 , 0.50
ELSEIF EXP:1 < EXPLV:3
	SOURCE:20 = 300
	TIMES SOURCE:2 , 1.00
	TIMES SOURCE:22 , 1.00
ELSEIF EXP:1 < EXPLV:4
	SOURCE:20 = 200
	TIMES SOURCE:2 , 1.20
	TIMES SOURCE:22 , 1.20
ELSEIF EXP:1 < EXPLV:5
	SOURCE:20 = 100
	TIMES SOURCE:2 , 1.60
	TIMES SOURCE:22 , 1.60
ELSE
	SOURCE:20 = 50
	TIMES SOURCE:2 , 1.80
	TIMES SOURCE:22 , 1.80
ENDIF

;PALAM:윤활을 본다
IF PALAM:10 < PALAMLV:1
	TIMES SOURCE:2 , 0.10
	TIMES SOURCE:20 , 3.00
	TIMES SOURCE:22 , 0.10
ELSEIF PALAM:10 < PALAMLV:2
	TIMES SOURCE:2 , 0.20
	TIMES SOURCE:20 , 2.00
	TIMES SOURCE:22 , 0.20
ELSEIF PALAM:10 < PALAMLV:3
	TIMES SOURCE:2 , 0.60
	TIMES SOURCE:20 , 1.00
	TIMES SOURCE:22 , 0.60
ELSEIF PALAM:10 < PALAMLV:4
	TIMES SOURCE:2 , 1.00
	TIMES SOURCE:20 , 0.50
	TIMES SOURCE:22 , 1.00
ELSEIF PALAM:10 >= PALAMLV:4
	TIMES SOURCE:2 , 2.00
	TIMES SOURCE:20 , 0.10
	TIMES SOURCE:22 , 2.00
ENDIF

;PALAM:욕정을 본다
IF PALAM:13 < PALAMLV:1
	TIMES SOURCE:2 , 0.30
	TIMES SOURCE:22 , 0.30
ELSEIF PALAM:13 < PALAMLV:2
	TIMES SOURCE:2 , 0.60
	TIMES SOURCE:22 , 0.60
ELSEIF PALAM:13 < PALAMLV:3
	TIMES SOURCE:2 , 1.00
	TIMES SOURCE:22 , 1.00
ELSEIF PALAM:13 < PALAMLV:4
	TIMES SOURCE:2 , 1.30
	TIMES SOURCE:22 , 1.30
ELSEIF PALAM:13 >= PALAMLV:4
	TIMES SOURCE:2 , 1.60
	TIMES SOURCE:22 , 1.60
ENDIF

;Ａ민감둔감을 본다
;快Ａ自体のチェックは後でまとめてやる
IF TALENT:104
	TIMES SOURCE:22 , 1.50
	TIMES SOURCE:31 , 1.50
ELSEIF TALENT:105
	TIMES SOURCE:22 , 0.60
	TIMES SOURCE:31 , 0.60
ENDIF

;처녀에다 정조관념
SIF TALENT:0 == 1 && TALENT:30
	SOURCE:22 /= 3

CALL TRAIN_MESSAGE_COM063
TEQUIP:15 |= 1

TCVAR:1 += 2

IF TALENT:120 == 0 && TALENT:PLAYER:120 == 0
	TCVAR:12 += 1
ELSEIF TALENT:120 && TALENT:PLAYER:120
	TCVAR:13 += 2
ENDIF

IF NOITEM == 0
	ITEM:63 -= 1
	SIF FLAG:13 & 8 && MONEY:0 >= ITEMPRICE:63
		CALL FILL_PLURAL_ITEM, 63
ENDIF
RETURN 1

;-------------------------------------------------
;坐剤効果発揮中
;-------------------------------------------------
@EQUIP_COM63
PRINTL <좌약 효과 발휘 중>

DOWNBASE:0 += 50
DOWNBASE:1 += 30

LOCAL:1 = 300
LOCAL:2 = 100

;ABL:Ａ감각을 본다
IF ABL:2 == 0
	LOCAL = 5
ELSEIF ABL:2 == 1
	LOCAL = 10
ELSEIF ABL:2 == 2
	LOCAL = 20
ELSEIF ABL:2 == 3
	LOCAL = 50
ELSEIF ABL:2 == 4
	LOCAL = 100
ELSE
	LOCAL = 180
ENDIF

;Ａ민감둔감을 본다
;快Ａ自体のチェックは後でまとめてやる
IF TALENT:104
	TIMES LOCAL:1 , 1.50
	TIMES LOCAL:2 , 1.50
ELSEIF TALENT:105
	TIMES LOCAL:1 , 0.60
	TIMES LOCAL:2 , 0.60
ENDIF
;약독내성
IF TALENT:44
	TIMES LOCAL , 0.50
	TIMES LOCAL:1 , 0.50
	TIMES LOCAL:2 , 1.50
ENDIF

SOURCE:2 += LOCAL
SOURCE:22 += LOCAL:1
SOURCE:31 += LOCAL:2

TCVAR:1 += 1

IF TALENT:120 == 0 && TALENT:PLAYER:120 == 0
	TCVAR:12 += 1
ELSEIF TALENT:120 && TALENT:PLAYER:120
	TCVAR:13 += 2
ENDIF
RETURN 1

;--------------------------------------------------
;조교시 메시지
;--------------------------------------------------
@TRAIN_MESSAGE_COM063
CALL KOJO_TRAIN_MESSAGE_COM
SIF FLAG:10 == 0 || CFLAG:99 == 1 || RESULT != 0
	RETURN 0

PRINTL
IF TEQUIP:15 & 1
	PRINTFORML 좌약이 들어가 느슨해진 %CALLNAME:TARGET%의 애널에 
	PRINTFORML %조사선택(CALLNAME:PLAYER, "는")% 좌약을 한 개 더 넣었다.
ELSE
	PRINTFORML %조사선택(CALLNAME:PLAYER, "는")% %CALLNAME:TARGET%의 항문에 탄환 같이 생긴 좌약을 넣었다.
ENDIF

PRINTFORM %조사선택(CALLNAME:TARGET, "는")% 
IF EXP:1 < 20
	PRINTL 공포심에 얼굴이 굳어지면서
	PRINTFORM 어떻게든 %CALLNAME:PLAYER%의 손을 피하려고 날뛰고 있다….
ELSEIF EXP:1 < 40
	PRINT 항문에 들어오는 이물감에 굴욕적인 표정을 짓고 있다….
ELSEIF EXP:1 < 75
	PRINTL 싫어하는 눈치지만 별 저항 없이
	PRINT 점잖게 좌약을 받아들였다….
ELSEIF EXP:1 < 100
	PRINT 항문에 전해지는 자극을 참지 못해 부들부들 떨고 있다….
ELSEIF EXP:1 < 140
	PRINT 좌약이 들어오는 감각이 그리 싫진 않았는지, 찡그린 얼굴이 펴지고 있다….
ELSEIF EXP:1 < 180
	PRINT 약을 삽입한 뒤에도 허리를 움직이며 더 큰 쾌락을 얻으려 한다….
ELSE
	PRINTL 만족할 수 없는지, 약을 더 안으로 넣기 위해
	PRINT 스스로의 손가락을 엉덩이 속으로 마구 찔러 넣고 있다….
ENDIF
PRINTW
