;=============================================================================
;기승위 애널
;=============================================================================
;………………………………………………
;실행 판정
;………………………………………………
@COM_ABLE34
;개별 설정 커맨드 필터
SIF DITEMTYPE:999:34 > 0
	RETURN 0
;A조교 필터가 오프가 되지 않으면 안 됨
SIF FLAG:2 & 2
	RETURN 0
;조교자에게 페니스가 없으면 안 됨
SIF EXIST_BAR(PLAYER) == 0
	RETURN 0
;주인이 [금단의 지식] 소유가 아닐 때, 체격차이가 2 단계 이내나 조교자가 [금단의 지식] 소유가 아니면 안 됨
SIF TALENT:MASTER:56 == 0 && COMMON_BODYSIZE_CHECK(PLAYER, TARGET) == 0
	RETURN 0
;윤활 부족해, 순종 4 미만·레즈끼(BL끼) 4 미만의 ASSI의 경우, 조교 대상이 동성이라면 실행할 수 없다(새드나 새드끼 Lv3 이상, 또는 망신이나 복종이 있다면 OK)
IF PALAM:10 < PALAMLV:2 && ASSIPLAY
	SIF (ABL:ASSI:10 < 4 || ((TALENT:120 == 0 && TALENT:ASSI:120 == 0 && ABL:ASSI:22 < 4) || (TALENT:120 && TALENT:ASSI:120 && ABL:ASSI:23 < 4))) && TALENT:ASSI:81 == 0 && ABL:ASSI:20 < 3 && TALENT:ASSI:151 == 0 && TALENT:ASSI:160 == 0
		RETURN 0
ENDIF
;일정한 A경험이 필요
SIF EXP:1 < EXPLV:3
	RETURN 0
;그 조교중에 관장 플레이＋배설이 있었을 경우, 조교자가 더러운 무시 혹은 악취 둔감을 가지지 않으면 실행할 수 없다
SIF TFLAG:140 & 256 && TALENT:PLAYER:61 == 0 && TALENT:PLAYER:64 == 0
	RETURN 0
;수면중은 불가
SIF EQUIP:14
	RETURN 0
;실신중은 불가
SIF TFLAG:899 > 0
	RETURN 0
;쿠스코 삽입 중에는 불가
SIF TEQUIP:21
	RETURN 0
;V계 장착도구 사용중은 조교자의 기교 4이상 필수
SIF (TEQUIP:20 || TEQUIP:21) && ABL:PLAYER:12 < 4
	RETURN 0
;A계 장착도구 사용중은 불가
SIF TEQUIP:25 || TEQUIP:26
	RETURN 0
;밧줄 사용 중에는 불가
SIF TEQUIP:40
	RETURN 0
;승마 중에는 불가
SIF TEQUIP:43
	RETURN 0
;목욕탕 플레이중은 플레이 매트가 없으면 안 됨
SIF TEQUIP:53 && ITEM:42 == 0 && NOITEM == 0
	RETURN 0
;한증과 암반 욕 이외의00목욕탕 입욕 중에는 불가
SIF TEQUIP:55 && TEQUIP:55 != 12 && TEQUIP:55 != 15
	RETURN 0
;기저귀 플레이 중에는 불가
SIF TEQUIP:58
	RETURN 0
;훈도시 착용중의 경우 조교자의 기교 4이상 필수
SIF TEQUIP:59 == 17 && ABL:PLAYER:12 < 4
	RETURN 0
;조수가 훈도시 착용중의 경우 기교 4이상 필수
SIF ASSI >= 0 && ASSIPLAY && TEQUIP:ASSI:59 == 17 && ABL:12 < 4
	RETURN 0
;나체정식 중에는 안 됨
SIF TEQUIP:60
	RETURN 0
;시간정지 중에는 무리
SIF TEQUIP:71
	RETURN 0
;촉수 조교중일 시 주인의 촉수사역Lv2 이하는 안 됨
SIF TEQUIP:90 && ABL:MASTER:98 <= 2
	RETURN 0
RETURN 1

;--------------------------------------------------
;커맨드 본체
;--------------------------------------------------
@COM34
PRINTL 기승위 애널

;-------------------------------------------------
;실행할 수 있는가 판정
;-------------------------------------------------
IF TFLAG:699 == 0
	CALL COM_ORDER
	;실행할 수 없다
	SIF RESULT == 0
		RETURN 0
ENDIF

;-------------------------------------------------
;실행 결정
;-------------------------------------------------
TSTR:0 = 기승위 애널
CALL TRAIN_MESSAGE_COM034

;-------------------------------------------------
;사정 게이지 체크
;-------------------------------------------------
LOCAL = 0

;ABL:기교를 본다
IF ABL:12 == 0
	LOCAL = 1000
ELSEIF ABL:12 == 1
	LOCAL = 1300
ELSEIF ABL:12 == 2
	LOCAL = 1700
ELSEIF ABL:12 == 3
	LOCAL = 2200
ELSEIF ABL:12 == 4
	LOCAL = 3000
ELSE
	LOCAL = 4500
ENDIF

;윤활을 본다
LOCAL = EJACULATIONCHECK_PALAM_10_3(TARGET, LOCAL)
;욕정을 본다
LOCAL = EJACULATIONCHECK_PALAM_13_1(TARGET, LOCAL)
;순종을 본다
LOCAL = EJACULATIONCHECK_ABL_10_1(TARGET, LOCAL)
;욕망をみる
LOCAL = EJACULATIONCHECK_ABL_11_1(TARGET, LOCAL)
;봉사정신을 본다
LOCAL = EJACULATIONCHECK_ABL_13_2(TARGET, LOCAL)

;조교자의 Ｃ감각을 본다
LOCAL = EJACULATIONCHECK_ABL_0_1(PLAYER, LOCAL)

;EXP:Ａ경험을 본다
;Ａ経験が多くなると少し下がる
IF EXP:1 < EXPLV:1
	TIMES LOCAL , 1.50
ELSEIF EXP:1 < EXPLV:2
	TIMES LOCAL , 1.00
ELSEIF EXP:1 < EXPLV:3
	TIMES LOCAL , 0.90
ELSEIF EXP:1 < EXPLV:4
	TIMES LOCAL , 0.80
ELSEIF EXP:1 < EXPLV:5
	TIMES LOCAL , 0.70
ELSE
	TIMES LOCAL , 0.60
ENDIF

SIF EXIST_PENIS(PLAYER)
	BASE:PLAYER:2 += LOCAL

;-------------------------------------------------
;소스의 계산
;-------------------------------------------------
DOWNBASE:0 += 90
DOWNBASE:1 += 110

SOURCE:12 = 900
SOURCE:14 = 2300

;위쪽에서 계산한 더러움 데이터
SOURCE:30 = TFLAG:242*10 + 60

;ABL:봉사정신을 본다
IF ABL:13 == 0
	SOURCE:15 = 200
	SOURCE:16 = 50
	SOURCE:31 = 300
	TIMES SOURCE:30 , 4.00
ELSEIF ABL:13 == 1
	SOURCE:15 = 250
	SOURCE:16 = 200
	SOURCE:31 = 100
	TIMES SOURCE:30 , 2.50
ELSEIF ABL:13 == 2
	SOURCE:15 = 350
	SOURCE:16 = 550
	SOURCE:31 = 30
	TIMES SOURCE:30 , 1.50
ELSEIF ABL:13 == 3
	SOURCE:15 = 450
	SOURCE:16 = 900
	SOURCE:31 = 0
	TIMES SOURCE:30 , 1.00
ELSEIF ABL:13 == 4
	SOURCE:15 = 600
	SOURCE:16 = 1500
	SOURCE:31 = 0
	TIMES SOURCE:30 , 0.50
ELSE
	SOURCE:15 = 750
	SOURCE:16 = 2200
	SOURCE:31 = 0
	TIMES SOURCE:30 , 0.10
ENDIF

;ABL:Ａ감각을 본다
IF ABL:2 == 0
	SOURCE:2 = 10
	SOURCE:11 = 10
	SOURCE:22 += 80
	TIMES SOURCE:15 , 0.50
ELSEIF ABL:2 == 1
	SOURCE:2 = 20
	SOURCE:11 = 50
	SOURCE:22 += 300
	TIMES SOURCE:15 , 0.80
ELSEIF ABL:2 == 2
	SOURCE:2 = 200
	SOURCE:11 = 100
	SOURCE:22 += 700
	TIMES SOURCE:15 , 1.00
ELSEIF ABL:2 == 3
	SOURCE:2 = 450
	SOURCE:11 = 180
	SOURCE:22 += 1500
	TIMES SOURCE:15 , 1.20
ELSEIF ABL:2 == 4
	SOURCE:2 = 850
	SOURCE:11 = 300
	SOURCE:22 += 2600
	TIMES SOURCE:15 , 1.50
ELSE
	SOURCE:2 = 1300
	SOURCE:11 = 450
	SOURCE:22 += 4000
	TIMES SOURCE:15 , 2.00
ENDIF

;EXP:Ａ경험을 본다
IF EXP:1 < EXPLV:1
	TIMES SOURCE:2 , 0.10
	SOURCE:20 = 18000
	DOWNBASE:0 += 70
	DOWNBASE:1 += 80
ELSEIF EXP:1 < EXPLV:2
	TIMES SOURCE:2 , 0.30
	SOURCE:20 = 10000
	DOWNBASE:0 += 60
	DOWNBASE:1 += 70
ELSEIF EXP:1 < EXPLV:3
	TIMES SOURCE:2 , 0.50
	SOURCE:20 = 4500
	DOWNBASE:0 += 45
	DOWNBASE:1 += 55
ELSEIF EXP:1 < EXPLV:4
	TIMES SOURCE:2 , 1.00
	SOURCE:20 = 1500
	DOWNBASE:0 += 25
	DOWNBASE:1 += 35
ELSEIF EXP:1 < EXPLV:5
	TIMES SOURCE:2 , 1.40
	SOURCE:20 = 700
	DOWNBASE:0 += 10
	DOWNBASE:1 += 15
ELSE
	TIMES SOURCE:2 , 1.60
	SOURCE:20 = 300
	DOWNBASE:0 += 0
	DOWNBASE:1 += 0
ENDIF

;PALAM:윤활을 본다
IF PALAM:10 < PALAMLV:1
	TIMES SOURCE:2 , 0.40
	SOURCE:20 += 10000
ELSEIF PALAM:10 < PALAMLV:2
	TIMES SOURCE:2 , 0.80
	SOURCE:20 += 3600
ELSEIF PALAM:10 < PALAMLV:3
	TIMES SOURCE:2 , 1.00
	SOURCE:20 += 1200
ELSEIF PALAM:10 < PALAMLV:4
	TIMES SOURCE:2 , 1.40
	SOURCE:20 += 200
ELSE
	TIMES SOURCE:2 , 1.80
	SOURCE:20 += 100
ENDIF

;조교자가 [남자]
IF TALENT:PLAYER:120
	TIMES SOURCE:2 , 2.50
;조교자가 [후타나리]
ELSEIF TALENT:PLAYER:121
	TIMES SOURCE:22 , 1.50
	TIMES SOURCE:31 , 1.60
ENDIF

;小柄体形
SIF TALENT:128
	TIMES SOURCE:20 , 2.00

;정조관념
IF TALENT:30
	IF EXP:1 == 0
		TIMES SOURCE:11 , 1.20
		TIMES SOURCE:17 , 1.10
		SOURCE:32 = 500
	ELSE
		TIMES SOURCE:11 , 1.20
		TIMES SOURCE:17 , 1.10
		SOURCE:32 = 0
	ENDIF
ENDIF

;PALAM:욕정을 본다
IF PALAM:13 < PALAMLV:1
	TIMES SOURCE:2 , 0.60
	TIMES SOURCE:22 , 0.60
ELSEIF PALAM:13 < PALAMLV:2
	TIMES SOURCE:2 , 0.80
	TIMES SOURCE:22 , 0.80
ELSEIF PALAM:13 < PALAMLV:3
	TIMES SOURCE:2 , 1.00
	TIMES SOURCE:22 , 1.00
ELSEIF PALAM:13 < PALAMLV:4
	TIMES SOURCE:2 , 1.20
	TIMES SOURCE:22 , 1.20
ELSE
	TIMES SOURCE:2 , 1.40
	TIMES SOURCE:22 , 1.40
ENDIF

;ABL:순종을 본다
IF ABL:10 == 0
	TIMES SOURCE:2 , 0.50
	TIMES SOURCE:11 , 0.60
	TIMES SOURCE:32 , 2.00
	SOURCE:22 += 700
ELSEIF ABL:10 == 1
	TIMES SOURCE:2 , 0.80
	TIMES SOURCE:11 , 0.80
	TIMES SOURCE:32 , 1.50
	SOURCE:22 += 500
ELSEIF ABL:10 == 2
	TIMES SOURCE:2 , 1.00
	TIMES SOURCE:11 , 1.00
	TIMES SOURCE:32 , 1.00
	SOURCE:22 += 300
ELSEIF ABL:10 == 3
	TIMES SOURCE:2 , 1.30
	TIMES SOURCE:11 , 1.20
	TIMES SOURCE:32 , 0.80
	SOURCE:22 += 100
ELSEIF ABL:10 == 4
	TIMES SOURCE:2 , 1.60
	TIMES SOURCE:11 , 1.40
	TIMES SOURCE:32 , 0.60
	SOURCE:22 += 30
ELSE
	TIMES SOURCE:2 , 2.00
	TIMES SOURCE:11 , 1.60
	TIMES SOURCE:32 , 0.30
	SOURCE:22 += 0
ENDIF

;ABL:기교를 본다
IF ABL:12 == 0
	TIMES SOURCE:2 , 0.50
	TIMES SOURCE:11 , 0.60
ELSEIF ABL:12 == 1
	TIMES SOURCE:2 , 0.80
	TIMES SOURCE:11 , 0.80
ELSEIF ABL:12 == 2
	TIMES SOURCE:2 , 1.00
	TIMES SOURCE:11 , 1.00
ELSEIF ABL:12 == 3
	TIMES SOURCE:2 , 1.30
	TIMES SOURCE:11 , 1.20
ELSEIF ABL:12 == 4
	TIMES SOURCE:2 , 1.60
	TIMES SOURCE:11 , 1.40
ELSE
	TIMES SOURCE:2 , 2.00
	TIMES SOURCE:11 , 1.60
ENDIF

TCVAR:1 += 3
SIF ASSIPLAY == 0 && EXIST_BAR(MASTER)
	CFLAG:174 += 3

;-------------------------------------------------
;사정 체크
;-------------------------------------------------
TFLAG:91 = SAMEN_CHECK(PLAYER)

;사정시의 처리
IF TFLAG:91 >= 1
	;コマンドごとに固有の処理はこの下に書く
	TIMES SOURCE:15 , 3.00
	TIMES SOURCE:16 , 5.00
	TIMES SOURCE:22 , 1.50

	;ABL:정액중독을 본다
	IF ABL:31 == 0
		SOURCE:24 = 0
	ELSEIF ABL:31 == 1
		SOURCE:24 = 300
	ELSEIF ABL:31 == 2
		SOURCE:24 = 1000
	ELSEIF ABL:31 == 3
		SOURCE:24 = 1500
	ELSEIF ABL:31 == 4
		SOURCE:24 = 2200
	ELSE
		SOURCE:24 = 3000
	ENDIF

	;射精先タイプの設定
	TFLAG:703 = 2
	;射精先選択の可・不可
	TFLAG:704 = 1
	;汎用的な処理はこの関数で行う
	;（射精ゲージの再計算や射精経験の上昇と共通部分の表示）
	CALL SAMEN_SHOOT
ENDIF

;射精に関係なく行われる処理はここから
STAIN:4 |= STAIN:PLAYER:2
STAIN:PLAYER:2 |= STAIN:4

IF TALENT:120 == 0 && TALENT:PLAYER:120 == 0
	TCVAR:12 += 7
ELSEIF TALENT:120 && TALENT:PLAYER:120
	TCVAR:13 += 7
ENDIF
;주인 경험 플래그
SIF ASSIPLAY == 0
	TFLAG:50 += (ABL:2 >= 3) ? 3 # 2
;奉仕快楽経験入手判定
TFLAG:100 |= 1

;조교자가 후타나리
SIF TALENT:PLAYER:121
	SOURCE:22 /= 2

;処女喪失の後でペニスを抜いているか
TFLAG:101 = (TFLAG:101 == 2) ? 1 # 0
RETURN 1

;--------------------------------------------------
;명령 거부 메시지
;--------------------------------------------------
@DENIAL_MESSAGE_COM_34
CALL KOJO_DENIAL_MESSAGE_COM
SIF FLAG:10 == 0 || CFLAG:99 == 1 || RESULT != 0
	RETURN 0

PRINTL
IF TEQUIP:68
	SIF PALAM:0 >= (PALAMLV:4 - 1) ||  PALAM:1 >= (PALAMLV:4 - 1) ||  PALAM:2 >= (PALAMLV:4 - 1) ||  PALAM:10 >= (PALAMLV:4 - 1)
		PRINT 지금 당장이라도 갈 것 같은데도 불구하고 
	PRINTFORML %조사선택(CALLNAME:TARGET, "는")% 명령을 거부했다….
ENDIF

;--------------------------------------------------
;조교시 메시지
;--------------------------------------------------
@TRAIN_MESSAGE_COM034
CALL KOJO_TRAIN_MESSAGE_COM
SIF FLAG:10 == 0 || CFLAG:99 == 1 || RESULT != 0
	RETURN 0

PRINTL
;애태우기플레이 중に自慰系・기승위系・助手を犯させる
IF TEQUIP:68
	IF TFLAG:699 == 0
		SIF PALAM:0 >= (PALAMLV:4 - 1) ||  PALAM:1 >= (PALAMLV:4 - 1) ||  PALAM:2 >= (PALAMLV:4 - 1) ||  PALAM:3 >= (PALAMLV:4 - 1)
			PRINT 지금 당장이라도 갈 것 같길래 
		PRINTFORMW %조사선택(CALLNAME:TARGET, "가")% 좋을 대로 하게 놔두기로 했다….
	ELSE
		SIF PALAM:0 >= (PALAMLV:4 - 1) ||  PALAM:1 >= (PALAMLV:4 - 1) ||  PALAM:2 >= (PALAMLV:4 - 1) ||  PALAM:3 >= (PALAMLV:4 - 1)
			PRINT 더는 참을 수 없었는지 
		PRINTFORMW %조사선택(CALLNAME:TARGET, "는")% 멋대로 허리를 움직이기 시작했다….
	ENDIF
ENDIF

;前回の調教もAセックス系調教だった場合で調教対象が勝手にコマンドを実行している場合
IF TFLAG:102 && (PREVCOM >= 30 && PREVCOM <= 34) && TFLAG:699 > 0
	PRINTFORML %조사선택(CALLNAME:MASTER, "가")% 삽입한 채로 아무것도 하고 있지 않자 마치 만족하지 못했다는 듯 %조사선택(CALLNAME:TARGET, "는")% 
	SIF TALENT:170 || TEQUIP:11 == 1
		PRINT 더욱 큰 쾌락을 탐욕스럽게 구하며 
	SIF TALENT:81
		PRINTFORM 피로함을 호소하는 %조사선택(CALLNAME:MASTER, "를")% 무시하고 
	SIF TALENT:157
		PRINT 질투의 증오로 미친 듯이 
	SIF TALENT:158 && TEQUIP:42 == 0
		PRINTL 미쳐버린 듯한 웃음소리를 내며 
	SIF PREVCOM >= 30 && PREVCOM <= 33
		PRINTFORM %조사선택(CALLNAME:MASTER, "를")% 올라타듯이 넘어뜨리고 위로 올라가선 
	PRINTL 그대로 자신의 허리를 다시 움직이기 시작했다….
	IF EXP:1 >= EXPLV:3
		PRINTL
		PRINTFORML 아무래도 힘들어서 %조사선택(CALLNAME:MASTER, "는")% 거의 움직이지 않고 있음에도 불구하고 
		PRINTFORM %조사선택(CALLNAME:TARGET, "는")% 
		SIF TALENT:128
			PRINT 자그마한 몸으로 열심히 
		SIF TALENT:112 || TALENT:113
			PRINT 커다란 가슴을 흔들어대면서 
		PRINTL 남자를 범하는 쾌락에 몰두하며 허리를 계속 움직이고 있다….
		SIF TFLAG:699 == 3
			PRINTFORML %조사선택(CALLNAME:TARGET, "는")% 완전히 육봉의 포로가 되어버려 내버려 두면 말라붙을 때까지 몽땅 짜내버릴 것 같다….
	ENDIF
;調教対象が勝手にコマンドを実行している場合
ELSEIF TFLAG:699 > 0
	PRINTFORML %조사선택(CALLNAME:MASTER, "가")% 아무것도 하지 않자 명령한 적도 없는데 %조사선택(CALLNAME:TARGET, "는")% 
	SIF TALENT:170 || TEQUIP:11 == 1
		PRINT 격렬하게 육봉을 원하며 
	SIF TALENT:81
		PRINT 비웃음에 가까운 웃음을 띠며 입가를 일그러뜨린 채 
	SIF TALENT:157
		PRINT 질투의 증오로 미친 듯이 
	SIF TALENT:158 && TEQUIP:42 == 0
		PRINTL 미쳐버린 듯한 웃음소리를 내며 
	PRINTFORM %조사선택(CALLNAME:MASTER, "를")% 쓰러트리곤 스스로 페니스를 
	IF TALENT:128
		PRINT 여린 균열에 
	ELSEIF PALAM:10 < PALAMLV:2
		PRINT 젖지도 않은 애널에 
	ELSE
		PRINT 점액 투성이인 애널에 
	ENDIF
	PRINTL 갖다 대고는 그대로 깊숙이 묻었다….
	IF EXP:1 >= EXPLV:3
		PRINTL
		PRINTFORML 돌연히 일어난 일에 %조사선택(CALLNAME:MASTER, "는")% 거의 움직이지 못함에도 불구하고 
		PRINTFORM %조사선택(CALLNAME:TARGET, "는")% 
		SIF TALENT:128
			PRINT 자그마한 몸으로 열심히 
		SIF TALENT:112 || TALENT:113
			PRINT 커다란 가슴을 흔들어대면서 
		PRINTL 남자를 범하는 쾌락에 몰두하며 허리를 계속 움직이고 있다….
	ENDIF
;전회와 금회의 조교자가 동일で前回の調教が기승위であり抜かないフラグがある場合
ELSEIF TFLAG:102 && PREVCOM == 34 && ((ASSIPLAY && TFLAG:133) || (ASSIPLAY == 0 && TFLAG:133 == 0))
	PRINTFORML %조사선택(CALLNAME:TARGET, "는")% 삽입된 채로 물건을 꽉 조이며 다시 허리를 움직이기 시작했다….
;同様に前回の調教が正常位アナルか後背位アナルである場合
ELSEIF TFLAG:102 && (PREVCOM == 30 || PREVCOM == 31) && ((ASSIPLAY && TFLAG:133) || (ASSIPLAY == 0 && TFLAG:133 == 0))
	PRINTFORML 장에서 뽑아내지 않고 %조사선택(CALLNAME:TARGET, "를")% 안아서는 아래서 허리를 찔러 올렸다….
;同様に前回の調教が対面座位アナルか背面座位アナルである場合
ELSEIF TFLAG:102 && (PREVCOM == 32 || PREVCOM == 33) && ((ASSIPLAY && TFLAG:133) || (ASSIPLAY == 0 && TFLAG:133 == 0))
	PRINTFORML %조사선택(CALLNAME:MASTER, "는")% 삽입된 채로 누워서는 아래서 허리를 찔러 올렸다….
ELSE
	PRINTFORM %조사선택(CALLNAME:TARGET, "는")% %CALLNAME:PLAYER%의 위로 올라가더니 
	SIF PALAM:10 >= PALAMLV:4
		PRINT 점액 투성이가 된 
	PRINTL 애널로 천천히 물건을 삼켜갔다….
	IF EXP:1 >= EXPLV:3
		PRINTL
		PRINTFORM %조사선택(CALLNAME:TARGET, "는")% 
		SIF TALENT:128
			PRINT 자그마한 몸으로 열심히 
		IF TALENT:112
			PRINT 커다란 가슴을 흔들어대면서 
		ELSEIF TALENT:113
			PRINT 커다란 가슴에 몸마저 흔들어가며 
		ENDIF
		SIF EX:22
			PRINT 탐하는 듯한 격렬함으로 
		PRINTL 스스로 허리를 움직이고 있다….
	ENDIF
ENDIF
PRINTW

;-------------------------------------------------
;고유 실행 판정
;-------------------------------------------------
@COM_ORDER_34, ARG
LOCAL:99 = ARG
;ABL:욕망
IF ABL:11
	SIF LOCAL:99
		RESULTS = %RESULTS% + 
	LOCAL:99 = ABL:11
	TFLAG:240 += LOCAL:99 * 3
	RESULTS = %RESULTS% %ABLNAME:11%LV{LOCAL:99}({LOCAL:99 * 3})
ENDIF
;ABL:Ａ感覚
IF ABL:2
	SIF LOCAL:99
		RESULTS = %RESULTS% + 
	LOCAL:99 = ABL:2
	TFLAG:240 += LOCAL:99 * 2
	RESULTS = %RESULTS% %ABLNAME:2%LV{LOCAL:99}({LOCAL:99 * 2})
ENDIF
;ABL:봉사정신
IF ABL:13
	SIF LOCAL:99
		RESULTS = %RESULTS% + 
	LOCAL:99 = ABL:13
	TFLAG:240 += LOCAL:99 * 4
	RESULTS = %RESULTS% %ABLNAME:13%LV{LOCAL:99}({LOCAL:99 * 4})
ENDIF
;ABL:정액중독
IF ABL:31
	SIF LOCAL:99
		RESULTS = %RESULTS% + 
	LOCAL:99 = ABL:31
	TFLAG:240 += LOCAL:99
	RESULTS = %RESULTS% %ABLNAME:31%LV{LOCAL:99}({LOCAL:99})
ENDIF

;쾌락각인
IF MARK:1
	SIF LOCAL:99
		RESULTS = %RESULTS% + 
	LOCAL:99 = MARK:1
	TFLAG:240 += LOCAL:99 * 3
	RESULTS = %RESULTS% %MARKNAME:1%LV{LOCAL:99}({LOCAL:99 * 3})
ENDIF

;PALAM:潤滑（の不足）
IF PALAM:10 < PALAMLV:3
	RESULTS = %RESULTS% - 
	LOCAL:99 = 5
	TFLAG:240 -= LOCAL:99
	RESULTS = %RESULTS% 윤활 부족({LOCAL:99})
ENDIF
;PALAM:욕정
GETPALAMLV PALAM:13, 5
LOCAL:2 = RESULT
IF LOCAL:2
	SIF LOCAL:99
		RESULTS = %RESULTS% + 
	LOCAL:99 = LOCAL:2 * 3
	TFLAG:240 += LOCAL:99
	RESULTS = %RESULTS% %PALAMNAME:13%LV{LOCAL:2}({LOCAL:99})
ENDIF

;부끄럼쟁이
IF TALENT:35
	RESULTS = %RESULTS% - 
	LOCAL:99 = 2
	TFLAG:240 -= LOCAL:99
	RESULTS = %RESULTS% %TALENTNAME:35%({LOCAL:99})
ENDIF
;男嫌い
IF TALENT:37 && TALENT:PLAYER:120
	RESULTS = %RESULTS% - 
	LOCAL:99 = 12
	TFLAG:240 -= LOCAL:99
	RESULTS = %RESULTS% %TALENTNAME:37%({LOCAL:99})
;女嫌い
ELSEIF TALENT:38 && TALENT:PLAYER:120 == 0
	RESULTS = %RESULTS% - 
	LOCAL:99 = 12
	TFLAG:240 -= LOCAL:99
	RESULTS = %RESULTS% %TALENTNAME:38%({LOCAL:99})
ENDIF
;쾌감의 부정
IF TALENT:71
	RESULTS = %RESULTS% - 
	LOCAL:99 = 5
	TFLAG:240 -= LOCAL:99
	RESULTS = %RESULTS% %TALENTNAME:71%({LOCAL:99})
ENDIF
;연모
IF TALENT:150 && ASSIPLAY == 0
	SIF LOCAL:99
		RESULTS = %RESULTS% + 
	LOCAL:99 = 5
	TFLAG:240 += LOCAL:99
	RESULTS = %RESULTS% %TALENTNAME:150%({LOCAL:99})
ENDIF
;음고
IF TALENT:173
	SIF LOCAL:99
		RESULTS = %RESULTS% + 
	LOCAL:99 = 5
	TFLAG:240 += LOCAL:99
	RESULTS = %RESULTS% %TALENTNAME:173%({LOCAL:99})
ENDIF

;조교자가 후타나리
IF TALENT:PLAYER:121
	SIF LOCAL:99
		RESULTS = %RESULTS% + 
	LOCAL:99 = 4
	TFLAG:240 += LOCAL:99
	RESULTS = %RESULTS% %조사처리(CALLNAME:PLAYER,"가")% %TALENTNAME:121%({LOCAL:99})
ENDIF

;媚薬
IF TEQUIP:11
	SIF LOCAL:99
		RESULTS = %RESULTS% + 
	LOCAL:99 = 6
	TFLAG:240 += LOCAL:99
	RESULTS = %RESULTS% %ITEMNAME:61%({LOCAL:99})
ENDIF

;조교자의 Ｐ의 더러움
TFLAG:242 = COMORDER_STAIN_CALC(2, 0)
;기승위では汚れの影響が少なめ
TFLAG:242 /= 3

;불결 있음
IF TFLAG:242
	RESULTS = %RESULTS% - 
	TFLAG:240 -= TFLAG:242
	RESULTS = %RESULTS% 불결 있음
	;냄새둔감
	IF TALENT:61
		RESULTS = %RESULTS%%TALENTNAME:61%
	ELSEIF TALENT:62
		RESULTS = %RESULTS%%TALENTNAME:62%
	ENDIF
	RESULTS = %RESULTS%({TFLAG:242})
	LOCAL:99 = 1
ENDIF

TFLAG:241 = 24
