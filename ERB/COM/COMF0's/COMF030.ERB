;=============================================================================
;정상위 애널
;=============================================================================
;………………………………………………
;실행 판정
;………………………………………………
@COM_ABLE30
;개별 설정 커맨드 필터
SIF DITEMTYPE:999:30 > 0
	RETURN 0
;A조교 필터가 오프가 되지 않으면 안 됨
SIF FLAG:2 & 2
	RETURN 0
;조교자에게 페니스가 없는, 한편(아이템 있음 설정이라면) 페니스 밴드도 없으면 안 됨
SIF EXIST_BAR(PLAYER) == 0 && ITEM:2 == 0 && NOITEM == 0
	RETURN 0
;주인이 [금단의 지식] 소유가 아닐 때, 체격차이가 2 단계 이내나 조교자가 [금단의 지식] 소유가 아니면 안 됨
SIF TALENT:MASTER:56 == 0 && COMMON_BODYSIZE_CHECK(PLAYER, TARGET) == 0
	RETURN 0
;윤활 부족해, 순종 4 미만·레즈끼(BL끼) 4 미만의 ASSI의 경우, 조교 대상이 동성이라면 실행할 수 없다(새드나 새드끼 Lv3 이상, 또는 망신이나 복종이 있다면 OK)
IF PALAM:10 < PALAMLV:2 && ASSIPLAY
	SIF (ABL:ASSI:10 < 4 || ((TALENT:120 == 0 && TALENT:ASSI:120 == 0 && ABL:ASSI:22 < 4) || (TALENT:120 && TALENT:ASSI:120 && ABL:ASSI:23 < 4))) && TALENT:ASSI:81 == 0 && ABL:ASSI:20 < 3 && TALENT:ASSI:151 == 0 && TALENT:ASSI:160 == 0
		RETURN 0
ENDIF
;일정한 A경험이 필요
SIF EXP:1 < EXPLV:3
	RETURN 0
;그 조교중에 관장 플레이＋배설이 있었을 경우, 조교자가 더러운 무시 혹은 악취 둔감을 가지지 않으면 실행할 수 없다
SIF TFLAG:140 & 256 && TALENT:PLAYER:61 == 0 && TALENT:PLAYER:64 == 0
	RETURN 0
;V계 장착도구 사용중은 조교자의 기교 4이상 필수
SIF (TEQUIP:20 || TEQUIP:21) && ABL:PLAYER:12 < 4
	RETURN 0
;A계 장착도구 사용중은 불가
SIF TEQUIP:25 || TEQUIP:26
	RETURN 0
;쿠스코 삽입 중에는 불가
SIF TEQUIP:21
	RETURN 0
;승마 중에는 불가
SIF TEQUIP:43
	RETURN 0
;목욕탕 플레이중은 플레이 매트가 없으면 안 됨
SIF TEQUIP:53 && ITEM:42 == 0 && NOITEM == 0
	RETURN 0
;한증과 암반 욕 이외의00목욕탕 입욕 중에는 불가
SIF TEQUIP:55 && TEQUIP:55 != 12 && TEQUIP:55 != 15
	RETURN 0
;기저귀 플레이 중에는 불가
SIF TEQUIP:58
	RETURN 0
;훈도시 착용중의 경우 조교자의 기교 4이상 필수
SIF TEQUIP:59 == 17 && ABL:PLAYER:12 < 4
	RETURN 0
;조수가 훈도시 착용중의 경우 기교 4이상 필수
SIF ASSI >= 0 && ASSIPLAY && TEQUIP:ASSI:59 == 17 && ABL:12 < 4
	RETURN 0
;나체정식 중에는 안 됨
SIF TEQUIP:60
	RETURN 0
;촉수 조교중일 시 주인의 촉수사역Lv2 이하는 안 됨
SIF TEQUIP:90 && ABL:MASTER:98 <= 2
	RETURN 0
;인어는 안됨
SIF TALENT:229 == 1
	RETURN 0
RETURN 1

;--------------------------------------------------
;커맨드 본체
;--------------------------------------------------
@COM30

;전회의 조교가 둘이서 범하는 것이라고 하고 둘이서 범하는에
TFLAG:132 = 0
IF PREVCOM == 610
	CALL EXCOM_ABLE610
	IF RESULT == 1
		TFLAG:132 = 1
		JUMP COM610
	ENDIF
;조교자가 전회:조수로 이번:주인인가, 전회:주인으로 이번:조수 때
ELSEIF (ASSIPLAY && TFLAG:133 == 0) || (ASSIPLAY == 0 && TFLAG:133)
	;전회의 조교가 펠라치오나 이라마치오 때는 둘이서 범하는에
	IF PREVCOM == 41 || PREVCOM == 230
		CALL EXCOM_ABLE610
		SIF RESULT == 1
			JUMP COM610
	ENDIF
ENDIF

PRINTL 정상위 애널
TSTR:0 = 정상위 애널
CALL TRAIN_MESSAGE_COM030

;-------------------------------------------------
;사정 게이지 체크
;-------------------------------------------------
LOCAL = 0

;ABL:기교를 본다
IF ABL:12 == 0
	LOCAL = 1500
ELSEIF ABL:12 == 1
	LOCAL = 1600
ELSEIF ABL:12 == 2
	LOCAL = 1800
ELSEIF ABL:12 == 3
	LOCAL = 2000
ELSEIF ABL:12 == 4
	LOCAL = 2400
ELSE
	LOCAL = 3000
ENDIF

;순종을 본다
LOCAL = EJACULATIONCHECK_ABL_10_1(TARGET, LOCAL)
;욕망을 본다
LOCAL = EJACULATIONCHECK_ABL_11_1(TARGET, LOCAL)
;윤활을 본다
LOCAL = EJACULATIONCHECK_PALAM_10_1(TARGET, LOCAL)

;조교자의 C감각을 본다
LOCAL = EJACULATIONCHECK_ABL_0_1(PLAYER, LOCAL)

SIF TALENT:PLAYER:120 || TALENT:PLAYER:121
	BASE:PLAYER:2 += LOCAL

;-------------------------------------------------
;소스의 계산
;-------------------------------------------------
DOWNBASE:0 += 80
DOWNBASE:1 += 120

SOURCE:12 = 400

;ABL:A감각을 본다
IF ABL:2 == 0
	SOURCE:2 = 10
	SOURCE:11 = 10
	SOURCE:22 = 100
ELSEIF ABL:2 == 1
	SOURCE:2 = 30
	SOURCE:11 = 30
	SOURCE:22 = 500
ELSEIF ABL:2 == 2
	SOURCE:2 = 400
	SOURCE:11 = 150
	SOURCE:22 = 1200
ELSEIF ABL:2 == 3
	SOURCE:2 = 900
	SOURCE:11 = 300
	SOURCE:22 = 2400
ELSEIF ABL:2 == 4
	SOURCE:2 = 1600
	SOURCE:11 = 500
	SOURCE:22 = 3600
ELSE
	SOURCE:2 = 2100
	SOURCE:11 = 900
	SOURCE:22 = 5000
ENDIF

;EXP:A경험을 본다
IF EXP:1 < EXPLV:1
	TIMES SOURCE:2 , 0.10
	SOURCE:20 = 20000
	DOWNBASE:0 += 50
	DOWNBASE:1 += 100
ELSEIF EXP:1 < EXPLV:2
	TIMES SOURCE:2 , 0.30
	SOURCE:20 = 12000
	DOWNBASE:0 += 40
	DOWNBASE:1 += 80
ELSEIF EXP:1 < EXPLV:3
	TIMES SOURCE:2 , 0.50
	SOURCE:20 = 5000
	DOWNBASE:0 += 30
	DOWNBASE:1 += 60
ELSEIF EXP:1 < EXPLV:4
	TIMES SOURCE:2 , 1.00
	SOURCE:20 = 1800
	DOWNBASE:0 += 20
	DOWNBASE:1 += 40
ELSEIF EXP:1 < EXPLV:5
	TIMES SOURCE:2 , 1.40
	SOURCE:20 = 1000
	DOWNBASE:0 += 10
	DOWNBASE:1 += 20
ELSE
	TIMES SOURCE:2 , 1.60
	SOURCE:20 = 600
	DOWNBASE:0 += 0
	DOWNBASE:1 += 0
ENDIF

;PALAM:윤활을 본다
IF PALAM:10 < PALAMLV:1
	TIMES SOURCE:2 , 0.40
	SOURCE:20 += 10000
ELSEIF PALAM:10 < PALAMLV:2
	TIMES SOURCE:2 , 0.80
	SOURCE:20 += 3600
ELSEIF PALAM:10 < PALAMLV:3
	TIMES SOURCE:2 , 1.00
	SOURCE:20 += 1200
ELSEIF PALAM:10 < PALAMLV:4
	TIMES SOURCE:2 , 1.40
	SOURCE:20 += 200
ELSE
	TIMES SOURCE:2 , 1.80
	SOURCE:20 += 100
ENDIF

;조교자가[남자]
IF TALENT:PLAYER:120
	TIMES SOURCE:2 , 2.50
;조교자가[후타나리]
ELSEIF TALENT:PLAYER:121
	TIMES SOURCE:22 , 1.50
	TIMES SOURCE:31 , 1.60
ENDIF

;작은 몸집 체형
SIF TALENT:128
	TIMES SOURCE:20 , 2.00

;정조관념
IF TALENT:30
	IF EXP:1 == 0
		TIMES SOURCE:11 , 1.20
		SOURCE:32 = 500
	ELSE
		TIMES SOURCE:11 , 1.20
		SOURCE:32 = 0
	ENDIF
ENDIF

;PALAM:욕정을 본다
IF PALAM:13 < PALAMLV:1
	TIMES SOURCE:2 , 0.60
	TIMES SOURCE:22 , 0.60
ELSEIF PALAM:13 < PALAMLV:2
	TIMES SOURCE:2 , 0.80
	TIMES SOURCE:22 , 0.80
ELSEIF PALAM:13 < PALAMLV:3
	TIMES SOURCE:2 , 1.00
	TIMES SOURCE:22 , 1.00
ELSEIF PALAM:13 < PALAMLV:4
	TIMES SOURCE:2 , 1.20
	TIMES SOURCE:22 , 1.20
ELSE
	TIMES SOURCE:2 , 1.40
	TIMES SOURCE:22 , 1.40
ENDIF

;ABL:순종을 본다
IF ABL:10 == 0
	TIMES SOURCE:11 , 0.60
	TIMES SOURCE:32 , 2.00
ELSEIF ABL:10 == 1
	TIMES SOURCE:11 , 0.80
	TIMES SOURCE:32 , 1.50
ELSEIF ABL:10 == 2
	TIMES SOURCE:11 , 1.00
	TIMES SOURCE:32 , 1.00
ELSEIF ABL:10 == 3
	TIMES SOURCE:11 , 1.20
	TIMES SOURCE:32 , 0.80
ELSEIF ABL:10 == 4
	TIMES SOURCE:11 , 1.40
	TIMES SOURCE:32 , 0.60
ELSE
	TIMES SOURCE:11 , 1.60
	TIMES SOURCE:32 , 0.30
ENDIF

;A민감, 둔감을 본다
;쾌A자체의 체크는 다음에 정리해 준다
IF TALENT:105
	TIMES SOURCE:20 , 1.50
	TIMES SOURCE:22 , 1.50
ELSEIF TALENT:104
	TIMES SOURCE:20 , 0.60
	TIMES SOURCE:22 , 0.60
ENDIF

TCVAR:1 += 2
SIF ASSIPLAY == 0 && (TALENT:MASTER:120 || TALENT:MASTER:121 || TALENT:MASTER:1)
	CFLAG:174 += 2

;-------------------------------------------------
;사정 체크
;-------------------------------------------------
TFLAG:91 = SAMEN_CHECK(PLAYER)

;사정시의 처리
IF TFLAG:91 >= 1
	IF ABL:10 >= 3 && ABL:11 >= 3 && FLAG:10
		PRINTFORML %조사선택(CALLNAME:TARGET, "는")% %CALLNAME:PLAYER%의 허리에 다리를 감고 꽉 눌렀다….
		PRINTL
	ENDIF

	;사정처 타입의 설정
	TFLAG:703 = 2
	;사정처 선택의 가능·불가
	TFLAG:704 = 1
	;범용적인 처리는 이 함수로 실시한다
	;(사정 게이지의 재계산이나 사정 경험의 상승과 공통 부분의 표시)
	CALL SAMEN_SHOOT
	;커맨드 마다 고유의 처리는 이 아래에 쓴다
ENDIF

;사정에 관계없이 행해지는 처리는 여기로부터
;노예의 A⇔조교자의 P의 불결이 이동
STAIN:4 |= STAIN:PLAYER:2
STAIN:PLAYER:2 |= STAIN:4

IF TALENT:120 == 0 && TALENT:PLAYER:120 == 0
	TCVAR:12 += 7
ELSEIF TALENT:120 && TALENT:PLAYER:120
	TCVAR:13 += 7
ENDIF
;주인 경험 플래그
SIF ASSIPLAY == 0
	TFLAG:50 += (ABL:2 >= 3) ?  2 # 1

;처녀 상실의 후에 페니스를 뽑고 있을까
TFLAG:101 = (TFLAG:101 == 2) ?  1 # 0
RETURN 1

;--------------------------------------------------
;조교시 메시지
;--------------------------------------------------
@TRAIN_MESSAGE_COM030
CALL KOJO_TRAIN_MESSAGE_COM
SIF FLAG:10 == 0 || CFLAG:99 == 1 || RESULT != 0
	RETURN 0

PRINTL
;전회와 이번 조교자가 같아, 전회의 조교가 정상위 애널이며, 뽑지 않는 플래그가 있는 경우
IF TFLAG:102 && PREVCOM == 30 && ((ASSIPLAY && TFLAG:133) || (ASSIPLAY == 0 && TFLAG:133 == 0))
	;애태우기플레이
	IF TEQUIP:68
		PRINTFORM %CALLNAME:TARGET%의 
		SIF EX:22
			PRINT 오므린 채로 떨고 있는 
		PRINTW 항문에 삽입한 채로, 움직이지 않고 배를 쓰다듬었다….
	;조교자가 남자나 후타나리이다
	ELSEIF TALENT:PLAYER:120 || TALENT:PLAYER:121
		PRINTFORMW %CALLNAME:TARGET%의 애널의 감촉을 맛보면서, 다시 허리를 움직이기 시작했다….
	ELSE
	;페니스 밴드를 사용하고 있는 경우
		PRINTFORMW %CALLNAME:TARGET%의 장에서 뽑지 않고, 다시 허리를 움직이기 시작했다….
	ENDIF
;(와)과 같이  전회의 조교가 후배위 애널이나 대면좌위 애널이나 배면좌위 애널이나 기승위 애널의 경우
ELSEIF TFLAG:102 && (PREVCOM == 31 || PREVCOM == 32 || PREVCOM == 33 || PREVCOM == 34) && ((ASSIPLAY && TFLAG:133) || (ASSIPLAY == 0 && TFLAG:133 == 0))
	PRINTFORM 뽑지 않은 채로 %조사선택(CALLNAME:TARGET, "를")% 부드럽게 눕혔다. 
	SIF TEQUIP:68 == 0
		PRINTFORM 그리고, 다시금 허리를 움직이기 시작했다.
	PRINTW
ELSE
	PRINTFORM %CALLNAME:TARGET%의 
	SIF PALAM:10 >= PALAMLV:4
		PRINT 점액 투성이가 된 
	PRINTW 항문을 꿰뚫었다….
ENDIF
