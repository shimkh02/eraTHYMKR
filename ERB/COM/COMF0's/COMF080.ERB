;=============================================================================
;클리토리스 주사
;=============================================================================
;………………………………………………
;실행 판정
;………………………………………………
@COM_ABLE80
;개별 설정 커맨드 필터
SIF DITEMTYPE:999:80 > 0
	RETURN 0
;강력 아이템·커맨드 무효시는 안 됨
SIF (FLAG:13 & 2) == 0
	RETURN 0
;주사계 커맨드를 유효하게 하지 않으면 안 됨
SIF (FLAG:15 & 64) == 0
	RETURN 0
;약물내성이라면 안 됨
SIF TALENT:44
	RETURN 0
;남자라면 안 됨
SIF TALENT:120
	RETURN 0
;기계라면 안 됨
SIF TALENT:206
	RETURN 0
;기교 3 이하의 ASSI의 경우 주사할 수 없다(바늘 다루기라면 OK)
IF ASSIPLAY
	SIF ABL:ASSI:12 <= 3 && TALENT:ASSI:53 == 0
		RETURN 0
ENDIF
;주사 세트와 미약을 가지고 있는지, 아이템 없음 설정이 아니면 안 됨
SIF (ITEM:53 == 0 || ITEM:61 == 0) && NOITEM == 0
	RETURN 0
;수면중은 불가
SIF EQUIP:14
	RETURN 0
;C계 장착도구 사용중은 불가
SIF TEQUIP:30
	RETURN 0
;한증과 암반 욕 이외의00목욕탕 입욕 중에는 불가
SIF TEQUIP:55 && TEQUIP:55 != 12 && TEQUIP:55 != 15
	RETURN 0
;기저귀 플레이 중에는 불가
SIF TEQUIP:58
	RETURN 0
;훈도시 착용중의 경우는 안 됨
SIF TEQUIP:59 == 17
	RETURN 0
;웨딩 드레스·고스로리 착용중의 경우는 안 됨
SIF TEQUIP:59 == 19 || TEQUIP:59 == 20
	RETURN 0
;알몸 앞치마, 학교수영복 플레이 중에는 불가
SIF TEQUIP:59 == 1 || TEQUIP:59 == 2
	RETURN 0
;촉수 조교 중에는 불가
SIF TEQUIP:90
	RETURN 0
RETURN 1

;--------------------------------------------------
;커맨드 본체
;--------------------------------------------------
@COM80
PRINTL 클리토리스 주사
TSTR:0 = 클리토리스 주사
CALL TRAIN_MESSAGE_COM080

;조제 지식이 있으면 소비를 억제한다
LOCAL:3 = (TALENT:MASTER:55) ?  1 # 0
SIF ASSI >= 0 && TALENT:ASSI:55
	LOCAL:3 = 1

;조교 대상으로 미약중독이 있으면 소비를 억제한다
IF LOCAL:3
	LOCAL:1 = (TALENT:46) ?  375 # 750
	LOCAL:2 = (TALENT:46) ?  300 # 600
ELSE
	LOCAL:1 = (TALENT:46) ?  500 # 1000
	LOCAL:2 = (TALENT:46) ?  400 # 800
ENDIF

DOWNBASE:0 += LOCAL:1
DOWNBASE:1 += LOCAL:2

SOURCE:0 = 500
SOURCE:24 = 500
SOURCE:26 = 3000
SOURCE:31 = 3000

;촉수가 아닌 경우는 미약을 1개 사용
IF TEQUIP:90
	TFLAG:90 = 1
ELSEIF NOITEM == 0
	ITEM:61 -= 1
	SIF FLAG:13 & 4 && MONEY:0 >= ITEMPRICE:61
		CALL FILL_PLURAL_ITEM, 61
ENDIF

;ABL:순종을 본다
IF ABL:10 == 0
	TIMES SOURCE:31 , 3.00
ELSEIF ABL:10 == 1
	TIMES SOURCE:31 , 2.50
ELSEIF ABL:10 == 2
	TIMES SOURCE:31 , 2.00
ELSEIF ABL:10 == 3
	TIMES SOURCE:31 , 1.50
ELSEIF ABL:10 == 4
	TIMES SOURCE:31 , 1.00
ELSE
	TIMES SOURCE:31 , 0.80
ENDIF

;ABL:C감각을 본다
IF ABL:0 == 0
	TIMES SOURCE:0 , 0.50
ELSEIF ABL:0 == 1
	TIMES SOURCE:0 , 1.00
ELSEIF ABL:0 == 2
	TIMES SOURCE:0 , 1.50
ELSEIF ABL:0 == 3
	TIMES SOURCE:0 , 2.00
	EX:0 += RAND:2
ELSEIF ABL:0 == 4
	TIMES SOURCE:0 , 2.50
	EX:0 += 1
ELSE
	TIMES SOURCE:0 , 3.00
	EX:0 += 2
ENDIF

;ABL:마조끼를 본다
IF ABL:21 == 0
	TIMES SOURCE:24 , 0.80
	TIMES DOWNBASE:0 , 1.20
ELSEIF ABL:21 == 1
	TIMES SOURCE:24 , 1.10
	TIMES DOWNBASE:0 , 1.10
ELSEIF ABL:21 == 2
	TIMES SOURCE:24 , 1.30
	TIMES DOWNBASE:0 , 1.00
ELSEIF ABL:21 == 3
	TIMES SOURCE:24 , 1.50
	TIMES DOWNBASE:0 , 0.90
ELSEIF ABL:21 == 4
	TIMES SOURCE:24 , 1.70
	TIMES DOWNBASE:0 , 0.80
ELSE
	TIMES SOURCE:24 , 2.10
	TIMES DOWNBASE:0 , 0.70
ENDIF

;PALAM:고통을 본다
IF PALAM:16 < PALAMLV:1
	SOURCE:20 = 3000
ELSEIF PALAM:16 < PALAMLV:2
	SOURCE:20 = 3300
ELSEIF PALAM:16 < PALAMLV:3
	SOURCE:20 = 3600
ELSEIF PALAM:16 < PALAMLV:4
	SOURCE:20 = 4000
ELSE
	SOURCE:20 = 4500
ENDIF

;아양 약효과를 발휘
TEQUIP:11 = 1

;반항적
SIF TALENT:11
	TIMES SOURCE:20 , 1.10
;저항
SIF TALENT:34
	TIMES SOURCE:20 , 1.10
;미약중독
IF TALENT:46
	SOURCE:24 += 500
	TIMES SOURCE:0 , 1.50
	TIMES SOURCE:31 , 1.50
	SOURCE:42 = 300
ELSE
	SOURCE:42 = 2 + RAND:5
ENDIF
;C민감
IF TALENT:100
	IF ABL:0 <= 3
		TIMES SOURCE:0 , 1.80
	ELSEIF ABL:0 == 4
		TIMES SOURCE:0 , 2.00
		EX:0 += RAND:2
	ELSE
		TIMES SOURCE:0 , 2.20
		EX:0 += RAND:3
	ENDIF
;C둔감
ELSEIF TALENT:101
	IF ABL:0 <= 3
		TIMES SOURCE:0 , 0.70
		EX:0 -= (EX:0 > 1) ?  1 # 0
	ELSEIF ABL:0 == 4
		TIMES SOURCE:0 , 0.75
		EX:0 -= (EX:0 > 1) ?  1 # 0
	ELSE
		TIMES SOURCE:0 , 0.80
	ENDIF
ENDIF

;촉수 보정
IF TEQUIP:90
	TIMES SOURCE:20 , 0.30
	TIMES SOURCE:31 , 0.80
	LOCAL:1 -= 300
	DOWNBASE:0 -= LOCAL:1
	DOWNBASE:1 -= 400
ENDIF

;조교자에게 바늘 다루기
SIF TALENT:PLAYER:53
	TIMES SOURCE:20 , 0.90
;조교자에게 유치
SIF TALENT:PLAYER:131
	TIMES SOURCE:20 , 1.20

;레이센이나 에이린에 의한 사용
IF TEQUIP:90 == 0
	SIF NO:PLAYER == 28
		TIMES SOURCE:20 , 0.70
	SIF NO:PLAYER == 29
		TIMES SOURCE:20 , 0.50
ENDIF

IF TALENT:120 == 0 && TALENT:PLAYER:120 == 0
	TCVAR:12 += 2
ELSEIF TALENT:120 && TALENT:PLAYER:120
	TCVAR:13 += 2
ENDIF

;약물 경험
TCVAR:40 += 2

SIF PLAYER == MASTER
	TCVAR:41 += 2

;주인 경험 플래그
SIF ASSIPLAY == 0 && ABL:21 >= 3
	TFLAG:50 += 1

RETURN 1

;--------------------------------------------------
;조교시 메시지
;--------------------------------------------------
@TRAIN_MESSAGE_COM080
CALL KOJO_TRAIN_MESSAGE_COM
SIF FLAG:10 == 0 || CFLAG:99 == 1 || RESULT != 0
	RETURN 0

PRINTL
PRINTFORM %CALLNAME:TARGET%의 
SIF ABL:0 >= 4
	PRINT 조교로 비대해진 
SIF TALENT:100
	PRINT 민감한 
SIF EX:20 || EX:30
	PRINT 살짝 건드리면 가버릴 것 같은 
IF TALENT:121
	PRINT 페니스에 
ELSE
	PRINT 클리토리스에 
ENDIF
PRINTL 주사바늘을 꽂았다….
PRINTL
PRINTFORM %조사선택(CALLNAME:TARGET, "는")% 
IF ABL:22 >= 3
	PRINT 날카로운 감각에 
ELSE
	PRINT 통증에 
ENDIF
IF TFLAG:899 >= 1
	PRINT 실신하면서도 땀으로 흥건한 몸을 껴안고 
ELSEIF ABL:0 >= 5
	PRINT 너무나도 강렬한 쾌감에 절규하며 
ELSEIF ABL:0 >= 3
	PRINT 본의가 아닌 절정을 일으키며 
ELSE
	PRINT 차 올라오는 열기에 전율하며 
ENDIF
IF PALAM:10 >= PALAMLV:5 || TALENT:100
	IF TALENT:121
		PRINT 대량의 정액을 
	ELSE
		PRINT 애액을 촥촥 
	ENDIF
	PRINT 분출하며 광란하고 
ELSEIF PALAM:10 >= PALAMLV:3
	PRINT 허리를 튕기면서 
	IF TALENT:121
		PRINT 사정하고 
	ELSE
		PRINT 애액을 마구 튀기고 
	ENDIF
ELSE
	PRINT 머리를 흔들어 억지로 견디고 
ENDIF
PRINTW 있다….
