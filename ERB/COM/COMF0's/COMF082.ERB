;=============================================================================
;애널 주사
;=============================================================================
;………………………………………………
;실행 판정
;………………………………………………
@COM_ABLE82
;개별 설정 커맨드 필터
SIF DITEMTYPE:999:82 > 0
	RETURN 0
;강력 아이템·커맨드 무효시는 안 됨
SIF (FLAG:13 & 2) == 0
	RETURN 0
;주사계 커맨드를 유효하게 하지 않으면 안 됨
SIF (FLAG:15 & 64) == 0
	RETURN 0
;A조교 필터가 오프가 되지 않으면 안 됨
SIF FLAG:2 & 2
	RETURN 0
;약물내성이라면 안 됨
SIF TALENT:44
	RETURN 0
;기계라면 안 됨
SIF TALENT:206
	RETURN 0
;기교 3 이하의 ASSI의 경우 주사할 수 없다(바늘 다루기라면 OK)
IF ASSIPLAY
	SIF ABL:ASSI:12 <= 3 && TALENT:ASSI:53 == 0
		RETURN 0
ENDIF
;주사 세트와 미약을 가지고 있는지, 아이템 없음 설정이 아니면 안 됨
SIF (ITEM:53 == 0 || ITEM:61 == 0) && NOITEM == 0
	RETURN 0
;수면중은 불가
SIF EQUIP:14
	RETURN 0
;A계 장착도구 사용중은 불가
SIF TEQUIP:25 || TEQUIP:26
	RETURN 0
;승마 중에는 불가
SIF TEQUIP:43
	RETURN 0
;한증과 암반 욕 이외의00목욕탕 입욕 중에는 불가
SIF TEQUIP:55 && TEQUIP:55 != 12 && TEQUIP:55 != 15
	RETURN 0
;기저귀 플레이 중에는 불가
SIF TEQUIP:58
	RETURN 0
;훈도시 착용중의 경우는 안 됨
SIF TEQUIP:59 == 17
	RETURN 0
;웨딩 드레스·고스로리 착용중의 경우는 안 됨
SIF TEQUIP:59 == 19 || TEQUIP:59 == 20
	RETURN 0
;알몸 앞치마, 학교수영복 플레이 중에는 불가
SIF TEQUIP:59 == 1 || TEQUIP:59 == 2
	RETURN 0
;촉수 조교 중에는 불가
SIF TEQUIP:90
	RETURN 0
RETURN 1

;--------------------------------------------------
;커맨드 본체
;--------------------------------------------------
;애널은 둔해서 아픔도 적습니다만, A경험은 증가하지 않기 때문에
;그렇게 말한 의미에서는 제일 사용할 수 없을지도 모릅니다
@COM82
PRINTL 애널 주사
TSTR:0 = 애널 주사
CALL TRAIN_MESSAGE_COM082

;조제 지식이 있으면 소비를 억제한다
LOCAL:3 = (TALENT:MASTER:55) ?  1 # 0
SIF ASSI >= 0 && TALENT:ASSI:55
	LOCAL:3 = 1

;조교 대상으로 미약중독이 있으면 소비를 억제한다
IF LOCAL:3
	LOCAL:1 = (TALENT:46) ?  250 # 500
	LOCAL:2 = (TALENT:46) ?  100 # 200
ELSE
	LOCAL:1 = (TALENT:46) ?  350 # 700
	LOCAL:2 = (TALENT:46) ?  150 # 300
ENDIF

DOWNBASE:0 += LOCAL:1
DOWNBASE:1 += LOCAL:2

SOURCE:2 = 300
SOURCE:24 = 700
SOURCE:26 = 3300
SOURCE:31 = 4000

;촉수가 아닌 경우는 미약을 1개 사용
IF TEQUIP:90
	TFLAG:90 = 1
ELSEIF NOITEM == 0
	ITEM:61 -= 1
	SIF FLAG:13 & 4 && MONEY:0 >= ITEMPRICE:61
		CALL FILL_PLURAL_ITEM, 61
ENDIF

;ABL:순종을 본다
IF ABL:10 == 0
	TIMES SOURCE:2 , 2.00
ELSEIF ABL:10 == 1
	TIMES SOURCE:2 , 1.80
ELSEIF ABL:10 == 2
	TIMES SOURCE:2 , 1.50
ELSEIF ABL:10 == 3
	TIMES SOURCE:2 , 1.20
ELSEIF ABL:10 == 4
	TIMES SOURCE:2 , 1.00
ELSE
	TIMES SOURCE:2 , 0.80
ENDIF

;ABL:A감각을 본다
IF ABL:2 == 0
	TIMES SOURCE:2 , 0.50
ELSEIF ABL:2 == 1
	TIMES SOURCE:2 , 1.00
ELSEIF ABL:2 == 2
	TIMES SOURCE:2 , 1.50
ELSEIF ABL:2 == 3
	TIMES SOURCE:2 , 2.00
	EX:2 += RAND:2
ELSEIF ABL:2 == 4
	TIMES SOURCE:2 , 2.50
	EX:2 += 1
ELSE
	TIMES SOURCE:2 , 3.00
	EX:2 += 2
ENDIF

;ABL:마조끼를 본다
IF ABL:21 == 0
	TIMES SOURCE:24 , 0.80
	TIMES DOWNBASE:0 , 1.20
ELSEIF ABL:21 == 1
	TIMES SOURCE:24 , 1.10
	TIMES DOWNBASE:0 , 1.10
ELSEIF ABL:21 == 2
	TIMES SOURCE:24 , 1.30
	TIMES DOWNBASE:0 , 1.00
ELSEIF ABL:21 == 3
	TIMES SOURCE:24 , 1.50
	TIMES DOWNBASE:0 , 0.90
ELSEIF ABL:21 == 4
	TIMES SOURCE:24 , 1.70
	TIMES DOWNBASE:0 , 0.80
ELSE
	TIMES SOURCE:24 , 2.10
	TIMES DOWNBASE:0 , 0.70
ENDIF

;PALAM:고통을 본다
IF PALAM:16 < PALAMLV:1
	SOURCE:20 = 2700
ELSEIF PALAM:16 < PALAMLV:2
	SOURCE:20 = 3000
ELSEIF PALAM:16 < PALAMLV:3
	SOURCE:20 = 3300
ELSEIF PALAM:16 < PALAMLV:4
	SOURCE:20 = 3700
ELSE
	SOURCE:20 = 4000
ENDIF

;아양 약효과를 발휘
TEQUIP:11 = 1

;반항적
SIF TALENT:11
	TIMES SOURCE:20 , 1.10
;저항
SIF TALENT:34
	TIMES SOURCE:20 , 1.10
;미약중독
IF TALENT:46
	SOURCE:24 += 500
	TIMES SOURCE:2 , 1.50
	TIMES SOURCE:31 , 1.50
	SOURCE:42 = 300
ELSE
	SOURCE:42 = 2 + RAND:5
ENDIF
;A민감
IF TALENT:104
	IF ABL:2 <= 3
		TIMES SOURCE:2 , 1.80
	ELSEIF ABL:2 == 4
		TIMES SOURCE:2 , 2.00
		EX:2 += RAND:2
	ELSE
		TIMES SOURCE:2 , 2.20
		EX:2 += RAND:3
	ENDIF
;A둔감
ELSEIF TALENT:105
	IF ABL:2 <= 3
		TIMES SOURCE:2 , 0.70
		EX:2 -= (EX:2 > 1) ?  1 # 0
	ELSEIF ABL:2 == 4
		TIMES SOURCE:2 , 0.75
		EX:2 -= (EX:2 > 1) ?  1 # 0
	ELSE
		TIMES SOURCE:2 , 0.80
	ENDIF
ENDIF

;촉수 보정
IF TEQUIP:90
	TIMES SOURCE:20 , 0.30
	TIMES SOURCE:31 , 0.80
	LOCAL:1 -= 200
	DOWNBASE:0 -= LOCAL:1
	DOWNBASE:1 -= 400
ENDIF

;조교자에게 바늘 다루기
SIF TALENT:PLAYER:53
	TIMES SOURCE:20 , 0.90

;레이센이나 에이린에 의한 사용
IF TEQUIP:90 == 0
	SIF NO:PLAYER == 28
		TIMES SOURCE:20 , 0.70
	SIF NO:PLAYER == 29
		TIMES SOURCE:20 , 0.50
ENDIF

IF TALENT:120 == 0 && TALENT:PLAYER:120 == 0
	TCVAR:12 += 2
ELSEIF TALENT:120 && TALENT:PLAYER:120
	TCVAR:13 += 2
ENDIF

;약물 경험
TCVAR:40 += 2
;주인 경험 플래그
SIF ASSIPLAY == 0 && ABL:21 >= 3
	TFLAG:50 += 1

RETURN 1

;--------------------------------------------------
;조교시 메시지
;--------------------------------------------------
@TRAIN_MESSAGE_COM082
CALL KOJO_TRAIN_MESSAGE_COM
SIF FLAG:10 == 0 || CFLAG:99 == 1 || RESULT != 0
	RETURN 0

PRINTL
PRINTFORM %CALLNAME:TARGET%의 
SIF TALENT:104
	PRINT 민감한 
IF ABL:2 >= 4
	PRINT 넓어져버린 장벽에 
ELSE
	PRINT 애널의 입구에 
ENDIF
PRINTL 주사바늘을 꽂았다….
PRINTL
PRINTFORM %조사선택(CALLNAME:TARGET, "는")% 
IF ABL:2 >= 5
	PRINT 몸을 떨며 계속 절정을 맞이하며 
ELSEIF ABL:2 >= 3
	PRINT 민감한 성감대로 변해버린 애널의 쾌감에 굴복하며 
ELSE
	PRINT 입구에서 뱃속으로 전해지는 달콤한 약 기운에 
ENDIF
IF PALAM:10 >= PALAMLV:5 || TALENT:100
	PRINT 오줌을 싸듯이 애액과 장액을 흘려대며 
ELSEIF PALAM:10 >= PALAMLV:3
	PRINT 눈을 쳐 뜨며 혀를 내밀고 
ELSE
	PRINT 쳐 올라오는 검은 쾌락에 
ENDIF
SIF TFLAG:899 > 0
	PRINT 무의식 중에 
IF PALAM:13 >= PALAMLV:5
	PRINT 신음소리를 지르며 경련하고 
ELSEIF PALAM:13 >= PALAMLV:3
	PRINT 짐승 같이 울부짖으며 
ELSE
	PRINT 이빨을 바득바득 갈고 
ENDIF
PRINTW 있다….
