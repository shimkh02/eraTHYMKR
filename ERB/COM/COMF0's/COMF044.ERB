;=============================================================================
;애널 강제 핥기
;=============================================================================
;………………………………………………
;실행 판정
;………………………………………………
@COM_ABLE44
;개별 설정 커맨드 필터
SIF DITEMTYPE:999:44 > 0
	RETURN 0
;A조교 필터가 오프가 되지 않으면 안 됨
SIF FLAG:2 & 2
	RETURN 0
;대상이 고양이혀라면 고양이 페라 패치 유효하지 않으면 안 됨
SIF TALENT:54 && (FLAG:14 & 16) == 0
	RETURN 0
;고양이혀라면 기교 5·봉사 정신 4·순종 3이 필요
SIF TALENT:54 && (ABL:12 < 5 || ABL:13 < 4 || ABL:10 < 3)
	RETURN 0
;수면중은 불가
SIF EQUIP:14
	RETURN 0
;실신중은 불가
SIF TFLAG:899 > 0
	RETURN 0
;밧줄 사용 중에는 목분리시만 가능
SIF TEQUIP:40 && TALENT:230 == 0
	RETURN 0
;거미집이 결여되어 있을 때는 안 됨
SIF TEQUIP:40 == 7
	RETURN 0
;볼 개그 사용중은 불가
SIF TEQUIP:42
	RETURN 0
;승마 중에는 불가
SIF TEQUIP:43 && TALENT:230 == 0
	RETURN 0
;한증과 암반 욕 이외의00목욕탕 입욕 중에는 불가
SIF TEQUIP:55 && TEQUIP:55 != 12 && TEQUIP:55 != 15
	RETURN 0
;조수가 훈도시 착용중의 경우 기교 4이상 필수
SIF ASSI >= 0 && ASSIPLAY && TEQUIP:ASSI:59 == 17 && ABL:12 < 4
	RETURN 0
;나체정식 중에는 안 됨
SIF TEQUIP:60
	RETURN 0
;시간정지 중에는 무리
SIF TEQUIP:71
	RETURN 0
;촉수 조교 중에는 불가
SIF TEQUIP:90
	RETURN 0
RETURN 1

;--------------------------------------------------
;커맨드 본체
;--------------------------------------------------
@COM44
PRINTL 애널 강제 핥기

;-------------------------------------------------
;실행할 수 있을까의 판정
;-------------------------------------------------
IF TFLAG:699 == 0
	CALL COM_ORDER
	;실행할 수 없다
	SIF RESULT == 0
		RETURN 0
ENDIF

;-------------------------------------------------
;실행 결정
;-------------------------------------------------
TSTR:0 = 애널 강제 핥기
CALL TRAIN_MESSAGE_COM044

;-------------------------------------------------
;사정 게이지 체크(후타나리의 경우)
;-------------------------------------------------
LOCAL = 0

;ABL:기교를 본다
IF ABL:12 == 0
	LOCAL = 100
ELSEIF ABL:12 == 1
	LOCAL = 300
ELSEIF ABL:12 == 2
	LOCAL = 500
ELSEIF ABL:12 == 3
	LOCAL = 800
ELSEIF ABL:12 == 4
	LOCAL = 1200
ELSE
	LOCAL = 1600
ENDIF

;순종을 본다
LOCAL = EJACULATIONCHECK_ABL_10_1(TARGET, LOCAL)
;봉사 정신을 본다
LOCAL = EJACULATIONCHECK_ABL_13_1(TARGET, LOCAL)

;TALENT:혀사용을 본다
SIF TALENT:52
	TIMES LOCAL , 2.00
	
;TALENT:고양이혀을 본다
SIF TALENT:54
	TIMES LOCAL , 0.70

;조교자의 C감각을 본다
LOCAL = EJACULATIONCHECK_ABL_0_1(PLAYER, LOCAL)

SIF TALENT:PLAYER:120 || TALENT:PLAYER:121
	BASE:PLAYER:2 += LOCAL

;-------------------------------------------------
;소스의 계산
;-------------------------------------------------
DOWNBASE:0 += 10
DOWNBASE:1 += 100

SOURCE:22 = 1800
SOURCE:31 = 5000

;위 쪽으로 계산한 더러운 데이터
SOURCE:30 = TFLAG:242*80 + 50

;ABL:봉사 정신을 본다
IF ABL:13 == 0
	SOURCE:15 = 420
	SOURCE:16 = 150
	TIMES SOURCE:22 , 0.80
	TIMES SOURCE:30 , 4.00
ELSEIF ABL:13 == 1
	SOURCE:15 = 500
	SOURCE:16 = 300
	TIMES SOURCE:22 , 0.90
	TIMES SOURCE:30 , 2.50
ELSEIF ABL:13 == 2
	SOURCE:15 = 580
	SOURCE:16 = 600
	TIMES SOURCE:22 , 1.00
	TIMES SOURCE:30 , 1.50
ELSEIF ABL:13 == 3
	SOURCE:15 = 660
	SOURCE:16 = 900
	TIMES SOURCE:22 , 1.10
	TIMES SOURCE:30 , 1.00
ELSEIF ABL:13 == 4
	SOURCE:15 = 740
	SOURCE:16 = 1500
	TIMES SOURCE:22 , 1.20
	TIMES SOURCE:30 , 0.50
ELSE
	SOURCE:15 = 820
	SOURCE:16 = 2200
	TIMES SOURCE:22 , 1.50
	TIMES SOURCE:30 , 0.10
ENDIF

;ABL:마조끼를 본다
IF ABL:21 == 0
	TIMES SOURCE:22 , 0.80
	TIMES SOURCE:30 , 1.50
ELSEIF ABL:21 == 1
	TIMES SOURCE:22 , 0.90
	TIMES SOURCE:30 , 1.20
ELSEIF ABL:21 == 2
	TIMES SOURCE:22 , 1.00
	TIMES SOURCE:30 , 1.00
ELSEIF ABL:21 == 3
	TIMES SOURCE:22 , 1.20
	TIMES SOURCE:30 , 0.80
ELSEIF ABL:21 == 4
	TIMES SOURCE:22 , 1.50
	TIMES SOURCE:30 , 0.60
ELSE
	TIMES SOURCE:22 , 2.00
	TIMES SOURCE:30 , 0.30
ENDIF

;PALAM:욕정을 본다
IF PALAM:13 < PALAMLV:1
	SOURCE:11 = 20
ELSEIF PALAM:13 < PALAMLV:2
	SOURCE:11 = 50
ELSEIF PALAM:13 < PALAMLV:3
	SOURCE:11 = 100
ELSEIF PALAM:13 < PALAMLV:4
	SOURCE:11 = 200
ELSE
	SOURCE:11 = 400
ENDIF

;TALENT:헌신적을 본다
SIF TALENT:63
	TIMES SOURCE:11 , 2.00

;TALENT:도착적을 본다
IF TALENT:80
	TIMES SOURCE:11 , 1.50
	TIMES SOURCE:22 , 2.00
	TIMES SOURCE:30 , 0.70
	TIMES SOURCE:31 , 0.20
ENDIF

;TALENT:연모/망신/친애를 본다
IF (TALENT:150 || TALENT:151 || TALENT:152) && ASSIPLAY == 0
	TIMES SOURCE:11 , 2.50
	TIMES SOURCE:22 , 1.20
ENDIF

;ABL:기교를 본다
IF ABL:12 == 0
	TIMES SOURCE:15 , 0.50
	TIMES SOURCE:16 , 0.50
ELSEIF ABL:12 == 1
	TIMES SOURCE:15 , 0.80
	TIMES SOURCE:16 , 0.80
ELSEIF ABL:12 == 2
	TIMES SOURCE:15 , 1.00
	TIMES SOURCE:16 , 1.00
ELSEIF ABL:12 == 3
	TIMES SOURCE:15 , 1.50
	TIMES SOURCE:16 , 1.50
ELSEIF ABL:12 == 4
	TIMES SOURCE:15 , 2.50
	TIMES SOURCE:16 , 2.50
ELSE
	TIMES SOURCE:15 , 4.00
	TIMES SOURCE:16 , 4.00
ENDIF

;-------------------------------------------------
;사정 체크
;-------------------------------------------------
TFLAG:91 = SAMEN_CHECK(PLAYER)

;사정시의 처리
IF TFLAG:91 >= 1
	;커맨드 마다 고유의 처리는 이 아래에 쓴다
	TIMES SOURCE:15 , 3.00

	;ABL:정액 중독을 본다
	IF ABL:31 == 0
		SOURCE:24 = 0
		TIMES SOURCE:16 , 2.00
		TIMES SOURCE:22, 2.00
	ELSEIF ABL:31 == 1
		SOURCE:24 = 200
		TIMES SOURCE:16 , 2.50
		TIMES SOURCE:22, 1.60
	ELSEIF ABL:31 == 2
		SOURCE:24 = 500
		TIMES SOURCE:16 , 3.00
		TIMES SOURCE:22, 1.00
	ELSEIF ABL:31 == 3
		SOURCE:24 = 1200
		TIMES SOURCE:16 , 4.50
		TIMES SOURCE:22, 0.70
	ELSEIF ABL:31 == 4
		SOURCE:24 = 2500
		TIMES SOURCE:16 , 6.00
		TIMES SOURCE:22, 0.40
	ELSE
		SOURCE:24 = 5000
		TIMES SOURCE:16 , 8.00
		TIMES SOURCE:22, 0.10
	ENDIF

	;사정처 타입의 설정
	TFLAG:703 = 7
	;사정처 선택의 가능·불가
	TFLAG:704 = 0
	;범용적인 처리는 이 함수로 실시한다
	;(사정 게이지의 재계산이나 사정 경험의 상승과 공통 부분의 표시)
	CALL SAMEN_SHOOT
ENDIF

TCVAR:PLAYER:1 += 1

;노예의 입⇔조교자의 A의 불결이 이동
STAIN:0 |= STAIN:PLAYER:4
STAIN:PLAYER:4 |= STAIN:0

IF TALENT:120 == 0 && TALENT:PLAYER:120 == 0
	TCVAR:12 += 3
ELSEIF TALENT:120 && TALENT:PLAYER:120
	TCVAR:13 += 3
ENDIF
;봉사 쾌락 경험 입수 판정
TFLAG:100 |= 1

RETURN 1

;--------------------------------------------------
;명령 거부의 메시지
;--------------------------------------------------
@DENIAL_MESSAGE_COM_44
CALL KOJO_DENIAL_MESSAGE_COM
SIF FLAG:10 == 0 || CFLAG:99 == 1 || RESULT != 0
	RETURN 0

PRINTL
IF ASSIPLAY
	PRINTFORML %CALLNAME:TARGET%에게 %CALLNAME:PLAYER%의 애널을 빨도록 명령해 보았지만 거절당했다.
ELSE
	PRINTFORML %CALLNAME:TARGET%에게 자신의 애널을 빨도록 명령해 보았지만 거절당했다.
ENDIF
PRINTW 좀 더 조교가 필요한 것 같다.

;--------------------------------------------------
;조교시 메시지
;--------------------------------------------------
@TRAIN_MESSAGE_COM044
CALL KOJO_TRAIN_MESSAGE_COM
SIF FLAG:10 == 0 || CFLAG:99 == 1 || RESULT != 0
	RETURN 0

PRINTL
;연모인가 망신이나 친애로 주인 조교 때
IF (TALENT:150 || TALENT:151 || TALENT:152) && ASSIPLAY == 0
	PRINTFORML %조사선택(CALLNAME:PLAYER, "는")% %CALLNAME:TARGET%에게 애널을 빨라고 하며 엉덩이를 쭉 내밀었다.
	PRINTFORMW %조사선택(CALLNAME:TARGET, "는")% 마치 소중한 것이라도 되는 양, %CALLNAME:PLAYER%의 애널을 혀로 애무하기 시작했다….
;헌신적인 경우
ELSEIF TALENT:63
	PRINTFORML %조사선택(CALLNAME:PLAYER, "는")% %CALLNAME:TARGET%에게 애널을 빨게 했다.
	PRINTFORMW %조사선택(CALLNAME:TARGET, "는")% 열심히 애널을 혀로 빨아서 %조사선택(CALLNAME:PLAYER, "를")% 즐겁게 했다….
;도착적인 경우
ELSEIF TALENT:80
	PRINTFORML %조사선택(CALLNAME:PLAYER, "는")% %CALLNAME:TARGET%에게 애널을 빨게 했다.
	PRINTFORMW %조사선택(CALLNAME:TARGET, "는")% 더러운 곳을 빠는 자기 자신에게 조금 도취된 거 같다.
;반발 각인 소유로 연모가 없는 경우
ELSEIF MARK:9 && TALENT:150 == 0
	PRINTFORMW %조사선택(CALLNAME:PLAYER, "는")% 싫어서 버둥대는 %CALLNAME:TARGET%에게 애널을 억지로 빨게 했다….
;그 외의 경우
ELSE
	PRINTFORMW %조사선택(CALLNAME:PLAYER, "는")% %CALLNAME:TARGET%에게 애널을 빨게 했다….
ENDIF

;-------------------------------------------------
;고유의 실행 판정
;-------------------------------------------------

@COM_ORDER_44, ARG
LOCAL:99 = ARG
;ABL:욕망
IF ABL:11
	SIF LOCAL:99
		RESULTS = %RESULTS% + 
	LOCAL:99 = ABL:11
	TFLAG:240 += LOCAL:99
	RESULTS = %RESULTS% %ABLNAME:11%LV{LOCAL:99}({LOCAL:99})
ENDIF
;ABL:봉사 정신
IF ABL:13
	SIF LOCAL:99
		RESULTS = %RESULTS% + 
	LOCAL:99 = ABL:13
	TFLAG:240 += LOCAL:99 * 3
	RESULTS = %RESULTS% %ABLNAME:13%LV{LOCAL:99}({LOCAL:99 * 3})
ENDIF
;ABL:BL기분＆TALENT:남자
IF TALENT:PLAYER:120 && TALENT:120
	SIF LOCAL:99
		RESULTS = %RESULTS% - 
	;↓의 LOCAL:99의 기준(자위 5, 펠라/A애무 강제 10)
	LOCAL:99 = 12
	TFLAG:240 -= LOCAL:99
	RESULTS = %RESULTS% %TALENTNAME:120%같은 종류({LOCAL:99})
	IF ABL:23
		LOCAL:99 = ABL:23
		TFLAG:240 += LOCAL:99 * 4
		RESULTS = %RESULTS% + %ABLNAME:23%LV{LOCAL:99}({LOCAL:99 * 4})
	ENDIF
ENDIF
;쾌락 각인
IF MARK:1
	SIF LOCAL:99
		RESULTS = %RESULTS% + 
	LOCAL:99 = MARK:1
	TFLAG:240 += LOCAL:99 * 2
	RESULTS = %RESULTS% %MARKNAME:1%LV{LOCAL:99}({LOCAL:99 * 2})
ENDIF

;부끄럼쟁이
IF TALENT:35
	RESULTS = %RESULTS% - 
	LOCAL:99 = 1
	TFLAG:240 -= LOCAL:99
	RESULTS = %RESULTS% %TALENTNAME:35%({LOCAL:99})
ENDIF
;악취 둔감
IF TALENT:61
	SIF LOCAL:99
		RESULTS = %RESULTS% + 
	LOCAL:99 = 2
	TFLAG:240 += LOCAL:99
	RESULTS = %RESULTS% %TALENTNAME:61%({LOCAL:99})
;악취 민감
ELSEIF TALENT:62
	RESULTS = %RESULTS% - 
	LOCAL:99 = 5
	TFLAG:240 -= LOCAL:99
	RESULTS = %RESULTS% %TALENTNAME:62%({LOCAL:99})
ENDIF
;헌신적
IF TALENT:63
	SIF LOCAL:99
		RESULTS = %RESULTS% + 
	LOCAL:99 = 4
	TFLAG:240 += LOCAL:99
	RESULTS = %RESULTS% %TALENTNAME:63%({LOCAL:99})
ENDIF
;더러운 무시
IF TALENT:64
	SIF LOCAL:99
		RESULTS = %RESULTS% + 
	LOCAL:99 = 3
	TFLAG:240 += LOCAL:99
	RESULTS = %RESULTS% %TALENTNAME:64%({LOCAL:99})
ENDIF
;쾌감의 부정
IF TALENT:71
	RESULTS = %RESULTS% - 
	LOCAL:99 = 1
	TFLAG:240 -= LOCAL:99
	RESULTS = %RESULTS% %TALENTNAME:71%({LOCAL:99})
ENDIF
;도착적
IF TALENT:80
	SIF LOCAL:99
		RESULTS = %RESULTS% + 
	LOCAL:99 = 3
	TFLAG:240 += LOCAL:99
	RESULTS = %RESULTS% %TALENTNAME:80%({LOCAL:99})
ENDIF
;조교자가 새드
IF TALENT:PLAYER:81
	SIF LOCAL:99
		RESULTS = %RESULTS% + 
	LOCAL:99 = 3
	TFLAG:240 += LOCAL:99
	RESULTS = %RESULTS% %조사처리(CALLNAME:PLAYER,"가")% %TALENTNAME:81%({LOCAL:99})
ENDIF
;연모
IF TALENT:150 && ASSIPLAY == 0
	SIF LOCAL:99
		RESULTS = %RESULTS% + 
	LOCAL:99 = 5
	TFLAG:240 += LOCAL:99
	RESULTS = %RESULTS% %TALENTNAME:150%({LOCAL:99})
ENDIF

;조교자의 A의 더러움
TFLAG:242 = COMORDER_STAIN_CALC(4, 0)

;서로불결
IF TFLAG:242
	RESULTS = %RESULTS% - 
	TFLAG:240 -= TFLAG:242
	RESULTS = %RESULTS% 서로불결
	;악취 둔감
	IF TALENT:61
		RESULTS = %RESULTS%, %TALENTNAME:61%
	ELSEIF TALENT:62
		RESULTS = %RESULTS%, %TALENTNAME:62%
	ENDIF
	RESULTS = %RESULTS%({TFLAG:242})
	LOCAL:99 = 1
ENDIF

TFLAG:241 = 23
