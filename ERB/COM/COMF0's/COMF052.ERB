;=============================================================================
;육봉마찰
;=============================================================================
;………………………………………………
;실행 판정
;………………………………………………
@COM_ABLE52
;개별 설정 커맨드 필터
SIF DITEMTYPE:999:52 > 0
	RETURN 0
;조교자와 조교 대상의 쌍방으로 페니스가 없으면 안 됨
SIF (EXIST_BAR(PLAYER) == 0) || (EXIST_BAR(TARGET) == 0)
	RETURN 0
;주인이 [금단의 지식] 소유가 아닐 때, 체격차이가 2 단계 이내나 조교자가 [금단의 지식] 소유가 아니면 안 됨
SIF TALENT:MASTER:56 == 0 && COMMON_BODYSIZE_CHECK(PLAYER, TARGET) == 0
	RETURN 0
;순종 4 미만·레즈끼(BL끼) 4 미만의 ASSI의 경우, 조교 대상이 동성이라면 실행할 수 없다(소악마인가 망신이나 복종이라면 OK)
IF ASSIPLAY
	SIF (ABL:ASSI:10 < 4 || ((TALENT:120 == 0 && TALENT:ASSI:120 == 0 && ABL:ASSI:22 < 4) || (TALENT:120 && TALENT:ASSI:120 && ABL:ASSI:23 < 4))) && TALENT:ASSI:93 == 0 && TALENT:ASSI:151 == 0 && TALENT:ASSI:160 == 0
	RETURN 0
ENDIF
;수면중은 불가
SIF EQUIP:14
	RETURN 0
;패닉중은 불가
SIF TFLAG:108 > 0
	RETURN 0
;실신중은 불가
SIF TFLAG:899 > 0
	RETURN 0
;C계 장착도구 사용중은 불가
SIF TEQUIP:30
	RETURN 0
;밧줄 사용 중에는 불가
SIF TEQUIP:40
	RETURN 0
;승마 중에는 불가
SIF TEQUIP:43
	RETURN 0
;00목욕탕 입욕 중에는 불가
SIF TEQUIP:55
	RETURN 0
;기저귀 플레이 중에는 불가
SIF TEQUIP:58
	RETURN 0
;조수가 훈도시 착용중의 경우는 안 됨
SIF ASSI >= 0 && ASSIPLAY && TEQUIP:ASSI:59 == 17
	RETURN 0
;훈도시 착용중의 경우는 안 됨
SIF TEQUIP:59 == 17
	RETURN 0
;시간정지 중에는 무리
SIF TEQUIP:71
	RETURN 0
;촉수 조교중일 시 주인의 촉수사역Lv2 이하는 안 됨
SIF TEQUIP:90 && ABL:MASTER:98 <= 2
	RETURN 0
RETURN 1

;--------------------------------------------------
;커맨드 본체
;--------------------------------------------------
;수음을 개조해서 제작
@COM52
PRINTL 육봉마찰

;-------------------------------------------------
;실행할 수 있는가 판정
;-------------------------------------------------
IF TFLAG:699 == 0
	CALL COM_ORDER
	;실행할 수 없다
	SIF RESULT == 0
		RETURN 0
ENDIF

;-------------------------------------------------
;실행 결정
;-------------------------------------------------
TSTR:0 = 육봉마찰
CALL TRAIN_MESSAGE_COM052

;-------------------------------------------------
;사정 게이지 체크
;-------------------------------------------------
LOCAL = 0

;ABL:조교자의 기교를 본다
IF ABL:PLAYER:12 == 0
	LOCAL = 120
ELSEIF ABL:PLAYER:12 == 1
	LOCAL = 350
ELSEIF ABL:PLAYER:12 == 2
	LOCAL = 1000
ELSEIF ABL:PLAYER:12 == 3
	LOCAL = 2400
ELSEIF ABL:PLAYER:12 == 4
	LOCAL = 3500
ELSE
	LOCAL = 4300
ENDIF

;기교를 본다
LOCAL = EJACULATIONCHECK_ABL_12_1(TARGET, LOCAL)
;순종을 본다
LOCAL = EJACULATIONCHECK_ABL_10_4(TARGET, LOCAL)
;봉사정신을 본다
LOCAL = EJACULATIONCHECK_ABL_13_3(TARGET, LOCAL)

;조교자의 Ｃ감각을 본다
LOCAL = EJACULATIONCHECK_ABL_0_1(PLAYER, LOCAL)

SIF TALENT:PLAYER:120 || TALENT:PLAYER:121
	BASE:PLAYER:2 += LOCAL

;-------------------------------------------------
;소스의 계산
;-------------------------------------------------
DOWNBASE:0 += 20
DOWNBASE:1 += 150

SOURCE:31 = 750

;위쪽에서 계산한 더러움 데이터
SOURCE:30 = TFLAG:242*20 + 80

;ABL:봉사정신을 본다
IF ABL:13 == 0
	SOURCE:15 = 50
	SOURCE:16 = 20
	TIMES SOURCE:30 , 4.00
ELSEIF ABL:13 == 1
	SOURCE:15 = 100
	SOURCE:16 = 50
	TIMES SOURCE:30 , 3.00
ELSEIF ABL:13 == 2
	SOURCE:15 = 200
	SOURCE:16 = 100
	TIMES SOURCE:30 , 2.00
ELSEIF ABL:13 == 3
	SOURCE:15 = 300
	SOURCE:16 = 200
	TIMES SOURCE:30 , 1.00
ELSEIF ABL:13 == 4
	SOURCE:15 = 400
	SOURCE:16 = 300
	TIMES SOURCE:30 , 0.50
ELSE
	SOURCE:15 = 500
	SOURCE:16 = 500
	TIMES SOURCE:30 , 0.10
ENDIF

;ABL:기교를 본다
IF ABL:12 == 0
	TIMES SOURCE:15 , 0.50
	TIMES SOURCE:16 , 0.50
ELSEIF ABL:12 == 1
	TIMES SOURCE:15 , 0.60
	TIMES SOURCE:16 , 0.60
ELSEIF ABL:12 == 2
	TIMES SOURCE:15 , 0.70
	TIMES SOURCE:16 , 0.70
ELSEIF ABL:12 == 3
	TIMES SOURCE:15 , 1.00
	TIMES SOURCE:16 , 1.00
ELSEIF ABL:12 == 4
	TIMES SOURCE:15 , 1.20
	TIMES SOURCE:16 , 1.20
ELSE
	TIMES SOURCE:15 , 1.50
	TIMES SOURCE:16 , 1.50
ENDIF

;ABL:Ｃ감각을 본다
IF ABL:0 == 0
	SOURCE:0 = 10
ELSEIF ABL:0 == 1
	SOURCE:0 = 37
ELSEIF ABL:0 == 2
	SOURCE:0 = 150
ELSEIF ABL:0 == 3
	SOURCE:0 = 350
ELSEIF ABL:0 == 4
	SOURCE:0 = 550
ELSE
	SOURCE:0 = 750
ENDIF

;PALAM:욕정을 본다
IF PALAM:13 < PALAMLV:1
	TIMES SOURCE:0 , 0.30
ELSEIF PALAM:13 < PALAMLV:2
	TIMES SOURCE:0 , 0.60
ELSEIF PALAM:13 < PALAMLV:3
	TIMES SOURCE:0 , 1.00
ELSEIF PALAM:13 < PALAMLV:4
	TIMES SOURCE:0 , 1.30
ELSE
	TIMES SOURCE:0 , 1.60
ENDIF

;淫茎
IF TALENT:171
	SOURCE:11 = 500
	TIMES SOURCE:30 , 0.80
	TIMES SOURCE:31 , 0.80
ENDIF

;-------------------------------------------------
;사정 체크
;-------------------------------------------------
TFLAG:91 = SAMEN_CHECK(PLAYER)

;사정시의 처리
IF TFLAG:91 >= 1
	;コマンドごとに固有の処理はこの下に書く
	TIMES SOURCE:15 , 3.00

	;ABL:정액중독을 본다
	IF ABL:31 == 0
		SOURCE:24 = 0
		TIMES SOURCE:16 , 2.00
	ELSEIF ABL:31 == 1
		SOURCE:24 = 200
		TIMES SOURCE:16 , 2.50
	ELSEIF ABL:31 == 2
		SOURCE:24 = 600
		TIMES SOURCE:16 , 3.00
	ELSEIF ABL:31 == 3
		SOURCE:24 = 1500
		TIMES SOURCE:16 , 4.00
	ELSEIF ABL:31 == 4
		SOURCE:24 = 3000
		TIMES SOURCE:16 , 5.00
	ELSE
		SOURCE:24 = 6000
		TIMES SOURCE:16 , 6.00
	ENDIF

	;射精先タイプの設定
	TFLAG:703 = 14
	;射精先選択の可・不可
	TFLAG:704 = 0
	;汎用的な処理はこの関数で行う
	;（射精ゲージの再計算や射精経験の上昇と共通部分の表示）
	CALL SAMEN_SHOOT
ENDIF

;射精に関係なく行われる処理はここから(汚れ部位追加の可能性も考慮して元となったらしき部分の処理だけは残すことに)
;노예의 P⇔조교자의 Ｐ의 불결이 이동
STAIN:2 |= STAIN:PLAYER:2
STAIN:PLAYER:2 |= STAIN:2

IF TALENT:120 == 0 && TALENT:PLAYER:120 == 0
	TCVAR:12 += 4
ELSEIF TALENT:122 == 1 && TALENT:PLAYER:122 == 1
	TCVAR:13 += 4
ENDIF
;奉仕快楽経験入手判定
TFLAG:100 |= 1

;조교자가 후타나리
SIF TALENT:PLAYER:121
	SOURCE:22 /= 2

RETURN 1

;--------------------------------------------------
;명령 거부 메시지
;--------------------------------------------------
@DENIAL_MESSAGE_COM_52
CALL KOJO_DENIAL_MESSAGE_COM
SIF FLAG:10 == 0 || CFLAG:99 == 1 || RESULT != 0
	RETURN 0

PRINTL
PRINTFORML %CALLNAME:TARGET%에게 서로의 페니스로 칼 싸움을 하자고 했지만 무시했다.
PRINTW 좀 더 조교가 필요한 것 같다.

;--------------------------------------------------
;조교시 메시지
;--------------------------------------------------
@TRAIN_MESSAGE_COM052
CALL KOJO_TRAIN_MESSAGE_COM
SIF FLAG:10 == 0 || CFLAG:99 == 1 || RESULT != 0
	RETURN 0

PRINTL
PRINTFORM %조사선택(CALLNAME:PLAYER, "는")% 자신의 페니스를 %CALLNAME:TARGET%의 페니스에 리드미컬하게 비벼댔다….
;未陥落で반발각인2以上持ちの場合
IF TALENT:150 == 0 && TALENT:160 == 0 && TALENT:170 == 0 && MARK:9 > 1
	PRINTL
	PRINTFORMW %조사선택(CALLNAME:TARGET, "는")% 이 굴욕스런 치태에 눈물을 머금고 있다….
;연모で욕정が高まっていて主人調教の場合
ELSEIF ASSIPLAY == 0 && TALENT:150 && PALAM:13 > PALAMLV:5
	PRINTL
	PRINTFORMW %조사선택(CALLNAME:TARGET, "는")%, 사랑하는 사람이 자신의 페니스로 놀아줘서 좋은 것 같다….
;恥情が高まっている場合
ELSEIF PALAM:15 > PALAMLV:5
	PRINTL
	PRINTFORMW %조사선택(CALLNAME:TARGET, "는")% 페니스를 이 딴 식으로 사용하는 건 부끄럽다고 생각하는 것 같다….
;그 외의 경우
ELSE
	PRINTW
ENDIF

;-------------------------------------------------
;고유 실행 판정
;-------------------------------------------------
@COM_ORDER_52, ARG
LOCAL:99 = ARG
;ABL:욕망
IF ABL:11
	SIF LOCAL:99
		RESULTS = %RESULTS% + 
	LOCAL:99 = ABL:11
	TFLAG:240 += LOCAL:99
	RESULTS = %RESULTS% %ABLNAME:11%LV{LOCAL:99}({LOCAL:99})
ENDIF
;ABL:봉사정신
IF ABL:13
	SIF LOCAL:99
		RESULTS = %RESULTS% + 
	LOCAL:99 = ABL:13
	TFLAG:240 += LOCAL:99 * 4
	RESULTS = %RESULTS% %ABLNAME:13%LV{LOCAL:99}({LOCAL:99 * 4})
ENDIF

;ABL:노출벽
IF ABL:14 >= 2
	SIF LOCAL:99
		RESULTS = %RESULTS% +
	LOCAL:99 = ABL:14
	TFLAG:240 += LOCAL:99
	RESULTS = %RESULTS% %ABLNAME:14%LV{LOCAL:99}({LOCAL:99})
ENDIF

;ABL:정액중독
IF ABL:31
	SIF LOCAL:99
		RESULTS = %RESULTS% + 
	LOCAL:99 = ABL:31
	TFLAG:240 += LOCAL:99
	RESULTS = %RESULTS% %ABLNAME:31%LV{LOCAL:99}({LOCAL:99})
ENDIF

;쾌락각인
IF MARK:1
	SIF LOCAL:99
		RESULTS = %RESULTS% + 
	LOCAL:99 = MARK:1
	TFLAG:240 += LOCAL:99
	RESULTS = %RESULTS% %MARKNAME:1%LV{LOCAL:99}({LOCAL:99})
ENDIF

;PALAM:욕정
GETPALAMLV PALAM:13, 5
LOCAL:2 = RESULT
IF LOCAL:2
	SIF LOCAL:99
		RESULTS = %RESULTS% + 
	LOCAL:99 = LOCAL:2
	TFLAG:240 += LOCAL:99
	RESULTS = %RESULTS% %PALAMNAME:13%LV{LOCAL:2}({LOCAL:99})
ENDIF

;부끄럼쟁이
IF TALENT:35
	RESULTS = %RESULTS% - 
	LOCAL:99 = 3
	TFLAG:240 -= LOCAL:99
	RESULTS = %RESULTS% %TALENTNAME:35%({LOCAL:99})
;恥薄い
ELSEIF TALENT:36
	SIF LOCAL:99
		RESULTS = %RESULTS% +
	LOCAL:99 = 1
	TFLAG:240 += LOCAL:99
	RESULTS = %RESULTS% %TALENTNAME:36%({LOCAL:99})
ENDIF
;男嫌い
IF TALENT:37 && TALENT:PLAYER:120
	RESULTS = %RESULTS% - 
	LOCAL:99 = 5
	TFLAG:240 -= LOCAL:99
	RESULTS = %RESULTS% %TALENTNAME:37%({LOCAL:99})
;女嫌い
ELSEIF TALENT:38 && TALENT:PLAYER:120 == 0
	RESULTS = %RESULTS% - 
	LOCAL:99 = 5
	TFLAG:240 -= LOCAL:99
	RESULTS = %RESULTS% %TALENTNAME:38%({LOCAL:99})
ENDIF
;냄새둔감
IF TALENT:61
	SIF LOCAL:99
		RESULTS = %RESULTS% + 
	LOCAL:99 = 1
	TFLAG:240 += LOCAL:99
	RESULTS = %RESULTS% %TALENTNAME:61%({LOCAL:99})
;냄새민감
ELSEIF TALENT:62
	RESULTS = %RESULTS% - 
	LOCAL:99 = 3
	TFLAG:240 -= LOCAL:99
	RESULTS = %RESULTS% %TALENTNAME:62%({LOCAL:99})
ENDIF
;쾌감의 부정
IF TALENT:71
	RESULTS = %RESULTS% - 
	LOCAL:99 = 1
	TFLAG:240 -= LOCAL:99
	RESULTS = %RESULTS% %TALENTNAME:71%({LOCAL:99})
ENDIF
;도착적
IF TALENT:80
	SIF LOCAL:99
		RESULTS = %RESULTS% + 
	LOCAL:99 = 2
	TFLAG:240 += LOCAL:99
	RESULTS = %RESULTS% %TALENTNAME:80%({LOCAL:99})
ENDIF
;연모
IF TALENT:150 && ASSIPLAY == 0
	SIF LOCAL:99
		RESULTS = %RESULTS% + 
	LOCAL:99 = 5
	TFLAG:240 += LOCAL:99
	RESULTS = %RESULTS% %TALENTNAME:150%({LOCAL:99})
ENDIF
;조교자가 후타나리
IF TALENT:PLAYER:121
	SIF LOCAL:99
		RESULTS = %RESULTS% + 
	LOCAL:99 = 4
	TFLAG:240 += LOCAL:99
	RESULTS = %RESULTS% %조사처리(CALLNAME:PLAYER,"가")% %TALENTNAME:121%({LOCAL:99})
ENDIF

;조교자의 Ｐ의 더러움
TFLAG:242 = COMORDER_STAIN_CALC(2, 0)

;불결 있음
IF TFLAG:242
	RESULTS = %RESULTS% - 
	TFLAG:240 -= TFLAG:242
	RESULTS = %RESULTS% 불결 있음
	;냄새둔감
	IF TALENT:61
		RESULTS = %RESULTS%%TALENTNAME:61%
	ELSEIF TALENT:62
		RESULTS = %RESULTS%%TALENTNAME:62%
	ENDIF
	RESULTS = %RESULTS%({TFLAG:242})
	LOCAL:99 = 1
ENDIF

TFLAG:241 = 33
