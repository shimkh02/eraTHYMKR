;=============================================================================
;촉수 해킹
;=============================================================================
;………………………………………………
;실행 판정
;………………………………………………
@COM_ABLE556
;개별 설정 커맨드 필터
SIF DITEMTYPE:999:556 > 0
	RETURN 0
;신사적인 촉수계 커맨드를 유효로 하지 않으면 불가
SIF (FLAG:15 & 4096) == 0
	RETURN 0
;아이템進化の秘法を持っているか, 아이템なし設定でないとダメ
SIF ITEM:59 == 0 && NOITEM == 0
	RETURN 0
;촉수조교 중이 아니면 불가
SIF TEQUIP:90 == 0
	RETURN 0
;조교자는 촉수사역LV4이상 필요
SIF ABL:PLAYER:98 < 4
	RETURN 0
;촉수가 [放電촉수]を得ていないとダメ
SIF (FLAG:75 & 4) == 0
	RETURN 0
;촉수가 [極細촉수]を得ていないとダメ
SIF (FLAG:75 & 16) == 0
	RETURN 0
;촉수가 [遠隔精密制御]を得ていないとダメ
SIF (FLAG:75 & 512) == 0
	RETURN 0
;機械でないとダメ
SIF TALENT:206 == 0
	RETURN 0
RETURN 1

;--------------------------------------------------
;커맨드 본체
;--------------------------------------------------
;전제가 굉장히 어려운 분만큼, 효과는 극히 절대.
;기계의 노예의 각처에 특별한 촉수를 접속해 전류를 흘리면서 좋아해 마음대로 해킹 하는 커맨드
@COM556
PRINTL 촉수 해킹
TSTR:0 = 촉수 치키치키
CALL TRAIN_MESSAGE_COM556

DOWNBASE:0 += 20
DOWNBASE:1 += 80

;PALAM:고통을 본다
IF PALAM:16 < PALAMLV:1
	SOURCE:20 = 2000
ELSEIF PALAM:16 < PALAMLV:2
	SOURCE:20 = 2500
ELSEIF PALAM:16 < PALAMLV:3
	SOURCE:20 = 2800
ELSEIF PALAM:16 < PALAMLV:4
	SOURCE:20 = 3000
ELSEIF PALAM:16 < PALAMLV:5
	SOURCE:20 = 3300
ELSE
	SOURCE:20 = 3600
ENDIF

LOCAL:1 = 1000

;ABL:마조끼를 본다
IF ABL:21 == 0
	TIMES LOCAL:1 , 1.80
	LOCAL:2 = 5000
ELSEIF ABL:21 == 1
	TIMES LOCAL:1 , 1.50
	LOCAL:2 = 3000
ELSEIF ABL:21 == 2
	TIMES LOCAL:1 , 1.20
	LOCAL:2 = 2000
ELSEIF ABL:21 == 3
	LOCAL:2 = 1000
ELSEIF ABL:21 == 4
	TIMES LOCAL:1 , 0.80
	LOCAL:2 = 500
ELSE
	TIMES LOCAL:1 , 0.60
	LOCAL:2 = 200
ENDIF

;ABL:촉수 중독을 본다
IF ABL:35 == 0
	TIMES LOCAL:2 , 1.50
	LOCAL:3 = 0
ELSEIF ABL:35 == 1
	TIMES LOCAL:2 , 1.20
	LOCAL:3 = 10
ELSEIF ABL:35 == 2
	LOCAL:3 = 30
ELSEIF ABL:35 == 3
	TIMES LOCAL:2 , 0.80
	LOCAL:3 = 60
ELSEIF ABL:35 == 4
	TIMES LOCAL:2 , 0.60
	LOCAL:3 = 100
ELSE
	TIMES LOCAL:2 , 0.50
	LOCAL:3 = 250
ENDIF

SOURCE:24 = LOCAL:3
SOURCE:25 = (LOCAL:1 + LOCAL:2) / 2
SOURCE:31 = LOCAL:2

;촉수의 접속 or해방
IF TEQUIP:96
	TEQUIP:96 = 0
ELSE
	TEQUIP:96 = 1
ENDIF
;촉수 경험 플래그
TFLAG:90 = 0

;커맨드 속성：촉수
TFLAG:98 = 8
RETURN 1

;-------------------------------------------------
;촉수 치키치키 실행중
;-------------------------------------------------
@EQUIP_COM556
PRINTL <촉수 해킹 실행중>

DOWNBASE:0 += 10
DOWNBASE:1 += 20

;쾌CVAB의 획득 소스 합계를 달성감, 쿄우준 추가, 욕정 추가, 중독 충족에 가산한다
LOCAL:1 = SOURCE:0 + SOURCE:1 + SOURCE:2 + SOURCE:3
SOURCE:16 += LOCAL:1
SOURCE:17 += LOCAL:1
SOURCE:18 += LOCAL:1
SOURCE:24 += LOCAL:1
;불결 일탈 반감 추가의 획득 소스 합계를 쾌CVAB의 소스에 가산하지만, 해당 소스가 있는 경우만으로 한다.
LOCAL:2 = SOURCE:30 + SOURCE:31 + SOURCE:32
SOURCE:0 += (SOURCE:0 < 1) ?  0 # LOCAL:2
SOURCE:1 += (SOURCE:1 < 1) ?  0 # LOCAL:2
SOURCE:2 += (SOURCE:2 < 1) ?  0 # LOCAL:2
SOURCE:3 += (SOURCE:3 < 1) ?  0 # LOCAL:2

;불결~반감 추가 획득 소스가 있는 경우는 그것을 경감한다
FOR LOCAL, 30, 33
	IF SOURCE:LOCAL
		;ABL:촉수 중독을 본다
		IF ABL:35 == 0
			TIMES SOURCE:LOCAL , 0.90
		ELSEIF ABL:35 == 1
			TIMES SOURCE:LOCAL , 0.75
		ELSEIF ABL:35 == 2
			TIMES SOURCE:LOCAL , 0.50
		ELSEIF ABL:35 == 3
			TIMES SOURCE:LOCAL , 0.35
		ELSEIF ABL:35 == 4
			TIMES SOURCE:LOCAL , 0.20
		ELSE
			TIMES SOURCE:LOCAL , 0.10
		ENDIF
	ENDIF
NEXT

;전류의 고통은 받는다
;PALAM:고통을 본다
IF PALAM:16 < PALAMLV:1
	SOURCE:20 += 200
ELSEIF PALAM:16 < PALAMLV:2
	SOURCE:20 += 250
ELSEIF PALAM:16 < PALAMLV:3
	SOURCE:20 += 280
ELSEIF PALAM:16 < PALAMLV:4
	SOURCE:20 += 300
ELSEIF PALAM:16 < PALAMLV:5
	SOURCE:20 += 330
ELSE
	SOURCE:20 += 360
ENDIF

;ABL:순종을 본다
IF ABL:10 == 0
	LOCAL:3 = 30
	LOCAL:4 = 20
ELSEIF ABL:10 == 1
	LOCAL:3 = 80
	LOCAL:4 = 40
ELSEIF ABL:10 == 2
	LOCAL:3 = 160
	LOCAL:4 = 80
ELSEIF ABL:10 == 3
	LOCAL:3 = 320
	LOCAL:4 = 160
ELSEIF ABL:10 == 4
	LOCAL:3 = 700
	LOCAL:4 = 320
ELSE
	LOCAL:3 = 1200
	LOCAL:4 = 640
ENDIF

;복종
IF TALENT:160
	TIMES LOCAL:3 , 1.50
	TIMES LOCAL:4 , 1.20
ENDIF

;MARK:공포 각인을 본다
IF MARK:8 == 1
	TIMES LOCAL:4 , 1.20
ELSEIF MARK:8 == 2
	TIMES LOCAL:4 , 1.50
ELSEIF MARK:8 >= 3
	TIMES LOCAL:4 , 2.00
ENDIF

;굴종과 지배의 소스에 가산
SOURCE:22 += LOCAL:3
SOURCE:23 += LOCAL:4

;촉수 경험 플래그
TFLAG:90 += 1
RETURN 1

;--------------------------------------------------
;조교시 메시지
;--------------------------------------------------
@TRAIN_MESSAGE_COM556
CALL KOJO_TRAIN_MESSAGE_COM
SIF FLAG:10 == 0 || CFLAG:99 == 1 || RESULT != 0
	RETURN 0

PRINTL
IF TEQUIP:96
	PRINTFORMW 촉수는 %CALLNAME:TARGET%에게의 몸에의 간섭을 그만두고, 신체의 각부에서 떨어져 나왔다….
ELSE
	PRINTFORML 기분 나쁜 움직임을 하면서 촉수가 %CALLNAME:TARGET%의 신체에 매달려,
	PRINTL 몸의 내외의 몇 개의 장소에 「접속」되었다.
	PRINTFORML 그리고, 촉수를 통해서 %CALLNAME:TARGET%에게 다양한 프로그램적 간섭이 시작했다.
	PRINTFORMW 그 압도적이고 오만한 능욕에 %조사선택(CALLNAME:TARGET, "는")% 전신을 격렬하게 흔들며 반응 할 수 밖에 없었다….
ENDIF
