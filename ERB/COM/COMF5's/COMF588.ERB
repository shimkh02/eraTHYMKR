;=============================================================================
;촉수 은방울꽃미약
;=============================================================================
;………………………………………………
;실행 판정
;………………………………………………
@COM_ABLE588
;개별 설정 커맨드 필터
SIF DITEMTYPE:999:588 > 0
	RETURN 0
;신사적인 촉수계 커맨드를 유효로 하지 않으면 불가
SIF (FLAG:15 & 4096) == 0
	RETURN 0
;캐릭터 전용 커맨드를 유효로 하지 않으면 불가
SIF (FLAG:15 & 16384) == 0
	RETURN 0
;아이템進化の秘法を持っているか, 아이템なし設定でないとダメ
SIF ITEM:59 == 0 && NOITEM == 0
	RETURN 0
;촉수조교 중이 아니면 불가
SIF TEQUIP:90 == 0
	RETURN 0
;해제는 언제든 가능
SIF TEQUIP:95 & 8
	RETURN 1
;조교자는 촉수사역LV4이상 필요
SIF ABL:PLAYER:98 < 4
	RETURN 0
;薬毒耐性だとダメ
SIF TALENT:44
	RETURN 0
;기계면 불가
SIF TALENT:206
	RETURN 0
;촉수가 [MEDICINE촉수]を得ていないとダメ
SIF (FLAG:98 & 256) == 0
	RETURN 0
;メディスンを촉수生贄に捧げていない(=メディスンが何らかの形で存在している)とダメ
SIF (FLAG:97 & 256) == 0
	RETURN 0
RETURN 1

;--------------------------------------------------
;커맨드 본체
;--------------------------------------------------
;촉수미약 담그고의 효과를 분리한 위에 사양을 변경. 그렇지만, 기본은 미약인 채.
;촉수생지를 통해서 촉수와 일체화한 메디슨을 소환,
;은방울꽃형의 촉수로부터 분비되는 아양 약효과의 액체를 바르거나 구전 하거나 해 성감대의 감도를 높여 간다……라고 하는 매우 난해한 플레이

;개발도에 관해서는 이하와 같다
;C：촉수 C(P) 고문계 조교로 개발도 상승, 1000이상으로 감도가 1 랭크 상승
;V：촉수 V삽입계 조교로 개발도 상승, 2000이상으로 감도가 1 랭크 상승
;A：촉수 A삽입계 조교로 개발도 상승, 2500이상으로 감도가 1 랭크 상승
;B：촉수 B고문계 조교로 개발도 상승, 5000이상으로 감도가 1 랭크 상승
;※각부의 음란계 소질을 취득하고 있었을 경우, 개발도가 한층 더 상승된다
@COM588
PRINTL 촉수 은방울꽃미약
TSTR:0 = 촉수 은방울꽃미약
CALL TRAIN_MESSAGE_COM588

;해제시는 약간 소비가 저하
LOCAL:1 = (TEQUIP:95 & 8) ?  100 # 200
LOCAL:2 = (TEQUIP:95 & 8) ?  150 # 300

;조제 지식이 있으면 소비를 억제한다
IF TALENT:MASTER:55
	LOCAL:1 /= 2
	LOCAL:2 /= 2
ELSEIF ASSI >= 0 && TALENT:ASSI:55
	LOCAL:1 /= 2
	LOCAL:2 /= 2
ENDIF
;조교 대상으로 미약중독이 있으면 소비를 억제한다
IF TALENT:46
	LOCAL:1 /= 2
	LOCAL:2 /= 2
ENDIF
DOWNBASE:0 += LOCAL:1
DOWNBASE:1 += LOCAL:2

;미약의 효과를 받고 있었을 경우는 일부 효과감
;아양 약효과로 욕정의 업이  2배가 된다
IF TEQUIP:11
	SOURCE:18 = 1000
	SOURCE:26 = 1500
ELSE
	SOURCE:18 = 2500
	SOURCE:26 = 2500
ENDIF
SOURCE:31 = 5000

;미약중독
IF TALENT:46
	SOURCE:24 = 500
	TIMES SOURCE:18 , 1.50
	TIMES SOURCE:31 , 1.50
	SOURCE:42 = 100
ELSE
	SOURCE:42 = 1 + RAND:3
ENDIF

IF TEQUIP:95 & 8
	TEQUIP:95 -= 8
ELSE
	TEQUIP:11 = 1
	TEQUIP:95 |= 8
ENDIF

;약물 경험
TCVAR:40 += 2

;커맨드 속성：촉수
TFLAG:98 = 8
;촉수 경험 플래그
TFLAG:90 = 0
RETURN 1

;-------------------------------------------------
;촉수 은방울꽃미약주입중
;-------------------------------------------------
@EQUIP_COM588
PRINTL <촉수 은방울꽃미약주입중>

DOWNBASE:0 += 5
DOWNBASE:1 += 10

LOCAL = 0
LOCAL:1 = 0
LOCAL:2 = 0
LOCAL:3 = 0

;ABL:촉수 중독을 본다
IF ABL:35 == 0
	LOCAL:11 = 100
	LOCAL:12 = 80
	LOCAL:13 = 1000
ELSEIF ABL:35 == 1
	LOCAL:11 = 200
	LOCAL:12 = 160
	LOCAL:13 = 500
ELSEIF ABL:35 == 2
	LOCAL:11 = 400
	LOCAL:12 = 240
	LOCAL:13 = 250
ELSEIF ABL:35 == 3
	LOCAL:11 = 600
	LOCAL:12 = 320
	LOCAL:13 = 100
ELSEIF ABL:35 == 4
	LOCAL:11 = 800
	LOCAL:12 = 400
	LOCAL:13 = 50
ELSE
	LOCAL:11 = 1000
	LOCAL:12 = 500
	LOCAL:13 = 10
ENDIF

;메디슨과의 궁합 있고 없음으로 보정이 상하
LOCAL:10 = 33
IF RELATION:(LOCAL:10) > 0 && RELATION:(LOCAL:10) != 100
	LOCAL:11 *= RELATION:(LOCAL:10)
	LOCAL:11 /= 100
	LOCAL:12 *= RELATION:(LOCAL:10)
	LOCAL:12 /= 100
	LOCAL:13 *= RELATION:(LOCAL:10)
	LOCAL:13 /= 100
ENDIF

SOURCE:19 += LOCAL:11
SOURCE:24 += LOCAL:12
SOURCE:25 += (LOCAL:11 + LOCAL:12 + LOCAL:13)/3
SOURCE:26 += 500
SOURCE:31 += LOCAL:13

;촉수 구리 꾸짖어 촉수 페니스 꾸짖어 카테터
IF (TEQUIP:30 || TEQUIP:32) && TALENT:100 == 0
	IF TEQUIP:30
		LOCAL:14 = 2
		;음핵/음경
		SIF TALENT:171
			TIMES LOCAL:14 , 1.50
		LOCAL += LOCAL:14
	ENDIF
	IF TEQUIP:32
		LOCAL:14 = 1
		;흘리는 버릇
		SIF TALENT:45
			LOCAL:14 += 2
		;요도광
		SIF TALENT:175
			TIMES LOCAL:14 , 1.50
		LOCAL += LOCAL:14
	ENDIF
ENDIF
;촉수 삽입, G스포 꾸짖어 자궁 꾸짖어 촉수 유령질경
IF (TEQUIP:20 || TEQUIP:21) && TALENT:102 == 0
	IF TEQUIP:21
		LOCAL:1 = 3
	ELSE
		LOCAL:1 = TEQUIP:20
	ENDIF
	;음 단지
	SIF TALENT:172
		TIMES LOCAL:1 , 1.50
ENDIF
;어널 촉수, 촉수 소장 꾸짖어 촉수 관통, 촉수 관장
IF TEQUIP:25 && TALENT:104 == 0
	IF TEQUIP:25 == 7
		LOCAL:2 = 2
	ELSE
		LOCAL:2 = TEQUIP:25
	ENDIF
	;음고
	SIF TALENT:173
		TIMES LOCAL:2 , 1.50
ENDIF
;촉수 유두 꾸짖어 촉수 착유, 촉수 유선 삽입
IF TEQUIP:35 && TALENT:106 == 0
	IF TEQUIP:35 == 3
		LOCAL:3 = 5
	ELSEIF TEQUIP:35 == 2
		LOCAL:3 = 3
	ELSE
		LOCAL:3 = 1
	ENDIF
	;음유
	SIF TALENT:174
		TIMES LOCAL:3 , 1.50
ENDIF
REPEAT 4
	;[야]는 은방울꽃미약에 의한 개발을 할 수 없다. 또, 입수되는 소스가 변화
	IF TALENT:217
		SOURCE:12 += LOCAL:COUNT * 10
		SOURCE:26 += LOCAL:COUNT * 5
	ELSE
		CFLAG:(COUNT+60) += LOCAL:COUNT
		SOURCE:26 += LOCAL:COUNT * 10
	ENDIF
REND

;메디슨의 성별상, 조교 대상이♀의 때만 레즈비언 경험이 들어가는 사양에
SIF TALENT:120 == 0
	TCVAR:12 += 1

;약물 경험
TCVAR:40 += 1

;디버그 모드에서는 미약에 의한 개발 정도를 표시
IF TALENT:MASTER:998 && TALENT:217 == 0
	PRINTFORM C개발 Lv:\@(TALENT:100 == 0) ? {CFLAG:60, 3} # MAX\@　 
	PRINTFORM V개발 Lv:\@(TALENT:102 == 0) ? {CFLAG:61, 3} # MAX\@　 
	PRINTFORM A개발 Lv:\@(TALENT:104 == 0) ? {CFLAG:62, 3} # MAX\@　 
	PRINTFORML B개발 Lv:\@(TALENT:106 == 0) ? {CFLAG:63, 3} # MAX\@
ENDIF
;촉수 경험 플래그
TFLAG:90 += 1
RETURN 1

;--------------------------------------------------
;조교시 메시지
;--------------------------------------------------
@TRAIN_MESSAGE_COM588
CALL KOJO_TRAIN_MESSAGE_COM
SIF FLAG:10 == 0 || CFLAG:99 == 1 || RESULT != 0
	RETURN 0

PRINTL
IF TEQUIP:95 & 8
	PRINTFORM 메디슨은 %CALLNAME:PLAYER%의 명령을 받자,
	IF TEQUIP:42 == 0 && TEQUIP:91 != 1
		PRINTFORML 조금 아쉬운 듯 %CALLNAME:TARGET%에게서 몸을 떼어 놓았다.
		PRINTFORML 타액인가 미약인가. 흰 실이 메디슨과 %CALLNAME:TARGET%의 입에서 늘어지고 있어 그 모습은 마치 떨어지는 것을 아쉬워 하는 듯한 착각을 낳게 한다.
	ELSE
		PRINTFORML 잠시 재미 없단 표정을 지으며 %CALLNAME:TARGET%에게 모이고 있던 촉수를 떼어냈다.
		PRINTL 촉수에서 빠져 나온 흰 점액의 실에서 발해지는 은방울꽃의 향기가 촉수 본래의 악취와 서로 섞여, 뭐라고 말할 수 없는 공간을 낳고 있다.
	ENDIF
	PRINTL 메디슨이 촉수의 꽃 안에 돌아오자, 마치 어린 인형을 지키는 것 같이 고기의 꽃잎이 닫혔다.
	PRINTFORMW 그리고 은방울꽃의 향기를 발하는 액체로 흠뻑 젖은 %조사선택(CALLNAME:TARGET, "를")% 남기고, 촉수의 꽃봉오리는 벽의 저 편으로 돌아갔다….
ELSE
	PRINTFORML %CALLNAME:PLAYER%의 명령에 따라, 촉수의 벽에서 사람이 하나 들어갈 만한 극히 굵은 촉수가 나타났다.
	PRINTFORML 촉수는 %CALLNAME:TARGET%에게 가까워지더니 은방울꽃이 개화하는 것처럼 몸을 벌리고, 안에서 이형과 완전하게 융합한 메디슨이 무수한 점액으로 젖은 촉수를 인솔하면서 모습을 보였다.
	PRINTFORML 메디슨은 %CALLNAME:MASTER%에게 사랑스럽게 일례하고, 알카익 스마일을 띄우면서 %CALLNAME:TARGET%에게 가까워졌다.
	IF TEQUIP:42 == 0 && TEQUIP:91 != 1
		PRINTFORML 그리고 촉수로부터 분비된 최음효과가 있는 액체를 입에 머금고 입에서 입으로 무리하게 %CALLNAME:TARGET%의 목에 흘려 넣었다.
	ELSE
		PRINTFORML 그리고 교묘한 촉수 다루기로 %CALLNAME:TARGET%의 성감대에 최음효과가 있는 액체를 처발랐다.
	ENDIF
	IF TFLAG:899 == 0
		PRINTFORMW %조사선택(CALLNAME:TARGET, "는")% 난폭한 한숨을 돌리며 피부를 홍조시키고 있다….
	ELSE
		PRINTFORMW %CALLNAME:TARGET%의 피부가 요염하게 홍조하고 있다….
	ENDIF
ENDIF
